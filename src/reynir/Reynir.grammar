#
# Reynir.grammar
#
# A context-free grammar for modern Icelandic
#
# Reynir - a natural language processor for Icelandic
#
# Copyright (C) 2018 Miðeind ehf.
#
#    This program is free software: you can redistribute it and/or modify
#    it under the terms of the GNU General Public License as published by
#    the Free Software Foundation, either version 3 of the License, or
#    (at your option) any later version.
#
#    This program is distributed in the hope that it will be useful,
#    but WITHOUT ANY WARRANTY; without even the implied warranty of
#    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#    GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see http://www.gnu.org/licenses/.
#
#
# This file contains an extensive (but not complete) context-free grammar
# for parsing modern Icelandic text.
#
# The grammar is presented in augmented Backus-Naur form, where
# repetition specifiers are allowed on right-hand-side tokens
# (* for 0..n, + for 1..n, ? for 0..1 occurrences).
#
# Rules and productions are autogenerated for all variants
# (combinations) of applicable grammatical features, i.e. genders (m, f, n),
# cases, numbers (singular/plural) and persons (1st, 2nd, 3rd).
#
# As an example, the generic nonterminal Undirsetning/tala/fall,
# having variants for /tala (singular/plural) and /fall (case),
# is automatically expanded to 8 specific nonterminals:
# Undirsetning_et_nf, Undirsetning_et_þf, etc.,
# with corresponding substitutions being made in the right-hand-side
# productions (et = singular, nf = nominative...).
# The permissible variants are defined below.
#
# Nonterminal identifiers start with uppercase letters.
# Terminal identifiers start with lowercase letters.
#
# Literals within single quotes 'xyz' are matched with the stem
# of a word token, plus eventual variants.
#
# Literals within double quotes "xyz" are matched with the
# text of a word token as it appeared in the source, albeit after
# conversion to lowercase. Variants cannot be applied to double-quoted
# literals.
#
# For disambiguation in POS tagging, literals may specify an optional
# word category after a colon. Thus "og:st" means that this "og" is
# a conjunction, not an adverb (which would be specified as "og:ao").
#
# This feature is occasionally used for typographic purposes, i.e.
# to distinguish between em dashes ("-:em") and regular hyphens ("-").
#
# Terminal names generally take the form category_variant1_variant2...,
# where the most common categories are:
# * no for nouns (nafnorð)
# * so for verbs (sagnorð)
# * lo for adjectives (lýsingarorð)
# * fs for prepositions (forsetningar)
# * st for connecting words (samtengingar)
# * fn for pronouns (fornöfn)
# * ao for adverbs (atviksorð)
# * töl for number words (töluorð)
# * gr for determiners (greinir)
#
# The terminal names are matched against POS tags from the BIN
# database in the module binparser.py.
#
# Further documentation of the grammar terminals and variants is
# available at https://greynir.is/doc/
#
# Alternative productions are separated with the '|' (vertical bar) sign.
# If there is a priority ordering between the alternatives, so that
# a single highest priority alternative should be automatically
# selected from possible alternatives, use the '>' (greater than)
# sign between the productions instead.
#
# For the Sublime Text editor, a syntax highlighting specification
# for .grammar files, grammar.YAML-tmLanguage, is available.
# It can be found in the ./resources directory and its
# use is highly recommended.


# VARIANT DEFINITIONS

# Gender (m, f, n) variant
/kyn = kk kvk hk
/nkyn = kk kvk # Natural gender (male, female)

# Case (nom, acc, dat, poss) variant
/fall = nf þf þgf ef
/aukafall = þf þgf ef # All cases except nominative
/afall = nf þf þgf ef # Second case (andlagsfall) for dual argument verbs

# Number (singular/plural) variant
/tala = et ft

# Person (1st, 2nd, 3rd) variant
/pers = p1 p2 p3


# GRAMMAR RULES

# Note: comments from this point are mostly in Icelandic

# Setningargerðir

# Merkið > gefur til kynna forgangsröðun.
# Ef báðar koma til greina er Málsgrein alltaf tekin fram yfir Grein o.s.frv.
S0 → Málsgrein > Grein > Fyrirsögn

Málsgrein →
    MgrInnihald
    | '(' MgrInnihald ')'
    | '[' MgrInnihald ']'

MgrInnihald → Forskeyti? Yfirsetning StLiður* Tilvitnun? Lokatákn?

Forskeyti →
    "-:em"? Samtenging "bara:ao"? "-:em"?       # 'En - þegar allt er skoðað, var þetta fínt.'
    | Samtenging? uh TengimerkiVh Samtenging?   # Og já, þetta var ekki gott / Nei - en hann átti ekkert svar
    | raðnr_nf_kvk                              # Raðnúmer málsgreinar
    | "-:em"                                    # Þankastrik á undan setningu (allar gerðir strika varpast í '-')
    | 'mynd:kvk'_nf_et tala_nf_kvk AfTala? ':'? # Mynd 2 af 10
    | 'dæmi:hk'_et_nf tala_nf_hk? ':'
    | 'sjá:so'_0_gm_nh "einnig:ao"? ':'
    | "semsagt:ao" ':'
    | "sem:st" 'segja:so'_lhþt_sb_hk_nf_et ':'  # sem sagt
    | 'aðferð:kvk'_nf_et ':'
    | 'með_öðrum_orðum:ao' ':' | 'með:fs'_þgf 'annar:lo'_hk_þgf_ft 'orð:hk'_þgf_ft ':'
    | Greinarnúmer ':'

AfTala → 'af:fs'_þgf tala_þgf_kvk

$score(-6) Forskeyti # Ekki freistast í forskeyti ef annað er í boði

Tilvitnun →
    ',' SögnSegja/tala/pers NlFrumlag_nf/tala/pers AtvFsAuka? # , segir/fullyrðir Jón Ásmundsson, fulltrúi samtakanna, í dag í útvarpinu
    | ',' 'segja:so'_0_gm_fh_et_p3 'í:fs'_þgf Nl_þgf AtvFsAuka?  # , segir í tilkynningu ráðuneytisins í gær

SögnSegja/tala/pers →
    'segja:so'_0_gm_fh/tala/pers
    | 'flissa:so'_0_gm_fh/tala/pers
    | 'fullyrða:so'_0_gm_fh/tala/pers
    | 'staðhæfa:so'_0_gm_fh/tala/pers
    | 'ítreka:so'_0_gm_fh/tala/pers

$score(+32) Tilvitnun # Þarf að vera meiri plús en t.d. SagnFramhald

Lokatákn → '.' | Spurningarmerki | ':' | '!' | '(' '!' ')'
LokatáknEðaKomma → Lokatákn | ','

Spurningarmerki → '?'

# Til að gera spurnarsetningum hátt undir höfði.
$score(-12) Spurningarmerki


Fyrirsögn →
    Forskeyti? FyrirsögnInnihald

FyrirsögnInnihald →
    # Nafnliður í nefnifalli
    Nl_nf Atviksliður? Eignun? Lokatákn?
    | SetningÁnF Lokatákn?

Eignun →
    # Mynd: Jón Stefánsson
    # Erlendur: Friðgeir er frábær
    ':' Nl_nf

SetningÁnF →
    # Setning án frumlags: 'Stefndi í átt til Bláfjalla', 'Aftengdi sjálfstýringu'
    SetningÁnF_et_p3/kyn
    | SetningÁnF_ft_p3/kyn

Greinarnúmer →
    # Greinarnúmer, e.t.v. skammstafað, og e.t.v. með fyrirsögn
    raðnr_nf_kvk 'grein:kvk' # 'grein:kvk'_nf_et !!! Ef tilbrigðin eru með þá passar skammstöfunin gr. ekki við laufið

Grein →
    # Sjálfstæð fyrirsögn greinar
    Greinarnúmer ':'? Fyrirsögn? # 17. gr. Störf Alþingis [.]

$score(+6) Grein

StLiður →
    # Samtengingar yfirsetninga
    Tengimerki? Samtenging HreinYfirsetning
    | Tengimerki Yfirsetning
    # 'Það eru bara tveir kostir í boði: Selja bankann eða láta hann fara í þrot'
    | ':' SagnRuna
    # 'Aftur er umræðan sú sama: um rétt kirkjunnar þegna til að neita að gefa saman samkynhneigða'
    | ':' NhFylling
    # Flokkurinn stefnir á stjórnarþátttöku, fái hann til þess umboð

Tengimerki →
    ':' | ';' | ',' | "-:em"

TengimerkiVh →
    ';' | ',' | "-:em"

Samtenging →
    # Flóknari samtenging úr mörgum orðum
    "og:st" | "eða:st" | "ellegar:st" | "heldur:st" 
    | "enda:st" | "en:st" | "hvorki:st"
    | 'það:pfn'_hk_nf_et 'vera:so'_0_gm_fh_et_p3 | 'það_er:ao'
    | "þar:ao" 'á_meðal:ao' | 'þar_á_meðal:ao'
    | 'auk_þess:ao' "sem:st" "að:st"?
    | 'það_er_að_segja:ao' Tengimerki "að:st"?
    | "svo:st" "sem:st"
    | "eins:ao" "og:st"
    | "því:st" "að:st"

$score(+2) Samtenging
$tag(apply_length_bonus) Samtenging

Yfirsetning →
    # Yfirsetning getur hafist á runu upphrópana
    Upphrópun* HreinYfirsetning

Upphrópun →
    uh+ ',' # Jú jú, ég fór út í dag / Nei, hann kemur ekki.

HreinYfirsetning →
    Setning
    | SetningAukafall
    | SetningAukafallForgangur
    | SetningLo
    | SetningSo
    | AtvSetning        # Atvikssetning fremst í aðalsetningu
    | Spurnarsetning
    | SamsettSetning    # Annaðhvort X eða Y; bæði X og Y; ýmist X eða Y

$score(+2) HreinYfirsetning

$score(-2) SetningAukafall
$score(+1) SetningAukafallForgangur
$score(+2) SetningLo
$score(-1) SetningSo
$score(+3) Setning
$score(+12) Spurnarsetning
$score(+25) SamsettSetning

$tag(begin_prep_scope) HreinYfirsetning # Nýtt undirtré fyrir tengingu sagnar og forsetningar

# Spurnaraðalsetningar

Spurnarsetning →
    SpurnarNafnliður/tala/fall/kyn FsMeðFallstjórn? BeygingarliðurSögnFremst? "?"
    | SpurnarForsetningarliður FsMeðFallstjórn? BeygingarliðurSögnFremst? "?"
    | SpurnarAtviksorð FsMeðFallstjórn? BeygingarliðurSögnFremst?  "?"
    | SpurnarFrumlag/pers/tala/kyn FsMeðFallstjórn? BlTagl/tala/pers/kyn? '?'

# Spurnarorðið er frumlag setningarinnar, stendur utan beygingarliðar
BlTagl/tala/pers/kyn →
    FrumlagsInnskot/tala/pers/kyn? AtviksliðurEinkunn*
        SagnRuna/tala/pers/kyn Sjálfur/tala/kyn? KommaTilvísunarsetning/tala/pers/kyn?
        Upptalning/tala?

SpurnarFrumlag/pers/tala/kyn →
    SpurnarNafnliður/tala/fall/kyn

$score(+8) Spurnarsetning

# Atvikssetning fremst í setningu
# [Ef þetta gerist](, þá) getur fatan dottið
# [Meðan þetta heldur](, þá) sleppur þetta
# [Af því að hún sigraði](, þá) hlýtur hún gullið
# [Til þess að fólkið blotni ekki](, þá) verður það að nota regnhlíf
# [Þrátt fyrir að hann missti eggið](, þá) brotnaði það ekki

AtvSetning →
    Atvikssetning ","? "þá:st"? BeygingarliðurMeðUmröðun

$score(+12) AtvSetning

SamsettSetning →
    "annaðhvort:st" BeygingarliðurSögnFremst "eða:st" BeygingarliðurÁnUmröðunar
    | "bæði:st" BeygingarliðurSögnFremst "og:st" BeygingarliðurÁnUmröðunar
    | "ýmist:st" BeygingarliðurSögnFremst "eða:st" BeygingarliðurÁnUmröðunar

# Aukasetningar
Atvikssetning → 
    Tíðarsetning
    | Tilgangssetning
    | Viðurkenningarsetning
    | Afleiðingarsetning
    | Orsakarsetning
    | Skilyrðissetning
    | Samanburðarsetning

$score(+15) Atvikssetning

KommaAtvikssetning →
    ","? Atvikssetning

$tag(begin_prep_scope) Atvikssetning # Nýtt undirtré fyrir tengingu sagnar og forsetningar

Tíðarsetning → 
    Tíðartenging BeygingarliðurÁnUmröðunar
    | Tíðartenging BeygingarliðurÁnF
    | Tíðartenging BeygingarliðurMeðUmröðun
Tilgangssetning → 
    Tilgangstenging BeygingarliðurÁnUmröðunar
    | Tilgangstenging BeygingarliðurÁnF
Viðurkenningarsetning → 
    Viðurkenningartenging BeygingarliðurÁnUmröðunar
    | Viðurkenningartenging BeygingarliðurÁnF
Afleiðingarsetning → 
    Afleiðingartenging BeygingarliðurÁnUmröðunar
    | Afleiðingartenging BeygingarliðurÁnF
Orsakarsetning →
    Orsakartenging BeygingarliðurÁnUmröðunar
    | Orsakartenging BeygingarliðurÁnF
Samanburðarsetning → 
    Samanburðartenging BeygingarliðurÁnUmröðunar # Bara vh.
    | Samanburðartenging BeygingarliðurÁnF
Skilyrðissetning → 
    Skilyrðistenging BeygingarliðurÁnUmröðunar
    | Skilyrðistenging BeygingarliðurÁnF
    | Skilyrðissetning_vh

Skilyrðissetning_vh →
    TengimerkiVh? SagnliðurVh

$score(+4) Samanburðarsetning
$score(+4) Tíðarsetning
$score(-10) Skilyrðissetning # Skilyrðistenging er með plús á móti
$score(-14) Skilyrðissetning_vh # Draga enn úr skilyrðissetningum sem byggja á viðtengingarhætti

# Fallsetningar - skýringarsetningar og spurnaraukasetningar

Skýringarsetning → 
    Skýringartenging SkýringarBotn

$tag(begin_prep_scope) Skýringarsetning

Skýringartenging → 
    "að:st"

SkýringarBotn →
    BeygingarliðurÁnUmröðunar   # Ég taldi að Jón hefði dregið sér fé / að málmagnir væri að finna í hreyfli
    | AtvSetning                # Ég taldi að meðan þetta héldi yrðu ekki frekari skakkaföll
    | SetningUmAðRæða           # Ég taldi að um hefði verið að ræða mistök af hans hálfu
    | SetningÞað                # Ég taldi að ætíð yrði að gæta hagsmuna almennings

# Spurnaraukasetningar
Spurnaraukasetning →
    SpurnarNafnliður/tala/fall/kyn FsMeðFallstjórn? BeygingarliðurÁnUmröðunar?
    | "hvort:st" "að:st"? BeygingarliðurÁnUmröðunar?
    | SpurnarForsetningarliður BeygingarliðurÁnUmröðunar?
    | SpurnarAtviksorð FsMeðFallstjórn? BeygingarliðurÁnUmröðunar?
    | SpurnarFrumlag/pers/tala/kyn FsMeðFallstjórn? BlTagl/tala/pers/kyn?

SpurnarNafnliður/tala/fall/kyn →
    'hver:fn'/tala/fall/kyn Nl_ef_ft/pers/kyn?
    | 'hver:fn'/tala/fall/kyn AfLiður/kyn
    | 'hvor:fn'/tala/fall/kyn Nl_ef_ft/pers/kyn?
    | 'hvor:fn'/tala/fall/kyn Nl/fall/tala
    | 'hvor:fn'/tala/fall/kyn AfLiður/kyn
    | 'hvaða:fn'/tala/fall/kyn Nl/fall/tala
    | 'hver:fn'_ef_et_kk 'konar:kk'_ef_et Nl/fall/tala
    | HversuHve Lo/fall/tala/kyn Nl/fall/tala
    | HversuHve Lo/fall/tala/kyn AfLiður/kyn?
    | 'hvernig:ao' Nl/fall/tala     # Hægt að nota hliðstætt með nafnlið

SpurnarForsetningarliður →
    fs/fall SpurnarNafnliður/tala/fall/kyn

SpurnarAtviksorð →
    spao | "hvernig:ao" | 'af:fs'_þgf 'hver:fn'_þgf_et_hk | "hví:ao" | "afhverju:ao"
    | 'til:fs'_ef 'hver:fn'_ef_et_hk | 'hver:fn'_ef_et_hk 'vegna:fs'_ef
    | "hvort:st" "að:st"?

HversuHve →
    "hversu:ao" | "hve:ao"

AfLiður/kyn → 'af:fs'_þgf Nl_þgf_ft/pers/kyn

$score(+2) SpurnarAtviksorð
$score(+1) Spurnaraukasetning

# Beygingarliðir

# Venjuleg setningaskipan þar sem frumlag (oftast í nefnifalli) er fremst
BeygingarliðurÁnUmröðunar →
    BeygingarliðurÁnUmröðunar/tala/pers/kyn
    | SetningAukafall
    | SetningAukafallForgangur

BeygingarliðurÁnUmröðunar/tala/pers/kyn →
    Frumlag/pers/tala/kyn FrumlagsInnskot/tala/pers/kyn? AtviksliðurEinkunn*
        SagnRuna/tala/pers/kyn Sjálfur/tala/kyn? KommaTilvísunarsetning/tala/pers/kyn?
        SamanburðurNl? Upptalning/tala? KommaAtvikssetning?

# Sögn kemur fremst, svo frumlag. Enginn annar liður leyfist fremst, tengingin situr þar.
BeygingarliðurSögnFremst →
    BeygingarliðurSögnFremst/tala/pers/kyn

BeygingarliðurSögnFremst/tala/pers/kyn →
    SagnHluti/tala/pers/kyn Upptalning/tala? KommaAtvikssetning?

# Frumlag fyrir aftan sögn, annar liður getur komið fremst:
# 'Gengu þau galvösk á Esjuna þrátt fyrir veðurútlitið'
# 'Bersýnilega fór Jón sterki í bága við reglurnar með þessu'
# 'Undir húsinu voru illa farnar skolpleiðslur'
# 'Í skólanum borðaði Gunna aldrei matinn eftir þetta'
# 'Í grein sinni opinberaði Hannes niðurstöðu sína'
# Ath. að frumlag 3p. et. getur verið falið:
# (? Það virtist mér í dag kettinum líða betur)
# 'Í dag virtist mér kettinum líða betur'
# 'Eins og Páll er ég mjög ósáttur við þetta'
BeygingarliðurMeðUmröðun →
    BeygingarliðurMeðUmröðun/tala/pers/kyn

BeygingarliðurMeðUmröðun/tala/pers/kyn →
    SetningForskeyti? SagnHluti/tala/pers/kyn Upptalning/tala? KommaAtvikssetning?

# Mögulegar tengingar í aukasetningum eftir gerð aukasetningar
Tíðartenging →
    "löngu:ao"? "áður:ao" "en:st" "að:st"?  # áður en [að]
    | "löngu:ao"? "eftir:ao" "að:st"
    | 'allt:fn'_nf_hk_et? 'frá:fs'_þgf 'það:pfn'_hk_þgf_et "að:st"
    | 'allt:fn'_nf_hk_et? 'frá:fs'_þgf 'það:pfn'_hk_þgf_et "er:st"?
    | 'snemma:ao'_mst "en:st" "að:st"? # fyrr en [að]
    | 'ótt:ao'_mst "en:st" "að:st"? # óðar en [að]
    | "þá:ao" "er:st"
    | 'um:fs'_þf 'leið:kvk'_þf_et "og:st" # um leið og
    | 'um:fs'_þf 'svipaður:lo'_sb_hk_þf_et 'leyti:hk'_þf_et "og:st" # um svipað leyti og
    | 'um:fs'_þf 'samur:fn'_hk_þf_et 'leyti:hk'_þf_et "og:st" # um sama leyti og
    | 'á_sama_tíma:ao' "og:st"
    | "síðan:st"
    | "þegar:st"
    | "uns:st"
    | "meðan:st"
    | 'á_meðan:ao' "að:st"? # á meðan [að]
    | "þangað:ao" "til:st" "að:st"?
    | "þar:ao" "til:st" "að:st"?
    | 'allar_götur_síðan:ao'
    | TengiorðEr # "er:stt", líka tilvísunartenging - fær mínus

$tag(apply_length_bonus) Tíðartenging

Tilgangstenging → 
    'til:fs'_ef 'það:pfn'_hk_ef_et "að:st"
    | "svo:st" "að:st"    # Líka afleiðingartenging
    | SvoTenging

Viðurkenningartenging →
    "enda:st" "þótt:st" "að:st"?
    | "jafnvel:ao"? "þótt:st" "að:st"?
    | "jafnvel:ao"? "þó:st" "að:st"?
    | 'þrátt_fyrir:ao' 'það:pfn'_hk_þf_et? "að:st"

Afleiðingartenging →
    "þannig:ao" "að:st"
    | "svo:st" "að:st"   # Líka tilgangstenging
    | SvoTenging

Orsakartenging →
    "fyrst:ao" "að:st"?
    | 'sökum:fs'_ef 'það:pfn'_hk_ef_et "að:st"  # sökum þess að
    | 'sakir:fs'_ef 'það:pfn'_hk_ef_et "að:st"  # sakir þess að
    | 'úr:fs'_þgf 'það:pfn'_hk_þgf_et "að:st"?  # úr því [að]
    | 'þar_eð:st'
    | "þar:ao" "sem:st"
    | 'af:fs'_þgf 'það:pfn'_hk_þgf_et "að:st"?  # af því [að]
    | "því:st" "að:st"?                         # því [að]
    | 'vegna:fs'_ef 'það:pfn'_hk_ef_et "að:st"  # vegna þess að
    | 'með:fs'_þgf 'það:pfn'_hk_þgf_et "að:st"   # með því að

Skilyrðistenging →
    "ef:st" "að:st"?
    | "svo:st" "framarlega:ao" "sem:st"
    | "nema:st"

$score(+8) Skilyrðistenging # Skilyrðissetning er með mínus á móti

SvoTenging →
    "svo:st"

$score(-8) SvoTenging # Draga verulega úr "svo" einu og sér sem tengingu

Samanburðartenging →
    "eins:ao" "og:st"
    | "líkt:ao" "og:st"
    | "svo:st" "sem:st"

# Setningar með frumlagi

Setning → Setning/tala/pers/kyn

# Tilvik þar sem einu frumlagi fylgja nokkrir sagnliðir:
# 'Anna tók bílinn [þegar það hentaði henni], keyrði hann sjálf í bæinn [,] og keypti í matinn'

SagnRuna → SagnRuna/tala/pers/kyn

SagnRuna_p3_et/kyn → SagnRuna_et_p3/kyn
SagnRuna_p3_ft/kyn → SagnRuna_ft_p3/kyn

SagnRuna/tala/pers/kyn →
    SagnRunaKnöpp/tala/pers/kyn SagnFramhald/tala/pers/kyn*
    | "annaðhvort:st" SagnRunaKnöpp/tala/pers/kyn SagnKomma/tala/pers/kyn* 
        "eða:st" FsAtv? SagnRunaKnöpp/tala/pers/kyn
    | "bæði:st" SagnRunaKnöpp/tala/pers/kyn SagnKomma/tala/pers/kyn* 
        "og:st" FsAtv? SagnRunaKnöpp/tala/pers/kyn
    | "hvorki:st" SagnRunaKnöpp/tala/pers/kyn SagnKomma/tala/pers/kyn* 
        "né:st" FsAtv? SagnRunaKnöpp/tala/pers/kyn
    | "ýmist:st" SagnRunaKnöpp/tala/pers/kyn SagnKomma/tala/pers/kyn* 
        "eða:st" FsAtv? SagnRunaKnöpp/tala/pers/kyn

SagnRunaKnöpp/tala/pers/kyn →
    Sagnliður/tala/pers/kyn LoTengtSögn_nf/tala/kyn? SagnInnskotAtv?

SagnFramhald/tala/pers/kyn →
    SagnKomma/tala/pers/kyn* KommaOgEðaEn FsAtv? SagnRunaKnöpp/tala/pers/kyn

SagnKomma/tala/pers/kyn →
    ',' SagnRunaKnöpp/tala/pers/kyn

# Hvetja til framhalds sömu sagnrunu, fremur en að skipta niður í fleiri setningar
$score(+5) SagnFramhald/tala/pers/kyn
$score(+2) SagnKomma/tala/pers/kyn

$tag(begin_prep_scope) SagnRunaKnöpp/tala/pers/kyn

# Sagnir innan undirtrés geta ekki orsakað bónus á forsetningar utan þess
$tag(purge_verb) SagnRuna/tala/pers/kyn

SagnInnskot →
    FsAtv
    | ÁttFs # suður götuna
    | EkkiNeitt

# Gefa aukabónus fyrir forsetningar innan SagnInnskots ef þær passa við umlykjandi sögn
$tag(enable_prep_bonus) SagnInnskot

SagnInnskotAtv →
    # (Manninum var vikið) frá störfum [þegar málið kom upp]
    SagnInnskot KommaAtvikssetning?
    # (Ég ætla að fá mér ís) [,] þegar ég verð 18 ára í haust (og borða hann úti)
    | KommaAtvikssetning

# Gefa plús fyrir að forsetningar- og atviksliðir tengist sögnum,
# frekar en að þeir séu t.d. forskeyti á næstu setningu
$score(+2) SagnInnskotAtv

EkkiNeitt →
    "ekki:ao" 'neinn:fn'_et_hk_nf # (Hann þekkti þá) ekki neitt

$score(-2) EkkiNeitt # Betra að þekkja fornafnið 'neitt' sem nafnlið

SagnViðhengi/tala/pers/kyn →
    KommaOgEðaEn SagnRuna/tala/pers/kyn

# Setningarhluti sem byrjar á sögn og hefur innifalið frumlag

SagnHluti/tala/pers/kyn →
    SagnliðurMeðF/tala/pers/kyn SagnInnskot? SagnViðhengi/tala/pers/kyn?

SagnHluti_et_p3_hk →
    VirðistSem    # Samkvæmt heimildum mbl.is virtist sem Björn fengi aðsvif á sviðinu

$tag(begin_prep_scope) SagnHluti/tala/pers/kyn # Nýtt undirtré fyrir tengingu sagnar og forsetningar

FrumlagsInnskot/tala/pers/kyn →
    ',' SetningÁnF/tala/pers/kyn ','
    | ',' Samtenging HreinYfirsetning ','   # Lögheimili á Íslandi, þegar kosning fer fram, er skilyrði
    | InnskotsSamtenging HreinYfirsetning   # Lögheimili á Íslandi þegar kosning fer fram er skilyrði
    | Tímaliður                             # Tjónið þann 3. mars nam 1,2 milljarði króna

# Þröngt mengi samtenginga sem nota má án kommu í frumlagsinnskoti
# (sé mengið of vítt eykst margræðni verulega)
InnskotsSamtenging →
    "þegar:st" | "meðan:st" | 'á_meðan:ao' | "þar:ao" "sem:st" | 'þrátt_fyrir:ao'

Frumlag/pers/tala/kyn →
    NlFrumlag_nf/tala/pers/kyn

# Hakk til að láta 1.p. og 2.p. virka með fornafninu "allur",
# sem annars er aðeins brúklegt í 3.p.

Frumlag_p1/tala/kyn →
    FrumlagAllur/tala/kyn

Frumlag_p2/tala/kyn →
    FrumlagAllur/tala/kyn

FrumlagAllur/tala/kyn →
    # Allar eruð þið jafnar fyrir lögum
    # Öll erum við jöfn fyrir lögum
    'allur:fn'_nf/tala/kyn

$score(-5) FrumlagAllur/tala/kyn # Ekki nota þessa undantekningu nema þörf krefji

# Hakk til að láta "Þetta/Það eru aðallega fjögur atriði" virka,
# þ.e. þar sem ábendingarfornöfnin "þessi"/"sá" eru notuð í eintölu
# sem frumlag með andlagi í fleirtölu.

Frumlag_p3_ft_hk →
    'þessi:fn'_nf_et_hk | 'sá:fn'_nf_et_hk

Samanburðarmerki → ',' | ';' | "-:em"

SamanburðurNl →
    SamanburðarNlTenging Nl_nf AtvFs? # þar af einn alvarlega
    | Samanburðarmerki? "þar:ao" "með:ao" 'telja:so'_lhþt_sb_nf/tala/kyn Nl_nf/tala/kyn AtvFs? # þar með talinn allur þingflokkur Framsóknar
    | Samanburðarmerki 'annað:fn'_þgf_et_hk "nær:ao"   # Viðskiptablaðið styður ekki jafnaðarstefnuna; öðru nær.

SamanburðarNlTenging →
    "heldur:st" "en:st"             # 'Forseti á ekki að gera meiri breytingu á sér heldur en forsætisráðherra'
    | ','? "eins:ao" "og:st"        # 'Hann á ekki andstæðinga eins og aðrir kjörnir fulltrúar'
    | ','? "líkt:ao" "og:st"
    | Samanburðarmerki? "þar:ao" "af:ao"  # 'Fjöldi barna var í vélinni, þar af tvö kornabörn'
    | Samanburðarmerki? 'þar_á_meðal:ao'
    | Samanburðarmerki? "þar:ao" 'á_meðal:ao'
    | Samanburðarmerki? 'þar_með_talið:ao' # Skammstöfunin 'þ.m.t.'
    | Samanburðarmerki? "nema:st"   # Allir vita það, nema ég.
    | ','? "enda:st"                # 'Réttindi í almennum sjóðum breyttust, enda lægri fjárhæð en áður'
    | "sem:st"
    | SamanburðarTengingOg

SamanburðarTengingOg →
    ','? "og:st"               # Erfitt fjarskiptasamband er á staðnum og mikil snjóþyngsli

$score(+4) SamanburðarNlTenging
$score(-14) SamanburðarTengingOg

Sjálfur/tala/kyn → 'sjálfur:fn'_nf/tala/kyn

# 'Þrír þingmenn greiddu atkvæði gegn tillögunni: Jón, Sigríður og Guðrún.'
# 'Aðeins einn nemandi var með þetta rétt; strákurinn í rauðu skyrtunni.'
Formerki → ':' | ';' | "-:em" | ','

Upptalning/tala → Formerki Nl_nf/tala

Setning/tala/pers/kyn →
    # Venjuleg setningarskipan með frumlagi (í nefnifalli) fremst:
    # 'Jón sterki fór bersýnilega í bága við reglurnar með þessu'
    # 'Katrín formaður félagsins hefur augljóslega verið alveg hnífskörp í gær'
    # 'Rúmlega helmingur tvítugra karlmanna vill flugvöll í Vatnsmýri'
    # 'Ég bara spyr [sjálf]'
    # 'Allir mega koma til landsins [sjálfir] sem það vilja'
    BeygingarliðurÁnUmröðunar/tala/pers/kyn

    # Frumlag fyrir aftan sögn
    | BeygingarliðurMeðUmröðun/tala/pers/kyn

    # 'Það fara 40 mínútur í pappírsvinnuna'
    | 'það:pfn'_hk_nf_et SagnHluti/tala/pers/kyn Upptalning/tala?

Setning_et_p3_hk →
    # 'Að raða frímerkjum er góð skemmtun'
    SetningAð
    # 'Nú þarf/þyrfti að hugsa út fyrir kassann'
    # 'Ætíð verður að gæta hagsmuna almennings'
    # 'Þar hefur ekki gengið alltof vel á undanförnum árum'
    | SetningÞað
    # Um er/var að ræða þriggja ára stelpu
    # (-> Það er um að ræða þriggja ára stelpu)
    | SetningUmAðRæða
    # 'Það/nú/alltaf er/var/væri verið að reka fólk burt af jörðum sínum'
    # ('verið' er ekki til sem LHÞT, en það er hins vegar 'búið' o.fl.)
    | SetningForskeyti 'vera:so'_0_gm_p3_et 'vera:so'_sagnb NhFyllingAtv
    | 'það:pfn'_hk_nf_et 'vera:so'_0_gm_p3_et 'vera:so'_sagnb NhFyllingAtv

$tag(begin_prep_scope) Setning/tala/pers/kyn # Nýtt undirtré fyrir tengingu sagnar og forsetningar

# Setning þar sem frumlagið 'það' er falið
SetningÞað →
    SetningForskeyti SagnRuna_et_p3_hk NhFyllingAtv?

$score(-8) SetningÞað # Velja fremur hefðbundna setningu með tilgreindu frumlagi ef býðst

SetningAð →
    # 'Að raða frímerkjum [í ró og næði] er góð skemmtun'
    NhLiður FsAtv? SagnRuna_et_p3_hk
    | SetningAðHugsaSér

$score(-4) SetningAð # Óalgengt form

SetningAðHugsaSér →
    nhm 'hugsa:so'_1_þgf_gm_nh abfn_þgf # 'Að hugsa sér [, sagði Anna]'

$score(+2) SetningAðHugsaSér

SetningUmAðRæða →
    # Um væri að ræða
    # Um hafi verið að ræða
    # Um yrði að ræða
    # Um gæti hafa verið að ræða
    # ...o.s.frv.
    'um:fs'_þf HjSögnLhÞt_et_p3 nhm 'ræða:so'_0_gm_nh FsRunaInnskot? NlBeintAndlag_þf AtvFsAuka?
    # Í dag hafi verið um að ræða skipulegt undanhald
    | SetningForskeyti HjSögnLhÞt_et_p3 'um:fs'_þf nhm 'ræða:so'_0_gm_nh NlBeintAndlag_þf AtvFsAuka?

$score(+30) SetningUmAðRæða # Mjög líklega réttur skilningur á þessari orðaröð

SetningForskeyti →
    FsAtv | SamanburðarForskeyti

SamanburðarForskeyti →
    "eins:ao" "og:st" Nl_nf       # 'Eins og Páll borða ég ekki smjör'
    | "líkt:ao" "og:st" Nl_nf     # 'Líkt og bakarar eru smiðir óhressir með þetta'

# Hvetja til forskeyta fremur en samtenginga, ef val er þar á milli
$score(+10) SamanburðarForskeyti

# Sagnir innan forskeytis hafa ekki áhrif á tengsl forsetninga við sagnir í setningunni sjálfri
$tag(purge_verb) SetningForskeyti

# Setning sem byrjar á lýsingarorði í stað "það"
# 'Óheimilt er (að fangelsa mann fyrir engar sakir)'
# 'Óhætt má telja (að flokkurinn vinni kosningarnar)'
# 'Öruggt hefur verið talið [að fara Vatnaleiðina í þurru veðri]'
# 'Ljóst er (að Brandur var fallegasti kötturinn)'
SetningLo →
    LoTengtSögn_nf_et_hk SetningLoSögn AðLiður?
    # Kjörgengur [við Alþingiskosningar/í lok maí] skal vera hver sá ríkisborgari (sem dvalið hefur 5 ár í landinu)
    | LoTengtSögn_nf/tala/kyn FsRunaEftirSögn? HjSögnLhÞtSM/tala/pers Nl_nf/tala/pers/kyn AtvFs?
    # Aðspurður sagði Jón framburð Gunnu vera uppspuna frá rótum
    # Örvingluð eftir atburði næturinnar leitaði Anna til lögreglunnar
    | LoTengtSögn_nf/tala/kyn FsAtv? SagnHluti/tala/pers/kyn
    | SetningLoMeðSpurningu
    # Frábært að heyra [í þér]
    # Yndislegt að sjá og heyra
    | LoTengtSögn_nf_et_hk NhLiðir FsAtv?
    # Gaman að sjá til þín
    | "gaman:ao" NhLiðir FsAtv?

SetningLoMeðSpurningu →
    # Spurð/aðspurð [um / í framhjáhlaupi / eftir atburðinn] hvort Jón væri edrú sagði Gunna að hann væri það
    LoSpurnarUpplegg/tala/kyn LoSpurnarInnskot? Spurnaraukasetning SagnHluti/tala/pers/kyn

LoSpurnarUpplegg/tala/kyn →
    lo_sb_nf/tala/kyn | so_lhþt_sb_nf/tala/kyn # spurð/aðspurð/innt

LoSpurnarInnskot →
    "um:st" | FsAtv

$score(+4) SetningLoMeðSpurningu

SetningLoSögn →
    HjSögnLhÞtSM_et_p3 HreinSögn_lhþt_et_hk? # er/skal/mun/hefur verið talið/þótt...
    | HjSögnMegiSkuli_et_p3? so_0_mm_p3_et # sýnist/virðist/telst, má teljast
    | HjSögnMegiSkuli_et_p3 so_0_nh # má telja, skal ætla
    | 'þykja:so'_0_gm_et_p3 # '(öruggt) þykir (að Jón vinni mótið)'
    | 'vera:so'_0_gm_et_p3 'vera:so'_sagnb # Hægt og bítandi er/var/væri verið (að ýta fólki burt)

# Ekkert frumlag í nefnifalli, sögn sem sagnbót (oft í miðmynd):
# 'Í dag er búist við vondu veðri eftir hádegi'
# (í stað 'Það er búist við vondu veðri eftir hádegi í dag')
# 'Á morgun verður lent á neyðarbrautinni'
# 'Fylgst verður með kosningunum á RÚV næstu daga'
# (í stað 'Það verður fylgst með kosningunum á RÚV næstu daga')
# 'Í dag má sækja tölvuna'
# (í stað 'Það má sækja tölvuna í dag')

SetningSo →

    So_1_p3_nf/tala NlSagnfylling_nf NlFrumlag_nf/tala AtvFsAuka?                
    | So_1_p3/aukafall/tala NlBeintAndlag/aukafall NlFrumlag_nf/tala AtvFsAuka?

    # Leitað verður/skal/mun til sérfræðinga
    # Verið er í snatri að ráða hæfasta umsækjandann
    # Komið hefur/hafi til átaka milli þeirra
    # Farið hefur verið ofan í saumana á málinu
    # Búist er við að Sigmundur Davíð segi af sér
    | Sagnliður_sagnb

    | So_1_sagnb/fall HjSögnLhÞtSM_et_p3 FsAtv? Nl/fall AtvFsAuka?  # Horft hefur verið [til hennar] lostafullum augum

    | so_0_mm_nh HjSögnMegiSkuliÞurfi_et_p3 SagnInnskot             # Búast má/skal/þarf/hefur mátt við töfum á veginum

    | so_0_mm/pers/tala FsAtv? NlFrumlag_nf/tala/pers AtvFsAuka?    # Fórust með honum átján menn

    | FsAtv? SagnliðurÁnF                                           # [Með lögum] skal tryggja rétt almennings til að fara um landið

    | VeraSkulu_et_p3 FsRuna LoTengtSögn_nf/tala/kyn NlFrumlag_nf/tala/kyn AtvFsAuka? # Skal í frumvarpinu fólgin greinargerð um tekjur ríkisins og gjöld

    # !!! Ath.: So_1_nh leyfir miðmynd nafnháttar, sem ætti ekki að vera leyfilegur hér
    # !!! sbr. '?Endurnýjast á leigusamninginn í ár'
    | So_1_nh/fall HjSögnMegiSkuliÞurfi_et_p3 FsRuna?
        NlBeintAndlag/fall AtvFsAuka?  # Endurnýja þarf/skal/má [í snatri] leigusamninginn í ár
    | So_1_nh/fall HjSögnNhMeðSo/tala/pers NlFrumlag_nf/tala/pers/kyn
        NlBeintAndlag/fall AtvFsAuka?  # Gæta eiga/skulu/verða þeir þingskapa

    | Sagnliður_lhþt

    | Sögn_bh SögnFrh_bh?                                           # Farðu galvaskur að sækja kaffi strax

    | HjSögnMegiSkuli_et_p3 FsAtv? EinSögn_nh_et_hk SagnInnskot?    # Má með sanni segja (að þeir hafi sloppið með skrekkinn)

    | 'þurfa:so'_1_þf_gm_et_p3 FsRuna? NlBeintAndlag_þf AðLiður?    # Þarf til þess lagafyrirmæli að svipta mann réttindum

    | SagnliðurVh ","? "þá:st"? BeygingarliðurMeðUmröðun FsAtv?     # Haldi þessi þróun áfram er voðinn vís

    # (Því) hafi þurft að senda alla farþegana (á hótel)
    # (Í dag) gæti hafa rignt
    | FsAtv? HjSögnVh so_sagnb NhFylling?

    # Þykir/finnst þeim vera slagsíða [í umfjölluninni] / þeim [það] ósanngjarnt
    | HjSögnFinnst NlFrumlag_þgf SögnFinnstBotn

    | "svo:ao"? VirðistSem                                  # [Svo] virðist sem Björn hafi fengið aðsvif

VirðistSem →
    'virða:so'_op_mm_fh_p3_et "sem:st" HreinYfirsetning     # virtist/virðist sem Björn hafi fengið aðsvif á sviðinu

$score(+10) VirðistSem # Sennilega rétt greint

SögnFinnstBotn →
     'vera:so'_1_nf_nh NlSagnfylling_nf                     # Finnst þeim vera slagsíða í umfjölluninni
     | 'það:pfn'_hk_nf_et? LoSamanb_et_nf_hk AðLiður?

Sagnliður_lhþt →
    EinSögn_lhþt/tala/kyn OgSögn_lhþt/tala/kyn* HjSögnLhÞt_p3/tala
        NlFrumlag_nf/tala/kyn AtvFsAuka? # Teknar og skráðar hafa verið ákvarðanir í málinu

$score(-4) Sagnliður_lhþt # Knúsað orðalag - velja annað ef í boði

# Talið er (að Jón sé horfinn) / Verið er (að ráða hæfasta umsækjandann)
# Sagt er algerlega óhætt [að fara út]
# Sótt sé/verði/yrði að honum
Sagnliður_sagnb →
    so_sagnb HjSögnSagnb_et_p3 LoTengtSögn_nf_et_hk? SagnliðurBotn_sagnb

SagnliðurBotn_sagnb →
    NhFylling
    | SagnInnskot
    | SagnliðurBotnSetning

SagnliðurBotnSetning →
    "að:st" HreinYfirsetning

$tag(begin_prep_scope) Sagnliður_sagnb
$score(+8) Sagnliður_sagnb
$score(+8) SagnliðurBotnSetning

# Skal í frumvarpinu fólgin greinargerð
# Er í frumvarpinu fólgin greinargerð
# Verður í frumvarpinu fólgin greinargerð
# Mun í frumvarpinu fólgin greinargerð
VeraSkulu_et_p3 →
    'vera:so'_0_gm_et_p3 | 'verða:so'_0_gm_et_p3 | 'skulu:so'_0_gm_et_p3 | 'munu:so'_0_gm_et_p3

# Setning þar sem aðalnafnliður er í aukafalli
# en frumlag í 3p. et. ('það') er falið.
# Sögnin tekur viðfang (subject) í aukafalli eða er í miðmynd
# 'Mér blöskrar athæfið' (? Það blöskrar mér athæfið),
# 'Mér sýnist að öllu sé óhætt' (? Það sýnist mér að öllu sé óhætt),
# 'Helmingi ungmenna finnst skólinn skemmtilegur' (? Það finnst helmingi ungmenna skólinn skemmtilegur)

SetningAukafall →

    # Manninn langaði mjög í ferðina / að fara til útlanda strax
    # Köttum í landinu fjölgar
    NlFrumlag/aukafall so_subj_op/aukafall MiðmyndarBotn?
    # Mér blöskraði [af öllu hjarta] [mjög] vitleysan (í Páli), Mig þraut örendið
    | NlFrumlag/aukafall Atviksliður? so_subj_op/tala/aukafall
        FsRunaEftirSögn? NlBeintAndlag_nf_þf/tala AtvFsAuka?

    # Öfug orðaröð: 'Eignarrétti fylgja [í auknum mæli] skyldur' (í stað 'Skyldur fylgja eignarrétti')
    # 'Þetta skrifar Bjarni á Facebook-síðu sinni og bendir [galvaskur] á frammistöðu stjórnarinnar'
    | NlBeintAndlag/aukafall So_1/aukafall/tala/pers FsRunaEftirSögn? NlFrumlag_nf/tala/pers/kyn
        SagnInnskot? SagnFramhald/tala/pers/kyn*

    # Þessar skuldir hefur 365 ekki greitt
    # Þessa bók hef ég aldrei skilið
    | NlBeintAndlag/aukafall HjSögn/tala/pers NlFrumlag_nf/tala/pers/kyn
        AtvFs? So_1_sagnb/aukafall FsAtv?

    # Köttum hefur [ekki] fjölgað á árinu
    | NlFrumlag/aukafall HjSögn_et_p3 FsAtv? So_1_sagnb/aukafall FsAtv?

    # Mér hafa ofboðið lætin í hundinum [alla daga]
    | NlFrumlag/aukafall HjSögn/tala/pers FsAtv? so_subj_sagnb/aukafall
        NlBeintAndlag_nf/tala/pers AtvFsAuka?

    # Hælisleitendum fer/hefur farið fækkandi
    | NlFrumlag_þgf HjSögnLhNt so_1_þgf_lh_nt FsAtv?

    # Miðmynd sagna: Mér áskotnaðist, mér fyrirgefst, mér gremst, okkur finnst [öllum]...
    | NlFrumlag_þgf/tala/kyn so_0_mm LoÞgf/tala/kyn? MiðmyndarBotn?
    # Viðreisn hefur [ekki] borist/hlotnast/áskotnast [að þessu sinni] öflugur liðsauki [í dag]
    | NlFrumlag_þgf HjSögnMM FsAtv? so_mm_sagnb FsRunaEftirSögn? NlBeintAndlag_nf/tala/pers AtvFsAuka?
    # Bönkunum hefur/hafði/hefði [flestum] tekist að halda í horfinu
    | NlFrumlag_þgf/tala/kyn HjSögnMM LoÞgf/tala/kyn? so_mm_sagnb MiðmyndarBotn?

    # Öllum hefur verið/skal/mun tryggð heilbrigðisþjónusta / réttur til að lifa með reisn
    | NlFrumlag_þgf HjSögnLhÞtSM_p3/tala FsAtv? so_2_þgf_þf_lhþt/tala/kyn
        FsRunaEftirSögn? AukafallLhÞtAndlag/tala/pers/kyn AtvFsAuka?
    # Páli eru þröngar skorður [kyrfilega] settar [í þessu efni]
    | NlFrumlag_þgf HjSögnLhÞtSM_p3/tala FsAtv? AukafallLhÞtAndlag/tala/pers/kyn
        AtvFs? so_2_þgf_þf_lhþt/tala/kyn FsAtv?

    # Hjónunum var [í fyrra] [báðum] (falið að stýra rannsóknaverkefni)
    | NlFrumlag_þgf/tala/kyn HjSögnLhÞtSM_et_p3 FsAtv? LoÞgf/tala/kyn? AukafallÞgfAndlag

    # Þess skal [ekki] gætt (að fara varlega með sprengiefnið)
    # Nánari upplýsinga er [ekki] að vænta
    | NlFrumlag_ef HjSögnLhÞtSM_et_p3 FsAtv? AukafallEfAndlag

    # Köttum í landinu mun [ekki/enn] fjölga á árinu
    | NlFrumlag/aukafall HjSögnNh_et_p3 FsAtv? So_1_nh/aukafall FsAtv?

    # Þá ályktun má strax draga í dag (að Páll sé ekki með öllum mjalla)
    # Engan má skylda til að láta eign sína af hendi
    # Miklu má veðja að Guðný snúi aftur í pólitík
    | NlFrumlag/aukafall HjSögnMegiSkuli_et_p3 FsAtv? So_1_nh/aukafall AðLiður?

    # Nauðungarvinnu skal engum gert að leysa af hendi
    | NlFrumlag/aukafall HjSögnMegiSkuli_et_p3 FsAtv? AukafallÖfugSögn nhm So_1_nh/aukafall FsAtv?

    # Engan þarf/ætti að undra (að mig langi í ís)
    # Greinarmun verður [ekki] að gera (á Jóni og séra Jóni)
    | NlFrumlag/aukafall HjSögnÞurfaEiga_et_p3 FsAtv? nhm So_1_nh/aukafall AðLiður?

    # Engan má svipta íslenskum ríkisborgararétti að ósekju
    | NlÓbeintAndlag_þf HjSögnMegiSkuli_et_p3 FsAtv? So_2_nh_þf/fall NlBeintAndlag/fall AtvFsAuka?

    # Öllum má tryggja heilbrigðisþjónustu í dag
    | NlÓbeintAndlag_þgf HjSögnMegiSkuli_et_p3 FsAtv? So_2_nh_þgf/fall NlBeintAndlag/fall AtvFsAuka?

    # Öllum skal [ekki] standa [til boða] menntun (Menntun skal standa öllum til boða)
    | NlÓbeintAndlag/aukafall HjSögnMegiSkuli_et_p3 FsAtv? So_1_nh/aukafall FsRunaEftirSögn? NlFrumlag_nf AtvFsAuka?

    # Mér/okkur/ykkur/börnunum finnst/þykir [mörgum [það]] [ekki] skemmtilegt
    # [að borða góðan mat / hérna í kvöld / að Jón lesi bók]
    | NlFrumlag_þgf/tala/kyn HjSögnFinnst FinnstÞað/tala/kyn? FinnstAndlag AðLiður?

    # Mér hefur [ekki] þótt [það] vont að sofa á nýju dýnunni
    | NlFrumlag_þgf HjSögn_et_p3 FsAtv? HjSögnFinnst_sagnb 'það:pfn'_hk_nf_et? FinnstAndlag AðLiður?

    # Þann auð eigum við [ekki] að nýta (í þágu lands og þjóðar)
    | NlBeintAndlag/aukafall so_0/tala/pers NlFrumlag_nf/tala/pers AtvFs? nhm So_1_nh/aukafall FsAtv?

    # Þann auð munum/skulum við [ekki] nýta (í þágu lands og þjóðar)
    | NlBeintAndlag/aukafall HjSögnNh/tala/pers NlFrumlag_nf/tala/pers AtvFs? So_1_nh/aukafall FsAtv?

    # Þann auð ber okkur að nýta (til góðra verka)
    | NlBeintAndlag/aukafall so_subj_op/fall NlFrumlag/fall AtvFs? nhm So_1_nh/aukafall FsAtv?

    # Nöfn ráðherra var [ekki] að finna [í Panamaskjölunum]
    | NlBeintAndlag_þf 'vera:so'_0_gm_fh_p3_et FsAtv? nhm 'finna:so'_0_gm_nh FsAtv?

# Aukafallssetningar sem eru mjög líklega rétt greindar og fá þar
# af leiðandi plús-skor í stað mínuss
SetningAukafallForgangur →
    # Öllum er [í meginatriðum] frjálst/fyrir bestu að ákveða snarlega að flytja úr landi
    NlFrumlag_þgf HjSögnLhÞt_et_p3 FsRunaEftirSögn? LhÞtÞgfEinkunn AðLiður?

FinnstÞað/tala/kyn →
    'það:pfn'_hk_nf_et LoÞgf/tala/kyn       # Bílstjórum finnst það mörgum leiðinlegt
    | LoÞgf/tala/kyn 'það:pfn'_hk_nf_et?    # Bílstjórum finnst mörgum [það] leiðinlegt

FinnstAndlag →
    LoTengtSögn_nf_et_hk    # skemmtilegt
    | ao* so_lh_nt          # mjög endurnærandi, afskaplega frískandi

AukafallÞgfAndlag →
    # (Stóru skipunum hefur verið/skal/mun) stýrt (í land)
    # (Hannesi var í fyrra) falið að stýra rannsóknaverkefni
    # (Mér var strax) sagt að Friðgeir væri farinn að drekka aftur
    # (Manninum var) vikið [frá störfum [þegar málið kom upp]]
    so_1_þgf_sagnb AðLiður?

    # (Þátttakendum er) gert kleift að matast með reisn
    | 'gera:so'_sagnb LoTengtSögn_nf_et_hk NhFyllingAtv

    # (Öllum hefur verið/skal/mun) óskað [í snarheitum] góðs bata
    | so_2_þgf_ef_sagnb FsRunaInnskot? NlBeintAndlag_ef AtvFsAuka?

    # (Öllum hefur verið/skal/mun) skilað [í flýti] bílunum
    | so_2_þgf_þgf_sagnb FsRunaInnskot? NlBeintAndlag_þgf AtvFsAuka?

AukafallEfAndlag →
    # Þess skal [ekki] gætt að fara varlega með sprengiefnið
    so_1_ef_sagnb AðLiður?

    # Ákvörðunar var/er/verður/hefur verið/mun [ekki] að vænta [strax í dag]
    | nhm so_1_ef_nh FsAtv?

AukafallÖfugSögn →
    NlBeintAndlag/fall AtvFs? So_1_nh/fall         # (skal) engum gera
    | NlBeintAndlag/fall AtvFs? So_1_sagnb/fall    # (skal) engum gert

# Tilvik þar sem sögn í lhþt eintölu fylgir andlag í fleirtölu:
# 'Öllum skal tryggð mannhelgi og vernd gegn hvers kyns ofbeldi'

AukafallLhÞtAndlag/tala/pers/kyn →
    NlBeintAndlag_nf/tala/pers/kyn

AukafallLhÞtAndlag_et/pers/kyn →
    NlBeintAndlag_nf_ft/pers/kyn

AukafallsBotn →
    FsAtv                             # (Mér virtist ykkur ekki hungra) mjög undir ræðunni
    | FsRuna? NlFrumlag_nf AtvFsAuka? # (Mér virtist ykkur ekki falla) [í geð] ræðan í kvöld
    | NhFyllingAtv                    # (Mér sýndist ykkur langa) gríðarlega að fara til útlanda strax

MiðmyndarBotn →

    # (Mér sýndist) kötturinn [ekki] [vera að] lepja mjólkina og borða matinn í gær
    Nl_nf AtvFs? VeraInnskot? NhRuna FsAtv?

    # (Mér finnst) kettirnir [ekki] [vera] fallegir
    | MiðmyndarBotnLo

    # (Mér virtist) ykkur [ekki] blöskra undir ræðunni
    | NlFrumlag/aukafall AtvFs? so_subj_nh/aukafall AukafallsBotn?

    # (Mér finnst) [ekki / í meginatriðum] vanta blíðu [í söng Ara]
    # !!! ATH: Hvaða sagnir geta staðið hér? Aðeins 'vanta' og 'skorta'?
    | FsAtv? So_1_nh/aukafall NlBeintAndlag/aukafall FsAtv?

    # (Mér virtist) þeim [ekki] sýnast börnin vera falleg
    | Nl_þgf AtvFs? so_nh_mm MiðmyndarBotn

    # (Mér leyfist) vonandi að fara út
    | NhFyllingAtv

    # (Páli fyrirgefst) örugglega á morgun
    | FsAtv

MiðmyndarBotnLo →
    Nl_nf/tala/pers/kyn AtvFs? HjSögnVera? LoTengtSögn_nf/tala/kyn FsAtv?

$score(+4) MiðmyndarBotnLo # Gefa plús fyrir heildstætt samhengi lýsingarorðs

VeraInnskot →
    HjSögnVera nhm    # vera að / geta hafa verið að / verðir að / hafa orðið að...

Nl_nf_þf/tala → Nl_nf/tala | Nl_þf/tala

# Setningar án frumlags (notaðar t.d. í tengisetningum eftir 'sem').
# Í setningum án frumlags getur sagnliðurinn verið "öfugur",
# þ.e. "konurnar sem borðað höfðu matinn" í stað "höfðu borðað".
# Þá má atviksliður ekki koma inn á milli sagnanna, þ.e.
# "höfðu ekki borðað" er gilt, en "borðað ekki höfðu" ógilt.

SetningÁnF/tala/pers/kyn →
    EinSetningÁnF/tala/pers/kyn FramhaldÁnF/tala/pers/kyn*

EinSetningÁnF/tala/pers/kyn →
    Atviksliður? ÖfugurSagnliður/tala/pers/kyn
        LoTengtSögn_nf/tala/kyn? SagnInnskotAtv? ÖfugtFramhald/tala/pers/kyn*
    # Lýsingarorð á undan sögn:
    # (Ráðherrar taka þær ákvarðanir sem) nauðsynlegar eru/þykja/teljast til rækslu starfa þeirra
    | LoTengtSögn_nf/tala/kyn Sögn_0/tala/pers/kyn SagnInnskotAtv?

$tag(begin_prep_scope) EinSetningÁnF/tala/pers/kyn # Nýtt undirtré fyrir tengingu sagnar og forsetningar

ÖfugtFramhald/tala/pers/kyn →
    KommaOgEðaEn ÖfugurSagnliður/tala/pers/kyn LoTengtSögn_nf/tala/kyn? SagnInnskotAtv?

FramhaldÁnF/tala/pers/kyn →
    FramhaldÁnFMeðKommu/tala/pers/kyn
    | FramhaldÁnFÁnKommu/tala/pers/kyn

FramhaldÁnFMeðKommu/tala/pers/kyn →
    ',' FramhaldÁnFÁnKommu/tala/pers/kyn LokatáknEðaKomma

$score(+8) FramhaldÁnFMeðKommu/tala/pers/kyn    # Framhald innan kommu er mjög líklega rétt

FramhaldÁnFÁnKommu/tala/pers/kyn →
    Samtenging HreinYfirsetning                 # (sem borðað höfðu matinn) þegar hann var framreiddur fyrr um kvöldið
    | Samtenging EinSetningÁnF/tala/pers/kyn    # (sem borðað höfðu matinn) og þakkað auðmjúkar fyrir sig

# Sagnliðir

Sagnliður/tala/pers/kyn →

    Sögn_0/tala/pers/kyn       # Sögn með engu viðfangi: (veðrið) skánaði
    | Sögn_1/tala/pers/kyn     # Sögn með einu viðfangi: (Jón) sá köttinn
    | Sögn_2/tala/pers/kyn     # Sögn með tveimur viðföngum: (Kata) skrifaði Önnu bréf

    | Sögn_sagnb/tala/pers/kyn # Sagnbót: (Jón) hefur borðað matinn og farið að bóna bílinn
    | Sögn_nh/tala/pers/kyn    # Sagnir í nafnhætti: (Jón) mun/hyggst bóna bílinn og fara að kaupa inn
    | Sögn_lhþt/tala/pers/kyn  # Sagnir í lh. þt.: (Kata) er vöknuð og farin að sækja Pál
    | Sögn_lhnt/tala/pers      # Sagnir í lh. nt.: (Guðrún) var hringjandi og sannfærandi í allan dag

# Sagnliður með venjulegri eða 'öfugri' orðaröð: (Konan sem) matinn borðaði

ÖfugurSagnliður/tala/pers/kyn →

    Sagnliður/tala/pers/kyn

    | ÖfugSögn_0/tala/pers/kyn
    | ÖfugSögn_1/tala/pers/kyn
    | ÖfugSögn_2/tala/pers/kyn

    # !!! Bæta við ÖfugSögn_sagnb, _nh, _lhþt hér

# Sagnliður án sýnilegs frumlags: Í dag þarf að vökva grasið (=Það þarf að vökva grasið í dag)
# Sagnliður án frumlags þarf að geta staðið sem afleiðing í ef-þegar setningu:
# '(Ef hann hringir) má sækja tölvuna og byrja að vinna'

SagnliðurÁnF →
    # so_subj_gm_none NhFyllingAtv?                         # (Í dag) þarf [sannarlega] (að vökva grasið)
    so_0_mm_p3_et NhFyllingAtv?                             # (Í dag) dregst [sennilega] (að vökva grasið)

    # | HjSögn_et_p3 FsAtv? so_subj_sagnb_none NhFyllingAtv? # (Í ár) hefur [í sumarhitanum] þurft (að vökva grasið)
    | HjSögn_et_p3 FsAtv? so_mm_sagnb NhFyllingAtv?         # (Í ár) hefur [vegna leti] dregist (að vökva grasið)
    # | HjSögnNh_et_p3 FsAtv? so_subj_nh_none NhFyllingAtv? # (Í ár) mun [í sumarhitanum] þurfa (að vökva grasið)
    | HjSögnNh_et_p3 FsAtv? so_mm_nh NhFyllingAtv?          # (Í ár) mun [í sumarhitanum] dragast (að vökva grasið)

    # | HjSögnLhÞtSM_et_p3 FsAtv? so_sagnb NhFyllingEðaAtv? # (í dag) verður/skal/mun [líklega] lent á brautinni (eftir hádegi)

    | HjSögnMegiSkuli_et_p3 HreinSögn_nh_et_hk SagnInnskot? # [Í dag] má sækja tölvuna (og byrja að vinna)
    | so_0_nh HjSögnNhMeðSo_et_p3 HreinSögnAtv_nh?          # [hvort] leggja skuli/eigi/þarf/verður [á skatt, breyta honum eða afnema hann]

    | HjSögnNh_p3/tala FsAtv? so_mm_nh FsRunaEftirSögn? NlFrumlag_nf/tala # [Í kröfunum] skulu felast [í snatri] tillögur (um nýtt fyrirkomulag)

    | 'þurfa:so'_1_þf_gm_fh_et_p3 FsRunaEftirSögn? NlBeintAndlag_þf # Þarf [til þess] lagafyrirmæli

    | StökSögn_et_p3_gm                                     # (Í dag) segir
    | so_0_et_p3_gm SagnInnskot                             # (Í dag) fýkur í fólk / kreppir að / losnar um stífluna / segir

$tag(begin_prep_scope) SagnliðurÁnF # Nýtt undirtré fyrir tengingu sagnar og forsetningar

StökSögn_et_p3_gm →
    so_0_et_p3_gm

# Óalgengt að sagnorð standi ein og sér án frumlags
$score(-8) StökSögn_et_p3_gm

HreinSögnAtv_nh →
    FsAtv? HreinSögn_nh_et_hk

# Sagnliður sem hefst á viðtengingarhætti:
# 'Haldi þessi þróun áfram er voðinn vís / þarf að grípa til aðgerða'

SagnliðurVh →

    # Haldir þú [áfram að borða] (er voðinn vís)
    # Aki hann ölvaður (skal sekta hann um tíu þúsund krónur)
    so_0_vh/tala/pers NlFrumlag_nf/tala/pers/kyn LoTengtSögn_nf/tala/kyn? NhFyllingEðaAtv?

    # Borði börnin [í snarheitum] matinn (getum við farið í sund)
    | So_1_vh_nf/tala/pers NlFrumlag_nf/tala/pers FsRunaInnskot? NlSagnfylling_nf AtvFsAuka?
    | So_1_vh/aukafall/tala/pers NlFrumlag_nf/tala/pers FsRunaInnskot? NlBeintAndlag/aukafall AtvFsAuka?

    # Verði þessi tillaga felld (geng ég úr flokknum)
    # Verði karlarnir byrjaðir að grilla (sæki ég sósuna)
    | VeraVerða_vh/tala/pers NlFrumlag_nf/tala/pers/kyn HreinSögn_lhþt/tala/kyn FsAtv?

    # Verði þeim [ekki] vísað [úr landi] geta afleiðingarnar orðið hörmulegar
    # Sé börnunum sagt [að fara í háttinn] grenja þau stanslaust
    | VeraVerða_vh_et_p3 NlFrumlag_þgf AtvFs? so_1_þgf_sagnb NhFyllingEðaAtv?

    # Hafi maður verið sviptur frelsi að ósekju (skal hann eiga rétt til skaðabóta)
    | 'hafa:so'_0_vh/tala/pers NlFrumlag_nf/tala/pers/kyn AtvFs?
        'vera:so'_sagnb HreinSögn_lhþt/tala/kyn FsAtv?

    # Hafi manni verið hallmælt í útvarpi (skal hann fá kaffi og kleinu)
    | 'hafa:so'_0_vh_et_p3 NlFrumlag_þgf AtvFs?
        'vera:so'_sagnb so_1_þgf_sagnb FsAtv?

    # Hafi manni verið tekið blóð að óþörfu (skal hann liggja á bekk í stundarfjórðung)
    | 'hafa:so'_0_vh_et_p3 NlFrumlag_þgf AtvFs?
        'vera:so'_sagnb so_2_þgf_þf_lhþt/tala/kyn Nl_nf/tala/kyn AtvFs?

    # Hafi manni verið gert kleift að matast með reisn (skal hann liggja á bekk í stundarfjórðung)
    | 'hafa:so'_0_vh_et_p3 NlFrumlag_þgf AtvFs? 'vera:so'_sagnb
        'gera:so'_lhþt/tala/kyn LoTengtSögn_nf_et_hk NhFyllingAtv

    # Hafi manns verið leitað að óþörfu (skal hann fá axlarnudd)
    | 'hafa:so'_0_vh_et_p3 NlFrumlag_ef AtvFs? 'vera:so'_sagnb so_2_þgf_ef_sagnb FsAtv?

    # Fari [það] svo/þannig að ég nái ekki kjöri til forseta [,] [þá] getið þið sótt fyrirlestur um þetta hjá mér
    | 'fara:so'_0_vh_et_p3 'það:pfn'_hk_nf_et? FsAtv "að:st" HreinYfirsetning

$score(-4) SagnliðurVh

VeraVerða_vh/tala/pers →
    'vera:so'_0_vh/tala/pers | 'verða:so'_0_vh/tala/pers

VeraVerða_gm_p3/tala →
    'vera:so'_1_nf_gm_p3/tala | 'verða:so'_1_nf_gm_p3/tala

OgVeraVerða_gm_p3/tala →
    FsAtv? OgEðaEn FsAtv? VeraVerða_gm_p3/tala
    | FsAtv? ',' OgEðaEn FsAtv? VeraVerða_gm_p3/tala FsAtvKomma?

# Sagnliður með inniföldu frumlagi:
# '(Í dag) borðaði Jón matinn'
# '(Með því) var svæðinu stefnt í hættu'
# '(Í kjölfar þess) varð ég ósáttur'
# '(Nú) mega allir fara sem vilja'
# '(Í gær) skrifuðu þær konur þingmönnum bréf sem voru heitastar í málinu'

SagnliðurMeðF/tala/pers/kyn →
    SögnMeðF/tala/pers/kyn Tilvísunarsetning/tala/pers/kyn?

SögnMeðF/tala/pers/kyn →
    SögnMeðF_0/tala/pers/kyn   # Sögn með engu viðfangi: (Í dag) skánaði veðrið
    | SögnMeðF_1/tala/pers/kyn # Sögn með einu viðfangi: (Í dag) sá Jón köttinn
    | SögnMeðF_2/tala/pers/kyn # Sögn með tveimur viðföngum: (Í dag) skrifaði Kata Önnu bréf

$score(+1) SögnMeðF_0/tala/pers/kyn
$score(+3) SögnMeðF_1/tala/pers/kyn
$score(+5) SögnMeðF_2/tala/pers/kyn

OgSögn_0/tala/pers →
    FsAtv? OgEðaEn FsAtv? so_0/tala/pers
    | FsAtv? ',' OgEðaEn FsAtv? so_0/tala/pers FsAtvKomma?

OgSögn_mm/tala/pers →
    FsAtv? OgEðaEn FsAtv? so_mm/tala/pers
    | FsAtv? ',' OgEðaEn FsAtv? so_mm/tala/pers FsAtvKomma?

OgSögn_mm_nh →
    FsAtv? OgEðaEn FsAtv? so_mm_nh
    | FsAtv? ',' OgEðaEn FsAtv? so_mm_nh FsAtvKomma?

OgSögn_mm_sagnb →
    FsAtv? OgEðaEn FsAtv? so_mm_sagnb
    | FsAtv? ',' OgEðaEn FsAtv? so_mm_sagnb FsAtvKomma?

OgSögn_sagnb/tala/kyn →
    # (hefur staðið) keikur í brúnni og mótmælt yfirgangi Rússa
    LoTengtSögn_nf/tala/kyn? FsAtv? OgEðaEn FsAtv? EinSögn_sagnb/tala/kyn
    | LoTengtSögn_nf/tala/kyn? FsAtv? ',' OgEðaEn FsAtv? EinSögn_sagnb/tala/kyn FsAtvKomma?

$score(+6) OgSögn_sagnb/tala/kyn # Enn sterkari plús fyrir runu sagnb

OgSögn_nh/tala/kyn →
    SagnInnskot? OgEðaEnÞrátt FsAtv? EinSögn_nh/tala/kyn
    | SagnInnskot? ',' OgEðaEnÞrátt FsAtv? EinSögn_nh/tala/kyn FsAtvKomma?

# Þá segist hún styðja trúfrelsi, þrátt fyrir að vera andsnúin moskubyggingum
OgEðaEnÞrátt →
    OgEðaEn | 'þrátt_fyrir:ao' "að:st"

OgSögn_lhþt/tala/kyn →
    # Leyfa að 'og' sé sleppt, sbr. "Tveir menn voru stöðvaðir í morgun [sterklega] grunaðir um ölvun við akstur"
    FsAtv? OgEðaEnAtv? EinSögnFrh_lhþt/tala/kyn
    | FsAtv? ',' OgEðaEn? FsAtv? EinSögnFrh_lhþt/tala/kyn FsAtvKomma?

OgSögn_lhnt →
    FsAtv? OgEðaEn FsAtv? EinSögn_lhnt
    | FsAtv? ',' OgEðaEn FsAtv? EinSögn_lhnt FsAtvKomma?

OgSögn_1_þf_nh/tala/pers →
    FsAtv? OgEðaEn FsAtv? So_1_þf_nh/tala/pers
    | FsAtv? ',' OgEðaEn FsAtv? So_1_þf_nh/tala/pers FsAtvKomma?

# Ekki enda á FsAtv nema innan kommu
FsAtvKomma → FsAtv? ','

# Lýsingarháttur þátíðar (Við erum byrjaðir)

LhÞtBotn/tala/kyn →
    # (Mennirnir eru byrjaðir) galvaskir að grilla
    # (Mennirnir eru sagðir) fara herskáir að brenna þorp
    so_0_nh? NhFylling/tala/kyn
    | NhRuna                                # (Mennirnir eru sagðir) fara sjaldan í bað, og raka sig aldrei
    | LoTengtSögn_nf/tala/kyn               # (Mennirnir eru taldir) ritfærir
    | eo* Aldur 'gamall:lo'_sb_nf/tala/kyn?     # (Mennirnir eru orðnir) [tæplega] fimmtán ára [gamlir]

EinSögn_lhþt/tala/kyn →
    so_0_lhþt/tala/kyn LhÞtBotn/tala/kyn?           # (Mennirnir eru) lentir
    > so_2_þf_þgf_lhþt/tala/kyn NlBeintAndlag_þgf   # (Maðurinn var) sviptur frelsi
    > so_2_þgf_þf_lhþt/tala/kyn NlÓbeintAndlag_þgf  # (Ákvörðunin var) tilkynnt Páli
    > so_subj_lhþt/tala/kyn Nl_nf/tala              # (Katrín var) valin besti þjálfarinn
    > so_subj_lhþt_ft/kyn Nl_nf_et                  # (Þau voru) valin efnilegasta sundfólkið

EinSögnFrh_lhþt/tala/kyn →
    EinSögn_lhþt/tala/kyn                   # (Mennirnir) eru lentir og byrjaðir að grilla
    > NlFrumlag_nf/tala/kyn so_0_lhþt/tala/kyn     # veð tekið / veðin tekin / kötturinn kembdur
    > EinSögnFrh_lhþt_sagnb

# Framhald lýsingarháttar þátíðar sem fer yfir í sagnbót
# ('leyfisveiting yrði einfölduð og fólki leyft að leigja út eignir, og heimagisting flokkuð á annan hátt')
# Í dæminu er lhþt og sagnb, og lhþt
EinSögnFrh_lhþt_sagnb →
    NlFrumlag_þf/tala/kyn so_1_þf_sagnb NhFylling/tala/kyn?     # fólki leyft (að fara sársvöngu um borð)
    | NlFrumlag_þgf/tala/kyn so_1_þgf_sagnb NhFylling/tala/kyn? # miðunum breytt / fólkinu sagt (að fara um borð)
    | NlFrumlag_ef/tala/kyn so_1_ef_sagnb NhFylling/tala/kyn?   # matarins neytt / þess beðið (að fara um borð)

HreinSögn_lhþt/tala/kyn →
    FsAtv? EinSögn_lhþt/tala/kyn OgSögn_lhþt/tala/kyn*

# Hvetja til 'verði bókfærðar'
$score(+12) HreinSögn_lhþt/tala/kyn

Sögn_lhþt/tala/pers/kyn →
    # er farið, hefur verið farin, hafa verið farnar, skal farin
    HjSögnLhÞtSM/tala/pers HreinSögn_lhþt/tala/kyn
    # er leiðinlegur, hefur verið frábær, munu vera einstök
    | SögnErLo/tala/pers/kyn
    # talin er/hefur verið [liggja undir grun]
    | so_0_lhþt/tala/kyn HjSögnLhÞtSM/tala/pers LhÞtBotn/tala/kyn?

$score(+8) Sögn_lhþt/tala/pers/kyn

SögnErLo/tala/pers/kyn →
    # (Jón er orðinn) duglegur / tólf ára
    HjSögnLhÞt/tala/pers SögnErLoBotn/tala/kyn

SögnErLoBotn/tala/kyn →
    LoTengtSögn_nf/tala/kyn AðLiður?            # (Guðrún er) snögg [að svara fyrir sig] / (Það er) torsótt [að Siggi fari einn í skólann]
    | eo* Aldur 'gamall:lo'_sb_nf/tala/kyn?     # (Páll var) [rúmlega] tíu ára [gamall]
    | LoMeðÞf_nf/tala/kyn Nl_þf                 # (Jón var) viðstaddur blaðamannafundinn

# Lýsingarorð sem taka með sér þolfall

LoMeðÞf_nf/tala/kyn →
    'viðstaddur:lo'_sb_nf/tala/kyn      # viðstaddur fundinn
    | 'viðriðinn:lo'_sb_nf/tala/kyn     # viðriðinn málið
    | 'viðloðandi:lo'_sb_nf/tala/kyn    # viðloðandi kommúnistaflokka

# Hvetja til 'er/var/hefur verið...' + lýsingarorð í nefnifalli og sterkri beygingu
$score(+10) SögnErLo/tala/pers/kyn

LhÞtÞgfBotn → 'fyrir:fs'_þgf 'góður:lo'_esb_hk_þgf_et  # Það sem er börnum fyrir bestu

LhÞtÞgfEinkunn → LhÞtÞgfBotn | LoTengtSögn_nf_et_hk

Sögn_lhþt_et_p3_hk →
    # Sértilfelli fyrir 'Það er okkur mikilvægt/fyrir bestu [í þessu máli] að fara/gera...'
    HjSögnLhÞt/tala/pers NlFrumlag_þgf LhÞtÞgfEinkunn NhFylling

# Lýsingarháttur nútíðar ('við vorum hringjandi', 'hún hefur verið kvartandi')

Sögn_lhnt/tala/pers →
    HjSögnLhÞt/tala/pers HreinSögn_lhnt
    | 'þykja:so'_0/tala/pers HreinSögn_lhnt  # Almenningur þykir uggandi / ég þykist vitandi

HreinSögn_lhnt →
    FsAtv? EinSögn_lhnt OgSögn_lhnt*

EinSögn_lhnt →
    so_0_lh_nt                          # skimandi
    | so_1_nf_lh_nt NlSagnfylling_nf    # verandi lögmaður sakborningsins
    | so_1_þf_lh_nt NlBeintAndlag_þf    # reykjandi sígarettu
    | so_1_þgf_lh_nt NlBeintAndlag_þgf  # bölvandi Páli
    | so_1_ef_lh_nt NlBeintAndlag_ef    # saknandi alls þess sem áður var
    | so_2_þgf_þf_lh_nt Andlög_þgf_þf   # skrifandi konunum bréf / gefandi konunum meiri gaum en körlunum

# Sagnbót ('konurnar [segjast/þykjast/virðast] [galvaskar/sumar/allar] hafa borðað matinn')

Sögn_sagnb/tala/pers/kyn →
    HjSögn/tala/pers LoTengtSögn_nf/tala/kyn? HreinSögn_sagnb/tala/kyn

HreinSögn_sagnb/tala/kyn →
    FsAtv? EinSögn_sagnb/tala/kyn OgSögn_sagnb/tala/kyn*

# Útgáfa til brúks strax á eftir nafnlið
# (sem getur endað á FsRunu og er því tvíræður við FsAtv)
HreinSögnAtv_sagnb/tala/kyn →
    AtvFs? EinSögn_sagnb/tala/kyn OgSögn_sagnb/tala/kyn*

EinSögn_sagnb/tala/kyn →
    so_sagnb NhFylling/tala/kyn?        # Konurnar hafa farið [sjálfar/galvaskar] að grilla
    | So_1_þf_nh_sagnb SagnarBotn       # Konurnar hafa [ekki] látið Pál borða matinn / sagt niðurstöðuna vera ranga
    # !!! Á að höndla Sögn_1_sagnb + NhFylling fremur eins og sögn með tveimur viðföngum,
    # !!! sbr. Konurnar hafa lofað Páli ([því] að fara út): þarna er í raun 'lofa:so'_2_þgf_þgf_sagnb
    | Sögn_1_sagnb NhFylling/tala/kyn?  # Konurnar hafa [ekki] lofað Páli [að fara út]
    | Sögn_2_sagnb
    | so_mm_sagnb LhÞtBotn/tala/kyn   # Konurnar hafa reynst vera bíræfnir þjófar / þóst ritfærar / sagst fara að drífa sig

# Gefa auka stig hér enda smella þá fleiri en eitt púsl saman
$score(+8) EinSögn_sagnb/tala/kyn

# Nafnháttur

Sögn_nh/tala/pers/kyn →
    HjSögnNh/tala/pers LoTengtSögn_nf/tala/kyn? HreinSögn_nh/tala/kyn # Páll mun/skal [galvaskur/allur] skrifa bréfið
    | HjSögnNhMeðSagnb/tala/pers FsAtv? LoTengtSögn_nf/tala/kyn? nhm HreinSögn_nh/tala/kyn # Páll á/ætti/verður/þarf [ekki] [galvaskur/allur] að skrifa bréfið
    | Sögn_nh_mm/tala/pers/kyn

# Gefa auka stig hér (enda er oft tvíræðni milli sagna í nafnhætti og nafnorða)
$score(+2) Sögn_nh/tala/pers/kyn

# Miðmynd + nafnháttur
# TODO skgr. hvaða sagnir ganga sem "hjálparsagnir" hér?
Sögn_nh_mm/tala/pers/kyn →
    so_mm/tala/pers OgSögn_mm/tala/pers* FsAtv? Botn_nh_mm/tala/kyn
    # Hann segist munu borða kökuna
    # Ég telst vilja kjósa flokkinn
    # Þau segjast geta látið henda Jóni út
    | So_1_þf_mm HjSögnNh_ft_p3 HreinSögn_nh/tala/kyn

Botn_nh_mm/tala/kyn →
     HreinSögn_nh/tala/kyn      # (Páll hyggst/þykist/telst) skrifa söguna
     | HreinSögn_lhþt/tala/kyn  # (Kvennalistakonur virðast [ekki]) taldar sigurstranglegar
     # (Jón sagðist) þeirrar skoðunar [að múslimar væru óalandi og óferjandi]
     # (Maðurinn reyndist) þeirrar innréttingar að ekki var á hann treystandi
     | Botn_nh_ef

Botn_nh_ef →
     'sá:fn'_ef/tala/kyn Nl_ef/tala/kyn Útskýring?

# Hvetja til samhengis með miðmynd: (hún) segist ekki ætla (að fara í framboð)
$score(+4) Sögn_nh_mm/tala/pers/kyn

HreinSögn_nh/tala/kyn →
    FsAtv? EinSögn_nh/tala/kyn OgSögn_nh/tala/kyn*

EinSögn_nh/tala/kyn →
    so_0_nh NhFylling/tala/kyn?                    # Konurnar munu fara að grilla
    | Sögn_1_nh NhFylling/tala/kyn?                # Konurnar munu [ekki] lofa Páli [að fara út]
    | Sögn_2_nh
    # (Stjórnsýsla skal) vera [í öllum tilvikum] gegnsæ
    | HjSögnVeraVerða FsRunaInnskot? LoTengtSögn_nf/tala/kyn

# Boðháttur

Sögn_bh →
    # Farðu [út] að þvo bílinn [strax]
    so_0_bh BhBotn?
    # Förum [út] að þvo bílinn [strax]
    | so_0_gm_fh_p1_ft_nt BhBotn?
    # Lofið barninu [að borða] [í friði]
    | Sögn_1_bh BhBotn?
    # Skrifaðu jólasveininum bréf
    | Sögn_2_bh BhBotn?
    # Láttu/látið það ekki slá ykkur (út af laginu) og fara (með ykkur í gönur)
    | 'láta:so'_bh NlBeintAndlag_þf AtvFs? EinSögn_nh/tala/kyn OgSögn_nh/tala/kyn*

# Boðháttur er sjaldgæfur - viljum aðra kosti frekar
$score(-14) Sögn_bh
$tag(begin_prep_scope) Sögn_bh

BhBotn →
    # að borða í friði
    NhFylling SagnInnskot?
    | SagnInnskot
    # í skálinni meðan þið brjótið eggin
    | FsAtv? BhMeðan HreinYfirsetning

# (Hrærið [í deiginu]) uns (súkkulaðið bráðnar)
BhMeðan → 'á_meðan:ao' | "meðan:st" | "uns:st" | "þar:st" "til:st"

SögnFrh_bh →
    KommuSögn_bh* OgSögn_bh+

KommuSögn_bh →
    ',' FsAtv? Sögn_bh

OgSögn_bh →
    ','? OgEðaEn FsAtv? Sögn_bh

# ...en þegar eru komnar tvær eða fleiri sagnir í röð þá er það líklegri lausn
$score(+10) KommuSögn_bh
$score(+10) OgSögn_bh

# Sögn með engu viðfangi ('lenti')
# ('Maðurinn lenti')
Sögn_0/tala/pers/kyn →
    # (Mennirnir) lenda og fara [galvaskir að þvo bílinn]
    so_0/tala/pers OgSögn_0/tala/pers* NhFylling/tala/kyn?

# Sögn með engu viðfangi ('lenti') sem stendur í undirsetningu
# ('Maðurinn sem lenti')
ÖfugSögn_0/tala/pers/kyn →

    # Mennirnir sem nennt hafa [að þvo bílinn]
    so_sagnb HjSögn/tala/pers NhFylling/tala/kyn?

    # Mennirnir sem lenda munu
    | so_0_nh HjSögnNh/tala/pers NhFylling/tala/kyn?

    # Mennirnir sem lentir eru
    | so_0_lhþt/tala/kyn HjSögnLhÞt/tala/pers NhFylling/tala/kyn?

    # (Páll gerði allt sem) í hans valdi stóð
    # (...þau réttindi sem) í þessari stjórnarskrá felast
    | FsAtv so_0/tala/pers

# Sögn með inniföldu frumlagi en engu viðfangi ('lenti')
# ('Í dag lenti maðurinn')
SögnMeðF_0/tala/pers/kyn →

    # (Í dag) lenda og fara [í hendingskasti] mennirnir [galvaskir að þvo bílinn]
    so_0/tala/pers OgSögn_0/tala/pers* FsRunaInnskot? NlFrumlag_nf/tala/pers/kyn NhFylling/tala/kyn?

    # (Í dag) tókst og heppnaðist Seðlabankanum [galvöskum að lækka vexti]
    # !!! Ath: ekki _fh hér þar sem _vh er leyfilegt (takist Seðlabankanum...)
    # !!! Ath: Sögnin er óháð frumlaginu í tölu og persónu, hún er í reynd
    # !!! í 3p. et. (sbr. ?'Það tekst veiðimönnunum að veiða þann stóra')
    | so_mm_et_p3 OgSögn_mm_et_p3* NlFrumlag_þgf/tala/pers/kyn NhFylling_þgf/tala/kyn?

    # (Í dag) ber stjórnvöldum að taka hætturnar alvarlega
    # | so_subj_gm_op_p3_et_þgfnh NlFrumlag_þgf/tala/pers/kyn NhFylling/tala/kyn

    # (Í dag) líður mér [vel að hafa ekki farið þá leið]
    # (Í gær) dreymdi mig [um að fara alsæll í sólarlandaferð]
    # (Eftir matinn) svimaði mig
    | so_subj_op/aukafall NlFrumlag/aukafall/tala/pers/kyn NhFylling/tala/kyn?

    # (Í dag) hefur [ekki] gerst [við undirritunina] það sama
    # (Um árabil) hefur verið [í gildi] yfirlýsing
    | HjSögn/tala/pers FsAtv? so_sagnb FsRunaInnskot? NlFrumlag_nf/tala/pers/kyn NhFylling/tala/kyn?

    # (Í dag) hafa mennirnir [ekki getað] lent
    | HjSögn/tala/pers NlFrumlag_nf/tala/pers/kyn GetaAtv_sagnb? HreinSögnAtv_sagnb/tala/kyn

    # (Í dag) eiga mennirnir [ekki/eftir seinkun gærdagsins] að hafa/geta lent
    | HjSögnNhMeðSagnb/tala/pers NlFrumlag_nf/tala/pers/kyn nhm HafaGetaFá_nh HreinSögn_sagnb/tala/kyn

    # (Í dag) munu mennirnir [ekki/innan tíðar] lenda
    | SögnMeðF_nh/tala/pers/kyn

    # (Í dag) hyggjast mennirnir [ekki/innan tíðar] lenda
    # (Í dag) segist Illugi ekki hafa fengið laun
    | so_mm/tala/pers NlFrumlag_nf/tala/pers/kyn SögnMeðF_mm/tala/kyn

    # (Í dag) eru mennirnir [ekki/í millitíðinni] lentir
    # (Í dag) skulu mennirnir látnir lausir
    | HjSögnLhÞtSM/tala/pers NlFrumlag_nf/tala/pers/kyn HreinSögn_lhþt/tala/kyn

    # (Hér) væri [ennfremur] komin mikil framleiðsluspenna
    # Ath: Hjálparsögnin er í 3p þó frumlagið sé það ekki, sbr.
    # 'Hér er ennfremur kominn þú' (ekki 'ert')
    | HjSögnLhÞtSM_p3/tala FsAtv? so_0_lhþt/tala/kyn NlFrumlag_nf/tala/kyn

    # (Í ár) hefur krönunum [í borginni] fjölgað
    | HjSögn_et_p3 NlFrumlag/fall/tala/pers/kyn AtvFs? so_subj_sagnb/fall NhFylling/tala/kyn?
    # (Í dag) er þingmönnunum [í flokknum] stefnt (til þings)
    | HjSögnLhÞt_et_p3 NlFrumlag_þgf/tala/pers/kyn AtvFs? so_1_þgf_sagnb NhFylling/tala/kyn?
    # (Í kvöld) er viðurgjörningsins notið (í skíðaskálanum)
    | HjSögnLhÞt_et_p3 NlFrumlag_ef/tala/pers/kyn AtvFs? so_1_ef_sagnb NhFylling/tala/kyn?

    # (Í ár) mun krönunum [í borginni] fjölga
    | HjSögnNh_et_p3 NlFrumlag/fall/tala/pers/kyn AtvFs? so_subj_nh/fall NhFylling/tala/kyn?

    # (Í dag) hafa fjöllin verið klifin og sigruð / hefur þú verið sæmdur orðu
    | 'hafa:so'_0/tala/pers NlFrumlag_nf/tala/pers/kyn AtvFs? 'vera:so'_sagnb HreinSögn_lhþt/tala/kyn
    # (Í dag) hefur flugvélinni verið lent
    | 'hafa:so'_0_et_p3 NlFrumlag_þgf/tala/pers/kyn AtvFs?
        'vera:so'_sagnb FsAtv? so_1_þgf_sagnb NhFylling/tala/kyn?
    # (Í dag) hefur konunnar verið gætt
    | 'hafa:so'_0_et_p3 NlFrumlag_ef/tala/pers/kyn AtvFs?
        'vera:so'_sagnb FsAtv? so_1_ef_sagnb NhFylling/tala/kyn?

    # Skal sú ákvörðun [vera] rökstudd og tilkynnt forseta Alþingis
    | HjSögnSkuluMunu/tala/pers NlFrumlag_nf/tala/pers/kyn VeraVerðaAtv? HreinSögn_lhþt/tala/kyn

SögnMeðF_nh/tala/pers/kyn →
    HjSögnNh/tala/pers NlFrumlag_nf/tala/pers/kyn Atviksliður? SögnMeðFNhBotn/tala/pers/kyn

$score(+4) SögnMeðF_nh/tala/pers/kyn

SögnMeðFNhBotn/tala/pers/kyn →
    # (Í stað takka) vill Apple [nú] koma fyrir snertiskynjurum
    HreinSögn_nh/tala/kyn
    # (Samkvæmt fréttum) mun þingkonan [nú] hafa gefið í skyn / geta fengið gögnin / fá upplýst um málið
    | HafaGetaFá_nh HreinSögn_sagnb/tala/kyn

VeraVerðaAtv →
     FsAtv? HjSögnVeraVerða

SögnMeðF_mm/tala/kyn →
    HreinSögn_nh/tala/kyn                           # (Í dag hyggjast mennirnir) [ekki/innan tíðar] lenda
    | FsAtv? HjSögn_mm_nh HreinSögn_sagnb/tala/kyn  # (Í dag segist Illugi) ekki hafa fengið laun

OgSögn_1/fall/tala/pers →
    OgEðaEn FsAtv? so_1/fall/tala/pers
    | ',' OgEðaEn FsAtv? so_1/fall/tala/pers FsAtvKomma?

# Umraðanir

So_1/aukafall/tala/pers → so_1/aukafall/tala/pers
$tag(pick_up_verb) So_1/aukafall/tala/pers

So_1_sagnb_nf → so_1_nf_sagnb   # (Í fyrra höfðu konurnar [ekki]) verið leikarar
So_1_sagnb_þf → so_1_þf_sagnb   # (Í dag hafa konurnar [ekki]) borðað matinn
So_1_sagnb_þgf → so_1_þgf_sagnb # (Í dag hafa konurnar [ekki]) hafnað Páli
So_1_sagnb_ef → so_1_ef_sagnb   # (Í dag hafa konurnar [ekki]) notið matarins

So_1_nh_nf → so_1_nf_nh         # (Í dag munu konurnar [ekki]) vera leikarar
So_1_nh_þf → so_1_þf_nh         # (Í dag munu konurnar [ekki]) borða matinn
So_1_nh_þgf → so_1_þgf_nh       # (Í dag munu konurnar [ekki]) hafna Páli
So_1_nh_ef → so_1_ef_nh         # (Í dag munu konurnar [ekki]) njóta matarins

So_1_bh_nf → so_1_nf_bh | so_1_nf_gm_fh_p1_ft_nt # Vertu leikari / Verum leikarar
So_1_bh_þf → so_1_þf_bh | so_1_þf_gm_fh_p1_ft_nt # Borðaðu matinn / borðum matinn
So_1_bh_þgf → so_1_þgf_bh | so_1_þgf_gm_fh_p1_ft_nt # Hafnaðu Páli / höfnum Páli
So_1_bh_ef → so_1_ef_bh | so_1_ef_gm_fh_p1_ft_nt # Njóttu matarins / njótum matarins

$tag(pick_up_verb) So_1_bh/fall

So_1_vh_nf/tala/pers → so_1_nf_vh/tala/pers    # Væri hann leikari
So_1_vh_þf/tala/pers → so_1_þf_vh/tala/pers    # Borðir þú matinn
So_1_vh_þgf/tala/pers → so_1_þgf_vh/tala/pers  # Hafni ég Páli
So_1_vh_ef/tala/pers → so_1_ef_vh/tala/pers    # Nyti hann matarins

So_1_p3_nf/tala → so_1_nf_p3/tala
So_1_p3_þf/tala → so_1_þf_p3/tala
So_1_p3_þgf/tala → so_1_þgf_p3/tala
So_1_p3_ef/tala → so_1_ef_p3/tala

# Sagnbót með einu viðfangi

Sögn_1_sagnb →
    # Jón getur verið [af náttúrunnar hendi] hálfgerður asni
    So_1_sagnb_nf FsRunaEftirSögn? NlSagnfylling_nf
    # Þingmenn geta/hafa lagt [fyrir Alþingi] frumvörp (til laga)
    | So_1_sagnb/aukafall FsRunaEftirSögn? NlBeintAndlag/aukafall
    | 'enginn:fn'/fall/tala/kyn Nl/fall/tala/kyn So_1_sagnb/fall # (Þó getur hann) enga samninga gert

# Sögn í nafnhætti með einu viðfangi

Sögn_1_nh →
    So_1_nh/fall FsRunaInnskot? NlBeintAndlag/fall
    # sjá/láta Pál breyta [í snarheitum] sér (í kött)
    | So_1_þf_nh Nl_þf So_1_nh/fall FsRunaInnskot? Nl/fall

# Gefa forsetningarliðum milli sagnar og andlags mínus
# enda eru þær klasturslegt mál ('Hann mun sjá í snatri fílinn')
FsRunaInnskot →
    FsRuna

$score(-8) FsRunaInnskot

# Sögn í boðhætti með einu viðfangi

Sögn_1_bh →
    # Vertu [í alvörunni] þú sjálfur
    So_1_bh_nf FsRunaEftirSögn? NlSagnfylling_nf
    # Bakaðu [í snarheitum] kökuna
    | So_1_bh/aukafall FsRunaEftirSögn? NlBeintAndlag/aukafall

# Sagnbót með tveimur viðföngum

Sögn_2_sagnb →
    so_2_þgf_þf_sagnb Andlög_þgf_þf     # Konurnar höfðu [ekki] gefið Páli gjöfina
    | so_2_þf_ef_sagnb Andlög_þf_ef     # Konurnar höfðu [ekki] krafið Pál svara
    | so_2_þf_þf_sagnb Andlög_þf_þf     # Konurnar höfðu [ekki] kostað Pál skildinginn
    | so_2_þf_þgf_sagnb Andlög_þf_þgf   # Konurnar höfðu [ekki] kallað Pál öllum illum nöfnum
    | so_2_þgf_þgf_sagnb Andlög_þgf_þgf # Konurnar höfðu [ekki] skilað Páli bílnum
    | so_2_þgf_ef_sagnb Andlög_þgf_ef   # Konurnar höfðu [ekki] óskað Páli alls hins besta
    | so_2_þgf_nf_sagnb Andlög_þgf_nf   # Kristín hafði [ekki] verið Páli stoð og stytta
    | 'gera:so'_1_þgf_sagnb Nl_þgf LoTengtSögn_nf_et_hk NhFyllingAtv # Konurnar höfðu [ekki] gert Páli kleift að klæða sig

# Það er gott að ná tveimur viðföngum með sögn í sagnbeygingu
$score(+6) Sögn_2_sagnb

# Sögn í nafnhætti með tveimur viðföngum

Sögn_2_nh →
    so_2_þgf_þf_nh Andlög_þgf_þf        # Konurnar munu [ekki] gefa Páli gjöfina
    | so_2_þf_ef_nh Andlög_þf_ef        # Konurnar munu [ekki] krefja Pál svara
    | so_2_þf_þf_nh Andlög_þf_þf        # Konurnar munu [ekki] kosta Pál skildinginn
    | so_2_þf_þgf_nh Andlög_þf_þgf      # Konurnar munu [ekki] kalla Pál öllum illum nöfnum
    | so_2_þgf_þgf_nh Andlög_þgf_þgf    # Konurnar munu [ekki] skila Páli bílnum
    | so_2_þgf_ef_nh Andlög_þgf_ef      # Konurnar munu [ekki] óska Páli alls hins besta
    | so_2_þgf_nf_nh Andlög_þgf_nf      # Kristín mun [ekki] vera Páli stoð og stytta
    | 'gera:so'_1_þgf_nh Nl_þgf LoTengtSögn_nf_et_hk NhFyllingAtv # Konurnar munu [ekki] gera Páli kleift/mögulegt að klæða sig

Andlög/fall/afall →
    NlÓbeintAndlag/fall AndlögBotn/fall/afall

Andlög/fall/afall/tala/kyn →
    NlÓbeintAndlag/fall LoTengtSögn_nf/tala/kyn? AndlögBotn/fall/afall

AndlögBotn/fall/afall →
    # (gefa Páli) [stóra] gjöf
    NlBeintAndlag/afall
    # (gefa Páli) [talsvert] stærri gjöf/flottari bíl en mér/Sigríði
    | LoAtviksliður* lo_mst/afall/tala/kyn NlBeintAndlag/afall/tala/pers/kyn "heldur:st"? "en:st" NlÓbeintAndlag/fall

So_2_nh_þf_nf → so_2_þf_nf_nh
So_2_nh_þf_þf → so_2_þf_þf_nh
So_2_nh_þf_þgf → so_2_þf_þgf_nh
So_2_nh_þf_ef → so_2_þf_ef_nh

So_2_nh_þgf_nf → so_2_þgf_nf_nh
So_2_nh_þgf_þf → so_2_þgf_þf_nh
So_2_nh_þgf_þgf → so_2_þgf_þgf_nh
So_2_nh_þgf_ef → so_2_þgf_ef_nh


# Sögn í boðhætti með tveimur viðföngum

Sögn_2_bh →
    so_2_þgf_þf_bh Andlög_þgf_þf        # Gefðu Páli gjöfina / stærri gjöf en mér
    | so_2_þf_ef_bh Andlög_þf_ef        # Krefðu Pál svars / ítarlega svars en mig
    | so_2_þf_þf_bh Andlög_þf_þf        # Kostaðu Pál skildinginn / fleiri skildinga en mig
    | so_2_þf_þgf_bh Andlög_þf_þgf      # Kallaðu Pál öllum illum nöfnum / verri nöfnum en mig
    | so_2_þgf_þgf_bh Andlög_þgf_þgf    # Skilaðu Páli bílnum / betri bíl en mér
    | so_2_þgf_ef_bh Andlög_þgf_ef      # Óskaðu Páli alls hins besta / betri útkomu en mér
    | so_2_þgf_nf_bh Andlög_þgf_nf      # Vertu Páli stoð og stytta / tryggari vinur en mér
    | 'gera:so'_1_þgf_bh Nl_þgf LoTengtSögn_nf_et_hk NhFyllingAtv # Gerðu Páli kleift/mögulegt að klæða sig í fötin

# Sögn með einu viðfangi ('borðaði matinn')
Sögn_1/tala/pers/kyn →

    # (þf:)  Konurnar borðuðu og meltu [ekki] matinn
    # (þgf:) Konurnar afneituðu, og beinlínis höfnuðu, [ekki] Páli
    # (ef:)  Konurnar nutu [ekki] matarins
    # (nf:)  Konurnar voru [ekki] leikarar

    # (Páll) klappaði og strauk kettinum
    # (Kjósandi) velur [í lok maí] þá frambjóðendur sem hann kýs
    # (Við) einsetjum okkur að vinna með öðrum þjóðum
    # (Sveitarfélögin) ráða sjálf [í meginatriðum] málefnum sínum
    # (Hún) sagði lafmóð/sjálf söguna
    so_1_nf/tala/pers OgSögn_1_nf/tala/pers*
        FsRunaEftirSögn? LoTengtSögn_nf/tala/kyn? NlSagnfylling_nf NhFylling?
    | so_1/aukafall/tala/pers OgSögn_1/aukafall/tala/pers*
        FsRunaEftirSögn? LoTengtSögn_nf/tala/kyn? NlBeintAndlag/aukafall NhFylling?

    # (Ég tel/segi/álít) Pál vera dagfarsprúðan mann
    | So_1_þf_nh/tala/pers OgSögn_1_þf_nh/tala/pers* SagnarBotn

    # (Bankar) hafa reynst og gagnast mér (vel)
    | HjSögn/tala/pers FsAtv? so_mm_sagnb OgSögn_mm_sagnb* NlBeintAndlag_þgf

    # Konan hefur verið/skal/mun gefin Páli
    | HjSögnLhÞtSM/tala/pers FsAtv? so_2_þgf_þf_lhþt/tala/kyn Nl_þgf
    # Konan hefur verið/skal/mun spurð álits
    | HjSögnLhÞtSM/tala/pers FsAtv? so_2_þf_ef_lhþt/tala/kyn NlBeintAndlag_ef
    # Konan hefur verið/skal/mun kölluð öllum illum nöfnum
    | HjSögnLhÞtSM/tala/pers FsAtv? so_2_þf_þgf_lhþt/tala/kyn Nl_þgf

# Býli í landinu eru/geta verið/verða/munu vera 3.000/tuttugu [talsins]
Sögn_1_ft/pers/kyn →
    HjSögnLhÞt_ft/pers FsAtv? Töluorð_ft_nf/kyn

# Sagnir sem geta staðið með þf + nh
# '(Ég) álít (Pál vera dagfarsprúðan mann)'
# '(Guðrún) lét (soninn hafa bílinn)'
# '(Við) sáum (strákinn vaxa úr grasi)'
# '(Jón) kvað (stíginn vera öruggan)'
# '(Myndbandið) sýndi (björgunarmenn bera hann inn í sjúkrabíl)'
So_1_þf_nh/tala/pers →
    'segja:so'_1_þf_gm/tala/pers
    | 'telja:so'_1_þf_gm/tala/pers
    | 'láta:so'_1_þf_gm/tala/pers
    | 'álíta:so'_1_þf_gm/tala/pers
    | 'sjá:so'_1_þf_gm/tala/pers
    | 'kveða:so'_1_þf_gm/tala/pers
    | 'sýna:so'_1_þf_gm/tala/pers

So_1_þf_nh →
    'segja:so'_1_þf_gm_nh
    | 'telja:so'_1_þf_gm_nh
    | 'láta:so'_1_þf_gm_nh
    | 'álíta:so'_1_þf_gm_nh
    | 'sjá:so'_1_þf_gm_nh
    | 'kveða:so'_1_þf_gm_nh
    | 'sýna:so'_1_þf_gm_nh

# segist munu/vilja borða kökuna
# telst munu/vilja kjósa flokkinn
# læst munu/vilja virða meirihlutann
# kveðst munu/vilja virða ákvörðun ráðherrans
# sýnist munu/vilja slá metið
So_1_þf_mm →
    'segja:so'_mm
    | 'telja:so'_mm
    | 'láta:so'_mm
    | 'kveða:so'_mm
    | 'sýna:so'_mm

# látið/talið/sagt Pál borða matinn
So_1_þf_nh_sagnb →
    'segja:so'_1_þf_sagnb
    | 'telja:so'_1_þf_sagnb
    | 'láta:so'_1_þf_sagnb
    | 'álíta:so'_1_þf_sagnb
    | 'sjá:so'_1_þf_sagnb
    | 'kveða:so'_1_þf_sagnb
    | 'sýna:so'_1_þf_sagnb

$score(+2) So_1_þf_nh_sagnb

SagnarBotn →
    # (Páll segir) mistökin strax valda vanda
    Nl_þf AtvFs? NhRuna
    # (Þingmaðurinn telur) niðurstöðuna vera ranga / (Verkfræðingurinn segir) útkomuna verða tvísýna
    | Nl_þf/tala/kyn AtvFs? HjSögnVeraVerða LoLiður_nf/tala/kyn

    # (Páll segir) mistökin [ekki] hafa verið gerð (á vakt Jóns)
    # (Páll segir) mistökin [ekki] hafa verið (í uppleggi Friðgeirs)
    # Hér er notað HjSögnEkkiVera í stað HjSögnVera til að forðast margræðni
    # gagnvart NhRunu
    | Nl_þf/tala/kyn AtvFs? HjSögnEkkiVera so_lhþt/tala/kyn?

    # (Hann segir) hvern farþega [strax] hafa fengið tíu evrur / hafa farið að grilla
    | Nl_þf/tala/kyn AtvFs? 'hafa:so'_0_nh HreinSögn_sagnb/tala/kyn # !!! Hér vantar að senda þolfall: 'galvaskan að grilla'

    # (Páll segir) Ásu [ekki] vera/hafa verið góðan stjórnanda
    | Nl_þf/tala AtvFs? HjSögnEkkiVera Nl_þf/tala

    # (Páll segir) skorta [á] (að farið sé að lögum)
    | so_subj_nh_none NhFylling?
    # (Páll segir) tefjast (að hefja framkvæmdir)
    | so_mm_nh NhFylling?
    # (Már segir) misskilnings gæta (hjá Hannesi)
    | Nl_ef AtvFs? so_subj_nh_ef
    # (Anna telur) fólki fækka í landinu
    | Nl_þgf AtvFs? so_subj_nh_þgf
    # (Már segir) [í grundvallaratriðum] gæta misskilnings (hjá Hannesi)
    | FsAtv? so_subj_nh_ef Nl_ef
    # !!! Ekki virðist þörf fyrir öfuga orðaröð í þgf eða þf

$score(+6) SagnarBotn

# Sögn með einu viðfangi sem stendur í undirsetningu
# ('Konurnar sem borðuðu matinn')
ÖfugSögn_1/tala/pers/kyn →

    # (nf:)  Konurnar sem voru [ekki] leikarar
    # (þf:)  Konurnar sem borðuðu [ekki] matinn
    # (þgf:) Konurnar sem höfnuðu [ekki] Páli
    # (ef:)  Konurnar sem nutu [ekki] matarins

    # Konurnar sem verið höfðu leikarar [sjálfar/galvaskar]
    So_1_sagnb_nf HjSögn/tala/pers NlSagnfylling_nf LoTengtSögn_nf/tala/kyn?
    # Konurnar sem borðað höfðu matinn [sjálfar/galvaskar]
    | So_1_sagnb/aukafall HjSögn/tala/pers NlBeintAndlag/aukafall LoTengtSögn_nf/tala/kyn?

    # Konurnar sem leikarar höfðu [sjálfar/galvaskar] verið    
    | NlSagnfylling_nf HjSögn/tala/pers LoTengtSögn_nf/tala/kyn? FsAtv? So_1_sagnb_nf
    # Konurnar sem matinn höfðu [sjálfar/galvaskar] borðað
    | NlBeintAndlag/aukafall HjSögn/tala/pers LoTengtSögn_nf/tala/kyn? FsAtv? So_1_sagnb/aukafall

    # Konurnar sem vera munu leikarar [allar/galvaskar]
    | So_1_nh_nf HjSögnNh/tala/pers NlSagnfylling_nf LoTengtSögn_nf/tala/kyn?
    # Konurnar sem borða munu matinn [allar/galvaskar]
    | So_1_nh/aukafall HjSögnNh/tala/pers NlBeintAndlag/aukafall LoTengtSögn_nf/tala/kyn?

    # Konurnar sem leikarar munu [allar/galvaskar] vera
    | NlSagnfylling_nf HjSögnNh/tala/pers LoTengtSögn_nf/tala/kyn? FsAtv? So_1_nh_nf
    # Konurnar sem matinn munu [allar/galvaskar] borða
    | NlBeintAndlag/aukafall HjSögnNh/tala/pers LoTengtSögn_nf/tala/kyn? FsAtv? So_1_nh/aukafall

    # Konurnar sem matinn [snarlega] borðuðu [báðar/galvaskar]
    | NlSagnfylling_nf AtvFs? so_1_nf/tala/pers LoTengtSögn_nf/tala/kyn?
    # Konurnar sem matinn [snarlega] borðuðu [báðar/galvaskar]
    | NlBeintAndlag/aukafall AtvFs? so_1/aukafall/tala/pers LoTengtSögn_nf/tala/kyn?

    # Konan sem gefin hafði verið/skal/mun Páli
    | so_2_þgf_þf_lhþt/tala/kyn HjSögnLhÞtSM/tala/pers NlÓbeintAndlag_þgf
    # Konan sem Páli hafði verið/skal/mun gefin
    | NlÓbeintAndlag_þgf HjSögnLhÞtSM/tala/pers FsAtv? so_2_þgf_þf_lhþt/tala/kyn

    # Konan sem kölluð hafði verið/skal/mun illum nöfnum
    | so_2_þf_þgf_lhþt/tala/kyn HjSögnLhÞtSM/tala/pers NlÓbeintAndlag_þgf
    # Konan sem illum nöfnum hafði verið/skal/mun kölluð
    | Nl_þgf HjSögnLhÞtSM/tala/pers FsAtv? so_2_þf_þgf_lhþt/tala/kyn

    # Konan sem spurð hafði verið/skal/mun álits
    | so_2_þf_ef_lhþt/tala/kyn HjSögnLhÞtSM/tala/pers Nl_ef
    # Konan sem álits hafði verið/skal/mun spurð
    | Nl_ef HjSögnLhÞtSM/tala/pers FsAtv? so_2_þf_ef_lhþt/tala/kyn

    # Orðin sem Páll lét [ekki] falla
    | SoFrumlag_1_þf_nh FsAtv? NhRuna

    # so_2_þgf_ef, so_2_þgf_þgf og so_2_þf_þf virðast ekki geta staðið
    # í lhþt heldur aðeins sem sagnbót

SoFrumlag_1_þf_nh →
    NlFrumlag_nf/tala/pers So_1_þf_nh/tala/pers  # (Orðin sem) Páll lét (falla) / (Orðin sem) þú lést (falla)

SögnMeðF_1/tala/pers/kyn →

    # (nf:)  (Í fyrra) voru konurnar [ekki] leikarar [sjálfar/galvaskar]
    # (þf:)  (Í dag) borðuðu konurnar [ekki] matinn [sjálfar/galvaskar]
    # (þgf:) (Í dag) afneituðu og höfnuðu konurnar [ekki] Páli [báðar/galvaskar]
    # (ef:)  (Í gærkvöldi) óskuðu konurnar [ekki] leyfis [allar/galvaskar] [að höfða mál]

    so_1_nf/tala/pers OgSögn_1_nf/tala/pers*
        NlFrumlag_nf/tala/pers/kyn NlSagnfylling_nf
        LoTengtSögn_nf/tala/kyn? NhFylling?
    | so_1/aukafall/tala/pers OgSögn_1/aukafall/tala/pers*
        NlFrumlag_nf/tala/pers/kyn NlBeintAndlag/aukafall
        LoTengtSögn_nf/tala/kyn? NhFylling?

    | HjSögnLhÞtSM/tala/pers NlFrumlag_nf/tala/pers/kyn AtvFs? SagnarBotnLhÞt/tala/kyn

    # (Þegar gögnin hafa verið lesin) eru eimaðar upp úr þeim upplýsingar [á borð við nöfn]
    | HjSögnLhÞt/tala/pers FsAtv? so_lhþt_nf/tala/kyn FsAtv? NlFrumlag_nf/tala/pers/kyn

    # '(og) var Ástþór mjög ósáttur (við útvarpsstjóra)'
    | HjSögnLoMeðF/tala/pers/kyn LoTengtSögn_nf/tala/kyn

    # (Í dag) segir hann (köttinn vera svangan)
    | So_1_þf_nh/tala/pers OgSögn_1_þf_nh/tala/pers* NlFrumlag_nf/tala/pers/kyn SagnarBotn

    # (Í dag) barst lögreglunni ábending
    | so_mm_op/tala NlÓbeintAndlag_þgf NlFrumlag_nf/tala

    # (Í dag) finnst þeim vera slagsíða (í umfjölluninni) / þykir okkur ósanngjarnt (að haldið sé áfram)
    | HjSögnFinnst NlFrumlag_þgf SögnFinnstBotn

    # (Áður) hafði KR látið Jón og Gunnar fara / sagt niðurstöðuna vera ranga
    | HjSögn/tala/pers NlFrumlag_nf/tala/pers So_1_þf_nh_sagnb SagnarBotn

SögnMeðF_1/tala/pers/kyn →
    # (Í dag) eru og verða þetta tvítugir strákar [galvaskir] [að hella sig fulla]
    VeraVerða_gm_p3/tala OgVeraVerða_gm_p3/tala* 'þessi:fn'_nf_hk_et
        NlSagnfylling_nf/tala/pers/kyn LoTengtSögn_nf/tala/kyn? NhFylling?

SagnarBotnLhÞt/tala/kyn →
    # (Í dag) verður konan gefin Páli [sjálf]
    so_2_þgf_þf_lhþt/tala/kyn Nl_þgf LoTengtSögn_nf/tala/kyn?
    # (Í dag) er/skal/mun þingmaðurinn kallaður illum nöfnum [saklaus]
    | so_2_þf_þgf_lhþt/tala/kyn Nl_þgf LoTengtSögn_nf/tala/kyn?
    # (Í dag) mun konan spurð álits [galvösk]
    | so_2_þf_ef_lhþt/tala/kyn Nl_ef LoTengtSögn_nf/tala/kyn?

# Sögn með tveimur viðföngum ('gaf Páli gjöfina')
Sögn_2/tala/pers/kyn →

    # Þágufall + þolfall ('að gefa einhverjum kött')
    so_2_þgf_þf/tala/pers Andlög_þgf_þf/tala/kyn     # Konurnar gáfu Páli [sjálfar] gjöfina / dýrari gjöf en mér

    # gera + lo ('að gera einhverjum kleift að fara út')
    | 'gera:so'_1_þgf/tala/pers NlÓbeintAndlag_þgf LoTengtSögn_nf_et_hk NhFyllingAtv # Konurnar gerðu Páli kleift að matast með reisn

    # Þolfall + eignarfall ('að spyrja einhvern einhvers')
    | so_2_þf_ef/tala/pers Andlög_þf_ef/tala/kyn     # Konurnar kröfðu Pál [herskáar] svars / ítarlegri svara en mig

    # Þágufall + eignarfall ('að óska einhverjum góðs bata')
    | so_2_þgf_ef/tala/pers Andlög_þgf_ef/tala/kyn   # Konurnar óskuðu Páli [allar] góðs bata / skjótari bata en mér

    # Þágufall + þágufall ('að skila einhverjum einhverju')
    | so_2_þgf_þgf/tala/pers Andlög_þgf_þgf/tala/kyn # Konurnar skiluðu Páli [sjálfar] bílnum / betri bíl en mér

    # Þágufall + nefnifall ('að vera einhverjum eitthvað')
    | so_2_þgf_nf/tala/pers Andlög_þgf_nf/tala/kyn   # Konurnar voru Páli [allar] stoð og stytta / traustari vinir en mér

    # Þolfall + þolfall ('að kosta einhvern eitthvað')
    | so_2_þf_þf/tala/pers Andlög_þf_þf/tala/kyn     # Konurnar kostuðu Pál [allar] skildinginn / minna en mig

    # Þolfall + þágufall ('að kalla einhvern illum nöfnum')
    | so_2_þf_þgf/tala/pers Andlög_þf_þgf/tala/kyn   # Konurnar kölluðu Pál [æstar] illum nöfnum / verri nöfnum en mig

# Sögn með tveimur viðföngum sem stendur í undirsetningu
# ('Konan sem gaf Páli gjöfina')
ÖfugSögn_2/tala/pers/kyn →

    so_2_þgf_þf_sagnb HjSögn/tala/pers  Andlög_þgf_þf                # Konurnar sem gefið hafa Páli gjöfina
    | 'gera:so'_1_þgf_sagnb HjSögn/tala/pers
        NlÓbeintAndlag_þgf LoTengtSögn_nf_et_hk NhFyllingAtv         # Konurnar sem gert hafa Páli kleift að matast með reisn
    | so_2_þgf_þf_nh HjSögnNh/tala/pers Andlög_þgf_þf                # Konurnar sem gefa munu Páli gjöfina

    | so_2_þf_ef_sagnb HjSögn/tala/pers Andlög_þf_ef                 # Konurnar sem spurt hafa Pál spurningarinnar
    | so_2_þf_ef_nh HjSögnNh/tala/pers Andlög_þf_ef                  # Konurnar sem spyrja munu Pál spurningarinnar

    | so_2_þgf_ef_sagnb HjSögn/tala/pers Andlög_þgf_ef               # Konurnar sem óskað hafa Páli góðs bata
    | so_2_þgf_ef_nh HjSögnNh/tala/pers Andlög_þgf_ef                # Konurnar sem óska munu Páli góðs bata

    | so_2_þgf_þgf_sagnb HjSögn/tala/pers Andlög_þgf_þgf             # Konurnar sem skilað hafa Páli bílnum
    | so_2_þgf_þgf_nh HjSögnNh/tala/pers Andlög_þgf_þgf              # Konurnar sem skila munu Páli bílnum

    | so_2_þgf_nf_sagnb HjSögn/tala/pers Andlög_þgf_nf               # Konurnar sem verið hafa Páli stoð og stytta
    | so_2_þgf_nf_nh HjSögnNh/tala/pers Andlög_þgf_nf                # Konurnar sem vera munu Páli stoð og stytta

    | so_2_þf_þf_sagnb HjSögn/tala/pers Andlög_þf_þf                 # Konurnar sem kostað hafa Pál skildinginn
    | so_2_þf_þf_nh HjSögnNh/tala/pers Andlög_þf_þf                  # Konurnar sem kosta munu Pál skildinginn

    | so_2_þf_þgf_sagnb HjSögn/tala/pers Andlög_þf_þgf               # Konurnar sem kallað hafa Pál illum nöfnum
    | so_2_þf_þgf_nh HjSögnNh/tala/pers Andlög_þf_þgf                # Konurnar sem kalla munu Pál illum nöfnum

SögnMeðF_2/tala/pers/kyn →

    # Þágufall + þolfall ('að gefa einhverjum kött')
    so_2_þgf_þf/tala/pers NlFrumlag_nf/tala/pers/kyn Andlög_þgf_þf      # (Í dag) gáfu konurnar [bersýnilega] Páli [ekki] gjöfina

    # 'gera' + lo ('að gera einhverjum eitthvað kleift')
    | 'gera:so'_1_þgf/tala/pers NlFrumlag_nf/tala/pers/kyn NlÓbeintAndlag_þgf LoTengtSögn_nf_et_hk NhFyllingAtv # (Í dag) gerðu konurnar Páli kleift að matast með reisn

    # Þolfall + eignarfall ('að spyrja einhvern einhvers')
    | so_2_þf_ef/tala/pers NlFrumlag_nf/tala/pers/kyn Andlög_þf_ef      # (Í gær) kröfðu konurnar Pál [ekki] svars

    # Þágufall + eignarfall ('að óska einhverjum einhvers')
    | so_2_þgf_ef/tala/pers NlFrumlag_nf/tala/pers/kyn Andlög_þgf_ef    # (Í gær) óskuðu konurnar Páli [ekki] góðs bata

    # Þágufall + nefnifall ('að vera einhverjum eitthvað')
    | so_2_þgf_nf/tala/pers NlFrumlag_nf/tala/pers/kyn Andlög_þgf_nf    # (Í gær) voru konurnar Páli [ekki] stoð og stytta

    # Þolfall + þolfall ('að kosta einhvern eitthvað')
    | so_2_þf_þf/tala/pers NlFrumlag_nf/tala/pers/kyn Andlög_þf_þf      # (Í gær) kostuðu konurnar Pál [ekki] skildinginn

    # Þágufall + þágufall ('að skila einhverjum einhverju')
    | so_2_þgf_þgf/tala/pers NlFrumlag_nf/tala/pers/kyn Andlög_þgf_þgf  # (Í gær) skiluðu konurnar Páli [ekki] bílnum

    # Þolfall + þágufall ('að kalla einhvern illum nöfnum')
    | so_2_þf_þgf/tala/pers NlFrumlag_nf/tala/pers/kyn Andlög_þf_þgf    # (Í gær) kölluðu konurnar Pál [ekki] illum nöfnum

    # Sagnbót og nafnháttur    
    | HjSögn/tala/pers NlFrumlag_nf/tala/pers/kyn AtvFs? Sögn_2_sagnb  # (Í morgun) höfðu konurnar [ekki] krafið Pál svara
    | HjSögnNh/tala/pers NlFrumlag_nf/tala/pers/kyn AtvFs? Sögn_2_nh   # (Á morgun) munu konurnar [ekki] krefja Pál svara

# Hjálparsagnasambönd: vera, hafa, geta, skulu, munu...
# Aðalsögnin er sagnbót (farið/gert/viljað/borðað...)
HjSögn/tala/pers →
    'hafa:so'/tala/pers Geta_sagnb?                 # hafðir [í fátinu ekki getað] farið
    | 'geta:so'/tala/pers                           # gast farið
    | 'fá:so'/tala/pers                             # (allir) fái notið (þeirra réttinda)
    | 'geta:so'/tala/pers FsAtv? 'hafa:so'_nh     # gast [ekki] hafa farið
    | 'vilja:so'/tala/pers FsAtv? HafaGetaFá_nh     # vildir [ekki] geta/hafa farið
    | 'mega:so'/tala/pers FsAtv? HafaGetaFá_nh      # máttir [ekki] geta/hafa farið
    | 'þykja:so'/tala/pers FsAtv? HafaGetaFá_nh     # þóttir [ekki] geta/hafa farið
    | 'skulu:so'/tala/pers FsAtv? HafaGetaFá_nh     # skulir [ekki] geta/hafa farið
    | 'munu:so'/tala/pers FsAtv? HafaGetaFá_nh      # munir [ekki] geta/hafa farið
    | 'eiga:so'/tala/pers FsAtv? nhm HafaGetaFá_nh  # átt [ekki] að geta/hafa farið
    | so_mm/tala/pers FsAtv? HjSögn_mm_nh             # segist [ekki] hafa farið

# Hjálparsagnir koma ekki til álita varðandi tengingu forsetningar við sögn
$tag(purge_verb) HjSögn/tala/pers

HafaGetaFá_nh →
    'hafa:so'_nh | 'geta:so'_nh | 'fá:so'_nh

Geta_sagnb →
    FsAtv? 'geta:so'_sagnb

GetaAtv_sagnb →
    AtvFs? 'geta:so'_sagnb

$score(+3) HafaGetaFá_nh        # Gefa plús fyrir flókin sagnform
$tag(purge_verb) HafaGetaFá_nh  # Ekki horfa á hjálparsögnina þegar forsetningar eru tengdar

HjSögn_mm_nh →
    'hafa:so'_nh                    # telst hafa farið
    | 'geta:so'_nh 'hafa:so'_nh?    # sýnist geta hafa farið
    | 'vilja:so'_nh 'hafa:so'_nh    # segist vilja hafa farið
    | 'mega:so'_nh 'hafa:so'_nh     # virðist mega hafa farið
    | 'munu:so'_nh 'hafa:so'_nh     # segist munu hafa farið
    | 'skulu:so'_nh 'hafa:so'_nh    # segjast skulu hafa farið(?)
    | 'fá:so'_nh                    # þykist fá notið

$tag(purge_verb) HjSögn_mm_nh

HjSögnVh →
    'hafa:so'_et_p3_vh                          # hafi þurft
    | 'geta:so'_et_p3_vh                        # gæti þurft
    | 'geta:so'_et_p3_vh FsAtv? 'hafa:so'_nh    # gæti [ekki] hafa þurft
    | 'mega:so'_et_p3_vh FsAtv? 'hafa:so'_nh    # mætti [ekki] hafa þurft
    | 'þykja:so'_et_p3_vh FsAtv? 'hafa:so'_nh   # þætti [ekki] hafa þurft
    | 'skulu:so'_et_p3_vh FsAtv? 'hafa:so'_nh   # skyldi [ekki] hafa þurft
    | 'munu:so'_et_p3_vh FsAtv? 'hafa:so'_nh    # myndi [ekki] hafa þurft

$tag(purge_verb) HjSögnVh

# Sögnin stendur í nafnhætti með annarri nafnháttarsögn,
# sbr. 'Gæta verða/skulu/eiga/vilja þeir þingskapa'
HjSögnNhMeðSo/tala/pers →
    'verða:so'/tala/pers       # gæta verða þeir / verður þú
    | 'skulu:so'/tala/pers     # gæta skulu þeir / skal ég
    | 'munu:so'/tala/pers      # gæta munu þeir / mun hann
    | 'mega:so'/tala/pers      # gæta mega þeir
    | 'eiga:so'/tala/pers      # gæta eiga þeir
    | 'þurfa:so'/tala/pers     # gæta þurfa þeir / þurfið þið
    | 'vilja:so'/tala/pers     # gæta vilja þeir / viljið þið

$tag(purge_verb) HjSögnNhMeðSo/tala/pers

HjSögnNhMeðSagnb/tala/pers →
    'verða:so'/tala/pers       # verða mennirnir að hafa/geta... lent
    | 'eiga:so'/tala/pers      # eiga mennirnir að hafa/geta... lent
    | 'þurfa:so'/tala/pers     # þurfa mennirnir að hafa/geta... lent

$tag(purge_verb) HjSögnNhMeðSagnb/tala/pers

# Sögnin stendur í nafnhætti
HjSögnNh/tala/pers →
    'vilja:so'/tala/pers       # vilja fara
    | 'láta:so'/tala/pers      # læt/læst fara
    | 'vilja:so'/tala/pers FsAtv? 'láta:so'_nh # vill láta fara
    | 'vilja:so'/tala/pers FsAtv? 'hafa:so'_nh 'láta:so'_sagnb # vildir [gjarnan] hafa látið fara
    | 'skulu:so'/tala/pers     # skulu fara
    | 'munu:so'/tala/pers      # munu fara
    | 'mega:so'/tala/pers      # mega fara
    | 'þykja:so'/tala/pers     # þykja/þykjast setja (leiðinlegan svip á bæinn)
    | 'mega:so'/tala/pers FsAtv? 'láta:so'_nh     # mætti láta fara
    | 'skulu:so'/tala/pers FsAtv? 'þykja:so'_nh   # (flegni kjóllinn) skuli [ekki] þykja vera (í lagi)
    | 'skulu:so'/tala/pers FsAtv? 'mega:so'_nh    # skyldu [ekki] mega fara
    | 'skulu:so'/tala/pers FsAtv? 'vilja:so'_nh   # skyldu [ekki] vilja fara
    | 'skulu:so'/tala/pers FsAtv? 'láta:so'_nh    # skuli [ekki] láta fara
    | 'munu:so'/tala/pers FsAtv? 'þykja:so'_nh    # (flegni kjóllinn) myndi [ekki] þykja vera (siðsamlegur)
    | 'munu:so'/tala/pers FsAtv? 'mega:so'_nh     # myndu [ekki] mega fara
    #| 'munu'/tala/pers FsAtv? 'hafa'_nh? 'vilja'_nh 'láta'_nh? # myndu [ekki] vilja [láta] fara   - GÖLLUÐ REGLA, leyfir líka "munu hafa vilja láta fara" TODO laga
    | 'munu:so'/tala/pers FsAtv? 'láta:so'_nh     # myndu [ekki] láta fara
    | 'hafa:so'/tala/pers FsAtv? 'þykja:so'_sagnb # hafa [ekki] þótt fara
    | 'hafa:so'/tala/pers FsAtv? 'mega:so'_sagnb  # hafa [ekki] mátt fara
    | 'hafa:so'/tala/pers FsAtv? 'vilja:so'_sagnb 'láta:so'_nh? # hafa [ekki] viljað [láta] fara
    | 'hafa:so'/tala/pers FsAtv? 'láta:so'_sagnb  # hafa [ekki] látið fara
    | 'geta:so'/tala/pers FsAtv? 'láta:so'_sagnb  # getur [ekki] látið fara
    | 'geta:so'/tala/pers FsAtv? 'þykja:so'_sagnb # getur [ekki] þótt fara
    | 'geta:so'/tala/pers FsAtv? 'mega:so'_sagnb  # getur [ekki] mátt fara
    | 'geta:so'/tala/pers FsAtv? 'hafa:so'_nh? 'vilja:so'_sagnb 'láta:so'_nh? # getur [ekki] viljað fara / gæti viljað láta fara

HjSögnNh_p3_et → HjSögnNh_et_p3
HjSögnNh_p3_ft → HjSögnNh_ft_p3

$tag(purge_verb) HjSögnNh/tala/pers

HjSögnÞurfaEiga_et_p3 →
    'þurfa:so'_et_p3                    # Greinarmun þarf/þyrfti að gera
    | 'eiga:so'_et_p3                   # Greinarmun á/ætti að gera
    | 'verða:so'_et_p3                  # Greinarmun verður/varð/yrði að gera
    | 'hafa:so'_et_p3 'þurfa:so'_sagnb  # Engan hefur þurft að undra
    | 'hafa:so'_et_p3 'eiga:so'_sagnb   # Engan hefði átt að undra
    | 'hafa:so'_et_p3 'verða:so'_sagnb  # Greinarmun hefði orðið að gera
    | 'munu:so'_et_p3 'þurfa:so'_nh     # Engan mun þurfa að undra
    | 'skulu:so'_et_p3 'þurfa:so'_nh    # Engan skal þurfa að undra
    | 'munu:so'_et_p3 'eiga:so'_nh      # Greinarmun mun eiga að gera
    | 'skulu:so'_et_p3 'eiga:so'_nh     # Greinarmun skal eiga að gera
    | 'munu:so'_et_p3 'verða:so'_nh     # Greinarmun mun verða að gera
    | 'skulu:so'_et_p3 'verða:so'_nh    # Greinarmun skal verða að gera

HjSögnMegiSkuli_et_p3 →
    'mega:so'_et_p3                     # (Þann lærdóm) megi/mætti/má (draga)
    | 'skulu:so'_et_p3                  # (Þann lærdóm) skuli/skyldi/skal (draga)
    | 'hafa:so'_et_p3 'mega:so'_sagnb   # (Þann lærdóm) hefur/hefði mátt (draga)

$tag(purge_verb) HjSögnMegiSkuli_et_p3

HjSögnMegiSkuliÞurfi_et_p3 →
    'mega:so'_et_p3                     # (Endurnýja) megi/mætti/má (leigusamninginn)
    | 'eiga:so'_et_p3                   # (Endurnýja) á/eigi/átti (leigusamninginn)
    | 'þurfa:so'_et_p3                  # (Endurnýja) þyrfti/þarf/þurfti (leigusamninginn)
    | 'skulu:so'_et_p3                  # (Endurnýja) skuli/skyldi/skal (leigusamninginn)
    | 'verða:so'_et_p3                  # (Endurnýja) verður/yrði/varð (leigusamninginn)
    | 'hafa:so'_et_p3 'mega:so'_sagnb   # (Endurnýja) hefur/hafði/hefði mátt (leigusamninginn)
    | 'hafa:so'_et_p3 'þurfa:so'_sagnb  # (Endurnýja) hefur/hafði/hefði þurft (leigusamninginn)
    | 'hafa:so'_et_p3 'eiga:so'_sagnb   # (Endurnýja) hefur/hafði/hefði átt (leigusamninginn)

$tag(purge_verb) HjSögnMegiSkuliÞurfi_et_p3

# Vera/verða sambönd, öll nema sjálfar sagnirnar 'vera' og 'verða'
HjSögnEkkiVera →
    'vilja:so'_nh 'vera:so'_nh                      # (Mér sýndist Anna) vilja vera (ein)
    | 'mega:so'_nh 'vera:so'_nh                     # (Mér sýndist Anna) mega vera (ein)
    | 'hafa:so'_nh 'vera:so'_sagnb                  # (Mér sýndist Anna) hafa verið (valin)
    | 'geta:so'_nh 'hafa:so'_nh? 'vera:so'_sagnb    # (Mér sýndist Anna) geta [hafa] verið (farin)
    | 'vilja:so'_nh 'hafa:so'_nh 'vera:so'_sagnb    # (Mér sýndist Anna) vilja hafa verið (ein)
    | 'mega:so'_nh 'hafa:so'_nh 'vera:so'_sagnb     # (Mér sýndist Anna) mega hafa verið (ánægð)
    | 'þykja:so'_nh 'hafa:so'_nh 'vera:so'_sagnb    # (Félögin) þykja hafa verið (illa rekin)
    | 'vilja:so'_nh 'verða:so'_nh
    | 'mega:so'_nh 'verða:so'_nh
    | 'hafa:so'_nh 'verða:so'_sagnb
    | 'geta:so'_nh 'hafa:so'_nh? 'verða:so'_sagnb
    | 'vilja:so'_nh 'hafa:so'_nh 'verða:so'_sagnb
    | 'mega:so'_nh 'hafa:so'_nh 'verða:so'_sagnb
    | 'þykja:so'_nh 'hafa:so'_nh 'verða:so'_sagnb # (Félögin) þykja hafa orðið (illa rekin)

HjSögnVeraVerða →
    'vera:so'_nh | 'verða:so'_nh

# Vera/verða í nafnhætti ('(Mér sýndist kötturinn) vera (gulbrúnn)')
HjSögnVera →
    HjSögnEkkiVera | HjSögnVeraVerða

# Lýsingarháttur þátíðar: vera farinn, hafa verið farinn, skal vera farinn, mun vera farinn
HjSögnLhÞt/tala/pers →
    'vera:so'/tala/pers                            # eru farnar
    | 'vilja:so'/tala/pers FsAtv? 'vera:so'_nh        # vildu [ekki] vera farnar
    | 'þykja:so'/tala/pers FsAtv? 'vera:so'_nh        # þóttu [ekki] vera farnar
    | 'mega:so'/tala/pers FsAtv? 'vera:so'_nh         # máttu [ekki] vera farnar
    | 'skulu:so'/tala/pers FsAtv? 'vera:so'_nh        # skulu [ekki] vera farnar
    | 'munu:so'/tala/pers FsAtv? 'vera:so'_nh         # munu [ekki] vera farnar
    | 'hafa:so'/tala/pers FsAtv? 'vera:so'_sagnb      # hafa [ekki] verið farnar
    | 'geta:so'/tala/pers FsAtv? 'hafa:so'_nh? 'vera:so'_sagnb # geta [ekki] verið farnar / geta hafa verið farnar
    | 'vilja:so'/tala/pers FsAtv? 'hafa:so'_nh 'vera:so'_sagnb # vildu [ekki] hafa verið farnar
    | 'þykja:so'/tala/pers FsAtv? 'hafa:so'_nh 'vera:so'_sagnb # þóttu [ekki] hafa verið farnar
    | 'mega:so'/tala/pers FsAtv? 'hafa:so'_nh 'vera:so'_sagnb # máttu [ekki] hafa verið farnar
    | 'skulu:so'/tala/pers FsAtv? 'hafa:so'_nh 'vera:so'_sagnb # skulu [ekki] hafa verið farnar
    | 'munu:so'/tala/pers FsAtv? 'hafa:so'_nh 'vera:so'_sagnb  # munu [ekki] hafa verið farnar
    | 'verða:so'/tala/pers                          # verða farnar
    | 'vilja:so'/tala/pers FsAtv? 'verða:so'_nh        # vildu [ekki] verða farnar
    | 'þykja:so'/tala/pers FsAtv? 'verða:so'_nh        # þóttu [ekki] verða farnar
    | 'mega:so'/tala/pers FsAtv? 'verða:so'_nh         # máttu [ekki] verða farnar
    | 'skulu:so'/tala/pers FsAtv? 'verða:so'_nh        # skulu [ekki] verða farnar
    | 'munu:so'/tala/pers FsAtv? 'verða:so'_nh         # munu [ekki] verða farnar
    | 'hafa:so'/tala/pers FsAtv? 'verða:so'_sagnb      # hafa [ekki] orðið farnar
    | 'geta:so'/tala/pers FsAtv? 'hafa:so'_nh? 'verða:so'_sagnb # geta [ekki] orðið farnar / geta hafa orðið farnar
    | 'vilja:so'/tala/pers FsAtv? 'hafa:so'_nh 'verða:so'_sagnb # vildu [ekki] hafa orðið farnar
    | 'þykja:so'/tala/pers FsAtv? 'hafa:so'_nh 'verða:so'_sagnb # þóttu [ekki] hafa orðið farnar
    | 'mega:so'/tala/pers FsAtv? 'hafa:so'_nh 'verða:so'_sagnb  # máttu [ekki] hafa orðið farnar
    | 'skulu:so'/tala/pers FsAtv? 'hafa:so'_nh 'verða:so'_sagnb # skulu [ekki] hafa orðið farnar
    | 'munu:so'/tala/pers FsAtv? 'hafa:so'_nh 'verða:so'_sagnb  # munu [ekki] hafa orðið farnar

$tag(purge_verb) HjSögnLhÞt/tala/pers

HjSögnLhÞt_p3_et → HjSögnLhÞt_et_p3
HjSögnLhÞt_p3_ft → HjSögnLhÞt_ft_p3

# Hjálparsagnir með inniföldu frumlagi þegar lýsingarorð kemur á eftir
# ('Í dag þykir Ástþór [ekki lengur] vera (sáttur)')
HjSögnLoMeðF/tala/pers/kyn →
    'vera:so'/tala/pers NlFrumlag_nf/tala/pers/kyn                          # er Páll (sáttur)
    | so_mm/tala/pers NlFrumlag_nf/tala/pers/kyn                            # telst Páll (sáttur)

    | 'vilja:so'/tala/pers NlFrumlag_nf/tala/pers/kyn AtvFs? 'vera:so'_nh   # vill Páll [ekki] vera (sáttur)
    | 'þykja:so'/tala/pers NlFrumlag_nf/tala/pers/kyn AtvFs? 'vera:so'_nh   # þótti Páll [ekki] vera (sáttur)
    | 'mega:so'/tala/pers NlFrumlag_nf/tala/pers/kyn AtvFs? 'vera:so'_nh    # mátti Páll [ekki] vera (sáttur)
    | 'skulu:so'/tala/pers NlFrumlag_nf/tala/pers/kyn AtvFs? 'vera:so'_nh   # skal Páll [ekki] vera (sáttur)
    | 'munu:so'/tala/pers NlFrumlag_nf/tala/pers/kyn AtvFs? 'vera:so'_nh    # mun Páll [ekki] vera (sáttur)
    | so_mm/tala/pers NlFrumlag_nf/tala/pers/kyn AtvFs? 'vera:so'_nh        # sagðist Páll [ekki] vera (sáttur)

    | 'hafa:so'/tala/pers NlFrumlag_nf/tala/pers/kyn AtvFs? 'vera:so'_sagnb  # hefur Páll [ekki] verið (sáttur)
    | 'geta:so'/tala/pers NlFrumlag_nf/tala/pers/kyn AtvFs? 'hafa:so'_nh? 'vera:so'_sagnb # getur Páll [ekki] [hafa] verið (sáttur)
    | 'vilja:so'/tala/pers NlFrumlag_nf/tala/pers/kyn AtvFs? 'hafa:so'_nh 'vera:so'_sagnb # vildi Páll [ekki] hafa verið (sáttur)
    | 'þykja:so'/tala/pers NlFrumlag_nf/tala/pers/kyn AtvFs? 'hafa:so'_nh 'vera:so'_sagnb # þótt Páll [ekki] hafa verið (sáttur)
    | 'mega:so'/tala/pers NlFrumlag_nf/tala/pers/kyn AtvFs? 'hafa:so'_nh 'vera:so'_sagnb # mátti Páll [ekki] hafa verið (sáttur)
    | 'skulu:so'/tala/pers NlFrumlag_nf/tala/pers/kyn AtvFs? 'hafa:so'_nh 'vera:so'_sagnb # skal Páll [ekki] hafa verið (sáttur)
    | 'munu:so'/tala/pers NlFrumlag_nf/tala/pers/kyn AtvFs? 'hafa:so'_nh 'vera:so'_sagnb  # mun Páll [ekki] hafa verið (sáttur)
    | so_mm/tala/pers NlFrumlag_nf/tala/pers/kyn AtvFs? 'hafa:so'_nh 'vera:so'_sagnb  # sagðist Páll [ekki] hafa verið (sáttur)

    | 'verða:so'/tala/pers NlFrumlag_nf/tala/pers/kyn                          # verður Páll (sáttur)
    | 'vilja:so'/tala/pers NlFrumlag_nf/tala/pers/kyn AtvFs? 'verða:so'_nh     # vildi Páll [ekki] verða (sáttur)
    | 'þykja:so'/tala/pers NlFrumlag_nf/tala/pers/kyn AtvFs? 'verða:so'_nh     # þótti Páll [ekki] verða (sáttur)
    | 'mega:so'/tala/pers NlFrumlag_nf/tala/pers/kyn AtvFs? 'verða:so'_nh      # mátti Páll [ekki] verða (sáttur)
    | 'skulu:so'/tala/pers NlFrumlag_nf/tala/pers/kyn AtvFs? 'verða:so'_nh     # skal Páll [ekki] verða (sáttur)
    | 'munu:so'/tala/pers NlFrumlag_nf/tala/pers/kyn AtvFs? 'verða:so'_nh      # mun Páll [ekki] verða (sáttur)
    | so_mm/tala/pers NlFrumlag_nf/tala/pers/kyn AtvFs? 'verða:so'_nh          # sagðist Páll [ekki] verða (sáttur)
    | 'hafa:so'/tala/pers NlFrumlag_nf/tala/pers/kyn AtvFs? 'verða:so'_sagnb   # hefur Páll [ekki] orðið (sáttur)

    | 'geta:so'/tala/pers NlFrumlag_nf/tala/pers/kyn AtvFs? 'hafa:so'_nh? 'verða:so'_sagnb # getur Páll [ekki] orðið (sáttur) / geta hafa orðið (sáttur)
    | 'vilja:so'/tala/pers NlFrumlag_nf/tala/pers/kyn AtvFs? 'hafa:so'_nh 'verða:so'_sagnb # vildi Páll [ekki] hafa orðið (sáttur)
    | 'þykja:so'/tala/pers NlFrumlag_nf/tala/pers/kyn AtvFs? 'hafa:so'_nh 'verða:so'_sagnb # þótti Páll [ekki] hafa orðið (sáttur)
    | 'mega:so'/tala/pers NlFrumlag_nf/tala/pers/kyn AtvFs? 'hafa:so'_nh 'verða:so'_sagnb  # mátti Páll [ekki] hafa orðið (sáttur)
    | 'skulu:so'/tala/pers NlFrumlag_nf/tala/pers/kyn AtvFs? 'hafa:so'_nh 'verða:so'_sagnb # skal Páll [ekki] hafa orðið (sáttur)
    | 'munu:so'/tala/pers NlFrumlag_nf/tala/pers/kyn AtvFs? 'hafa:so'_nh 'verða:so'_sagnb  # mun Páll [ekki] hafa orðið (sáttur)
    | so_mm/tala/pers NlFrumlag_nf/tala/pers/kyn AtvFs? 'hafa:so'_nh 'verða:so'_sagnb   # sagðist Páll [ekki] hafa orðið (sáttur)

# 'Konan skal/mun gefin Páli'
HjSögnSkuluMunu/tala/pers →
    'skulu:so'/tala/pers | 'munu:so'/tala/pers

$tag(purge_verb) HjSögnSkuluMunu/tala/pers

# Sameiginleg regla fyrir LhÞt og SkuluMunu

HjSögnLhÞtSM/tala/pers →
    HjSögnLhÞt/tala/pers | HjSögnSkuluMunu/tala/pers

HjSögnLhÞtSM_p3_et →
    HjSögnLhÞt_et_p3 | HjSögnSkuluMunu_et_p3

HjSögnLhÞtSM_p3_ft →
    HjSögnLhÞt_ft_p3 | HjSögnSkuluMunu_ft_p3

HjSögnSagnb_et_p3 →
    HjSögnLhÞtSM_et_p3 # | 'hafa:so'_et_p3

# 'Hælisleitendum fer/fór/færi/hefur farið/mun fara fækkandi'

HjSögnLhNt →
    'fara:so'_gm_p3_et
    | 'hafa:so'_gm_p3_et FsAtv? 'geta:so'_sagnb? 'fara:so'_sagnb # hefur/hafði/hefði [ekki] [getað] farið (fækkandi)
    | 'geta:so'_gm_p3_et FsAtv? 'hafa:so'_nh? 'fara:so'_sagnb    # getur/gat/gæti [hafa] farið (fækkandi)
    | 'munu:so'_gm_p3_et FsAtv? 'fara:so'_nh                  # mun/myndi [ekki] fara (fækkandi)
    | 'þykja:so'_gm_p3_et FsAtv? 'fara:so'_nh                 # þykir/þótti/þætti [ekki] fara (fækkandi)
    | 'munu:so'_gm_p3_et FsAtv? 'hafa:so'_nh 'fara:so'_sagnb     # mun/myndi [ekki] hafa farið (fækkandi)
    | 'þykja:so'_gm_p3_et FsAtv? 'hafa:so'_nh 'fara:so'_sagnb    # þykir/þótti/þætti [ekki] hafa farið (fækkandi)

# 'Íslandsbanka hefur tekist að halda í horfinu'

HjSögnMM →
    'hafa:so'_gm_p3_et FsAtv?               # hefur [ekki] tekist
    | 'geta:so'_gm_p3_et FsAtv? 'hafa:so'_nh?  # getur [ekki] [hafa] tekist
    | 'munu:so'_gm_p3_et FsAtv? 'hafa:so'_nh   # mun [ekki] hafa tekist
    | 'skulu:so'_gm_p3_et FsAtv? 'hafa:so'_nh  # skal [ekki] hafa tekist
    | 'þykja:so'_gm_p3_et FsAtv? 'hafa:so'_nh  # þykir [ekki] hafa tekist
    | 'mega:so'_gm_p3_et FsAtv? 'hafa:so'_nh   # má [ekki] hafa tekist

HjSögnFinnst →
    'finna:so'_mm_p3_et      # Okkur finnst/fannst skemmtilegt
    | 'finna:so'_mm_vh_p3_et # Okkur finnist/fyndist skemmtilegt
    | 'þykja:so'_gm_p3_et    # Okkur þykir/þótti skemmtilegt
    | 'þykja:so'_gm_vh_p3_et # Okkur þyki/þætti skemmtilegt

HjSögnFinnst_sagnb →
    'finna:so'_mm_sagnb    # Okkur hefur fundist skemmtilegt
    | 'þykja:so'_gm_sagnb  # Okkur hefur þótt skemmtilegt

# HjSögnÞaðLo_et_p3 →
#     'vera:so'_gm_p3_et 'verða:so'_sagnb? # Það er [orðið] brýnt að...
#     | 'verða:so'_gm_p3_et  # Það verður brýnt að bæta umgengni
#     | 'hafa:so'_gm_p3_et 'vera:so'_sagnb  # Það hefur verið brýnt að...
#     | 'hafa:so'_gm_p3_et 'verða:so'_sagnb # Það hefur orðið brýnt að...
#     | 'hafa:so'_gm_p3_et 'þykja:so'_sagnb # Það hefur þótt brýnt að...
#     | 'þykja:so'_gm_p3_et 'vera:so'_nh? # Það þykir [vera] brýnt að...
#     | 'þykja:so'_gm_p3_et 'vera:so'_nh? 'verða'_sagnb # Það þykir [vera] orðið brýnt að...
#     | 'munu:so'_gm_p3_et 'vera:so'_nh 'verða:so'_sagnb? # Það mun vera [orðið] brýnt að...
#     | 'munu:so'_gm_p3_et 'hafa:so'_nh 'þykja:so'_sagnb # Það mun hafa þótt brýnt að...
#     | 'munu:so'_gm_p3_et 'þykja:so'_nh # Það mun þykja brýnt að...
#     | 'geta:so'_gm_p3_et 'hafa:so'_nh? 'vera:so'_sagnb # Það getur [hafa] verið brýnt að...
#     | 'geta:so'_gm_p3_et 'hafa:so'_nh? 'verða:so'_sagnb # Það getur [hafa] orðið brýnt að...
#     | 'geta:so'_gm_p3_et 'hafa:so'_nh? 'þykja:so'_sagnb # Það getur [hafa] þótt brýnt að...

# Nafnháttarfylling
# - tengd frumlagi
# '(Þeir ætluðu) [sannarlega galvaskir/sjálfir] að þvo bílinn í gær'
NhFylling/tala/kyn →
    NhFylling_nf/tala/kyn    # Nefnifall sjálfgefið

$score(+6) NhFylling/tala/kyn # Gefa 'að borða köku' forgang sem NhFylling fremur en fs+no, til dæmis fs+no_þgf+no_ef

# Aðeins nefnifall og þágufall eru notuð
NhFylling_nf/tala/kyn → EinNhFylling_nf/tala/kyn OgNhFylling_nf/tala/kyn*
NhFylling_þgf/tala/kyn → EinNhFylling_þgf/tala/kyn OgNhFylling_þgf/tala/kyn*

EinNhFylling/fall/tala/kyn →
    # '(Hann ætlar) strax að fara að millilenda'
    SagnInnskot? NhLiðir
    # '(Hann er) í reynd strax sjálfur að hafna þessu'
    | FsRuna? LoTengtSögn/tala/fall/kyn NhLiðir
    # 'að þykja og vera fallegt'
    | SagnInnskot? nhm so_1_nf_nh OgNhSögn_nf* LoTengtSögn/tala/fall/kyn
    # 'að sýnast og þykjast fallegt'
    | SagnInnskot? nhm so_mm_nh OgSögn_mm_nh* LoTengtSögn/tala/fall/kyn

OgNhFylling/fall/tala/kyn →
    OgEða EinNhFylling/fall/tala/kyn

# - ótengd frumlagi: lýsingarliður ekki mögulegur
# (nema þá hugsanlega í 3p. et.?)
NhFylling →
    FsAtv? NhLiðir

NhFyllingAtv →
    NhFylling SagnInnskot?

NhFyllingÁnAtv →
    NhLiðir SagnInnskot? # Getur ekki byrjað á FsAtv

$tag(begin_prep_scope) NhFyllingAtv # Nýtt undirtré fyrir tengingu sagnar og forsetningar
$tag(begin_prep_scope) NhFyllingÁnAtv # Nýtt undirtré fyrir tengingu sagnar og forsetningar

NhFyllingEðaAtv →
    NhFyllingAtv | SagnInnskot

# Mér fannst skemmtilegt [ að Jón læsi bókina / að fara út í þessu veðri / í dag ]
AðLiður →
    NhFyllingAtv
    | SagnInnskotAtv
    # Málinu væri stillt upp þannig að Sjálfstæðisflokkurinn bæri ábyrgð
    | Atviksliður? Skýringarsetning

# Nafnháttarliður
# 'að lenda snarlega á flugvellinum'
# 'að sigla skipinu í land strax'
NhLiður →
    nhm NhRuna

$score(+2) NhLiður

NhLiðurFrh →
    SagnInnskot? NhLiður

# 'að fara strax að athuga málið'
NhLiðir →
    NhLiður NhLiðurFrh*

NhRuna → NhSögn OgNhLiður*

OgNhLiður →
    # (að selja skyldi hestinn) snarlega, en ekki hætta baráttunni
    # (er ekki unnt að fara) nema nota allt fé ríkisins
    SagnInnskot? KommaOgEðaEnNema SagnInnskot? NhSögn

NhSögn →
    so_0_nh so_0_gm_vh_p3_et? OgNhSögn*                                     # að kjósa [þurfi] og telja (upp á nýtt)
    | so_1_nf_nh so_0_gm_vh_p3_et? OgNhSögn_nf* FsRunaEftirSögn? NlBeintAndlag_nf    # að hrjósa [muni] hugur
    | so_1_þf_nh so_0_gm_vh_p3_et? OgNhSögn_þf* FsRunaEftirSögn? NlBeintAndlag_þf    # að selja [skyldi] í snatri hestinn
    | so_1_þgf_nh so_0_gm_vh_p3_et? OgNhSögn_þgf* FsRunaEftirSögn? NlBeintAndlag_þgf # að hætta [ætti] baráttunni
    | so_1_ef_nh so_0_gm_vh_p3_et? OgNhSögn_ef* FsRunaEftirSögn? NlBeintAndlag_ef    # að freista [eigi] gæfunnar
    | so_2_þgf_þf_nh so_0_gm_vh_p3_et? OgNhSögn_þgf_þf* FsRunaEftirSögn? NlÓbeintAndlag_þgf NlBeintAndlag_þf # að skrifa [skuli] og senda Karólínu bréfið
    | so_2_þf_ef_nh so_0_gm_vh_p3_et? OgNhSögn_þf_ef* FsRunaEftirSögn? NlÓbeintAndlag_þf NlBeintAndlag_ef    # að spyrja og krefja Pál svars
    | so_2_þf_þgf_nh so_0_gm_vh_p3_et? OgNhSögn_þf_þgf* FsRunaEftirSögn? Nl_þf Nl_þgf # að kalla [ætti] Pál illum nöfnum
    | so_2_þgf_ef_nh so_0_gm_vh_p3_et? OgNhSögn_þgf_ef* FsRunaEftirSögn? NlÓbeintAndlag_þgf NlBeintAndlag_ef # að óska [eigi] og óska Páli góðs bata
    | so_2_þgf_nf_nh so_0_gm_vh_p3_et? OgNhSögn_þgf_nf* FsRunaEftirSögn? Nl_þgf Nl_nf # að vera [eigi] og vera Páli stoð og stytta
    #| so_2_þf_þf_nh so_0_gm_vh? OgNhSögn_þf_þf* Nl_þf Nl_þf    # !!! þarf?
    #| so_2_þgf_þgf_nh so_0_gm_vh? OgNhSögn_þgf_þgf* Nl_þgf Nl_þgf    # !!! þarf?
    | 'gera:so'_1_þgf_nh so_0_gm_vh_p3_et? NlÓbeintAndlag_þgf LoTengtSögn_nf_et_hk NhFyllingAtv # að gera [eigi] Páli kleift að matast með reisn
    # að reistar yrðu og byggðar íbúðir / að barinn skyldi fiskurinn og flakaður
    # !!! Ath: sennilega geta aðeins hjálparsagnir staðið í viðtengingarhætti hér,
    # !!! ekki allar sagnir
    | so_0_lhþt/tala/kyn so_0_gm_vh/tala OgNhSögn_lhþt/tala/kyn* NlFrumlag_nf/tala/kyn OgNhSögn_lhþt/tala/kyn*

    # 'giftast sér' / 'tengjast henni' !!! ATH: Þetta leyfir mun fleiri sagnir en raunverulega geta tekið þgf
    # | so_mm_nh Nl_þgf

    # (Páll fór inn eftir að) hafa fært bílinn / (fyrir að) geta lyft grettistaki
    | NhSögnHafaGetaFá

    # láta sjúklinginn ná eðlilegum líkamshita
    # segja Pál fara með rangt mál
    # telja verkefnið verða óviðráðanlegt
    # sjá drenginn fara í hundana
    | So_1_þf_nh Nl_þf NhSögn

    # betur megi fara / strax skuli tilkynna
    | HjSögnMegiSkuli_et_p3 NhSögn

NhSögnHafaGetaFá →
    HafaGetaFá_nh HreinSögn_sagnb/tala/kyn # !!! Strangt til tekið ætti að sækja tölu og kyn ofan að úr trénu

# Gefa þessu gott forskot til að keppa við SamstættFall með greini,
# til dæmis "hafa ráðið Jóni bana", þar sem "ráðið" er sögnin "ráða"
# en ekki nafnorðið "ráð" með greini
$score(+8) NhSögnHafaGetaFá

# Samtengingar sagna

OgNhSögn →
    OgEða so_0_nh so_0_gm_vh_p3_et?
OgNhSögn_þf →
    OgEða so_1_þf_nh so_0_gm_vh_p3_et?
OgNhSögn_þgf →
    OgEða so_1_þgf_nh so_0_gm_vh_p3_et?
OgNhSögn_ef →
    OgEða so_1_ef_nh so_0_gm_vh_p3_et?
OgNhSögn_nf →
    OgEða so_1_nf_nh so_0_gm_vh_p3_et?
OgNhSögn_þgf_þf →
    OgEða so_2_þgf_þf_nh so_0_gm_vh_p3_et?
OgNhSögn_þf_ef →
    OgEða so_2_þf_ef_nh so_0_gm_vh_p3_et?
OgNhSögn_þf_þgf →
    OgEða so_2_þf_þgf_nh so_0_gm_vh_p3_et?
OgNhSögn_þgf_ef →
    OgEða so_2_þgf_ef_nh so_0_gm_vh_p3_et?
OgNhSögn_þgf_nf →
    OgEða so_2_þgf_nf_nh so_0_gm_vh_p3_et?
OgNhSögn_lhþt/tala/kyn →
    OgEða so_0_lhþt/tala/kyn

# Gefa til kynna hvar lesa á upp sagnir til að nota í
# tengingu sagna og forsetninga

$tag(pick_up_verb) Sagnliður/tala/pers/kyn
$tag(pick_up_verb) Sagnliður_sagnb
$tag(pick_up_verb) SögnMeðF/tala/pers/kyn
$tag(pick_up_verb) ÖfugSögn_0/tala/pers/kyn
$tag(pick_up_verb) ÖfugSögn_1/tala/pers/kyn
$tag(pick_up_verb) ÖfugSögn_2/tala/pers/kyn
$tag(pick_up_verb) SagnliðurÁnF
$tag(pick_up_verb) NhLiður

# Nafnliðir

# (Kötturinn ákvað) [strax] [,] að Jón væri farinn heim
NlSkýringarsetning → AtviksliðurEinkunn* ","? Skýringarsetning
$score(-2) NlSkýringarsetning

# (Lögreglan rannsakar) [nú] hvort Jón hafi ekið bílnum
NlSpurnaraukasetning → AtviksliðurEinkunn* Spurnaraukasetning
$score(-2) NlSpurnaraukasetning

NlFrumlag/fall → Nl/fall | NlSkýringarsetning
NlFrumlag/fall/tala/pers/kyn → Nl/fall/tala/pers/kyn
NlFrumlag_nf_et_p3_hk → NlSkýringarsetning
NlFrumlag_þf_et_p3_hk → NlSkýringarsetning
NlFrumlag_þgf_et_p3_hk → NlSkýringarsetning
NlFrumlag_ef_et_p3_hk → NlSkýringarsetning
NlFrumlag/fall/tala/kyn → Nl/fall/tala/kyn
NlFrumlag_nf_et_hk → NlSkýringarsetning
NlFrumlag_þf_et_hk → NlSkýringarsetning
NlFrumlag_þgf_et_hk → NlSkýringarsetning
NlFrumlag_ef_et_hk → NlSkýringarsetning
NlFrumlag/fall/tala → Nl/fall/tala
NlFrumlag_nf_et → NlSkýringarsetning
NlFrumlag_þf_et → NlSkýringarsetning
NlFrumlag_þgf_et → NlSkýringarsetning
NlFrumlag_ef_et → NlSkýringarsetning
NlFrumlag/fall/tala/pers → Nl/fall/tala/pers
NlFrumlag_nf_et_p3 → NlSkýringarsetning
NlFrumlag_þf_et_p3 → NlSkýringarsetning
NlFrumlag_þgf_et_p3 → NlSkýringarsetning
NlFrumlag_ef_et_p3 → NlSkýringarsetning
NlBeintAndlag/fall → Nl/fall | NlSpurnaraukasetning | NlSkýringarsetning
NlBeintAndlag/fall/tala/pers/kyn → Nl/fall/tala/pers/kyn
NlBeintAndlag_nf_et_p3_hk → NlSpurnaraukasetning | NlSkýringarsetning
NlBeintAndlag_þf_et_p3_hk → NlSpurnaraukasetning | NlSkýringarsetning
NlBeintAndlag_þgf_et_p3_hk → NlSpurnaraukasetning | NlSkýringarsetning
NlBeintAndlag_ef_et_p3_hk → NlSpurnaraukasetning | NlSkýringarsetning
NlBeintAndlag/fall/tala/pers → Nl/fall/tala/pers
NlBeintAndlag_nf_et_p3 → NlSpurnaraukasetning | NlSkýringarsetning
NlBeintAndlag_þf_et_p3 → NlSpurnaraukasetning | NlSkýringarsetning
NlBeintAndlag_þgf_et_p3 → NlSpurnaraukasetning | NlSkýringarsetning
NlBeintAndlag_ef_et_p3 → NlSpurnaraukasetning | NlSkýringarsetning
NlBeintAndlag_nf_þf/tala → Nl_nf_þf/tala | NlSpurnaraukasetning | NlSkýringarsetning
NlÓbeintAndlag/fall → Nl/fall | NlSkýringarsetning
NlSagnfylling/fall → Nl/fall | NlSkýringarsetning
NlSagnfylling/fall/tala/pers/kyn → Nl/fall/tala/pers/kyn
NlSagnfylling_nf_et_p3_hk → NlSkýringarsetning
NlSagnfylling_þf_et_p3_hk → NlSkýringarsetning
NlSagnfylling_þgf_et_p3_hk → NlSkýringarsetning
NlSagnfylling_ef_et_p3_hk → NlSkýringarsetning

$score(+1) NlBeintAndlag/fall
$score(+1) NlBeintAndlag/fall/tala/pers
$score(+1) NlBeintAndlag/fall/tala/pers/kyn
$score(+1) NlBeintAndlag_nf_þf/tala
$score(+1) NlFrumlag/fall
$score(+1) NlFrumlag/fall/tala
$score(+1) NlFrumlag/fall/tala/kyn
$score(+1) NlFrumlag/fall/tala/pers
$score(+1) NlFrumlag/fall/tala/pers/kyn
$score(+1) NlSagnfylling/fall
$score(+1) NlSagnfylling/fall/tala/pers/kyn
$score(+1) NlÓbeintAndlag/fall

# Nafnliðarkjarni

Einkunn/tala/fall/kyn →

    LoLiður/tala/fall/kyn       # glæsileg (kona), bókfært (virði) (so_lhþt), leiðandi (fyrirtæki) (so_lh_nt)
    # AtviksliðurEinkunn undanskilur atviksorð sem geta líka verið forsetningar;
    # það dregur verulega úr margræðni
    | AtviksliðurEinkunn        # rúmlega (helmingur)
    | Fornafn/tala/fall/kyn     # Sá (stóri maður), allt þetta (víkjandi lán)
    | TöluorðForskeyti/tala/fall/kyn? Töluorð/tala/fall/kyn     # [samtals] þrjá (gangandi menn) / [hátt í] 26
    | OkkarYkkar                # 'okkar skoðanir', 'ykkar félag' (okkar og ykkar eru pfn, ekki fn)
    | GreinirMeðLo/tala/fall/kyn 

# 'Skjölin voru mörg hundruð síður', 'Í húsinu voru fleiri þúsund hænur', 'hún saknaði mörg hundruð hálsmena'
Einkunn_ft/fall/kyn →
    HundruðForskeyti Hundruð

$score(+4) Einkunn/tala/fall/kyn

HundruðForskeyti → 'margur:lo'_sb_hk_nf_ft | 'margur:lo'_mst_hk_nf_ft # mörg, fleiri

Hundruð → 'hundrað:hk'_nf_ft | 'þúsund:hk'_nf_ft # hundruð, þúsund

# Lýsingarorð eða eignarfallsliður sem kemur á eftir nafnlið

NlViðbót/tala/fall/kyn →
    LoEftirNl/tala/fall/kyn     # (Jón) sterki / (Jón), sultuslakur eftir gufubaðið,
    | EfLiður LoEftirNlMeðÞgf/fall/tala/kyn? # (forstöðumenn) fræðaseturs [sunnlendinga] [tengdir flokknum]

# Eintala

NlKjarni_et/fall/kyn →

    # 'fullorðni sprúðlandi sonur Íslands'

    EfLiðurForskeyti?           # 1. deildar (lið Vals)
    Einkunn_et/fall/kyn*        # leiðandi (fyrirtæki) / bókfært (virði)
    Fyrirbæri/fall/kyn          # Már Guðmundsson seðlabankastjóri
    NlViðbót_et/fall/kyn?       # (Jón) sterki / (Jón), sultuslakur eftir gufubaðið, / (sonur) skósmiðsins
    Númer?                      # (ákvæði laga) nr. 33

NlKjarni_et_kk/fall →
    tala_kk/fall 'hluti:kk'_et/fall EfLiður? # 1/3 hluti Alþingismanna (getur krafist atkvæðagreiðslu)

NlKjarni_et_nf_kk → NlKjarni_et_kk_nf
NlKjarni_et_þf_kk → NlKjarni_et_kk_þf
NlKjarni_et_þgf_kk → NlKjarni_et_kk_þgf
NlKjarni_et_ef_kk → NlKjarni_et_kk_ef

# Fleirtala

NlKjarni_ft/fall/kyn →

    EfLiðurForskeyti?
    Einkunn_ft/fall/kyn*
    Fyrirbæri_ft/fall/kyn
    NlViðbót_ft/fall/kyn?
    Númer?

Fyrirbæri_ft/fall/kyn →
    Skst? no_ft/fall/kyn                            # [Commodore PET] tölvur
    | Skst? no_ft_gr/fall/kyn Fjöldi_ft/fall/kyn    # Mennina tíu / félögin tvö
    | FyrirbæriMeðGreini_ft/fall/kyn

Númer → 'númer:hk' tala

Fjöldi_ft/fall/kyn → to_ft/fall/kyn | töl_ft/fall/kyn

# Persónufornöfn þriðju persónu eintölu, karl- og kvenkyn
# Þessi orð eru ekki merkt með kyni í BÍN;
# þess vegna þurfa þau sérstaka meðhöndlun hér

NlKjarni_et_nf_kk → 'hann:pfn'_kk_et_nf
NlKjarni_et_þf_kk → 'hann:pfn'_kk_et_þf
NlKjarni_et_þgf_kk → 'hann:pfn'_kk_et_þgf
NlKjarni_et_ef_kk → 'hann:pfn'_kk_et_ef

NlKjarni_et_nf_kvk → 'hún:pfn'_kvk_et_nf
NlKjarni_et_þf_kvk → 'hún:pfn'_kvk_et_þf
NlKjarni_et_þgf_kvk → 'hún:pfn'_kvk_et_þgf
NlKjarni_et_ef_kvk → 'hún:pfn'_kvk_et_ef

# Fleirtölumyndir 3. persónu eru í BÍN undir 'sá'

# Afturbeygt fornafn, eins í tölum og kynjum, aðeins til í aukaföllum 3p.

# sjálfan sig / sjálfa sig
# sjálfum sér / sjálfum sér
# sjálfs sín  / sjálfra sín

NlKjarni/tala/aukafall/kyn →
    'sjálfur:fn'/tala/aukafall/kyn abfn/aukafall/tala/kyn # Hér vitum við tölu og kyn
    | abfn/aukafall # Hér vitum við hvorki tölu né kyn

# Mörg/flest þessara mála, sum þeirra sem lentu í slysinu
# !!! TODO bæta öðrum fornöfnum sem taka hlutaeignarfall við
NlKjarni_ft/fall/kyn →
    'margur:lo'_sb_ft/fall/kyn EfLiður_ft/kyn
    | 'allmargur:lo'_sb_ft/fall/kyn EfLiður_ft/kyn
    | 'fár:lo'_sb_ft/fall/kyn EfLiður_ft/kyn
    | 'örfár:lo'_sb_ft/fall/kyn EfLiður_ft/kyn
    | 'sumur:fn'_ft/fall/kyn EfLiður_ft/kyn

# Prósentubil með eignarfalli

NlKjarni_ft/fall/kyn →
    Prósentubil EfLiður_ft/kyn # 10-25% kvenna eru galvaskar

NlKjarniPrósent_hk →
    Prósentubil EfLiður_ft/kyn # 10-25% kvenna eru galvösk

NlKjarni_ft_nf_hk → NlKjarniPrósent_hk
NlKjarni_ft_þf_hk → NlKjarniPrósent_hk
NlKjarni_ft_þgf_hk → NlKjarniPrósent_hk
NlKjarni_ft_ef_hk → NlKjarniPrósent_hk

# Umraðanir

NlStak_p3/fall/kyn → NlStak_et_p3/fall/kyn | NlStak_ft_p3/fall/kyn

NlStak_et_ef/kyn → NlStak_et_p1_ef/kyn | NlStak_et_p2_ef/kyn | NlStak_et_p3_ef/kyn
NlStak_ft_ef/kyn → NlStak_ft_p1_ef/kyn | NlStak_ft_p2_ef/kyn | NlStak_ft_p3_ef/kyn

# Nafnliðir

NlStak_et_p1/fall/kyn →
    'ég:pfn'_et/fall 'sjálfur:fn'_et/fall/kyn? FsRunaEftirNl? # Ég í öllu mínu veldi / (Hvað) mig sjálfan (varðar)
    | 'sjálfur:fn'_et/fall/kyn 'ég:pfn'_et/fall FsRunaEftirNl? # Sjálfan mig hef ég aldrei þolað

NlStak_et_p2/fall/kyn →
    'þú:pfn'_et/fall 'sjálfur:fn'_et/fall/kyn? FsRunaEftirNl? # Þú [sjálfur] ert ekki sem verstur

NlStak_p1_ft/fall/kyn →
    'ég:pfn'_ft/fall NlStakViðbót/fall/kyn?     # Við gömlu hjónin, við Sigmundur, við flest, við sjálfar
    | 'vér:pfn'_ft/fall NlStak_ft_p3/fall/kyn?  # Vér fóstbræður
    | NlEind/fall KommaOgEða 'ég:pfn'_et/fall   # Þú og ég fórum, Jón og ég fórum, stelpurnar og ég fórum
    | 'ég:pfn'_et/fall KommaOgEða NlEind/fall   # Ég og þú fórum, ég og Jón fórum, ég og stelpurnar fórum

NlStakViðbót/fall/kyn →
    NlStakViðbótNl/fall/kyn
    | 'allur:fn'_ft/fall/kyn            # við öll, okkur alla, okkar allra
    | 'sumur:fn'_ft/fall/kyn            # við sum, okkur sumra, okkar sumra
    | 'margur:lo'_ft/fall/kyn           # við mörg/flest, okkur margra/flestra, okkar margra/flestra
    | 'sjálfur:fn'_ft/fall/kyn          # við sjálf, okkur sjálfra, okkar sjálfra

$score(+8) NlStakViðbót/fall/kyn

NlStakViðbótNl/fall/kyn →
    NlStak_ft_p3/fall/kyn               # við fóstbræður, við alþingismennirnir

NlStakViðbótNl/fall/nkyn →
    Manneskja/fall/nkyn                 # (við) Sóley Tómasdóttir (fórum), (hann ræddi við okkur) Pál dómkirkjuprest

$score(-20) NlStakViðbótNl/fall/kyn     # Draga af stig fyrir "við fóstbræður" vegna hættu á ruglingi við forsetninguna 'við'

NlStak_ft_p1/fall/kyn → NlStak_p1_ft/fall/kyn FsRunaEftirNl?

NlStak_p2_ft/fall/kyn →
    'þú:pfn'_ft/fall NlStakViðbót/fall/kyn?     # þið öll, þið flest, þið stelpurnar, þið eplin
    | 'þér:pfn'_ft/fall NlStak_ft_p3/fall/kyn?  # Þér eplin
    | NlEind/fall KommaOgEða 'þú:pfn'_et/fall   # Jón og þú fóruð, stelpurnar og þú fóruð
    | 'þú:pfn'_et/fall KommaOgEða NlEind/fall   # Þú og Jón fóruð, þú og stelpurnar fóruð

NlStak_ft_p2/fall/kyn → NlStak_p2_ft/fall/kyn FsRunaEftirNl?

# !!! ATH: reglurnar hér fyrir ofan leyfa 'ég og þú fóruð til Frakklands'

# Sameiginlegar reglur fyrir 3p. eintölu og fleirtölu

NlStak_p3/tala/fall/kyn →

    NlKjarni/tala/fall/kyn Fornafn/tala/fall/kyn*           # (stjórnarframkvæmdum) þessum öllum
    | NlKjarni/tala/fall/kyn EignarFornafn/tala/fall/kyn KommaOgEðaEn EfLiður # skattskýrslur mínar og eiginkonu minnar
    | NlFornafn/tala/fall/kyn
    | Pfn/kyn/tala/fall to/tala/fall/kyn?                   # 'þið tvær', 'við fjögur', 'hann einn'
    | LoTöluorð_ef 'konar:kk'_ef_et NlKjarni/tala/fall/kyn  # 'tvenns/þrenns/ferns konar innstungur'
    | Raðnr/fall/kyn NlKjarni/tala/fall/kyn Fornafn/tala/fall/kyn*   # 1. verðlaunin öll, 1.-3. sætið, 10. og 12. árið

LoTöluorð_ef →
    'tvennur:lo'_ef_et_hk
    | 'þrennur:lo'_ef_et_hk
    | 'fern:lo'_ef_et_hk

# Feðgarnir séra Jón Steinsson [í Víðihlíð] og Arnaldur Jónsson bóndi
Skyldleiki_kk/fall → 'feðgar:kk'_gr_ft/fall | 'bróðir:kk'_gr_ft/fall | 'frændi:kk'_gr_ft/fall
# Mæðgurnar frú Jóhanna Sigurjónsdóttir og Anna Kristjánsdóttir
Skyldleiki_kvk/fall → 'mæðgur:kvk'_gr_ft/fall | 'systir:kvk'_gr_ft/fall | 'frænka:kvk'_gr_ft/fall
# Systkinin Jón og Anna, feðginin Friðmar og Jóna
Skyldleiki_hk/fall → 'systkin:hk'_gr_ft/fall | 'frændsystkin:hk'_gr_ft/fall | 'mæðgin:hk'_gr_ft/fall | 'feðgin:hk'_gr_ft/fall

Fjölskylda_ft/nkyn/fall → Skyldleiki/nkyn/fall Manneskja/fall/nkyn FsRuna? "og:st" Manneskja/fall/nkyn
Fjölskylda_ft_hk/fall → Skyldleiki_hk/fall Manneskja/fall FsRuna? "og:st" Manneskja/fall

NlStak_ft_p3/fall/kyn → Fjölskylda_ft/kyn/fall FsRunaEftirNl?

$score(+8) Fjölskylda_ft/kyn/fall

# Sértilvik fyrir "vinstri-græn" (ath: stutt bandstrik, ekki en- eða em-dash)

NlStak_ft_p3_nf_hk → 'vinstri:lo'_ft_nf_hk "-"? 'grænn:lo'_ft_nf_hk
NlStak_ft_p3_þf_hk → 'vinstri:lo'_ft_þf_hk "-"? 'grænn:lo'_ft_þf_hk
NlStak_ft_p3_þgf_hk → 'vinstri:lo'_ft_þgf_hk "-"? 'grænn:lo'_ft_þgf_hk
NlStak_ft_p3_ef_hk → 'vinstri:lo'_ft_ef_hk "-"? 'grænn:lo'_ft_ef_hk

# 'hvorugt okkar systkinanna', 'enginn okkar', 'einhver ykkar bræðranna', 'einn liðsmanna', 'annar mannanna'
NlStak_et_p3/fall/kyn → OkkarFornafn/fall/kyn OkkarFramhald/kyn

OkkarFramhald/kyn →
    'ég:pfn'_ft_ef Nl_ef_ft/kyn?
    | Nl_ef_ft/kyn

OkkarFornafn/fall/kyn →
    'hvorugur:fn'_et/fall/kyn | 'enginn:fn'_et/fall/kyn | 'einhver:fn'_et/fall/kyn
    | 'einn:fn'_et/fall/kyn | 'sérhver:fn'_et/fall/kyn
    | 'annar:fn'_et/fall/kyn 'hvor:fn'_et/fall/kyn?

# Eignarfornöfn standa með nafnlið í sama falli og hann,
# þannig að gera þarf undantekningu fyrir þau þegar þau
# tengjast eignarfallslið ('Ég seldi ekki íbúðina þína, heldur frænda þíns')
EignarFornafn/tala/fall/kyn →
    'minn:fn'/tala/fall/kyn
    | 'þinn:fn'/tala/fall/kyn

NlFornafn/tala/fall/kyn →
    # Stakt fornafn, e.t.v. með einkunn og töluorði
    # 'augljóslega sá vondi' / 'einhverjir tveir góðir' / 'ekkert uppbyggilegt' / 'fjórir aðrir'
    # 'þeir báðir' / 'hún öll' / 'þær sjálfar'
    # 'öllu því' / 'neinum þeirra'
    AtviksliðurEinkunn* Töluorð/tala/fall/kyn?
        Fornafn/tala/fall/kyn FornafnViðskeyti/tala/fall/kyn? LoLiður/tala/fall/kyn*
    # 'okkar besta' / 'óumdeilanlega ykkar versta'
    | AtviksliðurEinkunn* OkkarYkkar LoLiður/tala/fall/kyn
    | AtviksliðurEinkunn* 'margur:fn'/tala/fall/kyn 'annar:fn'/tala/fall/kyn

Pfn_kk/tala/fall → 'hann:pfn'_kk/tala/fall
Pfn_kvk/tala/fall → 'hún:pfn'_kvk/tala/fall
Pfn_hk/tala/fall → 'það:pfn'_hk/tala/fall

Pfn/tala/fall/kyn → Pfn/kyn/tala/fall

FornafnViðskeyti/tala/fall/kyn →
    AtviksliðurAuka? LoFornafn/fall/tala/kyn # (hafa þeir) [þó] báðir (lýst yfir sigri)
    | Pfn/kyn/tala/fall     # öllu því, ýmsum þeim
    | Töluorð/tala/fall/kyn # öllum fjórum, þeirra tveggja

FornafnViðskeyti_et/fall/kyn →
    # neinum þeirra þriggja, öllum þeirra, hvern þeirra fjögurra
    Pfn_ft_ef/kyn Töluorð_ft_ef/kyn? FornafnViðskeytiLo_ft_ef/kyn?

FornafnViðskeytiLo_ft_ef/kyn →
    AtviksliðurEinkunn* lo_vb_ft_ef/kyn # hvern þeirra fjögurra langsamlega sigursælustu

FornafnViðskeyti_ft/fall/kyn →
    HverHvor/fall/kyn       # þá hvern fyrir sig / þær hverja í sínu lagi / öll hvert á fætur öðru

# Sértilvik þar sem "allt" kemur framan við fornafn óháð tölu, falli og kyni
# allt annar | allt aðrir
# allt önnur | allt aðrar
# allt annað | allt önnur
NlFornafn/tala/fall/kyn →
    'allur:fn'_et_nf_hk 'annar:fn'/tala/fall/kyn

# Sértilvik þar sem lýsingarorðin "margur"/"fjölmargur" standa
# fyrir framan fornafn og úr verður sjálfstæður nafnliður.
# "flestur" er fornafn í BÍN og þarf ekki sérreglu.
NlFornafn/tala/fall/kyn →
    # margt annað / mörgu öðru / margs annars
    # margir aðrir / marga aðra / mörgum öðrum / margra annarra
    # fáir aðrir / örfáir aðrir
    'margur:lo'_sb/tala/fall/kyn 'annar:fn'/tala/fall/kyn
    | 'fjölmargur:lo'_sb/tala/fall/kyn 'annar:fn'/tala/fall/kyn
    | 'fár:lo'_sb/tala/fall/kyn 'annar:fn'/tala/fall/kyn
    | 'örfár:lo'_sb/tala/fall/kyn 'annar:fn'/tala/fall/kyn

# Sértilvik þar sem "ýmislegt annað" er sjálfstæður nafnliður:
# 'Ýmislegt annað (bendir til þess að Jón sé þjófurinn)'
# Þetta virðist aðeins vera hægt í eintölu, ekki fleirtölu.
NlFornafn_et_nf_hk → ÝmislegtAnnað_nf
NlFornafn_et_þf_hk → ÝmislegtAnnað_þf
NlFornafn_et_þgf_hk → ÝmislegtAnnað_þgf
NlFornafn_et_ef_hk → ÝmislegtAnnað_ef

ÝmislegtAnnað/fall →
    'ýmislegur:lo'_sb_et_hk/fall 'annar:fn'_et_hk/fall

$score(+15) ÝmislegtAnnað/fall

OkkarYkkar → "okkar:pfn" | "ykkar:pfn" # Þarf að kallast pfn til að greinast.

# Gefa stökum fornöfnum mínusstig - við viljum frekar sjá þau sem hluta af stærri nafnlið
$score(-4) NlFornafn/tala/fall/kyn

GreinirMeðLo/tala/fall/kyn →
    Greinir/tala/fall/kyn LoLiður/tala/fall/kyn

Greinir/tala/fall/kyn →
    'hinn:gr'/tala/fall/kyn

$score(+6) GreinirMeðLo/tala/fall/kyn

# Lýsingarorð sem stendur sem nafnliður:
# 'Margir/fjölmargir/allmargir/heilmargir komu og aðstoðuðu Pál'
# 'Páll aðstoðaði töluvert marga'
# 'Óleyfilegt er að mismuna samkynhneigðum'

NlStak_p3/tala/fall/kyn →
    AtviksliðurEinkunn* LoSemNafnliður/tala/fall/kyn

# 'Fleiri/færri komu í veisluna en ég bjóst við'
# 'Fleirum fannst kvikmyndin leiðinleg'
NlStak_p3_ft/fall/kyn →
    'margur:lo'_mst_ft/fall/kyn
    | 'fár:lo'_mst_ft/fall/kyn

NlStak_p3/tala/fall/kyn →
    Magn/fall/tala/kyn

Magn/fall/tala/kyn →
    TöluorðForskeyti/tala/fall/kyn* mælieining # rúmlega 690 MW
    | TöluorðForskeyti/tala/fall/kyn* Töluorð/tala/fall/kyn Mælieining/kyn/tala/fall # tæplega fjögurra tonna

Mælieining_hk/tala/fall →
    MælieiningÞyngd_hk/tala/fall

Mælieining_kvk/tala/fall →
    MælieiningHiti_kvk/tala/fall

Mælieining_kk/tala/fall →
    MælieiningLengd_kk/tala/fall
    | MælieiningRúmmál_kk/tala/fall
    | MælieiningFlatarmál_kk/tala/fall

MælieiningÞyngd_hk/tala/fall →
    'kíló:hk'/tala/fall
    | 'tonn:hk'/tala/fall
    | 'gramm:hk'/tala/fall
    | 'kílógramm:hk'/tala/fall
    | 'míkrógramm:hk'/tala/fall
    | 'milligramm:hk'/tala/fall
    | 'millígramm:hk'/tala/fall

MælieiningHiti_kvk/tala/fall →
    'gráða:kvk'/tala/fall

MælieiningLengd_kk/tala/fall →
    'metri:kk'/tala/fall
    | 'kílómetri:kk'/tala/fall
    | 'millimetri:kk'/tala/fall
    | 'millímetri:kk'/tala/fall
    | 'sentimetri:kk'/tala/fall
    | 'sentímetri:kk'/tala/fall

MælieiningRúmmál_kk/tala/fall →
    'rúmmetri:kk'/tala/fall
    | 'rúmsentimetri:kk'/tala/fall
    | 'rúmsentímetri:kk'/tala/fall
    | 'lítri:kk'/tala/fall
    | 'hektólítri:kk'/tala/fall
    | 'desilítri:kk'/tala/fall
    | 'desílítri:kk'/tala/fall
    | 'sentilítri:kk'/tala/fall
    | 'sentílítri:kk'/tala/fall
    | 'millilítri:kk'/tala/fall
    | 'millílítri:kk'/tala/fall

MælieiningFlatarmál_kk/tala/fall →
    'fermetri:kk'/tala/fall
    | 'fersentímetri:kk'/tala/fall
    | 'fersentimetri:kk'/tala/fall

$score(+8) Magn_nf/tala/kyn
$score(+8) Magn_þf/tala/kyn
$score(+8) Magn_þgf/tala/kyn
$score(+16) Magn_ef/tala/kyn # Gefa eignarfalli aukastig til að hvetja til forskeytis við nafnlið

# Unnt er að hafa nafnlið í þágufalli á undan lýsingarorðinu:
# '(sem varðar hagsmuni hans eða) honum [verulega] nákominna'
# Að svo komnu máli er slíkt aðeins leyft með lýsingarorðum í eignarfalli
# enda óljóst að þörf sé fyrir önnur föll.

NlStak_p3_et_ef/kyn →
    Nl_þgf AtviksliðurEinkunn* LoSemNafnliður_et_ef/kyn

NlStak_p3_ft_ef/kyn →
    Nl_þgf AtviksliðurEinkunn* LoSemNafnliður_ft_ef/kyn

LoSemNafnliður/tala/fall/kyn →
    lo_sb/tala/fall/kyn | lo_mst/tala/fall/kyn # Efsta stig (ESB) er innifalið í _sb
    | AlgengLoSemNafnliður_kk/tala/fall
    | AlgengLoSemNafnliður_hk_et/fall

# Margir telja að myndin sé falleg / Margur telur mig sig
# Fáir álíta að flokkurinn eigi möguleika
# Þeir vita mikið / Mikið af því sem Jón sagði er della / Ég vænti mikils af honum
AlgengLoSemNafnliður_kk_ft/fall →
    'margur:lo'_sb_ft_kk/fall           # margir telja
    | 'fjölmargur:lo'_sb_ft_kk/fall     # fjölmargir telja
    | 'allmargur:lo'_sb_ft_kk/fall      # allmargir telja
    | 'allflestur:lo'_sb_ft_kk/fall     # allflestir telja
    | 'langflestur:lo'_sb_ft_kk/fall    # langflestir telja
    | 'fár:lo'_sb_ft_kk/fall            # fáir / fæstir telja
    | 'örfár:lo'_sb_ft_kk/fall          # örfáir telja
AlgengLoSemNafnliður_kk_et/fall →
    'margur:lo'_sb_et_kk/fall           # margur telur
AlgengLoSemNafnliður_hk_et/fall →
    'mikill:lo'_sb_et_hk/fall           # þeir vita mikið / ég vænti mikils af honum

# Gefa lýsingarorðum sem nafnliðum mínusstig
$score(-14) LoSemNafnliður/tala/fall/kyn

# ...en hlífa þó lýsingarorðum sem standa oft sjálfstætt sem nafnliðir
$score(+10) AlgengLoSemNafnliður_kk/tala/fall
$score(+10) AlgengLoSemNafnliður_hk_et/fall

NlStak_p3/tala/fall/kyn →
    'hver:fn'/tala/fall/kyn 'sá:fn'/tala/fall/kyn # Hver sá, hver sú, hvert það, hverjir þeir, hverjar þær, hver þau
    | 'hvor:fn'/tala/fall/kyn 'tveir:to'_ft_ef/kyn  # (eða) hvor/hvort tveggja
    # '(Sýslumenn sinna) sama hlutverki og prestar'
    # '(Ríkissaksóknari nýtur) sömu verndar í starfi og dómarar'
    | 'sami:fn'/tala/fall/kyn Nl/fall/tala/kyn "og:st" Nl_nf

NlStak_p3/tala/fall/kyn →
    # 'Sú útskýring [núna], að Samfylkingin sé óalandi og óferjandi, (dugir ekki lengur)'
    'sá:fn'/tala/fall/kyn Nl/fall/tala/kyn AtvFs? Útskýring

NlStak_p3_et_nf_hk → NlHittOgÞetta_nf
NlStak_p3_et_þf_hk → NlHittOgÞetta_þf
NlStak_p3_et_þgf_hk → NlHittOgÞetta_þgf
NlStak_p3_et_ef_hk → NlHittOgÞetta_ef

NlHittOgÞetta/fall →
    'hinn:fn'_et_hk/fall "og:st" 'þessi:fn'_et_hk/fall  # hitt og þetta

$score(+16) NlHittOgÞetta/fall

# Töluorð sem sjálfstæðir nafnliðir

NlStak_p3/tala/fall/kyn →
    NlTöluorð/tala/fall/kyn

NlTöluorð/tala/fall/kyn →
    # '(Ég talaði við) tvær þeirra', 'fjórum þeirra (var kalt)'
    # 'Að minnsta kosti fjórir létust'
    # 'Um þrír af hverjum fjórum landsmönnum telja...'
    TöluorðForskeyti/tala/fall/kyn* TöluorðLo/tala? Töluorð/tala/fall/kyn 'sá:fn'_ft_ef/kyn?
    | TöluorðForskeyti/tala/fall/kyn* Töluorð/tala/fall/kyn TöluorðLo/tala  # (önnur af) þremur hröðustu (sem eftir standa)
    | 'sá:fn'_ft/fall/kyn? Töluorð/tala/fall/kyn EfLiður_ft/kyn?            # (ein af) þeim fjórum stúlknanna (sem stóðu sig best)

NlTöluorð_ft_nf_hk → Prósentubil
NlTöluorð_ft_þf_hk → Prósentubil
NlTöluorð_ft_þgf_hk → Prósentubil
NlTöluorð_ft_ef_hk → Prósentubil

Prósentubil → TöluorðForskeyti* tala "-:en" prósenta

$score(+4) Prósentubil

TöluorðForskeyti →
    'að_minnsta_kosti:ao' | "um:ao" | "minnst:ao" | "einungis:ao" | "aðeins:ao" | "ekki:ao"
    | "tæplega:ao" | "rúmlega:ao" | "ríflega:ao" | "einnig:ao" | "samtals:ao"
    | "næstum:ao" | "nálægt:ao" | "yfir:ao"
    # Fleiryrt forskeyti
    | "hátt:ao" "í:ao"
    | 'allur:fn'_et_nf_hk "að:ao" 
    | "innan:ao" "við:ao"
    | "svo:st" "sem:st"

TöluorðForskeyti/tala/fall/kyn →
    TöluorðForskeyti
    | lo_sb/tala/fall/kyn # tæpar/rúmar 115 milljónir

TöluorðForskeyti_ef_et_kk → TöluorðForskeyti_et_ef_kk
TöluorðForskeyti_ef_et_kvk → TöluorðForskeyti_et_ef_kvk
TöluorðForskeyti_ef_et_hk → TöluorðForskeyti_et_ef_hk
TöluorðForskeyti_ef_ft_kk → TöluorðForskeyti_ft_ef_kk
TöluorðForskeyti_ef_ft_kvk → TöluorðForskeyti_ft_ef_kvk
TöluorðForskeyti_ef_ft_hk → TöluorðForskeyti_ft_ef_hk

TöluorðLo/tala →
    lo_evb_nf_hk/tala # efstu tvo / efstu tvö / efstu tvær
    | lo_mst_nf_hk/tala # lægri tvo / lægri tvö / lægri tvær

# Gefa mínusstig fyrir stök töluorð - viljum helst sjá þau sem einkunn í stærri nafnlið
$score(-10) NlTöluorð/tala/fall/kyn
$score(+12) TöluorðForskeyti # Rétta mínusinn af ef fleira fylgir

# '1100 manns (voru á skemmtuninni)', '(Ég pantaði mat fyrir) sautján manns'
# Virðist ekki ganga upp í eignarfalli
# ('Ég lagði bílum 18 manns'?)
NlStak_p3_ft_nf_kk → Manns_nf
NlStak_p3_ft_þf_kk → Manns_þf
NlStak_p3_ft_þgf_kk → Manns_þgf

# Einkunnarorð eða tímaatviksorð
EoTao → eo | tao

# Hér ganga beygjanleg töluorð ekki (?'fjórir manns')
Manns/fall →
    EoTao* TöluorðForskeyti? töl_ft_kk/fall "manns:kk"
    | EoTao* TöluorðForskeyti? tala_ft_kk/fall "manns:kk"
    | EoTao* Talnabil "manns:kk" # 50-100 manns

# 3p. eintala

NlStak_et_p3/fall/kyn →

    NlStak_p3_et/fall/kyn VeikSögnLhÞt_et/fall/kyn? FsRunaEftirNl?  # fulltrúi bankans í nefndinni
    | NlStak_et_p3/fall/kyn "eða:st" NlStak_p3_et/fall              # Þingnefnd eða hluti Alþingismanna getur lagt fram frumvarp
    | 'hver:fn'_et/fall/kyn "og:st" 'einn:fn'_et/fall/kyn FsRunaEftirNl?   # 'hver og einn [í sinni sveit] (ber ábyrgð)'

# '(svör við bréfi sínu) dagsettu (13. mars sl.)'
VeikSögnLhÞt/tala/fall/kyn →
    Atviksliður? EinVeikSögnLhÞt/tala/fall/kyn                      # (Stúlkan sá pabba sinn) [vel og snyrtilega] rakaðan
    | ',' so_lhþt/tala/fall/kyn FsAtv? LokatáknEðaKomma

EinVeikSögnLhÞt/tala/fall/kyn → so_lhþt/tala/fall/kyn

$score(-4) EinVeikSögnLhÞt/tala/fall/kyn

# Meðhöndla "árið 2012" og "árin 2013 og 2014" með háum forgangi

NlStak_et_p3_nf_hk → Árið_nf
NlStak_et_p3_þf_hk → Árið_þf
NlStak_et_p3_þgf_hk → Árið_þgf
NlStak_et_p3_ef_hk → Árið_ef

Árið/fall →
    ártal
    | 'rekstrarár:hk'_gr_et/fall tala
    | 'skólaár:hk'_gr_et/fall tala

$score(+16) Árið/fall

NlStak_ft_p3_nf_hk → Árin_nf
NlStak_ft_p3_þf_hk → Árin_þf
NlStak_ft_p3_þgf_hk → Árin_þgf
NlStak_ft_p3_ef_hk → Árin_ef

Árin/fall → 
    ártal ÁraRuna* Til tala                          # (Ég á hér við) 1987, 1988 og 1989
    | 'ár:hk'_ft_gr/fall tala ÁraRuna* Til tala      # (Hann ræddi) árin 670-80, (Bókin tekur til) áranna 2016 t.o.m. 2017

ÁraRuna → ',' tala

$score(+20) Árin/fall

# Leyfa frístandandi sérnöfn (skrifuð sem runa orða með stórum staf)
# sem nafnliði 3p. eintölu hvorugkyns, í öllum föllum
# Orka Energy, OG Capital

NlStak_p3_et_nf_hk → NlSérnafn
NlStak_p3_et_þf_hk → NlSérnafn
NlStak_p3_et_þgf_hk → NlSérnafn
NlStak_p3_et_ef_hk → NlSérnafn

# Sérnöfn þurfa einnig að geta staðið í fleirtölu,
# sbr. 'Valsmenn skoruðu flest mörk', 'Dodgers áttu sigurinn vísan',
# 'Essenar voru trúflokkur'

NlStak_p3_ft_nf_hk → NlSérnafn_ft
NlStak_p3_ft_þf_hk → NlSérnafn_ft
NlStak_p3_ft_þgf_hk → NlSérnafn_ft
NlStak_p3_ft_ef_hk → NlSérnafn_ft

NlSérnafn_ft → NlSérnafn

# Gefa mínus fyrir fleirtölu m.v. eintölu
$score(-2) NlSérnafn_ft

# !!! no_ef er notað í stað EfLiður til að minnka margræðni þegar
# !!! mörg sérnöfn standa í röð
NlSérnafn → EoTao* Sérnafn NlSérnafnEf? # ekki Nakamoto, örugglega Nelson Mandela, Hong Kong norðursins

NlSérnafnEf → no_ef

# Forðast að nota sérnöfn sem nafnliði, ef aðrir kostir eru í boði
$score(-16) NlSérnafn

Sérnafn →
    sérnafn tala? | entity tala? | LangtSérnafn

$tag(apply_length_bonus) Sérnafn # Gefa bónus eftir því hversu margar eindir (tokens) falla undir sérnafnið

# ATH: eitt 'entity' getur verið fleiri en eitt samliggjandi orð.
# Ekki ætti að hafa samliggjandi 'entity' í málfræðireglu.
# Hér eru höfð stutt bandstrik milli nafna, ekki en- eða em-dash.

LangtSérnafn →
    sérnafn "-"? sérnafn+ tala?             # Glass-Steagall lögin
    | sérnafn+ "-"? entity+ sérnafn* tala?  # Eining með nafni sem þekkt er úr gagnagrunni eða runu slíkra
    | entity+ "-"? sérnafn+ tala?           # Eining með nafni sem þekkt er úr gagnagrunni eða runu slíkra
    | entity "-" entity tala?               # Villeneuve-Loubet
    | entity+ "-"? sérnafn* tala? fyrirtæki # Standard Oil Corp.
    | sérnafn+ tala? fyrirtæki              # Ísgerð Dísu ehf.
    | entity entity+ tala?                  # !!! Til prófunar

Sérnafn/fall →
    sérnafn/fall tala? | LangtSérnafn/fall tala?

$tag(apply_length_bonus) Sérnafn/fall # Gefa bónus eftir því hversu margar eindir (tokens) falla undir sérnafnið

LangtSérnafn/fall →
    sérnafn/fall "-"? sérnafn/fall+ tala?               # Stóra-Felli
    | sérnafn/fall+ "-"? entity+ sérnafn/fall* tala?    # Eining með nafni sem þekkt er úr gagnagrunni eða runu slíkra
    | entity+ "-"? sérnafn/fall+ tala?                  # Eining með nafni sem þekkt er úr gagnagrunni eða runu slíkra

# Leyfa löngu sérnafni í nefnifalli að passa við langt sérnafn án falls
LangtSérnafn_nf →
    LangtSérnafnNf

LangtSérnafnNf →
    LangtSérnafn

$score(-12) LangtSérnafnNf

# Löng sérnöfn, þ.e. runur orða með upphafsstöfum, eru mjög líklega rétt greind

$score(+6) LangtSérnafn
$score(+10) LangtSérnafn/fall

# Heimilisfang (götuheiti + tala)

NlStak_et_p3/fall/kyn → Heimilisfang/fall/kyn

Heimilisfang/fall/kyn → gata/fall/kyn tala # !!! TODO: húsnúmer (7a, 13C)

$score(+4) Heimilisfang/fall/kyn

NlStak_p3_et/fall → NlStak_p3_et/fall/kyn # Samtenging tveggja eintöluliða með 'eða' krefst ekki sama kyns báðum megin

# 3p. fleirtala

NlStak_ft_p3/fall/kyn →
    NlStak_p3_ft/fall/kyn VeikSögnLhÞt_ft/fall/kyn? FsRunaEftirNl?  # fulltrúar bankans í nefndinni
    | 'allur:fn'_ft/fall/kyn 'sá:fn'_ft/fall/kyn                    # Allir þeir, allar þær, öll þau

# Hvorki X né Y
# Bæði X og Y
# Annaðhvort X eða Y
# Annað hvort X eða Y
# Ýmist X eða Y

# Sama kyn á X og Y

NlStak_ft_p3/fall/kyn → NlBæðiHvorki/fall/kyn

NlBæðiHvorki/fall/kyn →
    "bæði:st" Nl/fall/kyn "og:st" Nl/fall/kyn                   # (Við þurftum að lóga) bæði hestinum og kettinum
    | "hvorki:st" Nl/fall/kyn "né:st" Nl/fall/kyn               # Hvorki hestinum né kettinum (var lógað)
    | "annaðhvort:st" Nl/fall/kyn "eða:st" Nl/fall/kyn          # Annaðhvort hestinum eða kettinum (var lógað)
    | 'annar:fn'_et_hk_nf 'hvor:fn'_et_hk_nf Nl/fall/kyn "eða:st" Nl/fall/kyn    # Annaðhvort hestinum eða kettinum (var lógað)
    | "ýmist:st" Nl/fall/kyn "eða:st" Nl/fall/kyn               # Ýmist hestinum eða kettinum (var lógað)
$score(+4) NlBæðiHvorki/fall/kyn

NlStak_ft_p3_nf_hk → NlStak_ft_p3_hk_nf
NlStak_ft_p3_þf_hk → NlStak_ft_p3_hk_þf
NlStak_ft_p3_þgf_hk → NlStak_ft_p3_hk_þgf
NlStak_ft_p3_ef_hk → NlStak_ft_p3_hk_ef

Nl/kyn/fall → Nl/fall/kyn

# Mismunandi kyn á X og Y, útkoman er hvorugkyns

NlStak_ft_p3_hk/fall → NlBæðiHvorki_p3_hk/fall

NlBæðiHvorki_p3_hk/fall →

    "bæði:st" Nl_kk/fall "og:st" Nl_kvk/fall
    | "bæði:st" Nl_kvk/fall "og:st" Nl_kk/fall
    | "bæði:st" Nl_kk/fall "og:st" Nl_hk/fall
    | "bæði:st" Nl_hk/fall "og:st" Nl_kk/fall
    | "bæði:st" Nl_kvk/fall "og:st" Nl_hk/fall
    | "bæði:st" Nl_hk/fall "og:st" Nl_kvk/fall

    | "hvorki:st" Nl_kk/fall "né:st" Nl_kvk/fall
    | "hvorki:st" Nl_kvk/fall "né:st" Nl_kk/fall
    | "hvorki:st" Nl_kk/fall "né:st" Nl_hk/fall
    | "hvorki:st" Nl_hk/fall "né:st" Nl_kk/fall
    | "hvorki:st" Nl_kvk/fall "né:st" Nl_hk/fall
    | "hvorki:st" Nl_hk/fall "né:st" Nl_kvk/fall

    | "annaðhvort:st" Nl_kk/fall "eða:st" Nl_kvk/fall
    | "annaðhvort:st" Nl_kvk/fall "eða:st" Nl_kk/fall
    | "annaðhvort:st" Nl_kk/fall "eða:st" Nl_hk/fall
    | "annaðhvort:st" Nl_hk/fall "eða:st" Nl_kk/fall
    | "annaðhvort:st" Nl_kvk/fall "eða:st" Nl_hk/fall
    | "annaðhvort:st" Nl_hk/fall "eða:st" Nl_kvk/fall

    | 'annar:fn'_nf_et_hk 'hvor:fn'_nf_et_hk Nl_kk/fall "eða:st" Nl_kvk/fall
    | 'annar:fn'_nf_et_hk 'hvor:fn'_nf_et_hk Nl_kvk/fall "eða:st" Nl_kk/fall
    | 'annar:fn'_nf_et_hk 'hvor:fn'_nf_et_hk Nl_kk/fall "eða:st" Nl_hk/fall
    | 'annar:fn'_nf_et_hk 'hvor:fn'_nf_et_hk Nl_hk/fall "eða:st" Nl_kk/fall
    | 'annar:fn'_nf_et_hk 'hvor:fn'_nf_et_hk Nl_kvk/fall "eða:st" Nl_hk/fall
    | 'annar:fn'_nf_et_hk 'hvor:fn'_nf_et_hk Nl_hk/fall "eða:st" Nl_kvk/fall

    | "ýmist:st" Nl_kk/fall "eða:st" Nl_kvk/fall
    | "ýmist:st" Nl_kvk/fall "eða:st" Nl_kk/fall
    | "ýmist:st" Nl_kk/fall "eða:st" Nl_hk/fall
    | "ýmist:st" Nl_hk/fall "eða:st" Nl_kk/fall
    | "ýmist:st" Nl_kvk/fall "eða:st" Nl_hk/fall
    | "ýmist:st" Nl_hk/fall "eða:st" Nl_kvk/fall

$score(+4) NlBæðiHvorki_p3_hk/fall

NlStak_ft_p1/fall/kyn → NlBæðiHvorki_p1_hk/fall

NlBæðiHvorki_p1_hk/fall →
    "bæði:st" Nl_p1/fall/kyn "og:st" Nl_p2/fall/kyn             # bæði mér og þér þykir þetta óásættanlegt
    | "hvorki:st" Nl_p1/fall/kyn "né:st" Nl_p2/fall/kyn         # hvorki ég né þú höfum vit á þessu
    | "annaðhvort:st" Nl_p1/fall/kyn "eða:st" Nl_p2/fall/kyn    # annaðhvort ég eða þú þurfum að fara
    | 'annar:fn'_nf_et_hk 'hvor:fn'_nf_et_hk Nl_p1/fall/kyn "eða:st" Nl_p2/fall/kyn # annað hvort ég eða þú þurfum að fara
    | "bæði:st" Nl_p2/fall/kyn "og:st" Nl_p1/fall/kyn           # bæði þér og mér þykir þetta óásættanlegt
    | "hvorki:st" Nl_p2/fall/kyn "né:st" Nl_p1/fall/kyn            # hvorki þú né ég erum fallegar/falleg/fallegir
    | "annaðhvort:st" Nl_p2/fall/kyn "eða:st" Nl_p1/fall/kyn    # annaðhvort þú eða ég erum fallegar/falleg/fallegir
    | 'annar:fn'_nf_et_hk 'hvor:fn'_nf_et_hk Nl_p2/fall/kyn "eða:st" Nl_p1/fall/kyn # annað hvort þú eða ég erum fallegar/falleg/fallegir
    | "ýmist:st" Nl_p2/fall/kyn "eða:st" Nl_p1/fall/kyn         # ýmist þú eða ég erum fallegar/falleg/fallegir

$score(+4) NlBæðiHvorki_p1_hk/fall

# Skammstafanir með bandstriki nota stutt bandstrik, ekki en- eða em-dash

Skst →
    Skst_abbrev
    | Sérnafn "-"?              # Barclays-bankann, Al Thani-málið

Skst_abbrev → no_abbrev "-"?    # FIH bankann

$score(+8) Skst
$score(+4) Skst_abbrev

Fyrirbæri/fall/kyn →
    Skst? no_et/fall/kyn        # Microsoft-hugbúnaðarrisinn, Nikon myndavél
    | FyrirbæriMeðGreini/fall/kyn
    | Úrslit/fall/kyn

# Raðnúmer

Raðnr/fall/kyn →
    raðnr/fall/kyn OgRaðnr/fall/kyn*
    | raðnr/fall/kyn Til raðnr/fall/kyn # 2.-3. sæti listans / 2. til 3. sæti listans

OgRaðnr/fall/kyn →
    "og:st" raðnr/fall/kyn

# Talnabil nota en-dash
Til → "-:en" | "til:st" | "og:st" | 'til:fs'_ef "og:st" 'með:fs'_þgf | 'til_og_með:ao'

# Úrslit í leikjum (32:27 sigur, 0:2-tap, 8:0 burst)

Úrslit →
    tala ':' tala

Úrslit/fall/kyn →
    Úrslit "-:en"? no_et/fall/kyn

$score(+2) Úrslit/fall/kyn

# nkyn = náttúrulegt kyn (karlkyn eða kvenkyn)
Fyrirbæri/fall/nkyn →
    Manneskja/fall/nkyn

# Fyrirtækisnöfn - hvorugkyn
# !!! Ath: Eiga fyrirtækisnöfn e.t.v. að vera NlKjarni, ekki Fyrirbæri?

Fyrirbæri_nf_hk →
    Fyrirtæki_nf
Fyrirbæri_þf_hk →
    Fyrirtæki_þf
Fyrirbæri_þgf_hk →
    Fyrirtæki_þgf
Fyrirbæri_ef_hk →
    Fyrirtæki_ef

Fyrirtæki/fall →
    Sérnafn/fall no/fall* fyrirtæki # hf., ehf., Inc., Corporation, SA o.s.frv.

# Fyrirtæki eru næstum örugglega rétt greind
$score(+12) Fyrirtæki/fall

# '(Danska) byggingavörukeðjan Bygma'
# '(Jón vann hjá) verkfræðistofunni Afli'
# 'Tryggingafélagið Vörður (hagnaðist um 300 milljónir)'

FyrirbæriMeðGreini/fall/kyn →
    SamstættFall/fall/kyn

FyrirbæriMeðGreini/aukafall/kyn →
    ÓsamstættFall/aukafall/kyn

# !!! ATH: Ef þessu er breytt gæti þurft að breyta processors/entities.py
SamstættFall/fall/kyn →
    # (Ég versla við) norsku herrafataverslunina Smekkmann
    # Lagahöfundurinn góðkunni Jónas Friðrik (hefur glatt marga)
    VeiktLo_et/fall/kyn* no_gr_et/fall/kyn VeiktLo_et/fall/kyn? SérnafnEðaManneskja/fall

SamstættFall_nf/kyn →
    # Enska dagblaðið The Mirror
    VeiktLo_et_nf/kyn* no_gr_et_nf/kyn VeiktLo_et_nf/kyn? SérnafnEðaManneskja

# !!! ATH: Ef þessu er breytt gæti þurft að breyta processors/entities.py
ÓsamstættFall/aukafall/kyn →
    VeiktLo_et/aukafall/kyn* no_gr_et/aukafall/kyn
        VeiktLo_et/aukafall/kyn? SérnafnEðaManneskja # (Ég versla við) herrafataverslunina Smekkmaður

SérnafnEðaManneskja/fall →
    # (danska húsgagnahönnuðinn) Arne Jacobsen
    # (suður-kóreska símaframleiðandann) Samsung
    # (kvikmyndina umtöluðu) 50 gráa skugga
    Sérnafn/fall | Persóna/fall | Fyrirtæki/fall | Nafn/fall

Persóna/fall →
    person/fall

$score(+8) Persóna/fall # Velja Persónu frekar en Sérnafn

SérnafnEðaManneskja →
    # (kvikmyndina umtöluðu) 50 gráir skuggar
    Sérnafn | Persóna | Fyrirtæki/fall | Nafn_nf

$score(-2) SérnafnEðaManneskja # Velja form með falli frekar en án falls

Nafn/fall →
    NlKjarni/tala/fall/kyn # 50 gráir skuggar

# Ekki sækja í að nota nafnlið sem nafn í kjölfar annars nafnliðar með greini
$score(-4) Nafn/fall

Persóna →
    person

$score(+8) Persóna

VeiktLo/tala/fall/kyn →
    SkstAtv? lo_vb/tala/fall/kyn

# suður-kóreski farsímaframleiðandinn Samsung

SkstAtv → SkstAtvOrð "-" # Stutt bandstrik

SkstAtvOrð →
    "norður:ao" | "suður:ao" | "vestur:ao" | "austur:ao"

# Hvetja til runa af lýsingarorðum í veikri beygingu með skilgreiningu sérnafna
$score(+8) VeiktLo/tala/fall/kyn
$score(+2) SkstAtv

$score(+5) SamstættFall/fall/kyn
$score(-1) ÓsamstættFall/aukafall/kyn

# 'Frumkvöðlarnir Jón og Guðrún'
# 'bandarísku sjóðirnir Eaton Vance Corp. og Autonomy Capital LP'

FyrirbæriMeðGreini_ft/fall/kyn →
    Skilgreining/fall/kyn Eindalisti/fall/kyn

Skilgreining/fall/kyn →
    VeiktLo_ft/fall/kyn* no_gr_ft/fall/kyn

Eindalisti/fall/kyn →
    SérnafnEðaManneskja/fall KommaSérnafnEðaManneskja/fall* "og:st" SérnafnEðaManneskja/fall
    | Töluorð_ft/fall/kyn # skipverjanna tveggja, börnin tvö, stúlkunum tveimur

KommaSérnafnEðaManneskja/fall →
    "," SérnafnEðaManneskja/fall

$score(+8) FyrirbæriMeðGreini_ft/fall/kyn

# [séra] Jóns Dalbú Hróbjartssonar [, dómkirkjuprests]
# [frú] Vigdís Finnbogadóttir [fyrrverandi forseti]
# [stórvesír] Ásbjörn Friðriksson [, alþingismann]
# [hún] Kristín (okkar)
Mannsnafn/fall/nkyn → person/fall/nkyn

Manneskja/fall/nkyn → Ávarp/nkyn/fall? Mannsnafn/fall/nkyn Titill/fall?
Manneskja/fall → Manneskja/fall/nkyn

Ávarp/nkyn/fall →
    no_et/fall | ÞekktÁvarp/nkyn/fall

$score(-6) Ávarp/nkyn/fall

ÞekktÁvarp_kk/fall →
    'herra:kk'_et/fall | 'séra:kk'_et/fall | 'hann:pfn'_kk_et/fall
    | 'doktor:kk'_et/fall
    | 'prófessor:kk'_et/fall
ÞekktÁvarp_kvk/fall →
    'frú:kvk'_et/fall | 'séra:kk'_et/fall | 'hún:pfn'_kvk_et/fall
    | 'doktor:kk'_et/fall
    | 'prófessor:kk'_et/fall

$score(+10) ÞekktÁvarp/nkyn/fall

# 'Már Guðmundsson, bankastjóri [Seðlabanka Íslands]'
# Reynt er að láta titil ekki enda á FsRuna/FsAtv eða á EfLiður
# því það skapar margræðni við nafnliðinn sjálfan
Titill/fall →
    ',' NlTitill/fall       # (Jón), [þá] formaður [félagsins]
    | KommulausTitill/fall  # (Jón) [þá] formaður [félagsins]
    | KommuTitill/fall      # (Jón), [þá] formaður námsmannafélagsins í Árósum og talsmaður þess,
    # Gunnlaugur H Jónsson eðlisfræðingur og fyrrverandi starfsmaður Orkustofnunar
    | EinnTitill/fall OgTitill/fall* ','? "og:st" NlTitill/fall

NlTitill/fall →
    "þá:ao"? TitilEinkunn/fall/kyn? no_et/fall/kyn  # (Jón) [þá] glæsilegur fulltrúi (félagsins)

TitilEinkunn/fall/kyn →
    Einkunn_et/fall/kyn

# Titileinkunn er frekar fátíð
$score(-2) TitilEinkunn/fall/kyn

EinnTitill/fall →
    NlTitill/fall EfLiður? FsRunaEftirNl?  # sendiherra Bandaríkjanna hérlendis, handhafi bókmenntaverðlaunanna í ár

OgTitill/fall →
    KommaEðaOg EinnTitill/fall

KommaEðaOg →
    ',' | "og:st" | ',' "og:st"

KommuTitill/fall →
    ',' EinnTitill/fall LokatáknEðaKomma
    | ',' EinnTitill/fall OgTitill/fall* ','? "og:st" EinnTitill/fall LokatáknEðaKomma

KommulausTitill/fall →
    NlTitill/fall

# Gefa plús fyrir titla
$score(+5) Titill/fall

# ...en ekki eins mikinn fyrir kommulausan titil
$score(-5) KommulausTitill/fall

# Gefa enn meiri plús fyrir heila titla innan kommu
$score(+14) KommuTitill/fall

# Eignarfallsliður á eftir nafnlið
# ('formaður Gimlis, félags eldri borgara')
# ('amma Jóns, föður Bjarna')
# karlsins langa, konunnar löngu, barnsins langa
# stóra karlsins, stóru konunnar, stóra barnsins
# karlanna löngu, kvennanna löngu, barnanna löngu

EfLiður → EfLiðurStakur EfLiðurFramhald? # Jóns, föður Bjarna sterka
EfLiður_ft/kyn → Nl_ef_ft/kyn # margar þessara kvenna, suma þeirra sem lentu í slysinu

# Eignarfallsliður getur verið í eintölu eða fleirtölu, og í
# öllum kynjum, óháð nafnliðnum sem hann stendur með
EfLiðurStakur → Nl_ef

EfLiðurFramhald → KommaEfLiður+ ','?

KommaEfLiður →
    ',' EfLiðurStakur

$score(+3) EfLiður
$score(+3) EfLiður_ft/kyn

# Eignarfallsliður sem stendur á undan nafnlið
# ('Íslands fullorðnu synir', 'allra uppáhald')
# Þessi liður er einfaldari en EfLiður til að
# minnka margræðni; leyfir til dæmis ekki einkunn
# (?'Litla Íslands fullorðnu synir'), né að
# lýsingarorð sé notað sem nafnorð

EfLiðurForskeytiTala/tala/kyn →
    Töluorð_ef/tala/kyn TilTölu/tala/kyn?  # (vel rúmlega) þriggja (klukkutíma bið) / 30 þúsund (króna tjón)

TilTölu/tala/kyn →
    "til:st" Töluorð_ef/tala/kyn+   # (á) tveggja til þriggja metra dýpi

EfLiðurForskeyti →
    Fornafn_ef/tala/kyn* EoTao* EfLiðurForskeytiTala/tala/kyn? EfLiðurForskeytiKjarni/tala/kyn
    | Fornafn_ef/tala/kyn+ EoTao* EfLiðurForskeytiTala/tala/kyn?
    | EoTao* EfLiðurForskeytiTala/tala/kyn ÓbeygðEining? # 'rúmlega sjö prósent (hækkun frá fyrra ári)'
    | 'meiri:lo'_mst_kk_ef_et 'háttur:kk'_ef_et     # meiri háttar vandræði
    | 'minni:lo'_mst_kk_ef_et 'háttur:kk'_ef_et     # minni háttar vandamál
    | Prósentubil               # (Spáð er) a.m.k. 10-20% (aukningu)
    | EoTao* Prósenta           # (eigi) nú 50% (hlut í fjarskiptafyrirtækinu) / rúmlega 690 MW (virkjun)
    | Magn_ef/tala/kyn          # 100 fermetra (einbýlishúsum), 10 tonna (trukk)

Prósenta →
    prósenta

$score(+8) Prósenta

ÓbeygðEining →
    'prósent:hk'_ef_ft
    | 'tomma:kvk'_ef_et     # '45 tommu flatskjá' (í stað '45 tomma flatskjá', sem er málfræðilega rétt)
    | 'stjarna:kvk'_ef_et   # Fjögurra stjörnu veitingastaður (ekki fjögurra stjarna veitingastaður)
    | 'hola:kvk'_ef_et      # Átján holu golfvöllur (ekki átján hol(n)a golfvöllur)

EfLiðurForskeytiKjarni_et/kyn → Fyrirbæri_ef/kyn
EfLiðurForskeytiKjarni_ft/kyn → no_ef_ft/kyn

# Ef hægt er að nota t.d. fornafn í stað eignarfalls-forskeytis er það betra
$score(-1) EfLiðurForskeyti
# Eignarfalls-forskeyti sem eru ekki tölur eða fornöfn eru sjaldgæf
$score(-4) EfLiðurForskeytiKjarni/tala/kyn

# Leyfa 'okkar'/'ykkar' í eignarfallsforskeyti (er aðeins kynlaust pfn í BÍN)
Fornafn_ft_ef/kyn → OkkarYkkar

Fornafn/tala/fall/kyn → fn/tala/fall/kyn
Fornafn/fall/tala/kyn → Fornafn/tala/fall/kyn

# Persónufornöfnin (pfn) "hans" og "hennar" eru án kyns í BÍN
Fornafn_et_ef_kk → 'hann:pfn'_kk_ef_et
Fornafn_et_ef_kvk → 'hún:pfn'_kvk_ef_et

# Einn og sami veiðimaðurinn, eitt og sama fyrirbæri
Fornafn_et/fall/kyn → 'einn:fn'_et/fall/kyn "og:st" 'sami:lo'_et/fall/kyn

Töluorð/tala/fall/kyn → to/tala/fall/kyn | tala/tala/fall/kyn

# Öll töluorð (merkt 'töl' í BÍN) tákna fleirtölu
# Talnabil eru líka alltaf í fleirtölu
Töluorð_ft/fall/kyn → töl_ft/fall/kyn | Talnabil

Talnabil → tala "-:en" tala
$score(+4) Talnabil

Töluorð/fall/tala/kyn → Töluorð/tala/fall/kyn

# Undirsetningar

# 1p: 'ég sem alltaf elda matinn samviskusamlega (nenni þessu ekki)'
# 2p: 'þú sem ég elska (vilt ekki giftast mér)'
# 3p: 'sjóarinn sem hafið hafnaði í fyrra (kom hér í gær og fékk sér kaffi)'

# Gefa undirsetningum afmörkuðum með kommum forgang,
# en forðast verri kosti

TilvísunarsetningMeðKommu/tala/pers/kyn →
      ',' Tilvísunarsetning/tala/pers/kyn ','
    >     Tilvísunarsetning/tala/pers/kyn
    >     Tilvísunarsetning/tala/pers/kyn ','
    > ',' Tilvísunarsetning/tala/pers/kyn

# KommaTilvísunarsetning er einungis notað í lok venjulegrar
# setningar með frumlagi í nefnifalli, sbr.
# Allir mega koma með okkur sem eru fullklæddir
KommaTilvísunarsetning/tala/pers/kyn →
    ','? "sem:stt" SetningÁnF/tala/pers/kyn

$score(-8) KommaTilvísunarsetning/tala/pers/kyn

Tilvísunarsetning/tala/pers/kyn →
    Tilvísunartenging Tengisetning/tala/pers/kyn OgTengisetning/tala/pers/kyn*

Tilvísunartenging →
    TengiorðSem | TengiorðEr | TengiorðÞarSem

TengiorðSem → "eða:st"? "sem:stt"

TengiorðÞarSem → "þar:ao" "sem:stt" # í landinu þar sem Jón átti heima

$score(-4) TengiorðÞarSem # Þetta er oftar samtenging setninga ofar í trénu

TengiorðEr → "er:stt"

$score(-8) TengiorðEr # Sjaldgæft - ekki velja nema það þurfi

Tengisetning/tala/pers/kyn →
    TengiYfirsetning                    # (Maðurinn sem) konan kyssti í gær / konurnar kysstu í gær
    | BeygingarliðurÁnF/tala/pers/kyn   # (Maðurinn sem) kyssti konuna í gær / (Maðurinn sem) var fallegur

# (Nokkurra ára niðursveiflur) sem þessar (eru óþekktar)
Tengisetning_et_p3/kyn →
    'þessi:fn'_et_nf/kyn
 
Tengisetning_ft_p3/kyn →
    'þessi:fn'_ft_nf/kyn
 
TengiYfirsetning →
    HreinYfirsetning

BeygingarliðurÁnF → 
    BeygingarliðurÁnF/tala/pers/kyn

BeygingarliðurÁnF/tala/pers/kyn →
    SetningÁnF/tala/pers/kyn

$score(+2) BeygingarliðurÁnF/tala/pers/kyn

# Nota frekar SetningÁnF heldur en HreinYfirsetning, ef völ er á hvoru tveggja
$score(-4) TengiYfirsetning

# ...sem hætta gæti stafað af og eru kílómetri í þvermál
OgTengisetning/tala/pers/kyn →
    ","? OgSem Tengisetning/tala/pers/kyn

$score(+2) OgTengisetning/tala/pers/kyn

NlEind/fall →
    NlEind/tala/pers/fall/kyn

NlEind/fall/kyn →
    NlEind/tala/pers/fall/kyn

NlEind_p3/fall/kyn →
    NlEind_et_p3/fall/kyn | NlEind_ft_p3/fall/kyn

NlEind/tala/pers/fall/kyn →
    NlStak/tala/pers/fall/kyn StakViðhengi/tala/fall/kyn? TilvísunarsetningMeðKommu/tala/pers/kyn?
        Þó/tala/pers/kyn? NlSkýring/tala/pers/fall/kyn?

# (vegna nauðasamninganna) sem slíkra
StakViðhengi/tala/fall/kyn →
    ',' "sem:st" 'slíkur:lo'/tala/fall/kyn LokatáknEðaKomma
    | "sem:st" 'slíkur:lo'/tala/fall/kyn

# (að endurskapa sig) sem þjóðhetju
# 'sem' með nefnifalli er talið vera forsetning
StakViðhengi/tala/aukafall/kyn →
    SemAukafall/aukafall/tala

SemAukafall/aukafall/tala →
    "sem:st" Nl/aukafall/tala

$score(-4) SemAukafall/aukafall/tala

NlEind_et_p3/fall/kyn →
    # það [eitt] [,] hvernig kerfið virkar [,] (er óskiljanlegt)
    'sá:fn'_et/fall/kyn 'einn:fn'_et/fall/kyn? Útskýring

NlEind_ft_p3/fall/kyn →
    # (Niðurstöðurnar voru) þær [einar] [,] að Katrín myndaði stjórn [,] (og kom það sumum á óvart)
    'sá:fn'_ft/fall/kyn 'einn:fn'_ft/fall/kyn? Útskýring

ÞaðTenging →
    Skýringarsetning
    | Spurnaraukasetning
    | NhFyllingÁnAtv        # Það að fara til tannlæknis (er ánægjuleg reynsla)

$score(+32) ÞaðTenging

# ÞaðTenging sem getur verið innan kommu

Útskýring →
    ÞaðTenging
    | ',' ÞaðTenging LokatáknEðaKomma

# Aðdróttanir, þó [í meginatriðum] sannar séu, varða fangelsisvist

Þó/tala/pers/kyn →
    ÞóTenging ÞóBotn/tala/pers/kyn
    | ',' ÞóTenging ÞóBotn/tala/pers/kyn LokatáknEðaKomma

ÞóTenging → "þó:st" | "þótt:st" | "þó:ao" "að:st"

ÞóBotn/tala/pers/kyn →
    FsRuna? LoLiður_nf/tala/kyn VeraVerða_vh/tala/pers

NlSkýring/tala/pers/fall/kyn →
    '(' SvigaForskeyti? SvigaInnihald/tala/pers/fall/kyn ')'
    | '[' SvigaForskeyti? SvigaInnihald/tala/pers/fall/kyn ']'
    | "-:em" SvigaForskeyti? SvigaInnihald/tala/pers/fall/kyn "-:em"
    | ',' SvigaInnihald/tala/pers/fall/kyn LokatáknEðaKomma
    | ',' Skýring Skýringarliður LokatáknEðaKomma
    # (Verslunin þjónar fjölbreyttum markhópi), allt frá börnum til aldraðra, (og er vinsæl í hverfinu)
    | Samanburðarmerki 'allur:fn'_et_nf_hk 'frá:fs'_þgf Nl_þgf
        'til:fs'_ef Nl_ef LokatáknEðaKomma

# Gefa aukastig fyrir skýringar á nafnliðum
$score(+6) NlSkýring/tala/pers/fall/kyn

SvigaInnihald/tala/pers/fall/kyn →
    Árabil
    > Tilvísunarsetning/tala/pers/kyn # (og sem ég er almennt fylgjandi)
    > Nl/fall/tala      # 'Ég spurði forsætisráðherra, Sigmund Davíð, um málið'
    > SvigaInnihaldFsRuna
    > SvigaInnihaldNl   # Ef ekki finnst sama fall og tala, prófa almennan nafnlið
    > HreinYfirsetning

# Árabil nota en-dash

Árabil →
    ártal "-:en" ártal   # 1951-1983
    > ártal "-:en" tala  # 2001-8

SvigaInnihaldNl →
    Nl

# Nafnliður í öðru falli en það sem verið er að skýra er ekki spennandi kostur
$score(-8) SvigaInnihaldNl

SvigaInnihaldFsRuna →
    FsRuna

# FsRuna innan kommu er oftast margræð við FsRunu eftir nafnlið
$score(-4) SvigaInnihaldFsRuna

Skýring →
    "svo:st" "sem:st"
    | "sem:st" 'segja:so'_lhþt_sb_hk_nf_et      # sem sagt
    | 'til:fs'_ef 'dæmi:hk'_ef_et | 'til_dæmis:ao'
    | "þar:ao" 'á_meðal:ao' | 'þar_á_meðal:ao'
    | "meðal:ao" "annars:ao" | 'meðal_annars:ao'
    | 'það:pfn'_hk_et_nf 'vera:so'_0_gm_fh_et_p3 | 'það_er:ao'
    | 'með:fs'_þgf 'annar:lo'_hk_þgf_ft 'orð:hk'_þgf_ft | 'með_öðrum_orðum:ao'
    | "en:st" "ekki:ao"
    | "þó:st" "ekki:ao"
    | "samt:ao" "ekki:ao"

SvigaForskeyti →
    Skýring
    | 'dæmi:hk'_et_nf ':'            # Ráðamenn (dæmi: forsetinn) hafa tjáð sig fjálglega
    | 'lesa:so'_0_gm_fh_et_p3 ':'    # Hann sagði að óþjóðalýður (les: kommúnistar) hefði ráðist á húsið
    # TODO: "lesist"?

Skýringarliður →
    Nl | FsAtv      # , meðal annars Jón í Holti, / , oft snarlega,

# Runur nafnliða

# !!! TODO: Skilja á milli samtenginga sem mynda fleirtölu úr eintöluliðum
# !!! (strákurinn og stelpan eru...), og þeirra sem halda eintölu
# !!! (hópurinn, þar á meðal ég, var... / Össur, ekki síður en þingflokkurinn, var...)

OgEða →
    "og:st" "jafnframt:ao"?
    | "eða:st" | "ellegar:st" | "eður:st"
    | "svo:st" "og:st" | "sem:st" "og:st" | "né:st"
    | ÞarÁMeðal | "meðal:ao" "annars:ao" | 'meðal_annars:ao'
    | 'það:pfn'_hk_et_nf "er:st" | 'það_er:ao'
    | "eins:ao" "og:st" | "líkt:ao" "og:st"
    | "og:st"? "þar:ao" "með:ao" 'telja:so'_lhþt_sb_hk_nf_et?
    | "svo:st" "sem:st" | "jafnt:ao" "sem:st"
    | EnÍOgEða

EnÍOgEða →
    "en:st"

$score(-24) EnÍOgEða # Frekar að líta á 'en' sem samtengingu ef það er hægt

OgSem →
    "og:st" "sem:stt"? | "eða:st" "sem:stt"? | "ellegar:st" | "en:st" "sem:stt"?

ÞarÁMeðal → "þar:ao" 'á_meðal:ao'

$score(+16) ÞarÁMeðal # Líta frekar á "þar á meðal" sem samtengingu en skýringu

KommaOgEða →
    ','? OgEða
    | "-:em" "eða:st" # 'Ég tók tvö tonn af eldsneyti - eða tvö þúsund lítra'
    | KommaOgEðaStrik
    | ',' "ekki:ao" # Við vildum gin, ekki viskí
    | ',' "núna:ao" "síðast:ao" # Tóku á móti mörgum flóttamönnum, núna síðast þremur þúsundum

KommaOgEðaStrik → "-:em"    # 'Ég tók tvö tonn af eldsneyti - tvö þúsund lítra'

$score(-8) KommaOgEðaStrik

# (Í fyrra barði Hannes Guðmund) og (særði Pál í dag)
# (Í fyrra barði Hannes Guðmund), (særði Pál), og (neitaði sök)
# (Í gær börðu strákarnir ekki Guðmund), heldur (stríddu Páli)

OgEðaEn →
    "og:st" | "svo:st" "og:st" | "eða:st" | "ellegar:st" | "heldur:st"
    | "eins:ao" "og:st" | En

En → "en:st"

$score(-2) En

OgEðaEnNema → OgEðaEn | "nema:st"

OgEðaEnAtv → OgEðaEn FsAtv?

KommaOgEðaEn → ',' | ','? OgEðaEn | "-:em" OgEðaEn
KommaOgEðaEnNema → ',' | ';' | ','? OgEðaEnNema

# Allir liðir af sama kyni

NlRuna/fall/kyn →
    KommuSamtenging/fall/kyn* KommuEndir/fall/kyn
    | ',' KommuStForskeyti? NlEind/fall/kyn AtvKomma # Konurnar, sérstaklega Anna, voru óánægðar

KommuStForskeyti →
    FsRunaEftirNl
    | "þar:ao" "af:ao"    # fjórir þingmenn, þar af einn nýútskrifaður, 

KommuSamtenging/fall/kyn → ',' KommuStForskeyti? NlEind/fall/kyn

KommuEndir/fall/kyn →
    Atviksliður? KommaOgEða KommuStForskeyti? NlEind/fall/kyn AtvKomma?
    | 'og_fleira:Ao' ','?
    | 'og_margt_fleira:ao' ','?
    | 'og_svo_framvegis:ao' ','?
    | 'og_þess_háttar:ao' ','?

# Hvetja til eins langra lista og mögulegt er
$score(+6) KommuSamtenging/fall/kyn

# Blandað kyn

NlRuna/fall →
    KommuSamtenging/fall* KommuEndir/fall
    | ',' KommuStForskeyti? NlEind/fall AtvKomma # Þingmenn, sérstaklega Jóhanna, voru óánægðir

KommuSamtenging/fall → ',' KommuStForskeyti? NlEind/fall

KommuEndir/fall →
    Atviksliður? KommaOgEða KommuStForskeyti? NlEind/fall AtvKomma?
    | 'og_fleira:ao' ','?
    | 'og_margt_fleira:ao' ','?
    | 'og_svo_framvegis:ao' ','?
    | 'og_þess_háttar:ao' ','?

# Hvetja til eins langra lista og mögulegt er
$score(+6) KommuSamtenging/fall

# Nafnliðir sem geta verið stakar eindir eða runur einda
# !!! ATH: Forskeytið "Nl" á nafnliði er harðkóðað í binparser.py,
# !!! nánar til tekið í BIN_Nonterminal.is_noun_phrase.

Nl →
    Nl/tala/pers/fall/kyn

Nl/fall →
    Nl/tala/pers/fall/kyn

Nl/fall/kyn →
    Nl/tala/pers/fall/kyn

Nl/fall/tala →
    Nl/tala/pers/fall/kyn

Nl/fall/pers →
    Nl/tala/pers/fall/kyn

Nl/fall/tala/kyn →
    Nl/tala/pers/fall/kyn

Nl/fall/tala/pers →
    Nl/tala/pers/fall/kyn

Nl/pers/fall/kyn →
    Nl/tala/pers/fall/kyn

Nl/fall/tala/pers/kyn →
    Nl/tala/pers/fall/kyn

Nl/tala/pers/fall/kyn →
    NlEind/tala/pers/fall/kyn

# Gefa nefnifalli forskot ef mismunandi föll eru í boði
$score(+4) Nl_nf

# Fleirtölu-nafnliður í 3p sem leyfir runu í sama falli og kyni
# Leyfir einnig 'þeir Jón og Gunnar [sendu bréfið]' eða 'þær Elísabet og mamma hennar [byggðu húsið]'
Nl_ft_p3/fall/kyn →
    NlSamaKyn/fall/kyn

NlSamaKyn/fall/kyn →
    NlSá_ft/fall/kyn? NlEind_p3/fall/kyn NlRuna/fall/kyn+ TilvísunarsetningMeðKommu_ft_p3/kyn?

# Fleirtölunafnliðir af sama kyni eru betri en blandaðir
$score(+2) NlSamaKyn/fall/kyn

NlSá_ft/fall/kyn →
    EoTao* 'sá:fn'_ft/fall/kyn # einnig þau Jón og Gunna

# Blandaðir fleirtölu-nafnliðir eru í hvorugkyni

# Leyfa 'maðurinn og konan' og 'kötturinn, hundurinn og bráðlega músin',
# og einnig 'sumir, þar á meðal Jón,'

Nl_ft_p3_nf_hk →
    NlSá_ft_nf_hk? NlEind_nf NlRuna_nf+ TilvísunarsetningMeðKommu_ft_p3_hk? # Blandað kyn verður að hvorugkyni fleirtölu

Nl_ft_p3_þf_hk →
    NlSá_ft_þf_hk? NlEind_þf NlRuna_þf+ TilvísunarsetningMeðKommu_ft_p3_hk? # Blandað kyn verður að hvorugkyni fleirtölu

Nl_ft_p3_þgf_hk →
    NlSá_ft_þgf_hk? NlEind_þgf NlRuna_þgf+ TilvísunarsetningMeðKommu_ft_p3_hk? # Blandað kyn verður að hvorugkyni fleirtölu

Nl_ft_p3_ef_hk →
    NlSá_ft_ef_hk? NlEind_ef NlRuna_ef+ TilvísunarsetningMeðKommu_ft_p3_hk? # Blandað kyn verður að hvorugkyni fleirtölu

# Lýsingarorðaliður

# Lýsingarorð með nafnlið

# !!! TODO: Hér er ekki gerður greinarmunur á sterkri og veikri
# beygingu lýsingarorða sem standa með nafnorðum, eftir því hvort
# nafnorðið er með greini eða án. (Sbr.: 'Fallega konan talaði við mig'
# vs. 'Falleg kona talaði við mig'.)
Lo/fall/tala/kyn →
    LoSemLo/fall/tala/kyn
    | LoMun/fall/tala/kyn
    | LoSem/fall/tala/kyn
    | LoSemSo/fall/tala/kyn

LoSemLo/fall/tala/kyn → 
    LoAtviksliður* lo/fall/tala/kyn # (Þingmenn kosnir) algerlega leynilegri (kosningu)

LoMun/fall/tala/kyn →
    'munur:kk'_þgf_et lo_mst/fall/tala/kyn # (Schengen rekur) mun öflugri gagnagrunn / (þau voru) mun frjálsari skoðana sinna

$score(+10) LoMun/fall/tala/kyn

LoSem/fall/tala/kyn →
    "sem:ao" lo_esb/fall/tala/kyn # (hvernig skuli stuðla að) sem jöfnustu hlutfalli

$score(+4) LoSem/fall/tala/kyn

# Leyfa sagnorðum að standa í stað lýsingarorða ef þau
# eru í lýsingarhætti þátíðar, sterkri eða veikri beygingu
# (_vb leyfir veika beygingu ásamt sterkri)
# ('Við litum til bókfærðs hagnaðar')
LoSemSo/fall/tala/kyn →
    LoAtviksliður* so_lhþt/fall/tala/kyn
    | LoAtviksliður* so_lh_nt

$score(-16) LoSemSo/fall/tala/kyn

$score(+6) LoSemLo/fall/tala/kyn  # Tilbrigði 3.1, eina sem þarf að toggla.

LoAtviksliðir →
    LoAtviksliður+ | "og:ao" # 'honum er og heimilt að merkja við fleiri en einn frambjóðanda'

# 'Ég vaknaði ekkert alltof fersk'
# 'Atburðarásin var nokkuð skýr'
# Ath.: 'nokkuð' er ekki leyft sem eo þar sem það er einnig fornafn.
# Þess vegna er það sértilvik hér. Ekki er hins vegar ráðlegt að
# leyfa atviksorðið almennt sem einkunn með nafnliðum; það myndi
# rekast á fornafnið.
LoAtviksliður →
    eo | tao | 'enginn:fn'_hk_et_nf | "nokkuð:ao"

# Lýsingarorð í sterkri beygingu
# ('Hún fór frjáls skoðana sinna í útlegð')

Lo_sb/tala/fall/kyn → LoAtviksliðir? lo_sb/tala/fall/kyn LoViðhengi?

Lo_sb_nf/tala/kyn →
    LoAtviksliðir? lo_sb_nf/tala/kyn LoViðhengi?
Lo_sb_nf_ft/kyn →
    'margur:lo'_sb_nf_ft/kyn 'hver:fn'_nf_ft/kyn # margir hverjir, mörg hver, margar hverjar

OgLo → "og:st" | "eða:st" | "en:st"

# (Ég var reiður) og sár
OgLo_sb/tala/fall/kyn →
    OgLo Lo_sb/tala/fall/kyn

# (Flokknum er rétt) [,] og skylt [,] (að fara að beiðni forsetans)
OgLo_sb_nf/tala/kyn →
    OgLo Lo_sb_nf/tala/kyn
    | ',' OgLo Lo_sb_nf/tala/kyn ','

$score(+2) OgLo_sb_nf/tala/kyn

# Miðstig
Lo_mst/tala/fall/kyn →
    LoAtviksliðir? lo_mst/tala/fall/kyn LoViðhengi? # Miðstig
Lo_mst_nf/tala/kyn →
    LoAtviksliðir? lo_mst_nf/tala/kyn LoViðhengi? # Miðstig

# Lýsingarorð sem stendur með sögn getur haft eignarfalls- eða
# þágufalls-viðhengi, sbr.:
# ef.: 'Þau voru frjáls skoðana sinna'
# þgf.: 'Sýningin var opin almenningi'
LoViðhengi →
    EfLiður | Nl_þgf

$score(-10) LoViðhengi

# Lýsingarorð sem einkunn með nafnlið

# '(Jón), sultuslakur eftir gufubaðið, (lék á als oddi)'
# '(Maður) alvarlega háður eiturlyfjum (bað mig um peninga)'
# '(Stjórnmálamaður) tengdur Framsóknarflokknum (hringdi í mig)'
# '(Ég ræddi við) konu tengda verkefninu'
LoEftirNl/tala/fall/kyn →
    StaktLoEftirNl/tala/fall/kyn
    | ',' LoLiður/tala/fall/kyn FsAtv? LokatáknEðaKomma
    | LoEftirNlMeðÞgf/fall/tala/kyn
    | ',' LoEftirNlMeðÞgf/fall/tala/kyn LokatáknEðaKomma

# '(Stjórnmálaflokkarnir), hver um sig, (lýstu andstöðu við málið)'
LoEftirNl_ft/fall/kyn →
    HverHvor/fall/kyn
    # | ',' HverHvor/fall/kyn LokatáknEðaKomma # Er höndlað sem NlSkýring

HverHvor/fall/kyn →
    'hver:fn'_et/fall/kyn HverHvorBotn/kyn      # hvern fyrir sig / af öðrum
    | 'hvor:fn'_et/fall/kyn HverHvorBotn/kyn    # hvorn í sínu lagi / á fætur öðrum

$score(+4) HverHvor/fall/kyn

HverHvorBotn/kyn →
    'fyrir:fs'_þf 'sig:abfn'_þf                         # fyrir sig
    | 'um:fs'_þf 'sig:abfn'_þf                          # um sig
    | 'í:fs'_þgf 'sinn:fn'_et_hk_þgf 'lag:hk'_et_þgf    # í sínu lagi
    | 'á_fætur:ao' 'annar:fn'_et_þgf/kyn                # á fætur annarri
    | 'af:fs'_þgf 'annar:fn'_et_þgf/kyn                 # af öðru

StaktLoEftirNl/tala/fall/kyn →
    ','? LoLiður/tala/fall/kyn

# Stakt lýsingarorð eftir nafnorði gæti líklega tilheyrt næsta nafnorði
$score(-6) StaktLoEftirNl/tala/fall/kyn

# Lýsingarorð sem getur tekið með sér þágufall
# !!! ATH: Til eru listar yfir slík orð

# '[sterklega] tengdur Framsóknarflokknum'
LoEftirNlMeðÞgf/fall/tala/kyn →
    AtviksliðurEinkunn* lo_sb/fall/tala/kyn Nl_þgf

$score(+4) LoEftirNlMeðÞgf/fall/tala/kyn

# '(Ég drakk) hvítu, volgu og feitu mjólkina' / '(Ég sá) hvíta, volga og feita mjólk'
# '(Íslendingar fluttu fé til annarra) og [í sumum tilvikum] öruggari landa'

LoLiður/tala/fall/kyn →
    Lo/fall/tala/kyn
    | LoLiður/tala/fall/kyn LoKomma/tala/fall/kyn* KommaOgEðaEn FsRuna? Lo/fall/tala/kyn ','?

LoLiður_nf/tala/kyn → LoLiður_et_nf/kyn | LoLiður_ft_nf/kyn

LoKomma/tala/fall/kyn →
    ',' Lo/fall/tala/kyn

# Fornöfn sem geta hegðað sér eins og lýsingarorð:
# 'Hann komst sjálfur út úr flakinu', 'Hún fór öll inn um gluggann',
# 'Þau eru bæði viðskiptafræðingar',  'Það reyndist vera sú sama'
# 'Þeir fóru sumir hverjir með ferjunni'
# ('báðir' er aðeins til í fleirtölu en það veldur ekki vandræðum hér)

LoFornafn/fall/tala/kyn →
    'allur:fn'/fall/tala/kyn | 'sjálfur:fn'/fall/tala/kyn | 'báðir:fn'/fall/tala/kyn
    | 'sami:fn'/fall/tala/kyn | 'sumur:fn'/fall/tala/kyn 'hver:fn'/fall/tala/kyn?

$score(+4) LoFornafn/fall/tala/kyn

# Lýsingarorð sem andlag sagnar
# Þar kemur til greina miðstig + 'en' + nafnliður

# '(Ég var) reiður' / '(Ég var) reiðari en þú' / '(Mér var verr við þetta) en þér'
# '(Ég var) reiðastur allra' / '(Ég talaði við þann) reiðasta þeirra allra'

# Ath.: miðstig lýsingarorða er merkt MST í BÍN og passar því ekki við lo_sb
# (en frumstig sterkrar beygingar er FSB og efsta stig er ESB)
LoSamanb/tala/fall/kyn →
    Lo_sb/tala/fall/kyn OgLo_sb/tala/fall/kyn?
    # Miðstig: (Ég varðveitti laxinn) [mun] betur [en bleikjuna / nú en áður]
    | MiðstigForskeyti? Lo_mst/tala/fall/kyn EnSamanb/fall?
    # (Hann tók við hrósinu) [,] ýmist glaður eða með fýlusvip
    | LoSamanbFleiri/fall/tala/kyn

LoSamanbFleiri/fall/tala/kyn →
    # ýmist blár eða með gullnum blæ
    Samanburðarmerki "ýmist:st" LoTengtSögn/tala/fall/kyn "eða:st" LoTengtSögn/tala/fall/kyn LokatáknEðaKomma
    | Samanburðarmerki "ýmist:st" LoTengtSögn/tala/fall/kyn "eða:st" FsAtv LokatáknEðaKomma
    | Samanburðarmerki "ýmist:st" FsAtv "eða:st" LoTengtSögn/tala/fall/kyn LokatáknEðaKomma
    | "ýmist:st" LoTengtSögn/tala/fall/kyn "eða:st" LoTengtSögn/tala/fall/kyn
    | "ýmist:st" FsAtv "eða:st" LoTengtSögn/tala/fall/kyn
    | "ýmist:st" LoTengtSögn/tala/fall/kyn "eða:st" FsAtv
    # bæði skjálfandi og í svitabaði
    | Samanburðarmerki "bæði:st" LoTengtSögn/tala/fall/kyn "og:st" LoTengtSögn/tala/fall/kyn LokatáknEðaKomma
    | Samanburðarmerki "bæði:st" LoTengtSögn/tala/fall/kyn "og:st" FsAtv LokatáknEðaKomma
    | Samanburðarmerki "bæði:st" FsAtv "og:st" LoTengtSögn/tala/fall/kyn LokatáknEðaKomma
    | "bæði:st" LoTengtSögn/tala/fall/kyn "og:st" LoTengtSögn/tala/fall/kyn
    | "bæði:st" FsAtv "og:st" LoTengtSögn/tala/fall/kyn
    | "bæði:st" LoTengtSögn/tala/fall/kyn "og:st" FsAtv
    # annaðhvort með fullan maga eða hungurmorða
    | Samanburðarmerki "annaðhvort:st" LoTengtSögn/tala/fall/kyn "eða:st" LoTengtSögn/tala/fall/kyn LokatáknEðaKomma
    | Samanburðarmerki "annaðhvort:st" LoTengtSögn/tala/fall/kyn "eða:st" FsAtv LokatáknEðaKomma
    | Samanburðarmerki "annaðhvort:st" FsAtv "eða:st" LoTengtSögn/tala/fall/kyn LokatáknEðaKomma
    | "annaðhvort:st" LoTengtSögn/tala/fall/kyn "eða:st" LoTengtSögn/tala/fall/kyn
    | "annaðhvort:st" FsAtv "eða:st" LoTengtSögn/tala/fall/kyn
    | "annaðhvort:st" LoTengtSögn/tala/fall/kyn "eða:st" FsAtv
    # hvorki í djúpum þönkum né hugsandi
    | Samanburðarmerki "hvorki:st" LoTengtSögn/tala/fall/kyn "né:st" LoTengtSögn/tala/fall/kyn LokatáknEðaKomma
    | Samanburðarmerki "hvorki:st" LoTengtSögn/tala/fall/kyn "né:st" FsAtv LokatáknEðaKomma
    | Samanburðarmerki "hvorki:st" FsAtv "né:st" LoTengtSögn/tala/fall/kyn LokatáknEðaKomma
    | "hvorki:st" LoTengtSögn/tala/fall/kyn "né:st" LoTengtSögn/tala/fall/kyn
    | "hvorki:st" FsAtv "né:st" LoTengtSögn/tala/fall/kyn
    | "hvorki:st" LoTengtSögn/tala/fall/kyn "né:st" FsAtv
    # Við vorum flest [,] ef ekki öll [,] hrifin af söng Maríu
    | LoEðaFs/fall/tala/kyn "ef:st" "ekki:ao" LoEðaFs/fall/tala/kyn
    | LoEðaFs/fall/tala/kyn "," "ef:st" "ekki:ao" LoEðaFs/fall/tala/kyn LokatáknEðaKomma
    | LoEðaFs/fall/tala/kyn "-:em" "ef:st" "ekki:ao" LoEðaFs/fall/tala/kyn "-:em"

$score(+4) LoSamanbFleiri/fall/tala/kyn

LoEðaFs/fall/tala/kyn →
     LoTengtSögn/tala/fall/kyn | FsAtv

MiðstigForskeyti →
    'munur:kk'_þgf_et           # mun meira
    | 'mikill:lo'_sb_hk_þgf_et  # miklu meira
    | Sinnum                    # x sinnum hraðar
    | 'tvisvar_sinnum:ao'
    | 'þrisvar_sinnum:ao'
    | 'fjórum_sinnum:ao'
    | 'mörgum_sinnum:ao'

$score(+2) MiðstigForskeyti

EnSamanb/fall →
    "en:st" Nl/fall                             # (Ég var [mun] reiðari) en Páll
    | "en:st" HreinYfirsetning                  # (Ég er ósáttari) en Páll gæti verið
    | "nú:ao"? "heldur:st"? "en:st" "áður:ao"   # (Ég er ánægðari) nú en áður

$score(+4) EnSamanb/fall

# Hann er reiður / [!!! ATH:] Hann er Önnu mun reiðari en Páli
LoSamanb_nf/tala/kyn →
    Lo_sb_nf/tala/kyn OgLo_sb_nf/tala/kyn?
    | MiðstigForskeyti? Lo_mst_nf/tala/kyn EnSamanb_nf? # Miðstig: (Ég var) [mun] reiðari [en Páll]
    # (Hann tók við hrósinu) [,] ýmist glaður eða með fýlusvip
    | LoSamanbFleiri_nf/tala/kyn

LoTengtSögn/tala/fall/kyn →
    LoSamanb/tala/fall/kyn
    | AtviksliðurEinkunn* LoFornafn/fall/tala/kyn # (Hann komst) [hins vegar] sjálfur (út úr flakinu)
    | LoTengtSögn/tala/fall/kyn LoKommaTengdSögn/tala/fall/kyn* KommaOgEðaEn LoSamanb/tala/fall/kyn

LoKommaTengdSögn/tala/fall/kyn →
    ',' LoSamanb/tala/fall/kyn

LoTengtSögn_nf/tala/kyn →
    LoSamanb_nf/tala/kyn
    | AtviksliðurEinkunn* LoFornafn_nf/tala/kyn # (Hann komst) sjálfur (út úr flakinu)
    | LoTengtSögn_nf/tala/kyn LoKommaTengdSögn_nf/tala/kyn* KommaOgEðaEn LoSamanb_nf/tala/kyn

LoTengtSögn_nf_ft/kyn →
    HverHvor_nf/kyn  # Norðurlöndin eru hvert/hvort um/fyrir sig ófær um að taka á vandanum

LoKommaTengdSögn_nf/tala/kyn →
    ',' LoSamanb_nf/tala/kyn

# Viðhengi með miðmyndarsögnum: (okkur finnst) sjálfum/mörgum/flestum [hverjum]...
LoÞgf_et/kyn →
    lo_sb_þgf_et/kyn
    | LoFornafn_þgf_et/kyn

LoÞgf_ft/kyn →
    lo_sb_þgf_ft/kyn 'hver:fn'_þgf_ft/kyn?
    | LoFornafn_þgf_ft/kyn 'hver:fn'_þgf_ft/kyn?

# Forsetningarliðir

FsMeðFallstjórn →

    fs/fall Nl/fall

    # Forsetningar sem geta tekið með sér sagnlið í nafnhætti
    # eru sérmerktar 'nh' í Main.conf og tengjast við fs_nh í binparser.py.
    # '(Beiðnin) um að [fara]...', '(Fíknin) í að [gera]...',
    # '(Biðin) eftir að [sækja]...', '(Þörfin) fyrir að [kaupa]...',
    # '(Útrásin) gegnum að [tala]...', '(Bannhelgin) kringum að [tala]...',
    # 'þrátt fyrir að (fara aldrei í Costco)'
    # 'með að [gera]...', '(Löngunin) til að [berja]...', '(Áhættan) við að [kaupa]...',
    # '(Hættan) á að [aka]...'
    | fs_nh FsAð

    # Fleiryrtar forsetningar sem eru ekki dregnar saman í Phrases.conf
    | 'nær:fs'_þgf 'draga:so'_op_gm_et_p3 Nl_þgf    # 'þegar nær dró/dregur jólum'
    | 'fyrir_hönd:fs'_ef Nl_ef                      # Skammstöfunin f.h.
    | 'beggja:fn'_hk_ef_ft 'vegna:fs'_ef Nl_ef      # 'beggja vegna vegarins'
    | 'með_tilliti_til:fs'_ef Nl_ef                 # Skammstöfunin m.t.t.
    | 'samanbera:so'_lhþt_sb_hk_nf_et 'við:fs'_þf Nl_þf        # samanborið við (fyrra ár)
    | 'bera:so'_lhþt_sb_hk_nf_et "saman:ao" 'við:fs'_þf Nl_þf  # borið saman við (fyrra ár)
    | "fyrir:ao" "nú:ao"? 'utan:fs'_þf Nl_þf
    | "innan:ao" 'við:fs'_þf Nl_þf
    | "innan:ao" "sem:st" 'utan:fs'_ef Nl_ef
    | "hvað:ao" FsHvaðVarðar
    | 'allt:fn'_nf_hk_et 'frá:fs'_þgf Nl_þgf        # allt frá hruni, allt frá börnum til fullorðinna
    # 'um að lög um endurupptökunefnd standist ekki stjórnarskrá' - Ath: 'um það að' er höndlað í ÞaðTenging
    | 'um:fs'_þf "að:st" HreinYfirsetning ','?
    # 'um hvort/hvenær/af hverju/til hvers/hví Jón kaus Framsóknarflokkinn'
    | 'um:fs'_þf SpurnarAtviksorð HreinYfirsetning ','?
    # 'er bjartsýnn á að Alfreð komi aftur í liðið' - Ath: 'á það/því að' er höndlað í ÞaðTenging
    | 'á:fs'_þf "að:st" HreinYfirsetning ','?
    | 'að:fs'_þgf 'nafn:hk'_þgf_et person_nf  # Ég ræddi við konu að nafni Aðalbjörg Jónsdóttir
    | 'af:fs'_þgf Nl_þgf nhm so_0_nh  # Af fyrstu fregnum að dæma (virðist veiðitímabilið byrja vel)
    | FsMiðaðVið
    | FsÍGegnUm
    | FsUmOgEftir
    | FsAðBaki
    | FsTilÞgf
    | AtvFsSaman    # inn eftir firðinum, fram undan rúminu
    # 'hvorki við stjórn spítalans né ráðherrann'
    # 'hvorki til mín né stjórnarinnar'
    | "hvorki:st" fs/fall Nl/fall "né:st" Nl/fall
    | FsÞgfTilEf

    | '(' FsMeðFallstjórn ')'

$score(+1) FsMeðFallstjórn

FsAð →
    NhLiðir SagnInnskot?    # Þörfin fyrir að læra spænsku í skólum
    | Skýringarsetning      # Þörfin fyrir að Jón seldi bílinn

$score(+4) FsAð

$tag(begin_prep_scope) FsAð # Nýtt samhengi fyrir samtengingu sagna og forsetninga

# Sagnorð í lýsingarhætti nútíðar sem stendur sjálfstætt með viðfangi, eins og forsetning
# 'Hún stóð í sömu sporum reykjandi sígarettu'
# 'Verandi lögmaður sakborningsins get ég ekki tjáð mig um málið'
# 'Bölvandi þjónustukerfinu beit hún á jaxlinn'
SögnLhNt → EinSögn_lhnt

$score(+7) SögnLhNt

# Áttirnar eru aðeins forsetningar í BÍN, ekki atviksorð - þess vegna þurfa þær sérmeðhöndlun

Átt → 'vestur:fs'_þf | 'austur:fs'_þf | 'suður:fs'_þf | 'norður:fs'_þf

ÁttFs → Átt Nl_þf    # suður götuna

$score(+12) ÁttFs

FsÞgfTilEf →
    Nl_þgf 'til:fs'_ef Nl_ef # máli sínu til stuðnings, barninu til hróss, henni til bjargar

# Ekki velja þetta nema nauðsyn beri til
$score(-6) FsÞgfTilEf

FsMiðaðVið →
    'miða:so'_lhþt_sb_hk_nf_et 'við:fs'_þf Nl_þf
    | 'miðað_við:fs'_þf Nl_þf # Skammstöfunin m.v.

$score(+12) FsMiðaðVið

FsUmOgEftir →
    # 'kvikmyndir um og eftir konur'
    'um:fs'_þf "og:st" 'eftir:fs'_þf Nl_þf

$score(+8) FsUmOgEftir

FsÍGegnUm →
    "í:ao" "gegn:ao" 'um:fs'_þf Nl_þf
    | "í:ao" 'gegnum:fs'_þf Nl_þf

$score(+6) FsÍGegnUm

FsAðBaki →
    'að:fs'_þgf 'bak:hk'_þgf_et Nl_þgf # 'að baki hverju bundnu sæti'

$score(+4) FsAðBaki

FsTilÞgf →
    'til:fs'_ef FsTilÞgfOrð Nl_þgf # 'til stuðnings/heiðurs Páli'

FsTilÞgfOrð →
    'stuðningur:kk'_ef_et | 'heiður:kk'_ef_et | 'háðung:kvk'_ef_et

So_1_gm_fh_p3_et_nf → so_1_nf_gm_fh_p3_et
So_1_gm_fh_p3_et_þf → so_1_þf_gm_fh_p3_et
So_1_gm_fh_p3_et_þgf → so_1_þgf_gm_fh_p3_et
So_1_gm_fh_p3_et_ef → so_1_ef_gm_fh_p3_et

FsHvaðVarðar →
    Nl/fall So_1_gm_fh_p3_et/fall  # '(hvað) Pál varðar/Páli viðvíkur'
    | So_1_gm_fh_p3_et/fall Nl/fall # '(hvað) varðar Pál/viðvíkur Páli'

$score(+4) FsHvaðVarðar

FsLiður → AtviksliðurAuka? FsMeðFallstjórn # snarlega í bæinn

# FsAtv getur verið runa af forsetningarliðum með atviksliðum inn á milli,
# á undan og í lok - eða einungis atviksliðir.

FsAtv →
    # (Ég samþykkti þetta), en með semingi þó [,]
    Samanburðarmerki? "en:st"? EinFsAtv+ FsAtvFramhald* ','?

FsAtvFramhald → KommaOgEðaEn EinFsAtv+

EinFsAtv →
    Nema? FsMeðFallstjórn
    | EinnAl
    # '(hann tók á móti mér) hlæjandi'
    | SögnLhNt
    | SamsettFsAtv

SamsettFsAtv →
    # 'hvorki í Frakklandi né annars staðar í Evrópusambandinu'
    "hvorki:st" EinEinföldFsAtv+ "né:st" EinEinföldFsAtv+
    # 'annað hvort í Breiðholti eða við World Class'
    | 'annar:fn'_nf_et_hk "hvort:st" EinEinföldFsAtv+ "eða:st" EinEinföldFsAtv+
    # 'annaðhvort í Breiðholti eða við World Class'
    | "annaðhvort:st" EinEinföldFsAtv+ "eða:st" EinEinföldFsAtv+
    # 'bæði við handavinnu og í viðgerðum'
    | "bæði:st" EinEinföldFsAtv+ ','? "og:st" EinEinföldFsAtv+
    # 'ýmist að fullu eða til hálfs'
    | "ýmist:ao" EinEinföldFsAtv+ "eða:st" EinEinföldFsAtv+
    # Hann sagði ósatt, annars vegar í fyrstu yfirheyrslu og hins vegar með framburði sínum í lok málsins
    | 'annars_vegar:ao' EinEinföldFsAtv+ ','? "og:st" 'hins_vegar:ao' EinEinföldFsAtv+

# Gefa samsetningum auka stig, þær smella vel saman
$score(+4) SamsettFsAtv

EinEinföldFsAtv → 
    FsMeðFallstjórn 
    | EinnAl 
    | SögnLhNt

Nema →
    "nema:st" "þá:ao"? "helst:ao"?

# AtvFs hefst alltaf á atvikslið en á eftir geta fylgt forsetningarliðir
# og atviksliðir.

AtvFs →
    ','? EinnAl EinFsAtv* FsAtvFramhald* ','?

# AL gefa vísbendingu um fall á FL
AtvFsSaman →
    BreytiAtv FsEftir_þgf
    | BreytiAtv FsVið_þf
    | "handan:ao" FsVið_þf
    | StaðarAtv FsUndirÁÍ_þgf
    | StefnuAtv FsUndirÁÍ_þf
    | ÁttarAtv FsUndirÁÍ_þf
    | ÁttarAtv FsUndirÁÍ_þgf
    | StefnuAtv FsYfirFyrir_þf
    | StefnuAtv FsMeð_þgf
    | ÁttarAtv FsMeð_þgf

$score(+18) AtvFsSaman

# Taka sérstaklega á runu forsetningarliða sem er afmörkuð milli komma;
# það minnkar tvíræðni

FsLiðurMeðKommu →
    FsLiður
    | FsLiður KommaOgEðaEn? AtviksliðurAuka? FsLiðurMeðKommu

# FsRuna er runa forsetninga og atviksliða, sem getur þó ekki
# endað á atvikslið. Hún er því rétti kosturinn á undan
# nafnlið, enda geta nafnliðir byrjað á atviksliðum (nánar
# til tekið á AtviksliðurEinkunn).

# FsRunaInnskot er FsRuna sem fær mínusstig og skal notast
# þar sem innskot forsetningar er klasturslegt en ekki með
# öllu óleyfilegt.

# FsRunaEftirSögn er FsRuna sem getur innifalið tímalið
# (sem er atviksliður en er ekki leyfður í AtviksliðurEinkunn).
# Nota á FsRunaEftirSögn á milli sagnar og nafnliðar (andlags),
# þar sem tímaliður getur passað við sögnina. Dæmi:
# 'Kjarninn birti í lok janúar úttekt á málinu.'

# FsRunaEftirNl er FsRuna sem stendur á eftir nafnlið og
# getur innifalið viss atviksorð sem hengja má á nafnliðinn,
# svo sem "erlendis" og "hérlendis". Dæmi:
# 'Lagaramminn hérlendis leyfir þetta ekki.'

# FsRuna getur ekki endað á atvikslið, nema innan kommu
# 'var, líkt og í búsáhaldabyltingunni, byggð á óánægju'
FsRuna →
    FsInnanKommu
    | FsLiðurMeðKommu

FsInnanKommu →
    ',' FsForskeyti? FsLiðurMeðKommu AtviksliðurAuka? LokatáknEðaKomma

$score(+4) FsInnanKommu

FsRunaEftirNl →
    FsRuna
    # Atviksorð sem geta fylgt á eftir nafnliðum
    # !!! Setja í conf?
    | "hérlendis:ao"
    | "erlendis:ao"
    | 'í_ár:ao'

# Hvetja létt til þess að forsetningar séu tengdar nafnlið
$score(+1) FsRunaEftirNl
$tag(purge_prep) FsRunaEftirNl # Ekki senda innifaldar forsetningar upp tréð

# Atviksliðir eru ekki leyfðir milli sagnar og andlags, enda
# er ætlast til að AtviksliðurEinkunn fylgi andlaginu sem slíku.
# Hins vegar eru til dæmis tímaliðir ekki leyfðir í AtviksliðurEinkunn
# og verða því að vera leyfðir hér - enda rökréttara að þeir fylgi
# sögninni fremur en andlaginu.
FsRunaEftirSögn →
    FsRuna
    | Tímaliður  # (Kjarninn birti) í lok janúar (úttekt á málinu)

# Forskeyti sem leyfileg eru á undan forsetningarlið innan kommu
FsForskeyti →
    "líkt:ao" "og:st" | "eins:ao" "og:st"
    | 'það_er:ao' | 'það:pfn'_hk_et_nf 'vera:so'_0_gm_fh_p3_et

# Forsetningar þar sem AL á undan gefur vísbendingar um fall í FL
FsEftir/fall →
    'eftir:fs'/fall Nl/fall
FsMeð/fall →
    'með:fs'/fall Nl/fall
FsVið/fall →
    'við:fs'/fall Nl/fall
FsYfirFyrir/fall →
    'yfir:fs'/fall Nl/fall
    | 'fyrir:fs'/fall Nl/fall
FsUndirÁÍ/fall →
    'undir:fs'/fall Nl/fall
    | 'á:fs'/fall Nl/fall
    | 'í:fs'/fall Nl/fall

# Atviksliður

Al → EinnAl+

EinnAl →
    ao | tao                # Strax tvöfalt til baka, loksins
    | Tímaliður             # (Ég tilkynnti þetta) strax 3. september
    | EkkiNeitt
    | UndirEins
    | SemStendur
    | FyrirLöngu
    | Sinnum
    | Úrslit                # KR sigraði Val 6:0
    | AlMiðstig             # [mun] hraðar en augað sér
    # Fjölyrtir atviksliðir sem ekki er unnt að búa til fasta frasa úr
    | 'einn:fn'_kvk_et_þf 'ferð:kvk'_þf_et_gr "enn:ao"  # eina ferðina enn
    | "heima:ao" "fyrir:ao"
    | "hingað:ao" "til:ao"
    | "sem:ao" 'segja:so'_lhþt_sb_hk_nf_et  # sem sagt
    | "svo:ao" "sem:ao"
    | "sem:ao" "fyrst:ao"
    | 'vinsamlegur:lo'_esb_hk_nf_et     # (Þátttakendur eru) vinsamlegast (beðnir að fylla út eyðublað)
    | 'náðarsamlegur:lo'_esb_hk_nf_et   # (Þátttakendur eru) náðarsamlegast (beðnir að fylla út eyðublað)
    | 'allur:fn'_ft_þf_kk "jafna:ao"    # Alla jafna eru menn rólegir yfir þessu

$tag(no_prep) EinnAl        # Engar forsetningar hér inni - óþarfi að skoða dótturgreinar

AlMiðstig →
    MiðstigForskeyti? ao_mst EnSamanb_nf? # (Gunnar ók) [mun/miklu] hraðar [en Jón gat ímyndað sér]

$score(-3) AlMiðstig

EkkiNeitt →
    "ekki:ao" "neitt:ao"    # (Ég þekki þetta) ekki neitt

$score(-6) EkkiNeitt

FyrirLöngu →
    'fyrir:fs'_þgf 'langur:lo'_þgf_et_hk # 'fyrir löngu'

$score(+8) FyrirLöngu

# Sem stendur er hann ekki viðlátinn
SemStendur →
    "sem:ao" 'standa:so'_0_gm_fh_et_p3

$score(-6) SemStendur

UndirEins →
    "undir:ao" "eins:ao"    # 'Ég fór undir eins og kannaði málið'

$score(+4) UndirEins

Sinnum →
    # tvisvar, þrisvar og fjórum sinnum er skilgreint í Main.conf
    TalaOg? töl_ft_hk_þgf 'sinn:hk'_þgf_ft      # tuttugu og fimm sinnum
    | tala_ft_hk_þgf 'sinn:hk'_þgf_ft           # 5 sinnum
    | tala_et_hk_þgf 'sinn:hk'_þgf_et           # 101 sinni
    | TalaOg 'tvisvar_sinnum:ao'
    | TalaOg 'þrisvar_sinnum:ao'
    | TalaOg 'fjórum_sinnum:ao'

TalaOg →
    töl_ft_hk_þgf "og:st"

$score(+24) Sinnum

Atviksliður →
    Al OgAtviksliður*

OgAtviksliður →
    KommaOgEðaEn Al         # Hratt, en örugglega og fumlaust

# Fanga hugsanlegan atvikslið innan kommu:
# Fyrirtækin annars vegar, og heimilin hins vegar, hafa dregið úr fjárfestingu
AtvKomma →
    AtviksliðurAuka? ','

AtviksliðurKomma →
    AtviksliðurAuka ','?

AtviksliðurEinkunn →
    eo | tao                        # Einkunnarorð = atviksorð sem geta ekki jafnframt verið forsetningar
    | Aldur                         # Hin 29 ára gamla fyrirsæta
    | 'allur:fn'_hk_et_nf "að:ao"   # 'fjárfesta fyrir allt að 100 milljónir króna'
    | "svo:ao" "sem:ao"             # 'enda kemur kynlíf þeirra svo sem engum við'
    | UmEðaYfir

UmEðaYfir →
    "um:ao" "eða:st" "yfir:ao"      # Um eða yfir 100 tyrkneskum eldflaugum rigndi yfir Aleppo

$score(+8) UmEðaYfir

# Atviksorð sem geta breytt falli innan forsetningarliðar sem fylgir þeim
BreytiAtv →
    StaðarAtv
    | StefnuAtv
    | UpprunaAtv
#    | ÁttarAtv

StaðarAtv →
    "frammi:ao"
    | "inni:ao"
    | "niðri:ao"
    | "uppi:ao"
    | "úti:ao"

StefnuAtv →
    "aftur:ao"
    | "fram:ao"
    | "inn:ao"
    | "niður:ao"
    | "upp:ao"
    | "út:ao"

UpprunaAtv →
    "aftan:ao"
    | "austan:ao"
    | "framan:ao"
    | "innan:ao"
    | "neðan:ao"
    | "norðan:ao"
    | "ofan:ao"
    | "sunnan:ao"
    | "utan:ao"
    | "vestan:ao"

ÁttarAtv →
    "austur:ao"
    | "norður:ao"
    | "suður:ao"
    | "vestur:ao"

# AL eða Atvikssetning koma bæði til greina
AtvFsAuka → 
    AtvFs | Atvikssetning
AtviksliðurAuka → 
    Atviksliður | Atvikssetning

# [tæpra] 29 ára, [rúmlega] eins árs, tveggja vikna, margra ára
Aldur →
    TöluorðForskeyti_ef/tala/kyn? Töluorð_ef/tala/kyn TímaNafnorðStærri_ef/tala/kyn
    | 'margur:lo'_sb_ef_ft/kyn TímaNafnorðStærri_ef_ft/kyn
    | 'allmargur:lo'_sb_ef_ft/kyn TímaNafnorðStærri_ef_ft/kyn
    | 'fjölmargur:lo'_sb_ef_ft/kyn TímaNafnorðStærri_ef_ft/kyn
    | 'fár:lo'_sb_ef_ft/kyn TímaNafnorðStærri_ef_ft/kyn
    | 'örfár:lo'_sb_ef_ft/kyn TímaNafnorðStærri_ef_ft/kyn

# Gefa plús fyrir aldur
$score(+6) Aldur


# Dagsetning og tími

Tímaliður → 
    FöstDagsetning
    | AfstæðDagsetning
    | FasturTímapunktur
    | AfstæðurTímapunktur
    | Tíðni
    | Tímabil

$score(+8) Tímaliður
$tag(apply_prep_bonus) Tímaliður    # Gefa bónus þegar dagsetning fylgir sögn frekar en nafnlið
$tag(apply_length_bonus) Tímaliður  # Gefa bónus eftir því hversu margar eindir (tokens) falla undir dagsetninguna

# Aðalliður: Föst dagsetning
FöstDagsetning → 
    Vikudagur_gr_þf_et? dagsföst
    | HinnSá_þf dagsföst
    | DaganaFast

DaganaFast →
    'dagur:kk'_ft_gr_þf? dagsafs Til dagsföst     # (dagana) 13. október og/til/- 14. nóvember 2017
    | 'dagur:kk'_ft_gr_þf? raðnr Til dagsföst     # (dagana) 13. og/til/- 14. nóvember 2017

# Aðalliður: Afstæð dagsetning 
AfstæðDagsetning →
    ÓfullkominDagsetning
    | 'í:fs'_þf Árstíð_þf_et/kyn        # í haust, í sumar
    | 'í:fs'_þgf 'liðinn:lo'_sb_þgf_et_kvk 'vika:kvk'_þgf_et # í liðinni viku
    | 'á:fs'_þgf 'liðinn:lo'_sb_þgf_ft_hk 'ár:hk'_þgf_ft OgÁratugum? # á liðnum árum [og áratugum]
    | 'í:fs'_þgf dagsafs NæstSíðast_þgf_et_kk? # í október [1933] [síðastliðnum]

    | ÍByrjunLok Mánuður_et_ef          # í byrjun septembermánaðar
    | ÍByrjunLok dagsafs                # í byrjun október (2014)
    | ÍByrjunLok 'ár:hk'_et_ef ártal    # í byrjun árs 1945
    | ÍByrjunLok TímaÁkvæðisliður_ef_et/kyn? TímaNafnorðStærri_ef_et/kyn # í byrjun/lok dags/viku

    | TímaNafnliðurStærri_þgf/tala/kyn ÁðurSíðarSeinna             # (hér um bil) 10 vikum síðar
    | 'fyrir:fs'_þgf TímaNafnliðurStærri_þgf/tala/kyn 'síðan:ao'?  # fyrir (rúmlega) 10 árum (síðan)
    | 'eftir:fs'_þf TímaNafnliðurStærri_þf/tala/kyn                # eftir tæplega átta daga

    | FyrrSíðarSeinna? AfstæðDagsetningFs                          # fyrr á árinu, síðar á árinu
    | FyrrSíðarSeinna TímaVísiNafnliðurStærri_þf_et/kyn            # fyrr sama mánuð, fyrr sama dag   - of vítt með spurningarmerki á FyrrSíðarSeinna
    | 'í_hitteðfyrra:ao'
    | 'í_hittiðfyrra:ao'
    | VísiDagsetning                                               # á föstudag, um helgina, á morgun, í gær

OgÁratugum →
    "og:st" 'áratugur:kk'_þgf_ft

ÓfullkominDagsetning →
    Vikudagur_þf_et? dagsafs NæstSíðast_þf_et_kk?
    | HinnSá_þf dagsafs NæstSíðast_þf_et_kk?                       # hinn/þann 14. nóvember sl.
    | ártal
    | Árstíð_þf_et/kyn ártal_þf_et                                 # haustið 2016, veturinn 1998
    | HinnSá_þf raðnr SáÞessi_ef_et_kk 'mánuður:kk'_ef_et          # þann/hinn 23. þess/þessa mánaðar
    | Mánaðamót                                                    # mánaðamótin mars-apríl
    | DaganaAfs                                                    # dagana 14. og 15. október 
    | ÁrinAfs                                                      # árin 1938 og 1967
    | TímaÁkvæðisliður_þf_et_kk Vikudagur_et_þf                    # næsta fimmtudag
    | TímaÁkvæðisliður_þf_et/kyn Árstíð_þf_et/kyn                  # næsta haust, þann sama vetur

VísiDagsetning → 
    'á:fs'_þf Vikudagur_et_þf           # á föstudag
    | 'um:fs'_þf 'helgi:kvk'_þf_et_gr   # um helgina
    | 'um:fs'_þf 'mánaðamót:hk'_þf_ft_gr # um mánaðamótin
    | 'um:fs'_þf 'áramót:hk'_þf_ft_gr   # um áramótin
    | 'á_morgun:ao'                     # á morgun
    | 'í:fs'_þf 'dagur:kk'_þf_et        # í dag
    | 'í_gær:ao'                        # í gær
    | 'í_fyrradag:ao'                   # í fyrradag
    | 'í:fs'_þf 'fyrri:lo'_þf_et_mst    # í fyrra
    | 'dagur:kk'_þf_et_gr 'eftir:ao'    # daginn eftir
    | 'annar:fn'_et_þf_hk 'kvöld:hk'_et_þf # annað kvöld
    | 'annar:fn'_et_þf_kvk 'nótt:kvk'_et_þf # aðra nótt
    | 'á:fs'_þf Vikudagur_gr_þf_et 'koma:so'_0_gm_fh_et_p3          # á sunnudaginn kemur
    | 'á:fs'_þf VikuMorgunnKvöld_gr_þf_et 'koma:so'_0_gm_fh_et_p3   # á laugardagskvöldið kemur

# Hér eru aðeins orð yfir mánuði sem ekki tengjast við 'dagsafs' lauf
Mánuður_et/fall →
    'janúarmánuður:kk'/fall
    | 'febrúarmánuður:kk'/fall
    | 'marsmánuður:kk'/fall
    | 'aprílmánuður:kk'/fall
    | 'maímánuður:kk'/fall
    | 'júnímánuður:kk'/fall
    | 'júlímánuður:kk'/fall
    | 'ágústmánuður:kk'/fall
    | 'septembermánuður:kk'/fall
    | 'októbermánuður:kk'/fall
    | 'nóvembermánuður:kk'/fall
    | 'desembermánuður:kk'/fall

DaganaAfs → 
    'dagur:kk'_gr_ft_þf? dagsafs Til dagsafs       # (dagana) 13. október og/til/- 14. nóvember
    | 'dagur:kk'_gr_ft_þf? raðnr Til dagsafs       # (dagana) 13. og/til/- 14. nóvember 

ÁrinAfs →
    ártal ÁraRuna* Til tala                        # 2016-2017, 2016 til 17
    | 'ár:hk'_ft_gr_þf tala ÁraRuna* Til tala      # árin 670-80, árin 2016 til 2017

Mánaðamót →
    'mánaðamót:hk'_ft_þf dagsafs '-:en' dagsafs

Árstíð_þf_et_hk →
    'vor:hk'_þf_et | 'sumar:hk'_þf_et | 'haust:hk'_þf_et

Árstíð_þf_et_kk →
    'vetur:kk'_þf_et

Árstíð_þf_et_kvk →
    "" # Never matches

# Bara AfstæðDagsetning -- aðrir svipaðir liðir fara í aðrar reglur
# (í viku (Tímabil), á ári (Tíðni), í mánuð (Tímabil), ...)
# á árinu, í vikunni, í mánuðinum
AfstæðDagsetningFs → 
    'á:fs'_þgf 'ár:hk'_þgf_et_gr                                # á árinu
    | 'á:fs'_þgf TímaÁkvæðisliður_þgf_et_hk 'ár:hk'_þgf_et      # á næsta ári
    | 'í:fs'_þgf 'mánuður:kk'_þgf_et_gr                         # í mánuðinum
    | 'í:fs'_þgf TímaÁkvæðisliður_þgf_et_kk 'mánuður:kk'_þgf_et # í síðasta mánuði
    | 'í:fs'_þgf 'vika:kvk'_þgf_et_gr                           # í vikunni
    | 'í:fs'_þgf TímaÁkvæðisliður_þgf_et_kvk 'vika:kvk'_þgf_et  # í sömu viku
    | 'í:fs'_þf Árstíð_þf_et/kyn                                # í haust, í vetur, í vor, í sumar

# Aðalliður: Fastur tímapunktur 
FasturTímapunktur →
    tímapunkturfast                                 # 14. nóvember 2017 kl. 14:35
    | 'áramót:hk'_gr_ft_þf tala '-:en' tala         # áramótin 2016-2017

# Aðalliður: Afstæður tímapunktur 
AfstæðurTímapunktur →
    ÓfullkominnTímapunktur      # Timestamp, underspecified
    | FasturTími                # Time, absolute
    | AfstæðurTími              # Time, relative
    | ÓfullkominnTími           # Time, underspecified

ÓfullkominnTímapunktur →
    tímapunkturafs
    | HinnSá_þf tímapunkturafs
    | HinnSá_þf dagsafs NæstSíðast_þf_et_kk? tími
    | Vikudagur_et_þf tímapunkturafs
    | Vikudagur_gr_et_þf dagsafs NæstSíðast_þf_et_kk? tími
    | tími Vikudagur_gr_et_þf dagsafs NæstSíðast_þf_et_kk?
    | tími Vikudagur_gr_et_þf dagsföst
    | tími VísiDagsetning
    | VísiDagsetning tími
    | tími HinnSá_þf dagsafs NæstSíðast_þf_et_kk?   # kl. 14:00 hinn/þann 14. nóvember [síðastliðinn]
    | tími HinnSá_þf dagsföst                       # kl. 14:00 hinn/þann 14. nóvember 2017

FasturTími →
    tími Dagspartur?                                # 14:00, kl. 14.00 [síðdegis/árdegis]
    | Mínútur YfirÍ_þf tími Dagspartur?             # tuttugu mínútur í tvö, korter yfir átta
    | Mínútur YfirÍ_þf Klukkutala_þf/tala Dagspartur?

AfstæðurTími →
    TímaNafnliðurMinni_þgf/tala/kyn ÁðurSíðarSeinna               # um það bil tæpum 10 sekúndum síðar
    | 'fyrir:fs'_þgf TímaNafnliðurMinni_þgf/tala/kyn 'síðan:ao'?  # fyrir hér um bil rúmum 10 klukkustundum síðan
    | 'eftir:fs'_þf TímaNafnliðurMinni_þf/tala/kyn

ÓfullkominnTími →
    ÁTímanum                                        # á sjötta tímanum
    | Dagspartur
    | ParturHluti_þf_et Vikudagur_ef_et             # fyrri part sunnudags / seinni helming mánudags
    | 'aðfaranótt:kvk'_et_þf AfstæðDagsetning
    | 'aðfaranótt:kvk'_et_þf Vikudagur_ef_et
    | TímaVísiNafnliðurMinni_þf_et/kyn              # sömu mínútu, sama klukkutíma
    | TímaVísiNafnliðurStærri_þf/tala/kyn           # undanfarið ár / undanfarna áratugi
    | FyrrSíðarSeinna? TímaÁkvæðisliður_þf_et/kyn 
        MorgunnKvöld/kyn                            # sama kvöld, fyrr sama morgun
    | FyrrSíðarSeinna? AfstæðurTímiFs

ÁTímanum → 
    'á:fs'_þgf RaðKlukkuTala_þgf_et_kk 'tími:kk'_þgf_et_gr  # Á sjötta tímanum

RaðKlukkuTala/fall/tala/kyn →
    'fyrstur:lo'/fall/tala/kyn
    | 'annar:lo'/fall/tala/kyn
    | 'þriðji:lo'/fall/tala/kyn
    | 'fjórði:lo'/fall/tala/kyn
    | 'fimmti:lo'/fall/tala/kyn
    | 'sjötti:lo'/fall/tala/kyn
    | 'sjöundi:lo'/fall/tala/kyn
    | 'áttundi:lo'/fall/tala/kyn
    | 'níundi:lo'/fall/tala/kyn
    | 'tíundi:lo'/fall/tala/kyn
    | 'ellefti:lo'/fall/tala/kyn
    | 'tólfti:lo'/fall/tala/kyn

ParturHluti/fall/tala →
    'fyrripartur:kk'/fall/tala
    | 'seinnipartur:kk'/fall/tala
    | 'fyrri:lo'_kk/fall/tala 'hluti:kk'/fall/tala/kyn
    | 'seinni:lo'_kk/fall/tala 'hluti:kk'/fall/tala/kyn
    | 'fyrri:lo'_kk/fall/tala 'helmingur:kk'/fall/tala/kyn
    | 'seinni:lo'_kk/fall/tala 'helmingur:kk'/fall/tala/kyn

MorgunnKvöld_kk → 'morgunn:kk'_þf_et
MorgunnKvöld_hk → 'kvöld:hk'_þf_et
MorgunnKvöld_kvk → "" # Never matches

AfstæðurTímiFs →
    'á:fs'_þgf TímaÁkvæðisliður_þgf_et_kvk 'sekúnda:kvk'_þgf_et         # á sömu sekúndu
    | 'á:fs'_þgf TímaÁkvæðisliður_þgf_et_kvk 'mínúta:kvk'_þgf_et        # á næstu mínútu
    | 'á:fs'_þgf TímaÁkvæðisliður_þgf_et_hk 'kortér:hk'_þgf_et          # á sama kortéri
    | 'á:fs'_þgf TímaÁkvæðisliður_þgf_et_hk 'korter:hk'_þgf_et          # á næsta korteri
    | 'á:fs'_þgf TímaÁkvæðisliður_þgf_et_kvk 'klukkustund:kvk'_þgf_et   # á síðustu klukkustund
    | 'á:fs'_þgf TímaÁkvæðisliður_þgf_et_kk 'klukkutími:kk'_þgf_et      # á sama klukkutíma

Mínútur →
    Mínútutala/tala 'mínúta:kvk'_þf/tala    # eina mínútu, tíu mínútur (yfir tvö)
    | 'korter:hk'_þf_et
    | 'korter:hk'_þgf_et                    # korteri (fyrir tólf)
    | 'kortér:hk'_þf_et
    | 'kortér:hk'_þgf_et                    # kortéri (eftir þrjú)
    | 'stundarfjórðungur:kk'_þf_et          # stundarfjórðung (fyrir tíu)
    | 'stundarfjórðungur:kk'_þgf_et         # stundarfjórðungi (eftir sjö)

Mínútutala/tala →
    Klukkutala_þf/tala

Mínútutala_ft → 
    'þrettán:töl'
    | 'fjórtán:töl'
    | 'fimmtán:töl'
    | 'sextán:töl'
    | 'sautján:töl'
    | 'átján:töl'
    | 'nítján:töl'
    | 'tuttugu:töl'

# Útskrifaðar tölur til að tákna tímasetningu
Klukkutala_þf_et → 
    'einn:to'_et_hk/fall        # eitt

Klukkutala_þf_ft →
    'tveir:to'_ft_hk_þf         # tvö
    | 'þrír:to'_ft_hk_þf        # þrjú
    | 'fjórir:to'_ft_hk_þf      # fjögur
    | 'fimm:töl'
    | 'sex:töl'
    | 'sjö:töl'
    | 'átta:töl'
    | 'níu:töl'
    | 'tíu:töl'
    | 'ellefu:töl'
    | 'tólf:töl'

YfirÍ_þf → 'yfir:fs'_þf | 'í:fs'_þf

# Liðir sem geta staðið einir og sér en eru minni eining en dagur
Dagspartur →
    'árdegis:ao'
    | 'miðdegis:ao'
    | 'síðdegis:ao'
    | FyrirEftirÁ/fall HádegiMiðnætti/fall
    | 'við:fs'_þf Sól_þf
    | 'við:fs'_þf DagsByrjunLok_þf
    | 'að:fs'_þgf Lag_þgf
    | ÍByrjunLok 'dagur:kk'_et_ef
    | ÍByrjunLok KvöldNótt_ef
    | 'í:fs'_þf KvöldNótt_þf
    | 'í_morgun:ao'
    | 'í_gærmorgun:ao'
    | 'í_gærkvöld:ao'
    | 'í_gærkvöldi:ao'
    | 'í:fs'_þf DagsByrjunLok_þf
    | 'í:fs'_þgf 'hádegi:hk'_þgf_et_gr
    | 'á:fs'_þf VikuMorgunnKvöld_et_þf      # á mánudagsmorgun/morgni, á fimmtudagskvöld/kvöldi
    | 'á:fs'_þgf VikuMorgunnKvöld_et_þgf    # á mánudagsmorgun/morgni, á fimmtudagskvöld/kvöldi
    | 'um:fs'_þf UmTímanafnorð_þf

FyrirEftirÁ_nf → "" # Never matches
FyrirEftirÁ_þf → 'fyrir:fs'_þf | 'eftir:fs'_þf
FyrirEftirÁ_þgf → 'á:fs'_þgf
FyrirEftirÁ_ef → "" # Never matches

HádegiMiðnætti/fall → 'hádegi:hk'_et/fall | 'miðnætti:hk'_et/fall

Sól/fall →
    'sólarupprás:kvk'_et/fall
    | 'sólsetur:hk'_et/fall

DagsByrjunLok/fall →
    'dagsbyrjun:kvk'_et/fall
    | 'dagslok:hk'_ft/fall

Lag/fall →
    'næturlag:hk'_et/fall
    | 'morgunlag:hk'_et/fall
    | 'kvöldlag:hk'_et/fall

KvöldNótt/fall → 'kvöld:hk'_et/fall | 'nótt:kvk'_et/fall

VikuMorgunnKvöld/tala/fall → VikuMorgunnKvöld/fall/tala
VikuMorgunnKvöld/fall/tala →
    'mánudagsmorgunn:kk'/fall/tala
    | 'mánudagskvöld:hk'/fall/tala
    | 'þriðjudagsmorgunn:kk'/fall/tala
    | 'þriðjudagskvöld:hk'/fall/tala
    | 'miðvikudagsmorgunn:kk'/fall/tala
    | 'miðvikudagskvöld:hk'/fall/tala
    | 'fimmtudagsmorgunn:kk'/fall/tala
    | 'fimmtudagskvöld:hk'/fall/tala
    | 'föstudagsmorgunn:kk'/fall/tala
    | 'föstudagskvöld:hk'/fall/tala
    | 'laugardagsmorgunn:kk'/fall/tala
    | 'laugardagskvöld:hk'/fall/tala
    | 'sunnudagsmorgunn:kk'/fall/tala
    | 'sunnudagskvöld:hk'/fall/tala

VikuMorgunnKvöld_gr/fall/tala →
    'mánudagsmorgunn:kk'_gr/fall/tala
    | 'mánudagskvöld:hk'_gr/fall/tala
    | 'þriðjudagsmorgunn:kk'_gr/fall/tala
    | 'þriðjudagskvöld:hk'_gr/fall/tala
    | 'miðvikudagsmorgunn:kk'_gr/fall/tala
    | 'miðvikudagskvöld:hk'_gr/fall/tala
    | 'fimmtudagsmorgunn:kk'_gr/fall/tala
    | 'fimmtudagskvöld:hk'_gr/fall/tala
    | 'föstudagsmorgunn:kk'_gr/fall/tala
    | 'föstudagskvöld:hk'_gr/fall/tala
    | 'laugardagsmorgunn:kk'_gr/fall/tala
    | 'laugardagskvöld:hk'_gr/fall/tala
    | 'sunnudagsmorgunn:kk'_gr/fall/tala
    | 'sunnudagskvöld:hk'_gr/fall/tala

# Safn tímanafnorða sem geta staðið í FL með 'um' og tákna einingu sem er styttri en dagur
UmTímanafnorð/fall →
    'aftann:kk'_et/fall
    | 'árdegi:hk'_et/fall
    | 'árdegisbil:hk'_et/fall
    | 'eftirmiðdagur:kk'_et/fall
    | 'eftirmiðdegi:hk'_et/fall
    | 'hádegi:hk'_et/fall
    | 'hádegisbil:hk'_et/fall
    | 'hádegisleyti:hk'_et/fall
    | 'kvöld:hk'_et/fall
    | 'kvöldbil:hk'_et/fall
    | 'kvöldleyti:hk'_et/fall
    | 'kvöldmatarleyti:hk'_et/fall
    | 'miðdegi:hk'_et/fall
    | 'miðdegisbil:hk'_et/fall
    | 'miðdegisleyti:hk'_et/fall
    | 'miðnætti:hk'_et/fall
    | 'miðnæturbil:hk'_et/fall
    | 'miðnæturleyti:hk'_et/fall
    | 'morgunbil:hk'_et/fall
    | 'morgunleyti:hk'_et/fall
    | 'morgunn:kk'_et/fall
    | 'nótt:kvk'_et/fall
    | 'síðdegi:hk'_et/fall
    | 'síðdegisbil:hk'_et/fall

# Aðalliður: Tíðni
Tíðni →
    'að_jafnaði:ao'
    | 'ótt_og_títt:ao'
    | 'sí_og_æ:ao'
    | 'í_sífellu:ao'
    | 'aldrei:ao'
    | 'alltaf:ao'
    | 'árlega:ao'
    | 'daglega:ao'
    | 'dagsdaglega:ao'
    | 'mánaðarlega:ao'
    | 'oft:ao'
    | 'reglulega:ao'
    | 'sjaldan:ao'
    | 'stundum:ao'
    | 'títt:ao'
    | 'vikulega:ao'
    | TíðniNafnliðurStærri
    | TíðniNafnliðurMinni
    | TíðniForsetningarliðurStærri
    | TíðniForsetningarliðurMinni

TíðniNafnliðurStærri →
    TíðniLo_þf_et/kyn TímaNafnorðStærri_þf_et/kyn       # sérhvern dag, hverja viku
    | 'dagur:kk'_þf_et TíðniLo_þf_et_kk                 # dag hvern
    | TíðniLo_þf_et_kk Vikudagur_þf_et                  # sérhvern fimmtudag
    | 'allur:fn'_þf_ft/kyn 'virkur:lo'_þf_ft/kyn?       # alla (virka) daga - ath. et. væri tímabil; allan daginn
        TímaNafnorðStærri_þf_ft/kyn       
    | 'allur:fn'_þf_ft_kk Vikudagur_þf_ft               # alla fimmtudaga

TíðniForsetningarliðurStærri →
    'á:fs'_þgf TíðniLo_þgf_et_kk                        # á hverjum (virkum) degi
        'virkur:lo'_þgf_et_kk? 'dagur:kk'_þgf_et    
    | 'á:fs'_þgf TíðniLo_þgf_et_kk Vikudagur_þgf_et     # á hverjum fimmtudegi
    | 'á:fs'_þgf Vikudagur_þgf_ft                       # á fimmtudögum
    | 'í:fs'_þgf TíðniLo_þgf_et_kvk 'vika:kvk'_þgf_et   # í hverri viku
    | 'í:fs'_þgf TíðniLo_þgf_et_kk 'mánuður:kk'_þgf_et  # í hverjum mánuði
    | 'á:fs'_þgf TíðniLo_þgf_et_hk 'ár:hk'_þgf_et       # á hverju ári

TíðniNafnliðurMinni →
    TíðniLo_þf_et/kyn MorgunnKvöld/kyn                  # sérhvern morgun, hvert kvöld
    | 'allur:fn'_þf_ft/kyn 'virkur:lo'_þf_ft/kyn?       # alla (virka) morgna, öll kvöld - ath. et. væri tímabil; allt kvöldið
        MorgunnKvöld/kyn

TíðniForsetningarliðurMinni →
    'á:fs'_þgf TíðniLo_þgf_et/kyn                       # á hverjum (virkum) morgni, á sérhverju kvöldi
        'virkur:lo'_þgf_et/kyn? MorgunnKvöld/kyn
    | 'á:fs'_þgf VikuMorgunnKvöld_þgf_ft                # á fimmtudagsmorgnum, á sunnudagskvöldum
    | 'á:fs'_þgf TíðniLo_þgf_et/kyn                     # á hverju fimmtudagskvöldi, á sérhverjum sunnudagsmorgni
        VikuMorgunnKvöld_þgf_et
    | 'á_morgnana:ao'
    | 'á_kvöldin:ao'

TíðniLo/fall/tala/kyn → 'hver:fn'/fall/tala/kyn | 'sérhver:fn'/fall/tala/kyn

# Aðalliður: Tímabil
Tímabil →
    FastTímabil
    | AfstættTímabil
    | TímabilTími

FastTímabil →
    DaganaTímabilFast
    | PunkturFs/aukafall FasturPunkturTNL
    | 'milli:fs'_ef FasturPunkturTNL 'og:st' FasturPunkturTNL
    | 'frá:fs'_þgf FasturPunkturTNL 'til:fs'_ef FasturPunkturTNL
    | 'milli:fs'_ef TímiPunkturTNL 'og:st' tímapunkturfast          # milli kl. 14:00 og 17:00 14. nóvember 2017

AfstættTímabil → 
    DaganaTímabilAfstætt
    | ÁrinTímabilAfstætt
    | FráTilTími VísiDagsetning
    | VísiDagsetning FráTilTími
    | MagnFs/aukafall TímaMagnNafnliðurStærri/aukafall/tala/kyn     # á 20 dögum, innan 6 daga, í 30 ár
    | 'út:fs'_þf TímaMagnNafnliðurStærri_þf/tala/kyn                # út árið
    | 'út:fs'_þf ártal
    | 'yfir:fs'_þf TímaMagnNafnliðurStærri_þf/tala/kyn              # yfir mánuðinn
    | PunkturFs/aukafall AfsPunkturTNL/aukafall
    | 'milli:fs'_ef AfsPunkturTNL_ef 'og:st' AfsPunkturTNL_ef
    | 'frá:fs'_þgf AfsPunkturTNL_þgf 'til:fs'_ef AfsPunkturTNL_ef
    | 'milli:fs'_ef FasturPunkturTNL 'og:st' AfsPunkturTNL_ef
    | 'frá:fs'_þgf FasturPunkturTNL 'til:fs'_ef AfsPunkturTNL_ef
    | 'milli:fs'_ef AfsPunkturTNL_ef 'og:st' FasturPunkturTNL
    | 'frá:fs'_þgf AfsPunkturTNL_þgf 'til:fs'_ef FasturPunkturTNL
    | 'milli:fs'_ef TímiPunkturTNL 'og:st' tímapunkturafs
    | TímaNafnorðStærri_þf/tala/kyn ÁðurEftir
    | TímaNafnorðStærri_þgf_ft/kyn "saman:ao"                       # Nefndin fundaði ekki mánuðum saman
    | "svo:ao" TímaNafnorðStærri_þgf_ft/kyn 'skipta:so'_1_þgf_gm_fh_et_p3 # svo mánuðum skiptir/skipti
    | ÞegarLíðaTekurÁ

TímabilTími →
    FráTilTími 
    | TímaNafnliðurMinni/fall/tala/kyn                              # um það bil tæpar 10 sekúndur
    | TímaMagnNafnliðurMinni/fall/tala/kyn                          # næstu 8 mínútur
    | MagnFs/aukafall TímaMagnNafnliðurMinni/aukafall/tala/kyn      # á 20 sekúndum, innan 6 sekúndna, í 30 millisekúndur
    | PunkturFs/aukafall TímiPunkturTNL                             # eftir kl. 14:00
    | 'milli:fs'_ef TímiPunkturTNL 'og:st' TímiPunkturTNL
    | 'frá:fs'_þgf TímiPunkturTNL 'til:fs'_ef TímiPunkturTNL
    | TímaNafnorðMinni_þf/tala/kyn ÁðurEftir
    | TímaNafnorðMinni_þgf_ft/kyn "saman:ao"                        # Hann kom ekki heim klukkutímunum saman
    | "svo:ao" TímaNafnorðMinni_þgf_ft/kyn 'skipta:so'_1_þgf_gm_fh_et_p3  # svo mínútum skiptir/skipti

FasturPunkturTNL → FöstDagsetning | FasturTímapunktur

TímiPunkturTNL → FasturTími | ÓfullkominnTími

# Dagsetningar/tímapunktar sem ganga innan FL til að tákna tímabil
AfsPunkturTNL/fall → 
    ÓfullkominDagsetning
    | ÓfullkominnTímapunktur
    | Vikudagur/fall/tala tími?

# Forsetningar sem standa með TNL sem sýnir magn - 20 sekúndur, 30 ár
MagnFs_þf →
    'í:fs'_þf

MagnFs_þgf →
    'á:fs'_þgf

MagnFs_ef →
    'innan:fs'_ef

# Forsetningar sem standa með TNL sem sýna fastan punkt - 14. nóvember 2016, kl. 14:00
PunkturFs_þf →
    'fyrir:fs'_þf       # Ath. með þgf táknar það dagsetningu/tíma.
    | 'eftir:fs'_þf
    | 'síðan:fs'_þf

PunkturFs_þgf →
    'frá:fs'_þgf OgMeð?

PunkturFs_ef →
    'til:fs'_ef OgMeð?
    | 'til_og_með:ao'

DaganaTímabilAfstætt →
    'dagur:kk'_gr_ft_þf? dagsafs TímabilTil dagsafs         # (dagana) 13. október til 14. nóvember
    | 'dagur:kk'_gr_ft_þf? raðnr TímabilTil dagsafs         # (dagana) 13. og 14. nóvember
    | Vikudagur_et_þf tímapunkturafs "-:en" tími            # miðvikudaginn 13. desember kl 14:00 - 15:00

ÁrinTímabilAfstætt →
    ártal ÁraRuna* TímabilTil tala                          # 2016-2017 / 2016 til 2017
    | 'ár:hk'_ft_gr_þf tala ÁraRuna* TímabilTil tala        # árin 2016-2017, árin 2016 til 2017

DaganaTímabilFast →
    'dagur:kk'_gr_ft_þf? dagsafs TímabilTil dagsföst        # (dagana) 13. október til 14. nóvember 2017; 13. október-14. nóvember 2017
    | 'dagur:kk'_gr_ft_þf? raðnr TímabilTil dagsföst        # (dagana) 13.-14. nóvember 2017, 13. til 14. nóvember 2017
    | Vikudagur_et_þf tímapunkturfast "-:en" tími           # miðvikudaginn 13. desember 2017 kl 14:00 - 15:00

FráTilTími → 
    'frá:fs'_þgf? tími TímabilTil tími DaganaTímabilAfstætt?
    | 'frá:fs'_þgf? tími TímabilTil tími DaganaTímabilFast?
    | 'frá:fs'_þgf? tími TímabilTil tímapunkturfast                # kl. 14:00-17:00 14. nóvember 2017
    | 'frá:fs'_þgf? tími TímabilTil tímapunkturafs                 # kl. 14:00-17:00 14. nóvember
    | 'frá:fs'_þgf? tími TímabilTil tími HinnSá_þf? dagsföst       # kl. 14:00 til 17:00 hinn 14. nóvember 2017
    | 'frá:fs'_þgf? tími TímabilTil tími HinnSá_þf? dagsafs        # kl. 14:00 til 17:00 þann 14. nóvember

TímabilTil → 'til:fs' | '-:en'

ÞegarLíðaTekurÁ →
    "þegar:st" 'líða:so'_0_nh 'taka:so'_0_gm_p3_et 'á:fs'_þf TímaNafnorðStærri_þf/tala/kyn

OgMeð → 'og:st' 'með:fs'_þgf    # (frá) og með, (til) og með

# Tímanafnliðir
TímaNafnliðurStærri/fall/tala/kyn →
    TímaAtviksEinkunn? TímaLo/fall/tala/kyn? 
        Tímatala/fall/tala/kyn? TímaNafnorðStærri/fall/tala/kyn

TímaNafnliðurMinni/fall/tala/kyn → 
    TímaAtviksEinkunn? TímaLo/fall/tala/kyn? 
        Tímatala/fall/tala/kyn? TímaNafnorðMinni/fall/tala/kyn

# næsta dag
TímaVísiNafnliðurStærri/fall/tala/kyn → 
    TímaÁkvæðisliður/fall/tala/kyn TímaNafnorðStærri/fall/tala/kyn

# sömu mínútu
TímaVísiNafnliðurMinni/fall/tala/kyn → 
    TímaÁkvæðisliður/fall/tala/kyn TímaNafnorðMinni/fall/tala/kyn

# næstu 20 daga
TímaMagnNafnliðurStærri/fall/tala/kyn →
    TímaAtviksEinkunn? TímaÁkvæðisliður/fall/tala/kyn? 
        Tímatala/fall/tala/kyn? TímaNafnorðStærri/fall/tala/kyn

# síðustu 40 mínútur
TímaMagnNafnliðurMinni/fall/tala/kyn →
    TímaAtviksEinkunn? TímaÁkvæðisliður/fall/tala/kyn? 
        Tímatala/fall/tala/kyn? TímaNafnorðMinni/fall/tala/kyn

# Tímaeiningar sem eru styttri en dagur
TímaNafnorðMinni/fall/tala/kyn → TímaNafnorðMinni/kyn/fall/tala

TímaNafnorðMinni_kvk/fall/tala →
    'nanósekúnda:kvk'/fall/tala
    | 'míkrósekúnda:kvk'/fall/tala
    | 'millisekúnda:kvk'/fall/tala
    | 'sekúnda:kvk'/fall/tala
    #| 'sek.'
    | 'mínúta:kvk'/fall/tala
    #| 'mín.'
    | 'klukkustund:kvk'/fall/tala
    #| 'klst.'

TímaNafnorðMinni_hk/fall/tala →
    'sekúndubrot:hk'/fall/tala
    | 'korter:hk'/fall/tala
    | 'kortér:hk'/fall/tala
    
TímaNafnorðMinni_kk/fall/tala →
    'stundarfjórðungur:kk'/fall/tala
    | 'hálftími:kk'/fall/tala
    | 'klukkutími:kk'/fall/tala

# Tímaeiningar sem eru dagur eða lengri
TímaNafnorðStærri/fall/tala/kyn → TímaNafnorðStærri/kyn/fall/tala

TímaNafnorðStærri_kk/fall/tala →
    'dagur:kk'/fall/tala
    | 'sólarhringur:kk'/fall/tala
    | 'mánuður:kk'/fall/tala
    | 'ársfjórðungur:kk'/fall/tala
    | 'áratugur:kk'/fall/tala
    | 'aldarfjórðungur:kk'/fall/tala
    | 'vetur:kk'/fall/tala

TímaNafnorðStærri_kvk/fall/tala →
    'vika:kvk'/fall/tala
    | 'önn:kvk'/fall/tala
    | 'öld:kvk'/fall/tala
    | 'þúsöld:kvk'/fall/tala

TímaNafnorðStærri_hk/fall/tala →
    'misseri:hk'/fall/tala
    | 'ár:hk'/fall/tala
    | 'árhundrað:hk'/fall/tala
    | 'árþúsund:hk'/fall/tala
    | 'sumar:hk'/fall/tala

TímaAtviksEinkunn →
    'rétt:ao' TímaAtviksEinkunn
    | 'rúmlega:ao'
    | 'næstum:ao'
    | 'innan_við:ao'
    | 'tæplega:ao'
    | 'nákvæmlega:ao'
    | 'hér_um_bil:ao'
    | 'ríflega:ao'
    | 'næstum:ao' 'það:fn'_þgf?     # næstum, næstum því
    | 'um:ao' 
    | 'um:fs'_þf 'sá:fn'_þf_et_hk 'bil:hk'_þf_et

Tímatala/fall/tala/kyn →
    tala/fall/tala/kyn
    | töl/fall/tala/kyn
    | to/fall/tala/kyn
    | 'hálfur:lo'/fall/tala/kyn
    | 'nokkur:fn'/fall/tala/kyn # nokkrum dögum seinna
    | 'fár:lo'_sb/fall/tala/kyn # fáum dögum síðar
    | 'örfár:lo'_sb/fall/tala/kyn # örfáum vikum síðar
    | 'margur:lo'_sb/fall/tala/kyn # mörgum vikum síðar
    | 'allmargur:lo'_sb/fall/tala/kyn # allmörgum vikum síðar
    | Tímatala/fall/tala/kyn 'til:ao' Tímatala/fall/tala/kyn
    | Tímatala/fall/tala/kyn '-:en' Tímatala/fall/tala/kyn

TímaLo/fall/tala/kyn → 'rúmur:lo'/fall/tala/kyn | 'tæpur:lo'/fall/tala/kyn

# Hjálparreglur
HinnSá/fall → 'sá:fn'_et_kk/fall | 'hinn:gr'_et_kk/fall

Vikudagur/tala/fall → Vikudagur/fall/tala
Vikudagur/fall/tala →
    # !!! Do the following in the tokenizer instead?
    'sunnudagur:kk'/fall/tala
    | 'mánudagur:kk'/fall/tala
    | 'þriðjudagur:kk'/fall/tala
    | 'miðvikudagur:kk'/fall/tala
    | 'fimmtudagur:kk'/fall/tala
    | 'föstudagur:kk'/fall/tala
    | 'laugardagur:kk'/fall/tala
    | 'sunnudagur:kk'/fall/tala

Vikudagur_gr/tala/fall → Vikudagur_gr/fall/tala
Vikudagur_gr/fall/tala →
    'sunnudagur:kk'_gr/fall/tala
    | 'mánudagur:kk'_gr/fall/tala
    | 'þriðjudagur:kk'_gr/fall/tala
    | 'miðvikudagur:kk'_gr/fall/tala
    | 'fimmtudagur:kk'_gr/fall/tala
    | 'föstudagur:kk'_gr/fall/tala
    | 'laugardagur:kk'_gr/fall/tala
    | 'sunnudagur:kk'_gr/fall/tala

FyrrSíðarSeinna →
    'snemma:ao'_mst     # fyrr
    | 'síðar:ao'        # síðar
    | 'seint:ao'_mst    # seinna

ÁðurSíðarSeinna →
    'áður:ao' 
    | 'síðar:ao'
    | 'seint:ao'_mst
    | 'snemma:ao'_mst # fyrr

TímaÁkvæðisliður/fall/tala/kyn →
    SáÞessi/fall/tala/kyn TímaÁkvæðisLo/fall/tala/kyn?
    | TímaÁkvæðisLo/fall/tala/kyn

TímaÁkvæðisLo/fall/tala/kyn →
    'næstur:lo'/fall/tala/kyn
    | 'síðari:lo'_evb/fall/tala/kyn     # síðastur
    | 'sami:fn'/fall/tala/kyn           # 'fyrr í þeim sama mánuði' -- hegðar sér frekar sem lo hér.
    | 'samur:fn'/fall/tala/kyn
    | 'seinn:lo'_evb/fall/tala/kyn      # seinastur
    | 'næstkomandi:lo'/fall/tala/kyn
    | 'síðastliðinn:lo'/fall/tala/kyn
    | 'undanfarinn:lo'/fall/tala/kyn
    | 'undangenginn:lo'/fall/tala/kyn

SáÞessi/fall/tala/kyn → 'sá:fn'/fall/tala/kyn | 'þessi:fn'/fall/tala/kyn

# Lýsingarorð sem ganga á eftir dagsetningum
NæstSíðast/fall/tala/kyn →
    'næstkomandi:lo'/fall/tala/kyn
    | 'síðastliðinn:lo'/fall/tala/kyn

ÁðurEftir →
    'áður:ao'
    | 'eftir:ao'
    | 'á_undan:ao'
    | 'á_eftir:ao'

ÍByrjunLok →
    'í:fs'_þgf 'byrjun:kvk'_þgf
    | 'í:fs'_þf 'lok:hk'_ft_þf
    | 'undir:fs'_þf 'lok:hk'_ft_þf


# ----------------------------------------------
#
# Query grammar
#
# The following grammar is used for queries only
#
# ----------------------------------------------

QueryRoot →

    QPerson > QCompany > QEntity > QTitle > QWord > QSearch

# Mark the QueryRoot nonterminal as a root in the grammar
$root(QueryRoot)

QPerson →
    Manneskja_nf
    | QPersonPrefix_nf Manneskja_nf '?'?
    | QPersonPrefix_þf Manneskja_þf '?'?
    | QPersonPrefix_þgf Manneskja_þgf # '?'?
    | QPersonPrefix_ef Manneskja_ef '?'?
    | QPersonPrefixAny Sérnafn '?'?

QPersonPrefixAny →
    QPersonPrefix/fall

$score(-2) QPersonPrefixAny # Discourage entity names if person names are available

QPersonPrefix_nf →
    "hver" "er"
    | "hvað" "gerir"
    | "hvað" "starfar"
    | "hvaða" "titil" "hefur"
    | "hvaða" "starfi" "gegnir"

QPersonPrefix_þf →
    "hvað" "veistu" "um"
    | "hvað" "geturðu" "sagt" "mér"? "um"

QPersonPrefix_ef →
    "hver" "er" "titill"
    | "hver" "er" "starfstitill"
    | "hvert" "er" "starf"

QPersonPrefix_þgf →
    "segðu" "mér"? "frá"

QCompany →
    QCompanyPrefix_nf Fyrirtæki_nf '?'?
    | QCompanyPrefix_þf Fyrirtæki_þf '?'?
    | QCompanyPrefix_þgf Fyrirtæki_þgf # '?'?

QCompanyPrefix_nf →
    "hvað" "er"

QCompanyPrefix_þf →
    "hvað" "veistu" "um"
    | "hvað" "geturðu" "sagt" "mér"? "um"

QCompanyPrefix_þgf →
    "segðu" "mér"? "frá"

QEntity → QEntityPrefix/fall QEntityKey/fall '?'?

QEntityKey/fall →
    Sérnafn/fall > Sérnafn > no/fall > no

QEntityPrefix_nf →
    "hvað" "er"
    | "hvað" "eru"
    | 0

QEntityPrefix_þf →
    "hvað" "veistu" "um"
    | "hvað" "geturðu" "sagt" "mér"? "um"

QEntityPrefix_þgf →
    "segðu" "mér"? "frá"

QEntityPrefix_ef →
    0

QTitle →
    QTitlePrefix_nf QTitleKey_nf '?'?
    | QTitlePrefix_ef QTitleKey_ef '?'?

QSegðuMér →
    "segðu" "mér"
    | "mig" "langar" "að" "vita"
    | "ég" "vil" "gjarnan"? "vita"

QTitlePrefix_nf →
    QSegðuMér? QTitlePrefixFrh_nf

QTitlePrefixFrh_nf →
    "hver" "er"
    | "hver" "var"
    | "hver" "hefur" "verið"

QTitlePrefix_ef →
    QSegðuMér? "hver" "gegnir" "starfi"

QTitleKey_nf →
    EinnTitill_nf OgTitill_nf*

QTitleKey_ef →
    EinnTitill_ef OgTitill_ef*

# Word relation query

QWord →
    QWordPerson
    > QWordEntity
    > QWordNoun
    > QWordVerb

QWordPrefix_þgf →
    "hvað" "tengist"
    | "hvað" "er" "tengt"
    | "hvaða" "orð" "tengjast"
    | "hvaða" "orð" "tengist"
    | "hvaða" "orð" "eru" "tengd"

QWordPrefix_nf →
    "hverju" "tengist"
    | "hvaða" "orðum" "tengist"

# 'Hvað tengist [orðinu/nafnorðinu] útihátíð?'

QWordNoun →
    QWordNoun_nf
    | QWordNoun_þgf

QWordNoun_þgf →
    QWordPrefix_þgf QWordNounKey_þgf '?'?

QWordNoun_nf →
    QWordNounKey_nf
    | QWordPrefix_þgf "orðinu" QWordNounKey_nf '?'?
    | QWordPrefix_þgf "nafnorðinu" QWordNounKey_nf '?'?
    | QWordPrefix_nf "orðið" QWordNounKey_nf '?'?
    | QWordPrefix_nf "nafnorðið" QWordNounKey_nf '?'?

QWordNounKey/fall → no/fall

# 'Hvaða orð tengjast Ragnheiði Ríkharðsdóttur?'
# 'Hvaða orð eru tengd nafninu Elliði Vignisson?'

QWordPerson →
    QWordPerson_nf
    | QWordPerson_þgf

QWordPerson_þgf →
    QWordPrefix_þgf QWordPersonKey_þgf '?'?

QWordPerson_nf →
    QWordPrefix_þgf "nafninu" QWordPersonKey_nf '?'?
    | QWordPrefix_nf "nafnið" QWordPersonKey_nf '?'?

QWordPersonKey/fall → person/fall

# 'Hvaða orð tengjast sögninni að teikna?'

QWordVerb →
    nhm? QWordVerbKey
    | QWordPrefix_þgf "orðinu" QWordVerbKey '?'?
    | QWordPrefix_þgf "sögninni" nhm? QWordVerbKey '?'?
    | QWordPrefix_þgf "sagnorðinu" nhm? QWordVerbKey '?'?
    | QWordPrefix_nf "orðið" QWordVerbKey '?'?
    | QWordPrefix_nf "sögnin" nhm? QWordVerbKey '?'?
    | QWordPrefix_nf "sagnorðið" nhm? QWordVerbKey '?'?

QWordVerbKey → so_nh

# 'Hvaða orð tengjast Wintris?'

QWordEntity →
    QWordEntityKey_nf
    | QWordPrefix_þgf QWordEntityKey_þgf '?'?
    | QWordPrefix_þgf "orðinu" QWordEntityKey_nf '?'?
    | QWordPrefix_þgf "nafninu" QWordEntityKey_nf '?'?
    | QWordPrefix_þgf "sérnafninu" QWordEntityKey_nf '?'?
    | QWordPrefix_þgf "heitinu" QWordEntityKey_nf '?'?
    | QWordPrefix_nf "orðið" QWordEntityKey_nf '?'?
    | QWordPrefix_nf "nafnið" QWordEntityKey_nf '?'?
    | QWordPrefix_nf "sérnafnið" QWordEntityKey_nf '?'?
    | QWordPrefix_nf "heitið" QWordEntityKey_nf '?'?

QWordEntityKey/fall → Sérnafn/fall > Sérnafn

# Arbitrary search

# Try to recognize the search query first as a sentence,
# then as a noun phrase, and finally as an arbitrary sequence of tokens

QSearch →
    QSearchSentence
    | QSearchNl 
    | QSearchArbitrary

QSearchSentence →
    Málsgrein
    | SetningÁnF_et_p3/kyn Lokatákn? # 'Stefndi í átt til Bláfjalla'
    | SetningÁnF_ft_p3/kyn Lokatákn? # 'Aftengdu fjarstýringu'

$score(+4) QSearchSentence

QSearchNl →
    Nl_nf Atviksliður? Lokatákn? # 'Tobías í turninum'

QSearchArbitrary →
    QSearchToken+ Lokatákn?

$score(-100) QSearchArbitrary

QSearchToken →
    person/fall/kyn > fyrirtæki > no/fall/tala/kyn
    > fn/fall/tala/kyn > pfn/fall/tala/kyn > entity > lo > so
    > eo > ao > spao > tao > fs/fall
    > dags > dagsafs > dagsföst
    > tímapunkturafs > tímapunkturfast > tími
    > raðnr > to > töl > ártal > tala > sérnafn
