#
# Greynir.grammar
#
# A context-free grammar for modern Icelandic
#
# Greynir - a natural language processor for Icelandic
# Copyright (C) 2020 Miðeind ehf.
#
# This software is licensed under the MIT License:
#
#     Permission is hereby granted, free of charge, to any person
#     obtaining a copy of this software and associated documentation
#     files (the "Software"), to deal in the Software without restriction,
#     including without limitation the rights to use, copy, modify, merge,
#     publish, distribute, sublicense, and/or sell copies of the Software,
#     and to permit persons to whom the Software is furnished to do so,
#     subject to the following conditions:
#
#     The above copyright notice and this permission notice shall be
#     included in all copies or substantial portions of the Software.
#
#     THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
#     EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
#     MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
#     IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY
#     CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT,
#     TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE
#     SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
#
# This file contains an extensive (but not complete) context-free grammar
# for parsing modern Icelandic text.
#
# The grammar is presented in augmented Backus-Naur form, where
# repetition specifiers are allowed on right-hand-side tokens
# (* for 0..n, + for 1..n, ? for 0..1 occurrences).
#
# Rules and productions are autogenerated for all variants
# (combinations) of applicable grammatical features, i.e. genders (m, f, n),
# cases, numbers (singular/plural) and persons (1st, 2nd, 3rd).
#
# As an example, the generic nonterminal Undirsetning/tala/fall,
# having variants for /tala (singular/plural) and /fall (case),
# is automatically expanded to 8 specific nonterminals:
# Undirsetning_et_nf, Undirsetning_et_þf, etc.,
# with corresponding substitutions being made in the right-hand-side
# productions (et = singular, nf = nominative...).
# The permissible variants are defined below.
#
# Nonterminal identifiers start with uppercase letters.
# Terminal identifiers start with lowercase letters.
#
# Literals within single quotes 'xyz' are matched with the stem
# of a word token, plus eventual variants.
#
# Literals within double quotes "xyz" are matched with the
# text of a word token as it appeared in the source, albeit after
# conversion to lowercase. Variants cannot be applied to double-quoted
# literals.
#
# For disambiguation in POS tagging, literals may specify an optional
# word category after a colon. Thus "og:st" means that this "og" is
# a conjunction, not an adverb (which would be specified as "og:ao").
#
# This feature is occasionally used for typographic purposes, i.e.
# to distinguish between em dashes ("-:em") and regular hyphens ("-").
#
# Terminal names generally take the form category_variant1_variant2...,
# where the most common categories are:
# * no for nouns (nafnorð)
# * so for verbs (sagnorð)
# * lo for adjectives (lýsingarorð)
# * fs for prepositions (forsetningar)
# * st for connecting words (samtengingar)
# * fn for pronouns (fornöfn)
# * ao for adverbs (atviksorð)
# * töl for number words (töluorð)
# * gr for determiners (greinir)
#
# The terminal names are matched against POS tags from the BIN
# database in the module binparser.py.
#
# Further documentation of the grammar terminals and variants is
# available at https://greynir.is/doc/
#
# Alternative productions are separated with the '|' (vertical bar) sign.
# If there is a priority ordering between the alternatives, so that
# a single highest priority alternative should be automatically
# selected from possible alternatives, use the '>' (greater than)
# sign between the productions instead.
#
# For the Sublime Text editor, a syntax highlighting specification
# for .grammar files, grammar.YAML-tmLanguage, is available.
# It can be found in the GitHub repo subdirectory
# https://github.com/mideind/Greynir/tree/master/resources
# and its use is highly recommended.


# VARIANT DEFINITIONS

# Gender (m, f, n) variant
/kyn = kk kvk hk
/nkyn = kk kvk # Natural gender (male, female)

# Case (nom, acc, dat, poss) variant
/fall = nf þf þgf ef
/aukafall = þf þgf ef   # All cases except nominative
/aukafall2 = þf þgf ef
/afall = nf þf þgf ef   # Second case (andlagsfall) for dual argument verbs
/sfall = sþf sþgf sef   # Adjective subject case

# Number (singular/plural) variant
/tala = et ft

# Person (1st, 2nd, 3rd) variant
/pers = p1 p2 p3

# Hacks to generate single variants only
/hk = hk
/kvk = kvk
/þf = þf
/et = et

# GRAMMAR RULES

# Note: comments from this point are mostly in Icelandic

# Setningargerðir

# Merkið > gefur til kynna forgangsröðun.
# Ef báðar koma til greina er Málsgrein alltaf tekin fram yfir Grein o.s.frv.

S0 → Málsgrein > Grein > Fyrirsögn > StaðurDags > Heimilisfang > Undirritun

Málsgrein →
    MgrInnihald
    | "(" MgrInnihald ")"
    | "[" MgrInnihald "]"

MgrInnihald →
    Forskeyti? Yfirsetning StLiður* Lokatákn?
    | Staðhæfing

Staðhæfing →
    Sagt Segjandi Lokatákn?

Segjandi →
    Segir                            # "Allt er klappað og klárt", segir Jón.
    | AðSögn                         # Allt er klappað og klárt að sögn ráðuneytisins.
    | AðÞvíErSegir                   # Allt er klappað og klárt að því er ráðuneytið segir.
    | SamkvæmtHeimild                # ...samkvæmt heimildum mbl.is

Forskeyti →
    "-:em"? Samtenging "bara:ao"? StrikEðaKomma?    # 'En - þegar allt er skoðað, var þetta fínt.'
    | Samtenging? uh TengimerkiVh Samtenging?       # Og já, þetta var ekki gott / Nei - en hann átti ekkert svar
    | raðnr_nf_kvk                                  # Raðnúmer málsgreinar
    | "-:em"                                        # Þankastrik á undan setningu (allar gerðir strika varpast í "-")
    | 'mynd:kvk'_nf_et Tala_kvk_nf AfTala? ":"?     # Mynd 2 af 10
    | 'dæmi:hk'_et_nf Tala_hk_nf? ":"
    | 'nýr:lo'_esb_nf_hk_et ":"                     # Nýjast:
    | 'sjá:so'_0_gm_nh "einnig:ao"? ":"
    | "semsagt:ao" ":"
    | "sem:st" 'segja:so'_lhþt_sb_hk_nf_et ":"      # sem sagt
    | 'aðferð:kvk'_nf_et ":"
    | 'með_öðrum_orðum:ao' ":" | 'með:fs'_þgf 'annar:lo'_hk_þgf_ft 'orð:hk'_þgf_ft ":"
    | GreinarForskeyti

GreinarForskeyti →
    Greinarnúmer ":"
    | sequence ")"    # Tengist við tölu; a,b,c...; i,ii,iii,iv...

AfTala → 'af:fs'_þgf Tala_kvk_þgf

StrikEðaKomma → "-:em" | ","

$score(-6) Forskeyti # Ekki freistast í forskeyti ef annað er í boði

Heimild_nf/tala/pers →
    Nl_nf/tala/pers
Heimild_þgf/tala/pers →
    Nl_þgf/tala/pers
Heimild_ef/tala/pers →
    Nl_ef/tala/pers

# Koma í veg fyrir að þessir hnútar séu bestaðir burt
$tag(keep) Heimild_nf/tala/pers
$tag(keep) Heimild_þgf/tala/pers
$tag(keep) Heimild_ef/tala/pers

Sagt → Forskeyti? Yfirsetning StLiður* 

Segir →
    # , segir/fullyrðir Jón Ásmundsson, fulltrúi samtakanna, í dag í útvarpinu
    "," SögnSegja/tala/pers Heimild_nf/tala/pers AtvFsAuka?

SögnSegja/tala/pers →
    'segja:so'_0_gm_fh/tala/pers
    | 'skrifa:so'_0_gm_fh/tala/pers
    | 'flissa:so'_0_gm_fh/tala/pers
    | 'fullyrða:so'_0_gm_fh/tala/pers
    | 'staðhæfa:so'_0_gm_fh/tala/pers
    | 'ítreka:so'_0_gm_fh/tala/pers
    | 'viðurkenna:so'_0_gm_fh/tala/pers

AðSögn →
    # , að sögn fulltrúa lögregluyfirvalda
    ","? 'að:fs'_þgf 'sögn:kvk'_et_þgf Heimild_ef/tala/pers AtvFsAuka?

AðÞvíErSegir →
    # , [að því er] segir í tilkynningu ráðuneytisins í gær
    ","? AðÞvíEr? SegirÍ AtvFsAuka?

AðÞvíEr →
    'að:fs'_þgf 'það:pfn'_hk_þgf_et "er:stt" # að því er [segir í tilkynningu]
    | 'samkvæmt:fs'_þgf 'það:pfn'_hk_þgf_et "er:stt" # samkvæmt því er

SegirÍ →
    'segja:so'_0_gm_fh_et_p3 'í:fs'_þgf Heimild_þgf/tala/pers

SamkvæmtHeimild →
    'samkvæmt:fs'_þgf Lo_sb_ft_þgf_kvk? 'heimild:kvk'_þgf_ft Heimild_ef/tala/pers?

$score(+72) Sagt # Þarf að vera meiri plús en t.d. SagnFramhald

Lokatákn → "." | Spurningarmerki | ":" | "!" | "(" "!" ")"
LokatáknEðaKomma → Lokatákn | ","

Spurningarmerki → "?"

# Til að gera spurnarsetningum hátt undir höfði
$score(-12) Spurningarmerki

Fyrirsögn →
    Forskeyti? FyrirsögnInnihald

FyrirsögnInnihald →
    # Nafnliður í nefnifalli
    Nl_nf Atviksliður? Eignun? Lokatákn?
    | SetningÁnF Lokatákn?

StaðurDags →
    # Staður og dagsetning, oft í bréfhaus eða undirritun
    Nl_þgf "," dagsföst "."?    # Reykjavík, 29. ágúst 2020

Undirritun →
    # Fyrir hönd X, oft í undirritun
    'fyrir_hönd:fs'_ef Nl_ef ","?
    | 'virðingarfyllst:ao'_est ","?

Heimilisfang →
    # Heimilisfang, oft í bréfhaus
    Heimilisfang_þgf/kyn | Heimilisfang_nf/kyn

Eignun →
    # Mynd: Jón Stefánsson
    # Erlendur: Friðgeir er frábær
    ":" Nl_nf

SetningÁnF →
    # Setning án frumlags: 'Stefndi í átt til Bláfjalla', 'Aftengdi sjálfstýringu'
    SetningÁnF_et_p3/kyn
    | SetningÁnF_ft_p3/kyn

Greinarnúmer →
    # Greinarnúmer, e.t.v. skammstafað, og e.t.v. með fyrirsögn
    raðnr_nf_kvk 'grein:kvk' # 'grein:kvk'_nf_et !!! Ef tilbrigðin eru með þá passar skammstöfunin gr. ekki við laufið

Grein →
    # Sjálfstæð fyrirsögn greinar
    Greinarnúmer ":"? Fyrirsögn? # 17. gr. Störf Alþingis [.]

$score(+6) Grein

StLiður →
    # Samtengingar yfirsetninga
    Tengimerki? Samtenging HreinYfirsetning
    | Tengimerki Yfirsetning
    # 'Það eru bara tveir kostir í boði: Selja bankann eða láta hann fara í þrot'
    | ":" SagnRuna
    # 'Aftur er umræðan sú sama: um rétt kirkjunnar þegna til að neita að gefa saman samkynhneigða'
    | ":" NhFylling
    # Flokkurinn stefnir á stjórnarþátttöku, fái hann til þess umboð

$score(+12) StLiður

Tengimerki →
    ":" | ";" | "," | "-:em"

$score(+4) Tengimerki

TengimerkiVh →
    ";" | "," | "-:em"

$score(+4) TengimerkiVh

Samtenging →
    # Flóknari samtenging úr mörgum orðum
    "og:st" | "eða:st" | "ellegar:st" | "heldur:st" 
    | "enda:st" | "en:st" | "hvorki:st"
    | 'það:pfn'_hk_nf_et 'vera:so'_0_gm_fh_et_p3 # það er
    | "þar:ao" 'á_meðal:ao'
    | 'auk_þess:ao' "sem:st" AðvörunAð?
    | 'það_er_að_segja:ao' Tengimerki? "að:st"?
    | "svo:st" "lengi:ao"? "sem:st"
    | "eins:ao" "og:st"
    | "því:st" "að:st"?
    | "þannig:ao" "að:st"
    | "og:st" "loks:ao" "að:st"

$score(+2) Samtenging
$tag(apply_length_bonus) Samtenging

Yfirsetning →
    # Yfirsetning getur hafist á runu upphrópana
    Upphrópun* HreinYfirsetning

Upphrópun →
    uh+ NauðsynlegKomma # Jú jú, ég fór út í dag / Nei, hann kemur ekki.

NauðsynlegKomma →
    ","

HreinYfirsetning →
    Setning
    | SetningAukafall
    | SetningAukafallForgangur
    | SetningLo
    | SetningSo
    | AtvSetning        # Atvikssetning fremst í aðalsetningu
    | Spurnarsetning
    | SamsettSetning    # Annaðhvort X eða Y; bæði X og Y; ýmist X eða Y

$score(+2) HreinYfirsetning

$score(-2) SetningAukafall
$score(+1) SetningAukafallForgangur
$score(+2) SetningLo
$score(-1) SetningSo
$score(+3) Setning
$score(+12) Spurnarsetning
$score(+25) SamsettSetning

$tag(begin_prep_scope) HreinYfirsetning # Nýtt undirtré fyrir tengingu sagnar og forsetningar

# Spurnaraðalsetningar

Spurnarsetning →
    SpurnarNafnliður/tala/fall/kyn BeygingarliðurSögnFremst? Spurningarmerki   
    | SpurnarForsetningarliður BeygingarliðurSögnFremst? Spurningarmerki       # Í hvaða fötum er barnið?
    | SpurnarAtviksorð BeygingarliðurSögnFremst? Spurningarmerki               # Hvernig missti tröllið lífið?
    | SpurnarFrumlag/pers/tala/kyn BlTagl/tala/pers/kyn? Spurningarmerki       # Hvað er í pokanum?
    | BeygingarliðurSögnFremst Spurningarmerki                                 # Getur þú hlaupið hratt?

# Spurnarorðið er frumlag setningarinnar, stendur utan beygingarliðar
BlTagl/tala/pers/kyn →
    FrumlagsInnskot/tala/pers/kyn? AtviksliðurEinkunn*
        SagnRuna/tala/pers/kyn Sjálfur/tala/kyn?
        KommaTilvísunarsetning/tala/pers/kyn? Upptalning/tala?

SpurnarFrumlag/pers/tala/kyn →
    SpurnarNafnliður/tala/fall/kyn FsMeðFallstjórn?

$score(+8) Spurnarsetning

# Atvikssetning fremst í setningu
# [Ef þetta gerist](, þá) getur fatan dottið
# [Meðan þetta heldur](, þá) sleppur þetta
# [Af því að hún sigraði](, þá) hlýtur hún gullið
# [Til þess að fólkið blotni ekki](, þá) verður það að nota regnhlíf
# [Þrátt fyrir að hann missti eggið](, þá) brotnaði það ekki

AtvSetning →
    Atvikssetning ","? "þá:ao"? BeygingarliðurMeðUmröðun

$score(+12) AtvSetning

SamsettSetning →
    AnnaðhvortSt BeygingarliðurSögnFremst Eða BeygingarliðurÁnUmröðunar
    | Bæði BeygingarliðurSögnFremst Og BeygingarliðurÁnUmröðunar
    | Ýmist BeygingarliðurSögnFremst Eða BeygingarliðurÁnUmröðunar

Bæði → "bæði:st"
Ýmist → "ýmist:st"
Og → "og:st"
Eða → "eða:st"

# Aukasetningar
Atvikssetning → 
    Tíðarsetning
    | Tilgangssetning
    | Viðurkenningarsetning
    | Afleiðingarsetning
    | Orsakarsetning
    | Skilyrðissetning
    | Samanburðarsetning
    | Hvortsetning

$score(+15) Atvikssetning

KommaAtvikssetning →
    ","? Atvikssetning

# Gott ef komma er greind næst á undan tengingu inn í atvikssetningu
$score(+10) KommaAtvikssetning

$tag(begin_prep_scope) Atvikssetning # Nýtt undirtré fyrir tengingu sagnar og forsetningar

Tíðarsetning → 
    Tíðartenging BeygingarliðurÁnUmröðunar
    | Tíðartenging BeygingarliðurÁnF
    | Tíðartenging BeygingarliðurMeðUmröðun
    | Tíðartenging BeygingarliðurTími

Tilgangssetning → 
    Tilgangstenging BeygingarliðurÁnUmröðunar
    | Tilgangstenging BeygingarliðurÁnF

Viðurkenningarsetning → 
    Viðurkenningartenging BeygingarliðurÁnUmröðunar
    | Viðurkenningartenging BeygingarliðurÁnF

Afleiðingarsetning → 
    Afleiðingartenging BeygingarliðurÁnUmröðunar
    | Afleiðingartenging BeygingarliðurÁnF

Orsakarsetning →
    Orsakartenging BeygingarliðurÁnUmröðunar
    | Orsakartenging BeygingarliðurÁnF
    | OrsakartengingUmröðun BeygingarliðurÁnUmröðunar
    | OrsakartengingUmröðun BeygingarliðurÁnF
    | OrsakartengingUmröðun BeygingarliðurMeðUmröðun

Samanburðarsetning → 
    Samanburðartenging BeygingarliðurÁnUmröðunar # Bara vh.
    | Samanburðartenging BeygingarliðurÁnF

# Ath.: Skilyrðissetning er notuð í Greynir/processors/entities.py;
# ef nafni hnútsins er breytt þarf að breyta þar einnig
Skilyrðissetning → 
    Skilyrðistenging BeygingarliðurÁnUmröðunar
    | Skilyrðistenging BeygingarliðurMeðUmröðun # 'Ef ekki næðist samkomulag yrði málið svæft'
    | Skilyrðistenging BeygingarliðurÁnF
    | Skilyrðissetning_vh

Skilyrðissetning_vh →
    TengimerkiVh? SagnliðurVh

Hvortsetning →
    # Hvort [heldur] sem bíllinn er lítill eða stór (eyðir hann bensíni)
    HvortSem BeygingarliðurÁnUmröðunar

$score(+4) Samanburðarsetning
$score(+5) Tíðarsetning
$score(-10) Skilyrðissetning # Skilyrðistenging er með plús á móti
$score(-14) Skilyrðissetning_vh # Draga enn úr skilyrðissetningum sem byggja á viðtengingarhætti

# Fallsetningar - skýringarsetningar og spurnaraukasetningar

Skýringarsetning → 
    Skýringartenging SkýringarBotn SkýringarFramhald*

$tag(begin_prep_scope) Skýringarsetning

Skýringartenging → 
    "að:st"

SkýringarBotn →
    BeygingarliðurÁnUmröðunar   # Ég taldi að Jón hefði dregið sér fé / að málmagnir væri að finna í hreyfli
    | AtvSetning                # Ég taldi að meðan þetta héldi yrðu ekki frekari skakkaföll
    | SetningUmAðRæða           # Ég taldi að um hefði verið að ræða mistök af hans hálfu
    | SetningSoÞað              # Ég taldi að farið hefði verið ofan í saumana á málinu
    | AtvSkýring                # Ég taldi að hér væri vandamál á ferðinni

# AtvSkýring er eins og BeygingarliðurMeðUmröðun nema hvað forskeytis er krafist,
# það er ekki valfrjálst eins og BeygingarliðMeðUmröðun
AtvSkýring →
    SetningForskeyti SagnHluti/tala/pers/kyn KommaAtvikssetning?

# (Þess er krafist að Jón borgi bílinn) [,] og að Guðrún greiði kostnaðinn
SkýringarFramhald →
    ","? Og SkýringarsetningFramhald

SkýringarsetningFramhald → Skýringartenging SkýringarBotn

# Spurnaraukasetningar
Spurnaraukasetning →
    SpurnarNafnliður/tala/fall/kyn FsMeðFallstjórn? BeygingarliðurÁnUmröðunar?
    | SpurnarForsetningarliður BeygingarliðurÁnUmröðunar?
    | SpurnarAtviksorð FsMeðFallstjórn? BeygingarliðurÁnUmröðunar?
    | SpurnarFrumlag/pers/tala/kyn BlTagl/tala/pers/kyn?

SpurnarNafnliður/tala/fall/kyn →
    'hver:fn'/tala/fall/kyn Nl_ef_ft/pers/kyn?
    | 'hver:fn'/tala/fall/kyn AfLiður/kyn
    | 'hvor:fn'/tala/fall/kyn Nl_ef_ft/pers/kyn?
    | 'hvor:fn'/tala/fall/kyn Nl/fall/tala
    | 'hvor:fn'/tala/fall/kyn AfLiður/kyn
    | 'hvaða:fn'/tala/fall/kyn Nl/fall/tala
    | 'hver:fn'_ef_et_kk 'konar:kk'_ef_et Nl/fall/tala
    | HversuHve Lo/fall/tala/kyn Nl/fall/tala
    | HversuHve Lo/fall/tala/kyn AfLiður/kyn?
    | HversuHve ao                  # TODO þrengja í ao sem tákna tíð, hátt, ...?
    | "hvernig:ao" Nl/fall/tala     # Hægt að nota hliðstætt með nafnlið

SpurnarForsetningarliður →
    fs/fall SpurnarNafnliður/tala/fall/kyn

SpurnarAtviksorð →
    "hvernig:ao" | 'af:fs'_þgf 'hver:fn'_þgf_et_hk | "hví:ao" | "afhverju:ao"
    | 'til:fs'_ef 'hver:fn'_ef_et_hk | 'hver:fn'_ef_et_hk 'vegna:fs'_ef
    | "hvort:st" AðvörunAð? | 'hvar:ao' | 'hvaðan:ao' | 'hvenær:ao' | 'hví:ao'
    | 'hvert:ao'

HversuHve →
    "hversu:ao" | "hve:ao"

HvortSem →
    # hvort [heldur] sem
    "hvort:st" 'gjarna:ao'_mst? "sem:st"

AfLiður/kyn → 'af:fs'_þgf Nl_þgf_ft/pers/kyn

$score(+2) SpurnarAtviksorð
$score(+1) Spurnaraukasetning
$score(+27) SpurnarForsetningarliður

# Beygingarliðir

# Venjuleg setningaskipan þar sem frumlag (oftast í nefnifalli) er fremst
BeygingarliðurÁnUmröðunar →
    BeygingarliðurÁnUmröðunar/tala/pers/kyn
    | SetningAukafall
    | SetningAukafallForgangur

BeygingarliðurÁnUmröðunar/tala/pers/kyn →
    Frumlag/pers/tala/kyn AðvörunKomma? BeygingarliðurMegin/tala/pers/kyn

BeygingarliðurMegin/tala/pers/kyn →
    FrumlagsInnskot/tala/pers/kyn? AtviksliðurEinkunn*
    SagnRuna/tala/pers/kyn Sjálfur/tala/kyn? KommaTilvísunarsetning/tala/pers/kyn?
    SamanburðurNl? Upptalning/tala? KommaAtvikssetning?

# Það er alls ekki víst, sem Jón hélt fram, að flokkurinn muni tapa í kosningunum
BeygingarliðurÁnUmröðunar_et_p3_hk →
    NlFrumlagÞað BeygingarliðurMegin_et_p3_hk AðvörunKomma? Skýringarsetning

# Sögn kemur fremst, svo frumlag. Enginn annar liður leyfist fremst, tengingin situr þar.
BeygingarliðurSögnFremst →
    BeygingarliðurSögnFremst/tala/pers/kyn

BeygingarliðurSögnFremst/tala/pers/kyn →
    SagnHluti/tala/pers/kyn Upptalning/tala? KommaAtvikssetning?

# Frumlag fyrir aftan sögn, annar liður getur komið fremst:
# 'Gengu þau galvösk á Esjuna þrátt fyrir veðurútlitið'
# 'Bersýnilega fór Jón sterki í bága við reglurnar með þessu'
# 'Undir húsinu voru illa farnar skolpleiðslur'
# 'Í skólanum borðaði Gunna aldrei matinn eftir þetta'
# 'Í grein sinni opinberaði Hannes niðurstöðu sína'
# Ath. að frumlag 3p. et. getur verið falið:
# (? Það virtist mér í dag kettinum líða betur)
# 'Í dag virtist mér kettinum líða betur'
# 'Eins og Páll er ég mjög ósáttur við þetta'
BeygingarliðurMeðUmröðun →
    BeygingarliðurMeðUmröðun/tala/pers/kyn

BeygingarliðurMeðUmröðun/tala/pers/kyn →
    SetningForskeyti? SagnHluti/tala/pers/kyn Upptalning/tala? KommaAtvikssetning?

# Mögulegar tengingar í aukasetningum eftir gerð aukasetningar
Tíðartenging →
    "nema:st"? "þegar:st" AðvörunAð?
    | "löngu:ao"? "áður:ao" "en:st" AðvörunAð?
    | "löngu:ao"? "eftir:ao" "að:st"
    | Allt? 'frá:fs'_þgf 'það:pfn'_hk_þgf_et "að:st"
    | VillaFráÞvíAð # frá því X
    | Allt? 'frá:fs'_þgf 'það:pfn'_hk_þgf_et "er:st"?
    | FyrrEn AðvörunAð?  # fyrr en
    | 'ótt:ao'_mst "en:st" AðvörunAð? # óðar en
    | "þá:ao" "er:st"
    | 'um:fs'_þf 'leið:kvk'_þf_et "og:st" # um leið og
    | 'um:fs'_þf 'svipaður:lo'_sb_hk_þf_et 'leyti:hk'_þf_et "og:st" # um svipað leyti og
    | 'um:fs'_þf 'samur:fn'_hk_þf_et 'leyti:hk'_þf_et "og:st" # um sama leyti og
    | 'á_sama_tíma:ao' "og:st"
    | 'í_sömu_andrá:ao' "og:st"
    | "síðan:st" AðvörunAð?
    | "uns:st"
    | "meðan:st" AðvörunAð?
    | 'á_meðan:ao' AðvörunAð? # á meðan
    | "þangað:ao" "til:st" AðvörunAð?
    | "þar:ao" "til:st" AðvörunAð?
    | 'allar_götur_síðan:ao' AðvörunAð?
    | 'í_fyrsta_sinn:ao' "sem:st" AðvörunAð?
    | 'í_síðasta_sinn:ao' "sem:st" AðvörunAð?
    | 'í_allra_síðasta_sinn:ao' "sem:st" AðvörunAð?
    | TengiorðEr # "er:stt", líka tilvísunartenging - fær mínus

$tag(apply_length_bonus) Tíðartenging

FyrrEn → "fyrr:ao" "en:st"

# Reyna að halda 'fyrr en' saman
$score(+20) FyrrEn

Allt → 'allur:fn'_nf_hk_et

$score(+4) Allt

# Allra er notað ef óskað er eftir NP-POSS nafnlið utan um fornafnið
Allra → 'allur:fn'_ef_hk_ft

Tilgangstenging → 
    'til:fs'_ef 'það:pfn'_hk_ef_et "að:st"
    | "svo:st" "að:st"    # Líka afleiðingartenging
    | SvoTenging

Viðurkenningartenging →
    "enda:st" "þótt:st" AðvörunAð?
    | "jafnvel:ao"? "þótt:st" AðvörunAð?
    | "jafnvel:ao"? "þó:st" "svo:ao"? "að:st"
    | VillaÞóAð
    | 'þrátt_fyrir:ao' 'það:pfn'_hk_þf_et? "að:st"

$score(+6) Viðurkenningartenging

Afleiðingartenging →
    "þannig:ao" "að:st"
    | "svo:st" "að:st"   # Líka tilgangstenging
    | SvoTenging

Orsakartenging →
    "fyrst:ao" AðvörunAð?
    | VillaVístAð
    | 'úr:fs'_þgf 'það:pfn'_hk_þgf_et   # úr því ("að" er höndlað neðar)
    | 'af:fs'_þgf 'það:pfn'_hk_þgf_et   # af því ("að" er höndlað neðar)
    | 'þar_eð:st'
    | "þar:ao" "sem:st" AðvörunAð?
    | "því:st"

# Orsakartengingar sem leyfa umröðun eftirfarandi beygingarliðar,
# t.d. "Kettir elta mýs vegna þess að í rauninni finnst þeim það skemmtilegt",
# "Jón taldist giftur Gunnu, með því að í kirkjubók fannst færsla því til sönnunar"
OrsakartengingUmröðun →
    'vegna:fs'_ef 'það:pfn'_hk_ef_et "að:st"        # vegna þess að
    | 'sökum:fs'_ef 'það:pfn'_hk_ef_et "að:st"      # sökum þess að
    | 'sakir:fs'_ef 'það:pfn'_hk_ef_et "að:st"      # sakir þess að
    | 'með:fs'_þgf 'það:pfn'_hk_þgf_et "að:st"      # með því að
    | 'af:fs'_þgf 'það:pfn'_hk_þgf_et "að:st"       # af því að
    | 'úr:fs'_þgf 'það:pfn'_hk_þgf_et "að:st"       # úr því að
    | "því:st" "að:st"                              # því að

$score(+2) OrsakartengingUmröðun

Skilyrðistenging →
    "ef:st" AðvörunAð?
    | "svo:ao" "framarlega:ao" "sem:st" AðvörunAð?
    | "nema:st" AðvörunAð?
    | 'koma:so'_0_gm_vh_p3_et 'til:fs'_ef 'það:pfn'_hk_ef_et "að:st" # komi til þess að
    | 'fara:so'_0_gm_vh_p3_et "svo:ao" "að:st" # fari svo að

$score(+8) Skilyrðistenging # Skilyrðissetning er með mínus á móti

SvoTenging →
    "svo:st"

$score(-8) SvoTenging # Draga verulega úr "svo" einu og sér sem tengingu

Samanburðartenging →
    "eins:ao" "og:st"
    | "líkt:ao" "og:st"
    | "svo:st" "sem:st"

# Setningar með frumlagi

Setning → Setning/tala/pers/kyn

# Tilvik þar sem einu frumlagi fylgja nokkrir sagnliðir:
# 'Anna tók bílinn [þegar það hentaði henni], keyrði hann sjálf í bæinn [,] og keypti í matinn'

SagnRuna → SagnRuna/tala/pers/kyn

SagnRuna_p3_et/kyn → SagnRuna_et_p3/kyn
SagnRuna_p3_ft/kyn → SagnRuna_ft_p3/kyn

SagnRuna/tala/pers/kyn →
    SagnRunaKnöpp/tala/pers/kyn SagnFramhald/tala/pers/kyn*
    | AnnaðhvortSt SagnRunaKnöpp/tala/pers/kyn SagnKomma/tala/pers/kyn* 
        Eða FsAtv? SagnRunaKnöpp/tala/pers/kyn
    | Bæði SagnRunaKnöpp/tala/pers/kyn SagnKomma/tala/pers/kyn* 
        Og FsAtv? SagnRunaKnöpp/tala/pers/kyn
    | Hvorki SagnRunaKnöpp/tala/pers/kyn SagnKomma/tala/pers/kyn* 
        Né FsAtv? SagnRunaKnöpp/tala/pers/kyn
    | Ýmist SagnRunaKnöpp/tala/pers/kyn SagnKomma/tala/pers/kyn* 
        Eða FsAtv? SagnRunaKnöpp/tala/pers/kyn

Hvorki → "hvorki:st"
Né → "né:st"

SagnRunaKnöpp/tala/pers/kyn →
    Sagnliður/tala/pers/kyn LoTengtSögn_nf/tala/kyn? SagnInnskotAtv?

SagnFramhald/tala/pers/kyn →
    SagnKomma/tala/pers/kyn* KommaOgEðaEn FsAtv? SagnRunaKnöpp/tala/pers/kyn

SagnKomma/tala/pers/kyn →
    "," SagnRunaKnöpp/tala/pers/kyn

# Hvetja til framhalds sömu sagnrunu, fremur en að skipta niður í fleiri setningar
$score(+32) SagnFramhald/tala/pers/kyn
$score(+2) SagnKomma/tala/pers/kyn

$tag(begin_prep_scope) SagnRunaKnöpp/tala/pers/kyn

# Sagnir innan undirtrés geta ekki orsakað bónus á forsetningar utan þess
$tag(purge_verb) SagnRuna/tala/pers/kyn

SagnInnskot →
    FsAtv
    | ÁttFs # suður götuna
    | EkkiNeitt

# Gefa aukabónus fyrir forsetningar innan SagnInnskots ef þær passa við umlykjandi sögn
$tag(enable_prep_bonus) SagnInnskot

SagnInnskotAtv →
    # (Manninum var vikið) frá störfum [þegar málið kom upp]
    SagnInnskot KommaAtvikssetning?
    # (Ég ætla að fá mér ís) [,] þegar ég verð 18 ára í haust (og borða hann úti)
    | KommaAtvikssetning

# Gefa plús fyrir að forsetningar- og atviksliðir tengist sögnum,
# frekar en að þeir séu t.d. forskeyti á næstu setningu
$score(+2) SagnInnskotAtv

EkkiNeitt →
    "ekki:ao" 'neinn:fn'_et_hk_nf # (Hann þekkti þá) ekki neitt

$score(-2) EkkiNeitt # Betra að þekkja fornafnið "neitt" sem nafnlið

SagnViðhengi/tala/pers/kyn →
    KommaOgEðaEn SagnRuna/tala/pers/kyn

$score(+32) SagnViðhengi/tala/pers/kyn

# Setningarhluti sem byrjar á sögn og hefur innifalið frumlag

SagnHluti/tala/pers/kyn →
    SagnliðurMeðF/tala/pers/kyn SagnInnskot? SagnViðhengi/tala/pers/kyn?

# Hér er falið frumlag 'það'
SagnHluti_et_p3_hk →
    # [Samkvæmt heimildum mbl.is] virtist sem Björn fengi aðsvif á sviðinu
    VirðistSem
    # [Í dag] má með sanni segja að Jón hafi sloppið með skrekkinn
    | SagnHlutiMegiSkuli

SagnHlutiMegiSkuli →
    # [Ætíð] skal fara með skjölin sem trúnaðarmál
    HjSögnMegiSkuli_et_p3 FsAtv? EinSögn_nh_et_hk SagnInnskot?

$tag(begin_prep_scope) SagnHluti/tala/pers/kyn # Nýtt undirtré fyrir tengingu sagnar og forsetningar

FrumlagsInnskot/tala/pers/kyn →
    "," SetningÁnF/tala/pers/kyn ","
    # Lögheimili á Íslandi þegar kosning fer fram er skilyrði
    | KommulaustInnskot
    # Lögheimili á Íslandi, þegar kosning fer fram, er skilyrði
    | "," InnskotsSamtenging HreinYfirsetning ","
    # Tjónið þann 3. mars, þegar veðrið var verst, nam 1,2 milljarði króna
    | Tímaliður FrumlagsInnskot/tala/pers/kyn?

# Gefum kommulausu innskoti mínus
KommulaustInnskot →
    InnskotsSamtenging HreinYfirsetning

$score(-4) KommulaustInnskot

# Þröngt mengi samtenginga sem nota má án kommu í frumlagsinnskoti
# (sé mengið of vítt eykst margræðni verulega)
InnskotsSamtenging →
    # (Lögheimili á Íslandi), þegar sótt er um atvinnuleyfi, (er skilyrði)
    "þegar:st" AðvörunAð?
    # (Lögheimili á Íslandi), meðan atvinna er stunduð, (er skilyrði)
    | "meðan:st" AðvörunAð?
    # (Lögheimili á Íslandi), á meðan atvinna er stunduð, (er skilyrði)
    | 'á_meðan:ao' AðvörunAð?
    # (Lögheimili á Íslandi), þar sem gott er að búa, (er skilyrði)
    | "þar:ao" "sem:st" AðvörunAð?
    # (Lögheimili á Íslandi), þrátt fyrir að margur efist um það, (er skilyrði)
    | 'þrátt_fyrir:ao' "að:st"
    # (Lögheimili á Íslandi), þ.e.a.s. að búið sé á landinu, (er skilyrði)
    | 'það_er_að_segja:ao' Tengimerki? "að:st"
    # (Lögheimili á Íslandi), eins og það er skilgreint í lögum, (er skilyrði)
    | "eins:ao" "og:st"
    # (Lögheimili á Íslandi), þannig að maður hafi búið hér í 3 ár hið minnsta, (er skilyrði)
    | "þannig:ao" "að:st"

Frumlag/pers/tala/kyn →
    NlFrumlag_nf/tala/pers/kyn

# Hakk til að láta "Þetta/Það eru aðallega fjögur atriði" virka,
# þ.e. þar sem ábendingarfornöfnin "þessi"/"sá" eru notuð í eintölu
# sem frumlag með andlagi í fleirtölu.

Frumlag_p3_ft_hk →
    'þessi:fn'_nf_et_hk | 'sá:fn'_nf_et_hk

Samanburðarmerki → "," | ";" | "-:em"

SamanburðurNl →
    SamanburðarNlTenging Nl_nf AtvFs? # þar af einn alvarlega
    | Samanburðarmerki? "þar:ao" "með:ao" 'telja:so'_lhþt_sb_nf/tala/kyn Nl_nf/tala/kyn AtvFs? # þar með talinn allur þingflokkur Framsóknar
    | Samanburðarmerki 'annað:fn'_þgf_et_hk "nær:ao"   # Viðskiptablaðið styður ekki jafnaðarstefnuna; öðru nær.

SamanburðarNlTenging →
    HeldurEn                                # 'Forseti á ekki að gera meiri breytingu á sér (heldur) en forsætisráðherra'
    | ","? Samanburðartenging               # 'Hann á ekki andstæðinga eins og aðrir kjörnir fulltrúar'
    | Samanburðarmerki? "þar:ao" "af:ao"    # 'Fjöldi barna var í vélinni, þar af tvö kornabörn'
    | Samanburðarmerki? "þar:ao" 'á_meðal:ao'
    | Samanburðarmerki? 'þar_með_talið:ao'  # Skammstöfunin 'þ.m.t.'
    | Samanburðarmerki? Nema                # Allir vita það, nema ég.
    | ","? "enda:st"                        # 'Réttindi í almennum sjóðum breyttust, enda lægri fjárhæð en áður'
    | "sem:st"
    | Samanburðarmerki En                   # Enginn lögreglumaður var handtekinn - en 100 mótmælendur
    | SamanburðarTengingOg

SamanburðarTengingOg →
    ","? AðvörunSem? "og:st"                # Erfitt fjarskiptasamband er á staðnum og mikil snjóþyngsli

$score(-8) SamanburðurNl
$score(+4) SamanburðarNlTenging
$score(-14) SamanburðarTengingOg

Sjálfur/tala/kyn → 'sjálfur:fn'_nf/tala/kyn

# 'Þrír þingmenn greiddu atkvæði gegn tillögunni: Jón, Sigríður og Guðrún.'
# 'Aðeins einn nemandi var með þetta rétt; strákurinn í rauðu skyrtunni.'
Formerki → ":" | ";" | "-:em" | ","

# (Tíu greindust með veiruna), tveir í morgun og átta [nú síðdegis]
Upptalning/tala → Formerki Nl_nf/tala Atviksliður?

Setning/tala/pers/kyn →
    # Venjuleg setningarskipan með frumlagi (í nefnifalli) fremst:
    # 'Jón sterki fór bersýnilega í bága við reglurnar með þessu'
    # 'Katrín formaður félagsins hefur augljóslega verið alveg hnífskörp í gær'
    # 'Rúmlega helmingur tvítugra karlmanna vill flugvöll í Vatnsmýri'
    # 'Ég bara spyr [sjálf]'
    # 'Allir mega koma til landsins [sjálfir] sem það vilja'
    BeygingarliðurÁnUmröðunar/tala/pers/kyn

    # Frumlag fyrir aftan sögn
    | BeygingarliðurMeðUmröðun/tala/pers/kyn

    # 'Það fer allt of mikill tími í pappírsvinnuna'
    | NlFrumlagLeppur SagnHluti/tala/pers/kyn Upptalning/tala?

Setning_et_p3_hk →
    # 'Að raða frímerkjum er góð skemmtun'
    SetningAð
    # Um er/var að ræða þriggja ára stelpu
    # (-> Það er um að ræða þriggja ára stelpu)
    | SetningUmAðRæða
    # 'Nú/þá/alltaf er/var/væri verið að reka fólk burt af jörðum sínum'
    # ('verið' er ekki til sem LHÞT, en það er hins vegar 'búið' o.fl.)    # ERROR -- bæta við í bín? og/eða er þetta villa?
    | SetningForskeyti 'vera:so'_0_gm_p3_et 'vera:so'_sagnb NhFyllingAtv
    # Það (var verið að eyðileggja menningarverðmæti)
    # Það (tekur mig þrjú ár að eignast bílinn)
    # Það (verður að viðurkennast að Jón er fær á sínu sviði)
    # Það (mun skapast hætta)
    | NlFrumlagÞað SögnÞað
    # 'Til stendur/stóð að halda landsfundinn í júní'
    | SetningTilStendur

SetningTilStendur →
    "til:ao" 'standa:so'_0_gm_p3_et NhFyllingAtv

$score(+20) SetningTilStendur

SögnÞað →
    # (Það) var verið að eyðileggja menningarverðmæti
    'vera:so'_0_gm_p3_et 'vera:so'_sagnb NhFyllingAtv
    # (Það) tekur mig tvö ár að eignast bílinn
    # (Það) kostaði mig blóð, svita og tár [í gær] að standa í þessu
    | so_2_þf_þf_gm_p3_et NlBeintAndlag_þf NlÓbeintAndlag_þf AðvörunKomma? NhFyllingAtv
    # (Það) verður [strax] að segjast/viðurkennast að Jón er fær á sínu sviði
    | 'verða:so'_0_gm_p3_et FsAtv? NhEinfaldur_mm ","? Skýringarsetning

# Nýtt undirtré fyrir tengingu sagnar og forsetningar
$tag(begin_prep_scope) Setning/tala/pers/kyn

SetningAð →
    # 'Að raða frímerkjum [í ró og næði] er góð skemmtun'
    NhLiður FsAtv? SagnRuna_et_p3_hk
    | SetningAðHugsaSér

$score(-4) SetningAð # Óalgengt form

$tag(begin_prep_scope) SetningAð

SetningAðHugsaSér →
    Nhm 'hugsa:so'_1_þgf_gm_nh abfn_þgf # 'Að hugsa sér [, sagði Anna]'

Nhm → nhm

$score(+2) SetningAðHugsaSér

# !!! ATH: Við lítum á nafnliðinn í "um að ræða" sem andlag (viðfang)
# !!! forsetningarinnar 'um', ekki sagnarinnar 'ræða'
SetningUmAðRæða →
    # Um væri að ræða
    # Um hafi verið að ræða
    # Um yrði að ræða
    # Um gæti hafa verið að ræða
    # ...o.s.frv.
    'um:fs'_þf SögnUmAðRæða Nl_þf AtvFsAuka?
    # Í dag hafi verið um að ræða skipulegt undanhald
    | SetningForskeyti SögnVarUmAðRæða AtvFsAuka?

$score(+64) SetningUmAðRæða # Mjög líklega réttur skilningur á þessari orðaröð

SetningForskeyti →
    FsAtv ","? | SamanburðarForskeyti ","?

SamanburðarForskeyti →
    # 'Líkt og bakarar eru smiðir óhressir með þetta'
    # 'Eins og Páll borða ég ekki smjör'
    Samanburðartenging Nl_nf       

# Hvetja til forskeyta fremur en samtenginga, ef val er þar á milli
$score(+10) SamanburðarForskeyti

# Sagnir innan forskeytis hafa ekki áhrif á tengsl forsetninga við sagnir í setningunni sjálfri
$tag(purge_verb) SetningForskeyti

# Setning sem byrjar á lýsingarorði í stað "það"
# 'Óheimilt er (að fangelsa mann fyrir engar sakir)'
# 'Óhætt má telja (að flokkurinn vinni kosningarnar)'
# 'Öruggt hefur verið talið [að fara Vatnaleiðina í þurru veðri]'
# 'Ljóst er (að Brandur var fallegasti kötturinn)'
SetningLo →
    LoTengtSögn_nf_et_hk SetningLoSögn AðLiður?
    # Kjörgengur [við Alþingiskosningar/í lok maí] skal vera hver sá ríkisborgari (sem dvalið hefur 5 ár í landinu)
    | LoTengtSögn_nf/tala/kyn FsRunaEftirSögn? HjSögnLhÞtSM/tala/pers Nl_nf/tala/pers/kyn AtvFs?
    # Aðspurður sagði Jón framburð Gunnu vera uppspuna frá rótum
    # Örvingluð eftir atburði næturinnar leitaði Anna til lögreglunnar
    | LoTengtSögn_nf/tala/kyn FsAtv? SagnHluti/tala/pers/kyn
    | SetningLoMeðSpurningu
    # Frábært að heyra [í þér]
    # Yndislegt að sjá og heyra
    | LoTengtSögn_nf_et_hk NhLiðir FsAtv?
    # Gaman að sjá til þín
    | "gaman:ao" NhLiðir FsAtv?

SetningLoMeðSpurningu →
    # Spurð/aðspurð [um / í framhjáhlaupi / eftir atburðinn] hvort Jón væri edrú sagði Gunna að hann væri það
    LoSpurnarUpplegg/tala/kyn LoSpurnarInnskot? Spurnaraukasetning SagnHluti/tala/pers/kyn

LoSpurnarUpplegg/tala/kyn →
    lo_sb_nf/tala/kyn | so_lhþt_sb_nf/tala/kyn # spurð/aðspurð/innt

LoSpurnarInnskot →
    "um:ao" | FsAtv

$score(+4) SetningLoMeðSpurningu

SetningLoSögn →
    HjSögnLhÞtSM_et_p3 HreinSögn_lhþt_et_hk? # er/skal/mun/hefur verið talið/þótt...
    | HjSögnMegiSkuli_et_p3? so_0_mm_p3_et # sýnist/virðist/telst, má teljast
    | HjSögnMegiSkuli_et_p3 so_0_nh # má telja, skal ætla
    | 'þykja:so'_0_gm_et_p3 # '(öruggt) þykir (að Jón vinni mótið)'
    | 'vera:so'_0_gm_et_p3 'vera:so'_sagnb # Hægt og bítandi er/var/væri verið (að ýta fólki burt)

# Ekkert frumlag í nefnifalli, sögn sem sagnbót (oft í miðmynd):
# 'Í dag er búist við vondu veðri eftir hádegi'
# (í stað 'Það er búist við vondu veðri eftir hádegi í dag')
# 'Á morgun verður lent á neyðarbrautinni'
# 'Fylgst verður með kosningunum á RÚV næstu daga'
# (í stað 'Það verður fylgst með kosningunum á RÚV næstu daga')
# 'Í dag má sækja tölvuna'
# (í stað 'Það má sækja tölvuna í dag')

SetningSo →

    # Í SetningSoÞað eru reglur sem *geta* staðið í skýringarsetningu
    # ('Ég taldi að...')
    SetningSoÞað

    # Hér fyrir neðan eru reglur sem geta *ekki* staðið í skýringarsetningu
    # ('Ég taldi að...')

    # Er/var/hefur verið [í þessu tilviki] spáð betra veðri [á morgun]
    | HjSögnLhÞtSM_et_p3 FsAtv? So_1_sagnb/fall NlBeintAndlag/fall AtvFsAuka?

    # Fórust með honum átján menn
    | so_0_mm/pers/tala FsAtv? NlFrumlag_nf/tala/pers AtvFsAuka?

    # Skal tryggja rétt almennings til að fara um landið
    | SagnliðurÁnF

    # Skal í frumvarpinu fólgin greinargerð um tekjur ríkisins og gjöld
    | VeraSkulu_et_p3 FsRuna LoTengtSögn_nf/tala/kyn NlFrumlag_nf/tala/kyn AtvFsAuka?

    # Varð uppljómaður bærinn þegar ýtt var á hnappinn
    | So_1_p3_nf/tala NlSagnfylling_nf NlFrumlag_nf/tala AtvFsAuka?
    # Signdi sig maðurinn í mikilli geðshræringu
    | So_1_p3/aukafall/tala NlBeintAndlag/aukafall NlFrumlag_nf/tala AtvFsAuka?

    # !!! Ath.: So_1_nh leyfir miðmynd nafnháttar, sem ætti ekki að vera leyfilegur hér
    # !!! sbr. '?Endurnýjast á leigusamninginn í ár'
    # Gæta eiga/skulu/verða þeir þingskapa
    | So_1_nh/fall HjSögnNhMeðSo/tala/pers NlFrumlag_nf/tala/pers/kyn
        NlBeintAndlag/fall AtvFsAuka?

    # Farðu [galvaskur] að sækja kaffi strax
    | Sögn_bh SögnFrh_bh?

    # Má með sanni segja (að þeir hafi sloppið með skrekkinn)
    | SagnHlutiMegiSkuli

    # Þarf til þess lagafyrirmæli að svipta mann réttindum
    | 'þurfa:so'_1_þf_gm_et_p3 FsAtv? NlBeintAndlag_þf AðLiður?

    # (Því) hafi þurft að senda alla farþegana (á hótel)
    # (Í dag) gæti hafa rignt
    | FsAtv? HjSögnVh so_sagnb NhFylling?

    # Þykir/finnst þeim vera slagsíða [í umfjölluninni] / þeim [það] ósanngjarnt
    | HjSögnFinnst NlFrumlag_þgf SögnFinnstBotn

    # Virðist sem Björn hafi fengið aðsvif
    | VirðistSem

# Hér eru setningargerðir sem byrja á sagnlið, innifela
# falið 3p. et. frumlag og geta þar af leiðandi staðið
# í skýringarsetningu (t.d. í framhaldi af 'Ég taldi að...')

SetningSoÞað →

    # Leitað verður/skal/mun til sérfræðinga
    # Verið er í snatri að ráða hæfasta umsækjandann
    # Komið hefur/hafi til átaka milli þeirra
    # Farið hefur verið ofan í saumana á málinu
    # Búist er við að Sigmundur Davíð segi af sér
    Sagnliður_sagnb

    | Sagnliður_lhþt

    # Horft hefur verið [til hennar] lostafullum augum
    | So_1_sagnb/fall HjSögnLhÞtSM_et_p3 FsAtv? NlBeintAndlag/fall AtvFsAuka?

    # Búast má/skal/þarf/hefur mátt við töfum á veginum
    | so_0_mm_nh HjSögnMegiSkuliÞurfi_et_p3 SagnInnskot

    # Haldi þessi þróun áfram [þá] er voðinn vís
    | SagnliðurVh ","? "þá:ao"? BeygingarliðurMeðUmröðun FsAtv?

    # Með lögum megi tryggja rétt almennings
    # Áður en ég fer má sækja tölvuna
    | AtvFsAuka AðvörunKomma? SagnliðurÁnF

    # Endurnýja þarf/skal/má [í snatri] leigusamninginn í ár
    | So_1_nh/fall HjSögnMegiSkuliÞurfi_et_p3 FsRuna?
        NlBeintAndlag/fall AtvFsAuka?

    # Svo virðist sem Björn hafi fengið aðsvif
    | "svo:ao" VirðistSem

VirðistSem →
    'virða:so'_op_mm_fh_p3_et SamanburðurSemSetning     # virtist/virðist [sem Björn hafi fengið aðsvif á sviðinu]

SamanburðurSemSetning →
    Sem AðvörunAð? HreinYfirsetning

Sem → "sem:st"

$score(+10) VirðistSem # Sennilega rétt greint

SögnFinnstBotn →
     'vera:so'_1_nf_nh NlSagnfylling_nf                 # Finnst þeim vera slagsíða í umfjölluninni
     | NlFrumlagÞað? LoSamanb_et_nf_hk AðLiður?

Sagnliður_lhþt →
    EinSögn_lhþt/tala/kyn OgSögn_lhþt/tala/kyn* HjSögnLhÞt_p3/tala
        NlFrumlag_nf/tala/kyn AtvFsAuka? # Teknar og skráðar hafa verið ákvarðanir í málinu    # ERROR -- mætti merkja sem leiðinlegt orðalag?

$score(-4) Sagnliður_lhþt # Knúsað orðalag - velja annað ef í boði

# Talið er (að Jón sé horfinn) / Verið er (að ráða hæfasta umsækjandann)
# Sagt er algerlega óhætt [að fara út]
# Sótt sé/verði/yrði að honum
# !!! Hér er álitamál hvort so_sagnb ætti frekar að vera so_lhþt_nf_hk_et
# !!! (vegna falda frumlagsins "það er..."). Hins vegar má hafa í huga að
# !!! t.d. orðmyndin 'ábyrgst' er aðeins í BÍN sem MM-SAGNB, þannig að reglan
# !!! hér nær yfir 'Ábyrgst hefur verið að greiðslan færi fram'.)
Sagnliður_sagnb →
    so_sagnb HjSögnSagnb LoTengtSögn_nf_et_hk? SagnliðurBotn_sagnb

SagnliðurBotn_sagnb →
    NhFylling
    | SagnInnskot
    | AðvörunKomma? Skýringarsetning

$tag(begin_prep_scope) Sagnliður_sagnb
$score(+12) Sagnliður_sagnb

# Skal í frumvarpinu fólgin greinargerð
# Er í frumvarpinu fólgin greinargerð
# Verður í frumvarpinu fólgin greinargerð
# Mun í frumvarpinu fólgin greinargerð
VeraSkulu_et_p3 →
    'vera:so'_0_gm_et_p3 | 'verða:so'_0_gm_et_p3 | 'skulu:so'_0_gm_et_p3 | 'munu:so'_0_gm_et_p3

# Setning þar sem aðalnafnliður er í aukafalli
# en frumlag í 3p. et. ('það') er falið.
# Sögnin tekur viðfang (subject) í aukafalli eða er í miðmynd
# 'Mér blöskrar athæfið' (? Það blöskrar mér athæfið),
# 'Mér sýnist að öllu sé óhætt' (? Það sýnist mér að öllu sé óhætt),
# 'Helmingi ungmenna finnst skólinn skemmtilegur' (? Það finnst helmingi ungmenna skólinn skemmtilegur)

SetningAukafall →

    # Manninn langaði [mjög í ferðina / að fara til útlanda strax]
    # Köttum [í landinu] fjölgar
    NlFrumlag/aukafall AðvörunKomma? ÓpSagnliður/aukafall

    # Mig/Þig/Hann/Mennina hefur [lengi] langað [til útlanda]
    | NlFrumlag/aukafall AðvörunKomma? HjSögn_et_p3 FsAtv? so_subj_sagnb/aukafall ÓpSagnarBotn?

    # Mig er [búið] að dreyma um ferð til útlanda
    | NlFrumlag_þf 'vera:so'_0_gm_fh_et_p3 'búa:so'_lhþt_sb_nf_et_hk?
        Nhm so_subj_nh_þf ÓpSagnarBotn?

    # Mér hafa ofboðið lætin í hundinum [alla daga]
    | NlFrumlag/aukafall AðvörunKomma? HjSögn/tala/pers FsAtv? so_subj_sagnb/aukafall
        NlBeintAndlag_nf/tala/pers AtvFsAuka?

    # Mér [algjörlega] blöskraði [af öllu hjarta] vitleysan (í Páli), Mig þraut örendið
    | NlFrumlag/aukafall AðvörunKomma? SögnÓp/aukafall

    # Þann auð ber okkur að nýta (til góðra verka)
    | NlBeintAndlag/aukafall2 AðvörunKomma? so_subj_op/aukafall NlFrumlag/aukafall AtvFs? Nhm So_1_nh/aukafall2 FsAtv?

    # Öfug orðaröð: 'Eignarrétti fylgja [í auknum mæli] skyldur' (í stað 'Skyldur fylgja eignarrétti')
    # 'Þetta skrifar Bjarni á Facebook-síðu sinni og bendir [galvaskur] á frammistöðu stjórnarinnar'
    | SetningAukafallÖfug

    # Þessar skuldir hefur 365 ekki greitt
    # Þessa bók hef ég aldrei skilið
    | NlBeintAndlag/aukafall AðvörunKomma? HjSögn/tala/pers NlFrumlag_nf/tala/pers/kyn
        AtvFs? So_1_sagnb/aukafall FsAtv?

    # Köttum hefur [ekki] fjölgað á árinu (= ?Það hefur ekki fjölgað köttum á árinu)
    # Páli hefur [ekki] batnað [síðan í gær]
    | NlFrumlag_þgf AðvörunKomma? SögnSagnbBreyting AtvFsAuka?

    # Köttum í landinu mun [ekki/enn] fjölga á árinu
    # Páli mun [ekki] vaxa skegg
    | NlFrumlag_þgf AðvörunKomma? SögnNhBreyting AtvFsAuka?

    # Hælisleitendum fer/hefur farið fækkandi/fjölgandi
    | NlFrumlag_þgf_ft AðvörunKomma? SögnLhNtBreyting AtvFsAuka?

    # Miðmynd sagna: Mér áskotnaðist, mér fyrirgefst, mér gremst, okkur finnst [öllum]...
    | NlFrumlag_þgf/tala/kyn so_0_mm LoÞgf/tala/kyn? MiðmyndarBotn?
    # Viðreisn hefur [ekki] borist/hlotnast/áskotnast [að þessu sinni] öflugur liðsauki [í dag]
    | NlFrumlag_þgf AðvörunKomma? HjSögnMM FsAtv? so_mm_sagnb FsRunaEftirSögn? NlBeintAndlag_nf/tala/pers AtvFsAuka?
    # Bönkunum hefur/hafði/hefði [flestum] tekist að halda í horfinu
    | NlFrumlag_þgf/tala/kyn AðvörunKomma? HjSögnMM LoÞgf/tala/kyn? so_mm_sagnb MiðmyndarBotn?

    # Öllum hefur verið/skal/mun tryggð heilbrigðisþjónusta / réttur til að lifa með reisn
    | NlFrumlag_þgf AðvörunKomma? HjSögnLhÞtSM_p3/tala FsAtv? so_2_þgf_þf_lhþt/tala/kyn
        FsRunaEftirSögn? AukafallLhÞtAndlag/tala/pers/kyn AtvFsAuka?
    # Páli eru þröngar skorður [kyrfilega] settar [í þessu efni]
    | NlFrumlag_þgf AðvörunKomma? HjSögnLhÞtSM_p3/tala FsAtv? AukafallLhÞtAndlag/tala/pers/kyn
        AtvFs? so_2_þgf_þf_lhþt/tala/kyn FsAtv?

    # Hjónunum var [í fyrra] [báðum] (falið að stýra rannsóknaverkefni)
    | NlFrumlag_þgf/tala/kyn AðvörunKomma? SögnAukafallÞgf/tala/kyn

    # Þess skal [ekki] gætt (að fara varlega með sprengiefnið)
    # Nánari upplýsinga er [ekki] að vænta
    | NlFrumlag_ef SögnAukafallEf

    # Þá ályktun má strax draga í dag (að Páll sé ekki með öllum mjalla)
    # Engan má skylda til að láta eign sína af hendi
    # Miklu má veðja að Guðný snúi aftur í pólitík
    | NlFrumlag/aukafall AðvörunKomma? HjSögnMegiSkuli_et_p3 FsAtv? So_1_nh/aukafall AðLiður?

    # Nauðungarvinnu skal engum gert að leysa af hendi
    | NlFrumlag/aukafall AðvörunKomma? HjSögnMegiSkuli_et_p3 FsAtv? AukafallÖfugSögn Nhm So_1_nh/aukafall FsAtv?

    # Engan þarf/ætti að undra (að mig langi í ís)
    # Greinarmun verður [ekki] að gera (á Jóni og séra Jóni)
    | NlFrumlag/aukafall HjSögnÞurfaEiga_et_p3 FsAtv? Nhm So_1_nh/aukafall AðLiður?

    # Engan má svipta íslenskum ríkisborgararétti að ósekju
    | NlÓbeintAndlag_þf HjSögnMegiSkuli_et_p3 FsAtv? So_2_nh_þf/fall NlBeintAndlag/fall AtvFsAuka?

    # Öllum má tryggja heilbrigðisþjónustu í dag
    | NlÓbeintAndlag_þgf HjSögnMegiSkuli_et_p3 FsAtv? So_2_nh_þgf/fall NlBeintAndlag/fall AtvFsAuka?

    # Öllum skal [ekki] standa [til boða] menntun (Menntun skal standa öllum til boða)
    | NlÓbeintAndlag/aukafall HjSögnMegiSkuli_et_p3 FsAtv? So_1_nh/aukafall FsRunaEftirSögn? NlFrumlag_nf AtvFsAuka?

    # Mér/okkur/ykkur/börnunum finnst/þykir [mörgum [það]] [ekki] skemmtilegt
    # [að borða góðan mat / hérna í kvöld / að Jón lesi bók]
    | NlFrumlag_þgf/tala/kyn AðvörunKomma? HjSögnFinnst FinnstÞað/tala/kyn? FinnstAndlag AðLiður?

    # Konunum þykir/finnst Páll algjör viðbjóður
    | NlFrumlag_þgf/tala/pers AðvörunKomma? HjSögnFinnst/tala/pers ÞykirAndlag

    # Mér hefur [ekki] þótt [það] vont að sofa á nýju dýnunni
    | NlFrumlag_þgf HjSögn_et_p3 FsAtv? HjSögnFinnst_sagnb 'það:pfn'_hk_nf_et? FinnstAndlag AðLiður?

    # Þann auð eigum við [ekki] að nýta (í þágu lands og þjóðar)
    | NlBeintAndlag/aukafall AðvörunKomma? so_0/tala/pers NlFrumlag_nf/tala/pers AtvFs? Nhm So_1_nh/aukafall FsAtv?

    # Þann auð munum/skulum við [ekki] nýta (í þágu lands og þjóðar)
    | NlBeintAndlag/aukafall AðvörunKomma? HjSögnNh/tala/pers NlFrumlag_nf/tala/pers AtvFs? So_1_nh/aukafall FsAtv?

    # Nöfn ráðherra var [ekki] að finna [í Panamaskjölunum]
    | NlBeintAndlag_þf 'vera:so'_0_gm_fh_p3_et FsAtv? Nhm 'finna:so'_0_gm_nh FsAtv?

# Aukafallssetningar sem eru mjög líklega rétt greindar og fá þar
# af leiðandi plús-skor í stað mínuss
SetningAukafallForgangur →
    # Öllum er [í meginatriðum] frjálst/fyrir bestu að ákveða snarlega að flytja úr landi
    # Forseta er [algerlega] óheimilt [í bráð og lengd] að hafa önnur launuð störf á hendi
    BeygingarliðurLoÞgf AðLiður?
    # Mér sýndist [eftir íhugun] að málið hefði fengið eðlilega meðferð
    | NlFrumlag_þgf so_0_mm FsAtv? AðvörunKomma? Skýringarsetning
    # Þess ber að geta að... / Því ber að fagna að...
    # Þess verður að geta að...
    # !!! TODO: 'Því bæri að fagna ef Jón hefði sannfærst'
    | 'það:pfn'_hk_et/aukafall HjSögnBer FsAtv? Nhm So_1_nh/aukafall AðLiður?

BeygingarliðurLoÞgf →
    NlFrumlag_þgf HjSögnLhÞt_et_p3 'það:pfn'_hk_et_nf? FsAtv? LoÞgfNh

# Lýsingarorð sem geta staðið með aukafalli og nafnháttarsetningu
LoÞgfNh →
    LhÞtÞgfBotn # '(Páli er) fyrir bestu (að fara heim)'
    # '(Mér er) ljóst (að Jón er farinn)'
    # '(Mér er) frjálst (að fara héðan)'
    | 'frjáls:lo'_nf_et_hk_sb
    | 'heimill:lo'_nf_et_hk_sb
    | 'óheimill:lo'_nf_et_hk_sb
    | 'eðlislægur:lo'_nf_et_hk_sb
    | 'ljós:lo'_nf_et_hk_sb
    # '(Mér er) skylt (að upplýsa að Jón dró sér fé)'
    | 'skyldur:lo'_nf_et_hk_sb
    # '(Mér er [það]) ljúft [og skylt] (að tilkynna að Gunna er frjáls)'
    | 'ljúfur:lo'_nf_et_hk_sb OgSkylt?
    | "bæði:st" 'ljúfur:lo'_nf_et_hk_sb OgSkylt

OgSkylt →
    "og:st" 'skyldur:lo'_nf_et_hk_sb

$score(+20) OgSkylt

# Öfug orðaröð: 'Eignarrétti fylgja [í auknum mæli] skyldur' (í stað 'Skyldur fylgja eignarrétti')
# 'Þetta skrifar Bjarni á Facebook-síðu sinni og bendir [galvaskur] á frammistöðu stjórnarinnar'
SetningAukafallÖfug →
    Andlagssagnliður/tala/pers  FsRunaEftirSögn? NlFrumlag_nf/tala/pers/kyn
        AtvFsAuka? SagnFramhald/tala/pers/kyn*

$score(-32) SetningAukafallÖfug

Andlagssagnliður/tala/pers → NlBeintAndlag/aukafall So_1/aukafall/tala/pers

ÓpSagnliður/aukafall → so_subj_op/aukafall ÓpSagnarBotn?

SögnÓp/aukafall →
    # (Mér) [algjörlega] blöskraði [af öllu hjarta] vitleysan
    # (Mig) þraut örendið
    Atviksliður? So_subj_op/fall/aukafall SögnÓpBotn/fall

SögnÓpBotn/fall →
    FsRunaEftirSögn? NlBeintAndlag/fall AtvFsAuka?
    | NhFyllingAtv  # Landinu bar [blátt áfram] að bjarga flóttamönnum í neyð

ÞykirAndlag →
    NlBeintAndlag_nf/tala/pers/kyn NlÓbeintAndlag_nf

$score(+20) ÞykirAndlag

FinnstÞað/tala/kyn →
    'það:pfn'_hk_nf_et LoÞgf/tala/kyn       # Bílstjórum finnst það mörgum leiðinlegt
    | LoÞgf/tala/kyn 'það:pfn'_hk_nf_et?    # Bílstjórum finnst mörgum [það] leiðinlegt

FinnstAndlag →
    LoTengtSögn_nf_et_hk    # skemmtilegt
    | ao* so_lh_nt          # mjög endurnærandi, afskaplega frískandi

SögnAukafallÞgf/tala/kyn →
    HjSögnLhÞtSM_et_p3 FsAtv? LoÞgf/tala/kyn? SögnAukafallÞgfBotn

SögnAukafallÞgfBotn →
    # (Stóru skipunum hefur verið/skal/mun) stýrt (í land)
    # (Hannesi var í fyrra) falið að stýra rannsóknaverkefni
    # (Mér var strax) sagt að Friðgeir væri farinn að drekka aftur
    # (Manninum var) vikið [frá störfum [þegar málið kom upp]]
    so_1_þgf_sagnb AðLiður?

    # (Þátttakendum er) gert kleift að matast með reisn
    | 'gera:so'_sagnb LoTengtSögn_nf_et_hk NhFyllingAtv

    # (Öllum hefur verið/skal/mun) óskað [í snarheitum] góðs bata
    | so_2_þgf_ef_sagnb FsRunaInnskot? NlBeintAndlag_ef AtvFsAuka?

    # (Öllum hefur verið/skal/mun) skilað [í flýti] bílunum
    | so_2_þgf_þgf_sagnb FsRunaInnskot? NlBeintAndlag_þgf AtvFsAuka?

SögnAukafallEf →
    HjSögnLhÞtSM_et_p3 FsAtv? SögnAukafallEfBotn

SögnAukafallEfBotn →
    # (Þess skal [ekki]) gætt að fara varlega með sprengiefnið
    so_1_ef_sagnb AðLiður?

    # Ákvörðunar var/er/verður/hefur verið/mun [ekki] að vænta [strax í dag]
    | Nhm so_1_ef_nh FsRunaEftirSögn?

$score(+3) SögnAukafallEfBotn

AukafallÖfugSögn →
    NlBeintAndlag/fall AtvFs? So_1_nh/fall         # (skal) engum gera
    | NlBeintAndlag/fall AtvFs? So_1_sagnb/fall    # (skal) engum gert

# Tilvik þar sem sögn í lhþt eintölu fylgir andlag í fleirtölu:
# 'Öllum skal tryggð mannhelgi og vernd gegn hvers kyns ofbeldi'

AukafallLhÞtAndlag/tala/pers/kyn →
    NlBeintAndlag_nf/tala/pers/kyn

AukafallLhÞtAndlag_et/pers/kyn →
    NlBeintAndlag_nf_ft/pers/kyn

AukafallsBotn →
    FsAtv                             # (Mér virtist ykkur ekki hungra) mjög undir ræðunni
    | FsRuna? NlFrumlag_nf AtvFsAuka? # (Mér virtist ykkur ekki falla) [í geð] ræðan í kvöld
    | NhFyllingAtv                    # (Mér sýndist ykkur langa) gríðarlega að fara til útlanda strax

ÓpSagnarBotn →
    FsAtv                             # (Mér líkaði) vel við Jón
    | NhFyllingAtv                    # (Okkur langaði) gríðarlega að fara til útlanda strax
    | FsAtv? HjSögnVantaSkorta NlBeintAndlag_þf AtvFsAuka? # (Okkur þótti) [algjörlega] vanta broddinn í ræðuna
    | Nl_nf AtvFs? VeraInnskot? NhRuna FsAtv? # (Okkur þótti) hún teygja lopann gríðarlega
    | MiðmyndarBotnLo                  # (Okkur þótti) kettirnir [ekk] [vera] fallegir

MiðmyndarBotn →
    MiðmyndarBotnFlókinn
    # (Páli fyrirgefst) örugglega á morgun
    | FsAtv

MiðmyndarBotnFlókinn →
    # (Mér sýndist) kötturinn [ekki] [vera [skyndilega] að] lepja mjólkina og borða matinn í gær
    Nl_nf AtvFs? VeraInnskot? NhRuna FsAtv?

    # (Mér leyfist) vonandi að fara út
    | NhFyllingAtv

    # (Mér finnst) kettirnir [ekki] [vera] fallegir
    | MiðmyndarBotnLo

    # (Mér virtist) ykkur [ekki] blöskra undir ræðunni
    | NlFrumlag/aukafall AtvFs? so_subj_nh/aukafall AukafallsBotn?

    # (Mér finnst) [ekki / í meginatriðum] vanta blíðu [í söng Ara]
    # !!! ATH: Hvaða sagnir geta staðið hér? Aðeins 'vanta' og 'skorta'?
    | FsAtv? HjSögnVantaSkorta NlBeintAndlag_þf AtvFsAuka?

    # (Mér virtist) þeim [ekki] sýnast börnin vera falleg
    | Nl_þgf AtvFs? so_nh_mm MiðmyndarBotn

$score(+14) MiðmyndarBotnFlókinn

MiðmyndarBotnLo →
    Nl_nf/tala/pers/kyn AtvFs? HjSögnVera? LoTengtSögn_nf/tala/kyn FsAtv?

$score(+24) MiðmyndarBotnLo # Gefa plús fyrir heildstætt samhengi lýsingarorðs

VeraInnskot →
    HjSögnVera FsAtv? Nhm  # vera [stöðugt] að / geta hafa verið [alltaf] að / verðir [í snatri] að / hafa orðið að...

# Setningar án frumlags (notaðar t.d. í tengisetningum eftir 'sem').
# Í setningum án frumlags getur sagnliðurinn verið "öfugur",
# þ.e. "konurnar sem borðað höfðu matinn" í stað "höfðu borðað".
# Þá má atviksliður ekki koma inn á milli sagnanna, þ.e.
# "höfðu ekki borðað" er gilt, en "borðað ekki höfðu" ógilt.

SetningÁnF/tala/pers/kyn →
    EinSetningÁnF/tala/pers/kyn FramhaldÁnF/tala/pers/kyn*

# Sértilvik fyrir falið 'það': 'kallað hafði verið eftir lækni'
SetningÁnF_et_p3_hk →
    SetningSoÞað

EinSetningÁnF/tala/pers/kyn →
    Atviksliður? ÖfugurSagnliður/tala/pers/kyn
        LoTengtSögn_nf/tala/kyn? SagnInnskotAtv? ÖfugtFramhald/tala/pers/kyn*
    # Lýsingarorð á undan sögn:
    # (Ráðherrar taka þær ákvarðanir sem) nauðsynlegar eru/þykja/teljast til rækslu starfa þeirra
    | LoTengtSögn_nf/tala/kyn Sögn_0/tala/pers/kyn SagnInnskotAtv?

$tag(begin_prep_scope) EinSetningÁnF/tala/pers/kyn # Nýtt undirtré fyrir tengingu sagnar og forsetningar

ÖfugtFramhald/tala/pers/kyn →
    KommaOgEðaEn ÖfugurSagnliður/tala/pers/kyn LoTengtSögn_nf/tala/kyn? SagnInnskotAtv?

$score(-3) ÖfugtFramhald/tala/pers/kyn

FramhaldÁnF/tala/pers/kyn →
    FramhaldÁnFMeðKommu/tala/pers/kyn
    | FramhaldÁnFÁnKommu/tala/pers/kyn

FramhaldÁnFMeðKommu/tala/pers/kyn →
    "," FramhaldÁnFÁnKommu/tala/pers/kyn LokatáknEðaKomma

$score(+8) FramhaldÁnFMeðKommu/tala/pers/kyn    # Framhald innan kommu er mjög líklega rétt

FramhaldÁnFÁnKommu/tala/pers/kyn →
    Samtenging HreinYfirsetning                 # (sem borðað höfðu matinn) þegar hann var framreiddur fyrr um kvöldið
    | Samtenging EinSetningÁnF/tala/pers/kyn    # (sem borðað höfðu matinn) og þakkað auðmjúkar fyrir sig

# Sagnliðir

Sagnliður/tala/pers/kyn →

    Sögn_0/tala/pers/kyn       # Sögn með engu viðfangi: (veðrið) skánaði
    | Sögn_1/tala/pers/kyn     # Sögn með einu viðfangi: (Jón) sá köttinn
    | Sögn_2/tala/pers/kyn     # Sögn með tveimur viðföngum: (Kata) skrifaði Önnu bréf

    | Sögn_sagnb/tala/pers/kyn # Sagnbót: (Jón) hefur borðað matinn og farið að bóna bílinn
    | Sögn_nh/tala/pers/kyn    # Sagnir í nafnhætti: (Jón) mun/hyggst bóna bílinn og fara að kaupa inn
    | Sögn_lhþt/tala/pers/kyn  # Sagnir í lh. þt.: (Kata) er vöknuð og farin að sækja Pál
    | Sögn_lhnt/tala/pers      # Sagnir í lh. nt.: (Guðrún) var hringjandi og sannfærandi í allan dag

# Sagnliður með venjulegri eða 'öfugri' orðaröð: (Konan sem) matinn borðaði

ÖfugurSagnliður/tala/pers/kyn →

    Sagnliður/tala/pers/kyn

    | ÖfugSögn_0/tala/pers/kyn
    | ÖfugSögn_1/tala/pers/kyn
    | ÖfugSögn_2/tala/pers/kyn

# Sagnliður án sýnilegs frumlags: Í dag þarf að vökva grasið (=Það þarf að vökva grasið í dag)
# Sagnliður án frumlags þarf að geta staðið sem afleiðing í ef-þegar setningu:
# '(Ef hann hringir) má sækja tölvuna og byrja að vinna'

SagnliðurÁnF →
    so_subj_gm_none NhFyllingAtv                            # (Í dag) þarf [sannarlega] (að vökva grasið)
    | so_0_mm_p3_et NhFyllingAtv                            # (Í dag) dregst [sennilega] (að vökva grasið)

    | HjSögn_et_p3 FsAtv? so_subj_sagnb_none NhFyllingAtv   # (Í ár) hefur [í sumarhitanum] þurft (að vökva grasið)
    | HjSögn_et_p3 FsAtv? so_mm_sagnb NhFyllingAtv          # (Í ár) hefur [vegna leti] dregist (að vökva grasið)
    | HjSögnNh_et_p3 FsAtv? so_subj_nh_none NhFyllingAtv    # (Í ár) mun [í sumarhitanum] þurfa (að vökva grasið)
    | HjSögnNh_et_p3 FsAtv? so_mm_nh NhFyllingAtv           # (Í ár) mun [í sumarhitanum] dragast (að vökva grasið)

    | HjSögnSagnb FsAtv? so_sagnb NhFyllingEðaAtv? # (í dag) verður/skal/mun [líklega] lent á brautinni (eftir hádegi)

    | HjSögnMegiSkuli_et_p3 HreinSögn_nh_et_hk SagnInnskot? # [Í dag] má sækja tölvuna (og byrja að vinna)
    | so_0_nh HjSögnNhMeðSo_et_p3 HreinSögnAtv_nh?          # [hvort] leggja skuli/eigi/þarf/verður [á skatt, breyta honum eða afnema hann]

    | HjSögnNh_p3/tala FsAtv? so_mm_nh FsRunaEftirSögn? NlFrumlag_nf/tala # [Í kröfunum] skulu felast [í snatri] tillögur (um nýtt fyrirkomulag)

    | Þarf_þf

    | StökSögn_et_p3_gm                                     # (Í dag) segir
    | so_0_et_p3_gm SagnInnskot                             # (Í dag) fýkur í fólk / kreppir að / losnar um stífluna / segir

$tag(begin_prep_scope) SagnliðurÁnF # Nýtt undirtré fyrir tengingu sagnar og forsetningar

Þarf_þf →
    'þurfa:so'_1_þf_gm_fh_et_p3 FsRunaEftirSögn? NlBeintAndlag_þf # Þarf [til þess] lagafyrirmæli

# Draga úr þessu frekar formlega orðalagi
$score(-12) Þarf_þf

StökSögn_et_p3_gm →
    so_0_et_p3_gm

# Óalgengt að sagnorð standi ein og sér án frumlags
$score(-16) StökSögn_et_p3_gm

HreinSögnAtv_nh →
    FsAtv? HreinSögn_nh_et_hk

# Sagnliður sem hefst á viðtengingarhætti:
# 'Haldi þessi þróun áfram er voðinn vís / þarf að grípa til aðgerða'
# !!! TODO: hér þyrfti að leyfa að setja "þá" á milli, mætti merkja sem óþarft samt.
# Komma þarf líka að leyfast, s.s. í aðalreglunni. Finna hvar þetta er notað.
SagnliðurVh →

    # Haldir þú [áfram að borða] (er voðinn vís)
    # Aki hann ölvaður (skal sekta hann um tíu þúsund krónur)
    so_0_vh/tala/pers NlFrumlag_nf/tala/pers/kyn LoTengtSögn_nf/tala/kyn? NhFyllingEðaAtv?

    # Borði börnin [í snarheitum] matinn (getum við farið í sund)
    | So_1_vh_nf/tala/pers NlFrumlag_nf/tala/pers FsRunaInnskot? NlSagnfylling_nf AtvFsAuka?
    | So_1_vh/aukafall/tala/pers NlFrumlag_nf/tala/pers FsRunaInnskot? NlBeintAndlag/aukafall AtvFsAuka?

    # Verði þessi tillaga felld (geng ég úr flokknum)
    # Verði karlarnir byrjaðir að grilla (sæki ég sósuna)
    | VeraVerða_vh/tala/pers NlFrumlag_nf/tala/pers/kyn HreinSögn_lhþt/tala/kyn FsAtv?

    # Verði þeim [ekki] vísað [úr landi] geta afleiðingarnar orðið hörmulegar
    # Sé börnunum sagt [að fara í háttinn] grenja þau stanslaust
    | VeraVerða_vh_et_p3 NlFrumlag_þgf AtvFs? so_1_þgf_sagnb NhFyllingEðaAtv?

    # Hafi maður verið sviptur frelsi að ósekju (skal hann eiga rétt til skaðabóta)
    | 'hafa:so'_0_vh/tala/pers NlFrumlag_nf/tala/pers/kyn AtvFs?
        'vera:so'_sagnb HreinSögn_lhþt/tala/kyn FsAtv?

    # Hafi manni verið hallmælt í útvarpi (skal hann fá kaffi og kleinu)
    | 'hafa:so'_0_vh_et_p3 NlFrumlag_þgf AtvFs?
        'vera:so'_sagnb so_1_þgf_sagnb FsAtv?

    # Hafi manni verið tekið blóð að óþörfu (skal hann liggja á bekk í stundarfjórðung)
    | 'hafa:so'_0_vh_et_p3 NlFrumlag_þgf AtvFs?
        'vera:so'_sagnb so_2_þgf_þf_lhþt/tala/kyn Nl_nf/tala/kyn AtvFs?

    # Hafi manni verið gert kleift að matast með reisn (skal hann liggja á bekk í stundarfjórðung)
    | 'hafa:so'_0_vh_et_p3 NlFrumlag_þgf AtvFs? 'vera:so'_sagnb
        'gera:so'_lhþt/tala/kyn LoTengtSögn_nf_et_hk NhFyllingAtv

    # Hafi manns verið leitað að óþörfu (skal hann fá axlarnudd)
    | 'hafa:so'_0_vh_et_p3 NlFrumlag_ef AtvFs? 'vera:so'_sagnb so_2_þgf_ef_sagnb FsAtv?

    # Fari [það] svo/þannig að ég nái ekki kjöri til forseta [,] [þá] getið þið sótt fyrirlestur um þetta hjá mér
    | 'fara:so'_0_vh_et_p3 'það:pfn'_hk_nf_et? FsAtv AtviksAðSetning

$score(-4) SagnliðurVh

VeraVerða_vh/tala/pers →
    'vera:so'_0_vh/tala/pers | 'verða:so'_0_vh/tala/pers

VeraVerða_gm_p3/tala →
    'vera:so'_1_nf_gm_p3/tala | 'verða:so'_1_nf_gm_p3/tala

OgVeraVerða_gm_p3/tala →
    FsAtv? Aðaltenging FsAtv? VeraVerða_gm_p3/tala
    | FsAtv? "," Aðaltenging FsAtv? VeraVerða_gm_p3/tala FsAtvKomma?

AtviksAðSetning → "að:st" HreinYfirsetning

# Sagnliður með inniföldu frumlagi:
# '(Í dag) borðaði Jón matinn'
# '(Með því) var svæðinu stefnt í hættu'
# '(Í kjölfar þess) varð ég ósáttur'
# '(Nú) mega allir fara sem vilja'
# '(Í gær) skrifuðu þær konur þingmönnum bréf sem voru heitastar í málinu'

SagnliðurMeðF/tala/pers/kyn →
    SögnMeðF/tala/pers/kyn Tilvísunarsetning/tala/pers/kyn?

$score(-2) SagnliðurMeðF/tala/pers/kyn

SögnMeðF/tala/pers/kyn →
    SögnMeðF_0/tala/pers/kyn   # Sögn með engu viðfangi: (Í dag) skánaði veðrið
    | SögnMeðF_1/tala/pers/kyn # Sögn með einu viðfangi: (Í dag) sá Jón köttinn
    | SögnMeðF_2/tala/pers/kyn # Sögn með tveimur viðföngum: (Í dag) skrifaði Kata Önnu bréf

$score(+1) SögnMeðF_0/tala/pers/kyn
$score(+3) SögnMeðF_1/tala/pers/kyn
$score(+5) SögnMeðF_2/tala/pers/kyn

OgSögn_0/tala/pers →
    FsAtv? Aðaltenging FsAtv? so_0/tala/pers
    | FsAtv? "," Aðaltenging FsAtv? so_0/tala/pers FsAtvKomma?

OgSögn_mm/tala/pers →
    FsAtv? Aðaltenging FsAtv? so_mm/tala/pers
    | FsAtv? "," Aðaltenging FsAtv? so_mm/tala/pers FsAtvKomma?

OgSögn_mm_nh →
    FsAtv? Aðaltenging FsAtv? so_mm_nh
    | FsAtv? "," Aðaltenging FsAtv? so_mm_nh FsAtvKomma?

OgSögn_mm_sagnb →
    FsAtv? Aðaltenging FsAtv? so_mm_sagnb
    | FsAtv? "," Aðaltenging FsAtv? so_mm_sagnb FsAtvKomma?

OgSögn_sagnb/tala/kyn →
    # (hefur staðið) keikur í brúnni og mótmælt yfirgangi Rússa
    LoTengtSögn_nf/tala/kyn? FsAtv? Aðaltenging FsAtv? EinSögn_sagnb/tala/kyn
    | LoTengtSögn_nf/tala/kyn? FsAtv? "," Aðaltenging FsAtv? EinSögn_sagnb/tala/kyn FsAtvKomma?

$score(+6) OgSögn_sagnb/tala/kyn # Enn sterkari plús fyrir runu sagnb

OgSögn_nh/tala/kyn →
    SagnInnskot? Aðaltenging FsAtv? EinSögn_nh/tala/kyn
    | SagnInnskot? ViðurkenningarNh 
    | SagnInnskot? "," Aðaltenging FsAtv? EinSögn_nh/tala/kyn FsAtvKomma?
    | SagnInnskot? KommaViðurkenningarNh

# Þá segist hún styðja trúfrelsi, þrátt fyrir að vera andsnúin moskubyggingum

KommaViðurkenningarNh → "," ViðurkenningarNh FsAtvKomma?

ViðurkenningarNh →
    Viðurkenningartenging FsAtv? EinSögn_nh/tala/kyn 

OgSögn_lhþt/tala/kyn →
    # Leyfa að 'og' sé sleppt, sbr. "Tveir menn voru stöðvaðir í morgun [sterklega] grunaðir um ölvun við akstur"
    FsAtv? OgEðaEnAtv? EinSögnFrh_lhþt/tala/kyn
    | FsAtv? "," Aðaltenging? FsAtv? EinSögnFrh_lhþt/tala/kyn FsAtvKomma?

OgSögn_lhnt →
    FsAtv? Aðaltenging FsAtv? EinSögn_lhnt
    | FsAtv? "," Aðaltenging FsAtv? EinSögn_lhnt FsAtvKomma?

OgSögn_1_þf_nh/tala/pers →
    FsAtv? Aðaltenging FsAtv? So_1_þf_nh/tala/pers
    | FsAtv? "," Aðaltenging FsAtv? So_1_þf_nh/tala/pers FsAtvKomma?

# Ekki enda á FsAtv nema innan kommu
FsAtvKomma → FsAtv? ","

# Lýsingarháttur þátíðar (Við erum byrjaðir)

LhÞtBotn/tala/kyn →
    # (Mennirnir eru byrjaðir) galvaskir að grilla
    # (Mennirnir eru sagðir) fara herskáir að brenna þorp
    so_0_nh? NhFylling/tala/kyn
    | NhRuna                                # (Mennirnir eru sagðir) fara sjaldan í bað, og raka sig aldrei
    | LoTengtSögn_nf/tala/kyn               # (Mennirnir eru taldir) ritfærir
    | eo* Aldur 'gamall:lo'_sb_nf/tala/kyn?     # (Mennirnir eru orðnir) [tæplega] fimmtán ára [gamlir]

EinSögn_lhþt/tala/kyn →
    so_0_lhþt/tala/kyn LhÞtBotn/tala/kyn?           # (Mennirnir eru) lentir
    > so_2_þf_þgf_lhþt/tala/kyn NlBeintAndlag_þgf   # (Maðurinn var) sviptur frelsi
    > so_2_þgf_þf_lhþt/tala/kyn NlÓbeintAndlag_þgf  # (Ákvörðunin var) tilkynnt Páli
    > so_subj_lhþt/tala/kyn Nl_nf/tala              # (Katrín var) valin besti þjálfarinn
    > so_subj_lhþt_ft/kyn Nl_nf_et                  # (Þau voru) valin efnilegasta sundfólkið

EinSögnFrh_lhþt/tala/kyn →
    EinSögn_lhþt/tala/kyn                           # (Mennirnir) eru lentir og byrjaðir að grilla
    > NlFrumlag_nf/tala/kyn so_0_lhþt/tala/kyn      # veð tekið / veðin tekin / kötturinn kembdur
    > EinSögnFrh_lhþt_sagnb

# Framhald lýsingarháttar þátíðar sem fer yfir í sagnbót
# ('leyfisveiting yrði einfölduð og fólki leyft að leigja út eignir, og heimagisting flokkuð á annan hátt')
# Í dæminu er lhþt og sagnb, og lhþt
EinSögnFrh_lhþt_sagnb →
    NlFrumlag_þf/tala/kyn so_1_þf_sagnb NhFylling/tala/kyn?     # fólki leyft (að fara sársvöngu um borð)
    | NlFrumlag_þgf/tala/kyn so_1_þgf_sagnb NhFylling/tala/kyn? # miðunum breytt / fólkinu sagt (að fara um borð)
    | NlFrumlag_ef/tala/kyn so_1_ef_sagnb NhFylling/tala/kyn?   # matarins neytt / þess beðið (að fara um borð)

HreinSögn_lhþt/tala/kyn →
    FsAtv? EinSögn_lhþt/tala/kyn OgSögn_lhþt/tala/kyn*

# Hvetja til 'verði bókfærðar'
$score(+12) HreinSögn_lhþt/tala/kyn

Sögn_lhþt/tala/pers/kyn →
    SögnErLo/tala/pers/kyn
    # talin er/hefur verið [liggja undir grun]
    | so_0_lhþt/tala/kyn HjSögnLhÞtSM/tala/pers LhÞtBotn/tala/kyn?

$score(+8) Sögn_lhþt/tala/pers/kyn

SögnErLo/tala/pers/kyn →
    # (Jón er orðinn) duglegur / tólf ára
    # er leiðinlegur, hefur verið frábær, munu vera einstök
    HjSögnLhÞtSM/tala/pers SögnErLoBotn/tala/kyn

# Hvetja til 'er/var/hefur verið...' + lýsingarorð í nefnifalli og sterkri beygingu
$score(+60) SögnErLo/tala/pers/kyn

SögnErLoBotn/tala/kyn →
    # (Guðrún er) snögg [að svara fyrir sig] / (Það er) torsótt [að Siggi fari einn í skólann]
    LoTengtSögn_nf/tala/kyn AðLiður?
    | LoTengtSögn_nf/tala/kyn Spurnaraukasetning
    # (Páll var) [rúmlega] tíu ára [gamall]
    | eo* Aldur 'gamall:lo'_sb_nf/tala/kyn?
    # (Jón hefur verið) ráðinn [sem] bæjarstjóri Ísafjarðarbæjar
    | SögnLhÞt_nf/tala/kyn NlBeintAndlag_nf/tala/pers
    # (Í herferðinni var) [ekki] notast (við nýjar aðferðir)
    | FsAtv? so_0_mm_sagnb
    # (Hún er/var/hefur verið) talin/álitin/sögð vera lykillinn [að því að málið leystist]
    | SögnLhÞtVera_nf/tala/kyn HjSögnVera NlSagnfylling_nf

SögnLhÞt_nf/tala/kyn →
    'kalla:so'_lhþt_sb_nf/tala/kyn                  # kallaður
    | 'nefna:so'_lhþt_sb_nf/tala/kyn "sem:ao"?      # nefndur
    | 'tilnefna:so'_lhþt_sb_nf/tala/kyn "sem:ao"?   # tilnefndur
    | 'útnefna:so'_lhþt_sb_nf/tala/kyn "sem:ao"?    # útnefndur
    | 'skíra:so'_lhþt_sb_nf/tala/kyn                # skírður
    | 'setja:so'_lhþt_sb_nf/tala/kyn "sem:ao"?      # settur
    | 'skipa:so'_lhþt_sb_nf/tala/kyn "sem:ao"?      # skipaður
    | 'ráða:so'_lhþt_sb_nf/tala/kyn "sem:ao"?       # ráðinn
    | 'velja:so'_lhþt_sb_nf/tala/kyn "sem:ao"?      # valinn
    | 'álíta:so'_lhþt_sb_nf/tala/kyn                # álitinn
    | 'telja:so'_lhþt_sb_nf/tala/kyn                # talinn

$score(+10) SögnLhÞt_nf/tala/kyn

SögnLhÞtVera_nf/tala/kyn →
    'segja:so'_lhþt_sb_nf/tala/kyn                  # sögð vera
    | 'álíta:so'_lhþt_sb_nf/tala/kyn                # álitin vera
    | 'telja:so'_lhþt_sb_nf/tala/kyn                # talin vera
    | 'láta:so'_lhþt_sb_nf/tala/kyn                 # látin vera

$score(+12) SögnLhÞtVera_nf/tala/kyn
$tag(purge_verb) SögnLhÞtVera_nf/tala/kyn

LhÞtÞgfBotn → 'fyrir:fs'_þgf 'góður:lo'_esb_hk_þgf_et  # Það sem er börnum fyrir bestu

LhÞtÞgfEinkunn →
    LhÞtÞgfBotn | LoTengtSögn_nf_et_hk

Sögn_lhþt_et_p3_hk →
    # Sértilfelli fyrir 'Það er okkur mikilvægt/fyrir bestu [í þessu máli] að fara/gera...'
    HjSögnLhÞt/tala/pers NlFrumlag_þgf LhÞtÞgfEinkunn NhFylling

# Lýsingarháttur nútíðar ('við vorum hringjandi', 'hún hefur verið kvartandi')

Sögn_lhnt/tala/pers →
    HjSögnLhÞt/tala/pers HreinSögn_lhnt
    | 'þykja:so'_0/tala/pers HreinSögn_lhnt  # Almenningur þykir uggandi / ég þykist vitandi

HreinSögn_lhnt →
    FsAtv? EinSögn_lhnt OgSögn_lhnt*

EinSögn_lhnt →
    so_0_lh_nt                          # skimandi
    | so_1_nf_lh_nt NlSagnfylling_nf    # verandi lögmaður sakborningsins
    | so_1_þf_lh_nt NlBeintAndlag_þf    # reykjandi sígarettu
    | so_1_þgf_lh_nt NlBeintAndlag_þgf  # bölvandi Páli
    | so_1_ef_lh_nt NlBeintAndlag_ef    # saknandi alls þess sem áður var
    | so_2_þgf_þf_lh_nt Andlög_þgf_þf   # skrifandi konunum bréf / gefandi konunum meiri gaum en körlunum

# Sagnbót ('konurnar [segjast/þykjast/virðast] [galvaskar/sumar/allar] hafa borðað matinn')

Sögn_sagnb/tala/pers/kyn →
    HjSögn/tala/pers LoTengtSögn_nf/tala/kyn? HreinSögn_sagnb/tala/kyn

HreinSögn_sagnb/tala/kyn →
    FsAtv? EinSögn_sagnb/tala/kyn OgSögn_sagnb/tala/kyn*
    # (Jón hafi) hvorki látið lífið né slasast alvarlega
    | Hvorki EinSögn_sagnb/tala/kyn OgSögn_sagnb/tala/kyn* ","? Né EinSögn_sagnb/tala/kyn

# Útgáfa til brúks strax á eftir nafnlið
# (sem getur endað á FsRunu og er því tvíræður við FsAtv)
HreinSögnAtv_sagnb/tala/kyn →
    AtvFs? EinSögn_sagnb/tala/kyn OgSögn_sagnb/tala/kyn*

EinSögn_sagnb/tala/kyn →
    so_sagnb NhFylling/tala/kyn?        # Konurnar hafa farið [sjálfar/galvaskar] að grilla
    | So_1_þf_nh_sagnb SagnarBotn       # Konurnar hafa [ekki] látið Pál borða matinn / sagt niðurstöðuna vera ranga
    # !!! Á að höndla Sögn_1_sagnb + NhFylling fremur eins og sögn með tveimur viðföngum,
    # !!! sbr. Konurnar hafa lofað Páli ([því] að fara út): þarna er í raun 'lofa:so'_2_þgf_þgf_sagnb
    | Sögn_1_sagnb NhFylling/tala/kyn?  # Konurnar hafa [ekki] lofað Páli [að fara út]
    | Sögn_2_sagnb
    | so_mm_sagnb LhÞtBotn/tala/kyn   # Konurnar hafa reynst vera bíræfnir þjófar / þóst ritfærar / sagst fara að drífa sig

# Gefa auka stig hér enda smella þá fleiri en eitt púsl saman
$score(+8) EinSögn_sagnb/tala/kyn

# Nafnháttur

Sögn_nh/tala/pers/kyn →
    # Páll mun/skal [galvaskur/allur] skrifa bréfið
    HjSögnNh/tala/pers LoTengtSögn_nf/tala/kyn? HreinSögn_nh/tala/kyn
    # Páll á/ætti/verður/þarf [ekki] [galvaskur/allur] að skrifa bréfið
    | HjSögnNhMeðSagnb/tala/pers FsAtv? LoTengtSögn_nf/tala/kyn? Nhm HreinSögn_nh/tala/kyn
    # Hann segist munu borða kökuna
    | Sögn_nh_mm/tala/pers/kyn
    # Jón hefur [haft] sögu að segja
    | HjSögnHafaNh/tala/pers SögnNhBotn

SögnNhBotn →
    # [Jón hefur] sögu að segja
    NlBeintAndlag/fall Nhm So_1_nh/fall
    # Jón hefur [talsvert] aðra/betri sögu að segja en þá venjulegu
    | NlAnnar/fall Nhm So_1_nh/fall FsAtv? SamanburðarNafnliður

NlAnnar/fall →
    AtviksliðurEinkunn* MiðstigEðaAnnar/fall/tala/kyn Nl/fall/tala/kyn

MiðstigEðaAnnar/fall/tala/kyn →
    Allt? 'annar:fn'/fall/tala/kyn  # [allt] aðra sögu
    | MiðstigForskeyti? lo_mst/fall/tala/kyn

SamanburðarNafnliður → HeldurEn Nl/fall

HeldurEn → AðvörunHeldur? "en:st"

# Gefa auka stig hér (enda er oft tvíræðni milli sagna í nafnhætti og nafnorða)
$score(+2) Sögn_nh/tala/pers/kyn

# Miðmynd + nafnháttur
# TODO skgr. hvaða sagnir ganga sem "hjálparsagnir" hér?
Sögn_nh_mm/tala/pers/kyn →
    so_mm/tala/pers OgSögn_mm/tala/pers* Botn_nh_mm/tala/kyn
    # Hann segist munu borða kökuna
    # Ég telst vilja kjósa flokkinn
    # Þau segjast geta látið henda Jóni út
    | So_1_þf_mm HjSögnNh_ft_p3 HreinSögn_nh/tala/kyn

Botn_nh_mm/tala/kyn →
    # (Páll hyggst/þykist/telst) skrifa söguna
     HreinSögn_nh/tala/kyn
     # (Kvennalistakonur virðast [ekki]) taldar sigurstranglegar
     | HreinSögn_lhþt/tala/kyn
     # Þær segjast [ekki] [vera] samþykkar málinu
     | FsAtv? HjSögnVera? LoTengtSögn_nf/tala/kyn
     # (Jón sagðist) þeirrar skoðunar [að múslimar væru óalandi og óferjandi]
     # (Maðurinn reyndist) þeirrar innréttingar að ekki var á hann treystandi
     | Botn_nh_ef

Botn_nh_ef →
     FsAtv? 'sá:fn'_ef/tala/kyn Nl_ef/tala/kyn Útskýring?

# Hvetja til samhengis með miðmynd: (hún) segist ekki ætla (að fara í framboð)
$score(+4) Sögn_nh_mm/tala/pers/kyn

HreinSögn_nh/tala/kyn →
    FsAtv? EinSögn_nh/tala/kyn OgSögn_nh/tala/kyn*

EinSögn_nh/tala/kyn →
    so_0_nh NhFylling/tala/kyn?                    # Konurnar munu fara að grilla
    | Sögn_1_nh NhFylling/tala/kyn?                # Konurnar munu [ekki] lofa Páli [að fara út]
    | Sögn_2_nh
    # (Stjórnsýsla skal) vera [í öllum tilvikum] gegnsæ
    | HjSögnVeraVerða FsRunaInnskot? LoTengtSögn_nf/tala/kyn

# Boðháttur

Sögn_bh →
    # Farðu [sjálfur] ([út] að þvo bílinn [fullur eldmóðs])
    so_0_bh/tala LoTengtSögn_nf/tala/kyn? BhBotnTagl/tala/kyn?
    # Förum [sjálfar] ([út] að þvo bílinn [fullar eldmóðs])
    | so_0_gm_fh_p1_ft_nt LoTengtSögn_nf_ft/kyn? BhBotnTagl_ft/kyn?
    # Lofið barninu [að borða] [í friði]
    | Sögn_1_bh BhBotn?
    # Skrifaðu jólasveininum bréf
    | Sögn_2_bh BhBotn?
    # Láttu/látið það ekki slá ykkur (út af laginu) og fara (með ykkur í gönur)
    | 'láta:so'_bh NlBeintAndlag_þf AtvFs? EinSögn_nh/tala/kyn OgSögn_nh/tala/kyn*

# Boðháttur er sjaldgæfur - viljum aðra kosti frekar
$score(-14) Sögn_bh
$tag(begin_prep_scope) Sögn_bh

BhBotnTagl/tala/kyn →
    BhBotn LoTengtSögn_nf/tala/kyn?

BhBotn →
    # að borða í friði
    NhFylling SagnInnskot?
    | SagnInnskot
    # í skálinni meðan þið brjótið eggin
    | FsAtv? Tíðartenging HreinYfirsetning

SögnFrh_bh →
    KommuSögn_bh* OgSögn_bh+

KommuSögn_bh →
    "," FsAtv? Sögn_bh

OgSögn_bh →
    ","? Aðaltenging FsAtv? Sögn_bh

# ...en þegar eru komnar tvær eða fleiri sagnir í röð þá er það líklegri lausn
$score(+10) KommuSögn_bh
$score(+10) OgSögn_bh

# Sögn með engu viðfangi ('lenti')
# ('Maðurinn lenti')
Sögn_0/tala/pers/kyn →
    # (Mennirnir) lenda og fara [galvaskir að þvo bílinn]
    so_0/tala/pers OgSögn_0/tala/pers* NhFylling/tala/kyn?

# Sögn með engu viðfangi ('lenti') sem stendur í undirsetningu
# ('Maðurinn sem lenti')
ÖfugSögn_0/tala/pers/kyn →

    # Mennirnir sem nennt hafa [að þvo bílinn]
    so_sagnb HjSögn/tala/pers NhFylling/tala/kyn?

    # Mennirnir sem lenda munu
    | so_0_nh HjSögnNh/tala/pers NhFylling/tala/kyn?

    # Mennirnir sem lentir eru / Ég sem farinn mun vera [að reskjast]
    | so_0_lhþt/tala/kyn HjSögnLhÞtSM/tala/pers NhFylling/tala/kyn?

    # (Páll gerði allt sem) í hans valdi stóð
    # (...þau réttindi sem) í þessari stjórnarskrá felast
    | FsAtv so_0/tala/pers

# Sögn með einu viðfangi sem stendur í undirsetningu
# ('Konurnar sem borðuðu matinn')
ÖfugSögn_1/tala/pers/kyn →

    # (nf:)  Konurnar sem voru [ekki] leikarar
    # (þf:)  Konurnar sem borðuðu [ekki] matinn
    # (þgf:) Konurnar sem höfnuðu [ekki] Páli
    # (ef:)  Konurnar sem nutu [ekki] matarins

    # Konurnar sem verið höfðu leikarar [sjálfar/galvaskar]
    So_1_sagnb_nf HjSögn/tala/pers NlSagnfylling_nf LoTengtSögn_nf/tala/kyn?
    # Konurnar sem borðað höfðu matinn [sjálfar/galvaskar]
    | So_1_sagnb/aukafall HjSögn/tala/pers NlBeintAndlag/aukafall LoTengtSögn_nf/tala/kyn?

    # Konurnar sem leikarar höfðu [sjálfar/galvaskar] verið    
    | NlSagnfylling_nf HjSögn/tala/pers LoTengtSögn_nf/tala/kyn? FsAtv? So_1_sagnb_nf
    # Konurnar sem matinn höfðu [sjálfar/galvaskar] borðað
    | NlBeintAndlag/aukafall HjSögn/tala/pers LoTengtSögn_nf/tala/kyn? FsAtv? So_1_sagnb/aukafall

    # Konurnar sem vera munu leikarar [allar/galvaskar]
    | So_1_nh_nf HjSögnNh/tala/pers NlSagnfylling_nf LoTengtSögn_nf/tala/kyn?
    # Konurnar sem borða munu matinn [allar/galvaskar]
    | So_1_nh/aukafall HjSögnNh/tala/pers NlBeintAndlag/aukafall LoTengtSögn_nf/tala/kyn?

    # Konurnar sem leikarar munu [allar/galvaskar] vera
    | NlSagnfylling_nf HjSögnNh/tala/pers LoTengtSögn_nf/tala/kyn? FsAtv? So_1_nh_nf
    # Konurnar sem matinn munu [allar/galvaskar] borða
    | NlBeintAndlag/aukafall HjSögnNh/tala/pers LoTengtSögn_nf/tala/kyn? FsAtv? So_1_nh/aukafall

    # Konurnar sem matinn [snarlega] borðuðu [báðar/galvaskar]
    | NlSagnfylling_nf AtvFs? so_1_nf/tala/pers LoTengtSögn_nf/tala/kyn?
    # Konurnar sem matinn [snarlega] borðuðu [báðar/galvaskar]
    | NlBeintAndlag/aukafall AtvFs? so_1/aukafall/tala/pers LoTengtSögn_nf/tala/kyn?

    # Konan sem gefin hafði verið/skal/mun Páli
    | so_2_þgf_þf_lhþt/tala/kyn HjSögnLhÞtSM/tala/pers NlÓbeintAndlag_þgf
    # Konan sem Páli hafði verið/skal/mun gefin
    | NlÓbeintAndlag_þgf HjSögnLhÞtSM/tala/pers FsAtv? so_2_þgf_þf_lhþt/tala/kyn

    # Konan sem kölluð hafði verið/skal/mun illum nöfnum
    | so_2_þf_þgf_lhþt/tala/kyn HjSögnLhÞtSM/tala/pers NlÓbeintAndlag_þgf
    # Konan sem illum nöfnum hafði verið/skal/mun kölluð
    | Nl_þgf HjSögnLhÞtSM/tala/pers FsAtv? so_2_þf_þgf_lhþt/tala/kyn

    # Konan sem spurð hafði verið/skal/mun álits
    | so_2_þf_ef_lhþt/tala/kyn HjSögnLhÞtSM/tala/pers Nl_ef
    # Konan sem álits hafði verið/skal/mun spurð
    | Nl_ef HjSögnLhÞtSM/tala/pers FsAtv? so_2_þf_ef_lhþt/tala/kyn

    # Orðin sem Páll lét [ekki] falla
    | SoFrumlag_1_þf_nh FsAtv? NhRuna

    # so_2_þgf_ef, so_2_þgf_þgf og so_2_þf_þf virðast ekki geta staðið
    # í lhþt heldur aðeins sem sagnbót

# Sögn með tveimur viðföngum sem stendur í undirsetningu
# ('Konan sem gaf Páli gjöfina')
ÖfugSögn_2/tala/pers/kyn →

    so_2_þgf_þf_sagnb HjSögn/tala/pers  Andlög_þgf_þf                # Konurnar sem gefið hafa Páli gjöfina
    | 'gera:so'_1_þgf_sagnb HjSögn/tala/pers
        NlÓbeintAndlag_þgf LoTengtSögn_nf_et_hk NhFyllingAtv         # Konurnar sem gert hafa Páli kleift að matast með reisn
    | so_2_þgf_þf_nh HjSögnNh/tala/pers Andlög_þgf_þf                # Konurnar sem gefa munu Páli gjöfina

    | so_2_þf_ef_sagnb HjSögn/tala/pers Andlög_þf_ef                 # Konurnar sem spurt hafa Pál spurningarinnar
    | so_2_þf_ef_nh HjSögnNh/tala/pers Andlög_þf_ef                  # Konurnar sem spyrja munu Pál spurningarinnar

    | so_2_þgf_ef_sagnb HjSögn/tala/pers Andlög_þgf_ef               # Konurnar sem óskað hafa Páli góðs bata
    | so_2_þgf_ef_nh HjSögnNh/tala/pers Andlög_þgf_ef                # Konurnar sem óska munu Páli góðs bata

    | so_2_þgf_þgf_sagnb HjSögn/tala/pers Andlög_þgf_þgf             # Konurnar sem skilað hafa Páli bílnum
    | so_2_þgf_þgf_nh HjSögnNh/tala/pers Andlög_þgf_þgf              # Konurnar sem skila munu Páli bílnum

    | so_2_þgf_nf_sagnb HjSögn/tala/pers Andlög_þgf_nf               # Konurnar sem verið hafa Páli stoð og stytta
    | so_2_þgf_nf_nh HjSögnNh/tala/pers Andlög_þgf_nf                # Konurnar sem vera munu Páli stoð og stytta

    | so_2_þf_þf_sagnb HjSögn/tala/pers Andlög_þf_þf                 # Konurnar sem kostað hafa Pál skildinginn
    | so_2_þf_þf_nh HjSögnNh/tala/pers Andlög_þf_þf                  # Konurnar sem kosta munu Pál skildinginn

    | so_2_þf_þgf_sagnb HjSögn/tala/pers Andlög_þf_þgf               # Konurnar sem kallað hafa Pál illum nöfnum
    | so_2_þf_þgf_nh HjSögnNh/tala/pers Andlög_þf_þgf                # Konurnar sem kalla munu Pál illum nöfnum

# Sögn með inniföldu frumlagi en engu viðfangi ('lenti')
# ('Í dag lenti maðurinn')
SögnMeðF_0/tala/pers/kyn →

    # (Í dag) lenda og fara [burt í hendingskasti] mennirnir [galvaskir að þvo bílinn]
    so_0/tala/pers OgSögn_0/tala/pers* FsAtv? NlFrumlag_nf/tala/pers/kyn NhFylling/tala/kyn?

    # (Í dag) tókst og heppnaðist Seðlabankanum [galvöskum að lækka vexti]
    # !!! Ath: ekki _fh hér þar sem _vh er leyfilegt (takist Seðlabankanum...)
    # !!! Ath: Sögnin er óháð frumlaginu í tölu og persónu, hún er í reynd
    # !!! í 3p. et. (sbr. ?'Það tekst veiðimönnunum að veiða þann stóra')       # ERROR
    | so_mm_et_p3 OgSögn_mm_et_p3* NlFrumlag_þgf/tala/pers/kyn NhFylling_þgf/tala/kyn?

    # (Í dag) ber stjórnvöldum að taka hætturnar alvarlega
    # | so_subj_gm_op_p3_et_þgfnh NlFrumlag_þgf/tala/pers/kyn NhFylling/tala/kyn

    # (Í dag) líður mér [vel að hafa ekki farið þá leið]
    # (Í gær) dreymdi mig [um að fara alsæll í sólarlandaferð]
    # (Eftir matinn) svimaði mig
    | so_subj_op/aukafall NlFrumlag/aukafall/tala/pers/kyn NhFylling/tala/kyn?

    # (Í dag) hefur [ekki] gerst [við undirritunina] það sama
    # (Um árabil) hefur verið [í gildi] yfirlýsing
    | HjSögn/tala/pers FsAtv? so_sagnb FsRunaInnskot? NlFrumlag_nf/tala/pers/kyn NhFylling/tala/kyn?

    # (Í dag) mun [ekki] skapast [af þessu] veruleg hætta
    | HjSögnSkuluMunu/tala/pers FsAtv? so_0_mm_nh FsRunaInnskot? NlFrumlag_nf/tala/pers

    # (Í dag) hafa mennirnir [ekki getað] lent
    | HjSögn/tala/pers NlFrumlag_nf/tala/pers/kyn GetaAtv_sagnb? HreinSögnAtv_sagnb/tala/kyn

    # (Í dag) eiga mennirnir [ekki/eftir seinkun gærdagsins] að hafa/geta lent
    | HjSögnNhMeðSagnb/tala/pers NlFrumlag_nf/tala/pers/kyn Nhm HafaGetaFá_nh HreinSögn_sagnb/tala/kyn

    # (Í dag) munu mennirnir [ekki/innan tíðar] lenda
    | SögnMeðF_nh/tala/pers/kyn

    # (Í dag) hyggjast mennirnir [ekki/innan tíðar] lenda
    # (Í dag) segist Illugi ekki hafa fengið laun
    | so_mm/tala/pers NlFrumlag_nf/tala/pers/kyn SögnMeðF_mm/tala/kyn

    # (Í dag) eru mennirnir [ekki/í millitíðinni] lentir
    # (Í dag) skulu mennirnir látnir lausir
    | HjSögnLhÞtSM/tala/pers NlFrumlag_nf/tala/pers/kyn HreinSögn_lhþt/tala/kyn

    # (Í dag) væri [ennfremur] komin mikil framleiðsluspenna
    # Ath: Hjálparsögnin er í 3p þótt frumlagið sé það ekki, sbr.
    # '(Í dag) er ennfremur kominn þú' (ekki 'ert')
    | HjSögnLhÞtSM_p3/tala FsAtv? so_0_lhþt/tala/kyn NlFrumlag_nf/tala/kyn

    # (Í ár) hefur krönunum [í borginni] fjölgað
    | HjSögn_et_p3 NlFrumlag/fall/tala/pers/kyn AtvFs? so_subj_sagnb/fall NhFylling/tala/kyn?
    # (Í dag) er þingmönnunum [í flokknum] stefnt (til þings)
    | HjSögnSagnb NlFrumlag_þgf/tala/pers/kyn AtvFs? so_1_þgf_sagnb NhFylling/tala/kyn?
    # (Í kvöld) er viðurgjörningsins notið (í skíðaskálanum)
    | HjSögnSagnb NlFrumlag_ef/tala/pers/kyn AtvFs? so_1_ef_sagnb NhFylling/tala/kyn?
    # [Áður en þetta gerðist] er/var/yrði/skal mér sagt að fara út / að Jón væri farinn
    | HjSögnSagnb NlFrumlag_þgf/tala/pers/kyn AtvFs? so_2_þgf_þf_sagnb AðAndlag

    # (Í ár) mun krönunum [í borginni] fjölga
    | HjSögnNh_et_p3 NlFrumlag/fall/tala/pers/kyn AtvFs? so_subj_nh/fall NhFylling/tala/kyn?

    # (Í dag) hafa fjöllin verið klifin og sigruð / hefur þú verið sæmdur orðu
    | 'hafa:so'_0/tala/pers NlFrumlag_nf/tala/pers/kyn AtvFs?
        HjSögn_sagnb HreinSögn_lhþt/tala/kyn
    # (Í dag) hefur manninum verið sagt [að vera rólegur]
    # (Frá því í gær) hefur stjórnvöldum verið ljóst að greiða þyrfti atkvæði um málið
    | 'hafa:so'_0_et_p3 NlFrumlag_þgf/tala/pers/kyn AtvFs?
        HjSögn_sagnb FsAtv? SögnMeðFSagnbBotn
    # (Í dag) hefur konunnar getað verið gætt
    | 'hafa:so'_0_et_p3 NlFrumlag_ef/tala/pers/kyn AtvFs?
        HjSögn_sagnb FsAtv? so_1_ef_sagnb NhFylling/tala/kyn?

    # (Í dag) skal sú ákvörðun [vera] rökstudd og tilkynnt forseta Alþingis
    | HjSögnSkuluMunu/tala/pers NlFrumlag_nf/tala/pers/kyn VeraVerðaAtv? HreinSögn_lhþt/tala/kyn

    # (Í svarinu) segir [jafnframt] að Jón sé ómögulegur
    | 'segja:so'_0_gm_et_p3 FsAtv? AðvörunKomma? Skýringarsetning

SögnMeðF_nh/tala/pers/kyn →
    HjSögnNh/tala/pers NlFrumlag_nf/tala/pers/kyn Atviksliður? SögnMeðFNhBotn/tala/pers/kyn

$score(+4) SögnMeðF_nh/tala/pers/kyn

SögnMeðFSagnbBotn →
    # (Í dag hefur manninum verið) sagt [að vera rólegur]
    so_1_þgf_sagnb NhFylling/tala/kyn?
    # (Í dag hefur stjórnvöldum verið) [það] [algjörlega] ljóst [að málið færi í atkvæðagreiðslu]
    # (Frá því í gær hefur mér verið) alvarleiki málsins hugleikinn
    # !!! ATH: Nl_nf á í raun að vera frumlag hér, ef sá nafnliður er fyrir hendi á annað borð
    | Nl_nf/tala/kyn? LoAtviksliðir? lo_sb_nf_sþgf/tala/kyn Skýringarsetning?

SögnMeðFNhBotn/tala/pers/kyn →
    # (Í stað takka) vill Apple [nú] koma fyrir snertiskynjurum
    HreinSögn_nh/tala/kyn
    # (Samkvæmt fréttum) mun þingkonan [nú] hafa gefið í skyn / geta fengið gögnin / fá upplýst um málið
    | HafaGetaFá_nh HreinSögn_sagnb/tala/kyn

VeraVerðaAtv →
     FsAtv? HjSögnVeraVerða

SögnMeðF_mm/tala/kyn →
    HreinSögn_nh/tala/kyn                           # (Í dag hyggjast mennirnir) [ekki/innan tíðar] lenda
    | FsAtv? HjSögn_mm_nh HreinSögn_sagnb/tala/kyn  # (Í dag segist Illugi) ekki hafa fengið laun

OgSögn_1/fall/tala/pers →
    Aðaltenging FsAtv? so_1/fall/tala/pers
    | "," Aðaltenging FsAtv? so_1/fall/tala/pers FsAtvKomma?

# Sagnir sem geta staðið í samhenginu 'Páli/Köttum hefur [ekki] X [síðan í gær]'
SoBreyting_sagnb →
    'fækka:so'_sagnb                     # Köttum hefur fækkað
    | 'fjölga:so'_sagnb                  # Köttum hefur fjölgað
    | 'batna:so'_sagnb                   # Páli hefur batnað
    | 'versna:so'_sagnb                  # Páli hefur versnað
    | 'vaxa:so'_sagnb NlBeintAndlag_nf   # Páli hefur vaxið skegg
    | 'elna:so'_sagnb NlBeintAndlag_nf   # Páli hefur elnað sóttin
    | 'skána:so'_sagnb NlBeintAndlag_nf? # Páli hefur skánað [sóttin]
    | 'fara:so'_sagnb FsAtv? AfturFram   # Páli hefur farið [verulega] aftur/fram

# Sagnir sem geta staðið í samhenginu 'Páli/Köttum mun [ekki] X [síðan í gær]'
SoBreyting_nh →
    'fækka:so'_nh                     # Köttum mun fækka
    | 'fjölga:so'_nh                  # Köttum mun fjölga
    | 'batna:so'_nh                   # Páli mun batna
    | 'versna:so'_nh                  # Páli mun versna
    | 'vaxa:so'_nh NlBeintAndlag_nf   # Páli mun vaxa skegg
    | 'elna:so'_nh NlBeintAndlag_nf   # Páli mun elna sóttin
    | 'skána:so'_nh NlBeintAndlag_nf? # Páli mun skána [sóttin]
    | 'fara:so'_nh FsAtv? AfturFram   # Páli mun fara [verulega] aftur/fram

AfturFram → "aftur:ao" | "fram:ao"

# Sagnir sem geta staðið í lh. nt. í samhenginu 'Páli/Köttum hefur [ekki] farið X [síðan í gær]'
SoBreyting_lh_nt →
    'fækka:so'_0_lh_nt                   # Köttum hefur farið fækkandi
    | 'fjölga:so'_0_lh_nt                # Köttum hefur farið fjölgandi

# Stigagjöfin hér þarf að yfirvinna MiðmyndarBotn sem fær 14 stig
$score(+16) SoBreyting_sagnb
$score(+16) SoBreyting_nh
$score(+24) SoBreyting_lh_nt

# 'Páli hefur [ekki] vaxið skegg'
SögnSagnbBreyting →
    HjSögn_et_p3 FsAtv? SoBreyting_sagnb

# 'Hælisleitendum hefur farið [stöðugt] fækkandi'
SögnLhNtBreyting →
    HjSögnLhNt FsAtv? SoBreyting_lh_nt

# 'Köttum mun [ekki] fækka á árinu'
SögnNhBreyting →
    HjSögnNh_et_p3 FsAtv? SoBreyting_nh

# Umraðanir

So_1/aukafall/tala/pers → so_1/aukafall/tala/pers
$tag(pick_up_verb) So_1/aukafall/tala/pers

So_1_sagnb_nf → so_1_nf_sagnb   # (Í fyrra höfðu konurnar [ekki]) verið leikarar
So_1_sagnb_þf → so_1_þf_sagnb   # (Í dag hafa konurnar [ekki]) borðað matinn
So_1_sagnb_þgf → so_1_þgf_sagnb # (Í dag hafa konurnar [ekki]) hafnað Páli
So_1_sagnb_ef → so_1_ef_sagnb   # (Í dag hafa konurnar [ekki]) notið matarins

$tag(pick_up_verb) So_1_sagnb/fall

So_1_nh_nf → so_1_nf_nh         # (Í dag munu konurnar [ekki]) vera leikarar
So_1_nh_þf → so_1_þf_nh         # (Í dag munu konurnar [ekki]) borða matinn
So_1_nh_þgf → so_1_þgf_nh       # (Í dag munu konurnar [ekki]) hafna Páli
So_1_nh_ef → so_1_ef_nh         # (Í dag munu konurnar [ekki]) njóta matarins

$tag(pick_up_verb) So_1_nh/fall

So_1_bh_nf → so_1_nf_bh | so_1_nf_gm_fh_p1_ft_nt # Vertu leikari / Verum leikarar
So_1_bh_þf → so_1_þf_bh | so_1_þf_gm_fh_p1_ft_nt # Borðaðu matinn / borðum matinn
So_1_bh_þgf → so_1_þgf_bh | so_1_þgf_gm_fh_p1_ft_nt # Hafnaðu Páli / höfnum Páli
So_1_bh_ef → so_1_ef_bh | so_1_ef_gm_fh_p1_ft_nt # Njóttu matarins / njótum matarins

$tag(pick_up_verb) So_1_bh/fall

So_1_vh_nf/tala/pers → so_1_nf_vh/tala/pers    # Væri hann leikari
So_1_vh_þf/tala/pers → so_1_þf_vh/tala/pers    # Borðir þú matinn
So_1_vh_þgf/tala/pers → so_1_þgf_vh/tala/pers  # Hafni ég Páli
So_1_vh_ef/tala/pers → so_1_ef_vh/tala/pers    # Nyti hann matarins

$tag(pick_up_verb) So_1_vh/fall/tala/pers

So_1_p3_nf/tala → so_1_nf_p3/tala
So_1_p3_þf/tala → so_1_þf_p3/tala
So_1_p3_þgf/tala → so_1_þgf_p3/tala
So_1_p3_ef/tala → so_1_ef_p3/tala

$tag(pick_up_verb) So_1_p3/fall/tala

# Ópersónulegar sagnir sem standa með frumlagi í aukafalli og hafa jafnframt
# andlag, t.d. 'Mig dreymdi hest', þar sem 'dreymdi'
# hefur bæði frumlag í þolfalli og andlag í þolfalli;
# 'Mér blöskraði vitleysan'; 'Mig þraut örendið'
So_subj_op_nf/aukafall → so_1_nf_subj_op/aukafall
So_subj_op_þf/aukafall → so_1_þf_subj_op/aukafall
So_subj_op_þgf/aukafall → so_1_þgf_subj_op/aukafall
So_subj_op_ef/aukafall → so_1_ef_subj_op/aukafall

$score(+4) So_subj_op/fall/aukafall

# Sagnbót með einu viðfangi

Sögn_1_sagnb →
    # Jón getur verið [af náttúrunnar hendi] hálfgerður asni
    So_1_sagnb_nf FsRunaEftirSögn? NlSagnfylling_nf
    # Þingmenn geta/hafa lagt [fyrir Alþingi] frumvörp (til laga)
    | So_1_sagnb/aukafall FsRunaEftirSögn? NlBeintAndlag/aukafall
    | NlEnginnAndlag/fall So_1_sagnb/fall # (Þó getur hann) enga samninga gert

NlEnginnAndlag/fall →
    'enginn:fn'/fall/tala/kyn Nl/fall/tala/kyn

# Sögn í nafnhætti með einu viðfangi

Sögn_1_nh →
    So_1_nh/fall FsRunaInnskot? NlBeintAndlag/fall
    # sjá/láta/'horfa á' Pál breyta [í snarheitum] sér (í kött)
    | So_1_þf_nh Nl_þf So_1_nh/fall FsRunaInnskot? Nl/fall

# Gefa forsetningarliðum milli sagnar og andlags mínus
# enda eru þær klasturslegt mál ('Hann mun sjá í snatri fílinn')
FsRunaInnskot →
    FsRuna

$score(-8) FsRunaInnskot

# Sögn í boðhætti með einu viðfangi

Sögn_1_bh →
    # Vertu [í alvörunni] þú sjálfur
    So_1_bh_nf FsRunaEftirSögn? NlSagnfylling_nf
    # Bakaðu [í snarheitum] kökuna
    | So_1_bh/aukafall FsRunaEftirSögn? NlBeintAndlag/aukafall

# Sagnbót með tveimur viðföngum

Sögn_2_sagnb →
    so_2_þgf_þf_sagnb Andlög_þgf_þf     # Konurnar höfðu [ekki] gefið Páli gjöfina
    | so_2_þf_ef_sagnb Andlög_þf_ef     # Konurnar höfðu [ekki] krafið Pál svara
    | so_2_þf_þf_sagnb Andlög_þf_þf     # Konurnar höfðu [ekki] kostað Pál skildinginn
    | so_2_þf_þgf_sagnb Andlög_þf_þgf   # Konurnar höfðu [ekki] kallað Pál öllum illum nöfnum
    | so_2_þgf_þgf_sagnb Andlög_þgf_þgf # Konurnar höfðu [ekki] skilað Páli bílnum
    | so_2_þgf_ef_sagnb Andlög_þgf_ef   # Konurnar höfðu [ekki] óskað Páli alls hins besta
    | so_2_þgf_nf_sagnb Andlög_þgf_nf   # Kristín hafði [ekki] verið Páli stoð og stytta
    | 'gera:so'_1_þgf_sagnb Nl_þgf LoTengtSögn_nf_et_hk NhFyllingAtv # Konurnar höfðu [ekki] gert Páli kleift að klæða sig

# Það er gott að ná tveimur viðföngum með sögn í sagnbeygingu
$score(+6) Sögn_2_sagnb

# Sögn í nafnhætti með tveimur viðföngum

Sögn_2_nh →
    so_2_þgf_þf_nh Andlög_þgf_þf        # Konurnar munu [ekki] gefa Páli gjöfina
    | so_2_þf_ef_nh Andlög_þf_ef        # Konurnar munu [ekki] krefja Pál svara
    | so_2_þf_þf_nh Andlög_þf_þf        # Konurnar munu [ekki] kosta Pál skildinginn
    | so_2_þf_þgf_nh Andlög_þf_þgf      # Konurnar munu [ekki] kalla Pál öllum illum nöfnum
    | so_2_þgf_þgf_nh Andlög_þgf_þgf    # Konurnar munu [ekki] skila Páli bílnum
    | so_2_þgf_ef_nh Andlög_þgf_ef      # Konurnar munu [ekki] óska Páli alls hins besta
    | so_2_þgf_nf_nh Andlög_þgf_nf      # Kristín mun [ekki] vera Páli stoð og stytta
    | 'gera:so'_1_þgf_nh Nl_þgf LoTengtSögn_nf_et_hk NhFyllingAtv # Konurnar munu [ekki] gera Páli kleift/mögulegt að klæða sig

Andlög/fall/afall →
    NlÓbeintAndlag/fall AndlögBotn/fall/afall

Andlög/fall/afall/tala/kyn →
    NlÓbeintAndlag/fall LoTengtSögn_nf/tala/kyn? AndlögBotn/fall/afall

AndlögBotn/fall/afall →
    # (gefa Páli) [stóra] gjöf
    NlBeintAndlag/afall
    # (gefa Páli) [talsvert] stærri gjöf/flottari bíl en mér/Sigríði
    | LoAtviksliður* lo_mst/afall/tala/kyn NlBeintAndlag/afall/tala/pers/kyn SamanburðarNafnliður

So_2_nh_þf_nf → so_2_þf_nf_nh
So_2_nh_þf_þf → so_2_þf_þf_nh
So_2_nh_þf_þgf → so_2_þf_þgf_nh
So_2_nh_þf_ef → so_2_þf_ef_nh

So_2_nh_þgf_nf → so_2_þgf_nf_nh
So_2_nh_þgf_þf → so_2_þgf_þf_nh
So_2_nh_þgf_þgf → so_2_þgf_þgf_nh
So_2_nh_þgf_ef → so_2_þgf_ef_nh

# Sögn í boðhætti með tveimur viðföngum

Sögn_2_bh →
    so_2_þgf_þf_bh Andlög_þgf_þf        # Gefðu Páli gjöfina / stærri gjöf en mér
    | so_2_þf_ef_bh Andlög_þf_ef        # Krefðu Pál svars / ítarlega svars en mig
    | so_2_þf_þf_bh Andlög_þf_þf        # Kostaðu Pál skildinginn / fleiri skildinga en mig
    | so_2_þf_þgf_bh Andlög_þf_þgf      # Kallaðu Pál öllum illum nöfnum / verri nöfnum en mig
    | so_2_þgf_þgf_bh Andlög_þgf_þgf    # Skilaðu Páli bílnum / betri bíl en mér
    | so_2_þgf_ef_bh Andlög_þgf_ef      # Óskaðu Páli alls hins besta / betri útkomu en mér
    | so_2_þgf_nf_bh Andlög_þgf_nf      # Vertu Páli stoð og stytta / tryggari vinur en mér
    | 'gera:so'_1_þgf_bh Nl_þgf LoTengtSögn_nf_et_hk NhFyllingAtv # Gerðu Páli kleift/mögulegt að klæða sig í fötin

# Sögn með einu viðfangi ('borðaði matinn')
Sögn_1/tala/pers/kyn →

    # (þf:)  Konurnar borðuðu og meltu [ekki] matinn
    # (þgf:) Konurnar afneituðu, og beinlínis höfnuðu, [ekki] Páli
    # (ef:)  Konurnar nutu [ekki] matarins
    # (nf:)  Konurnar voru [ekki] leikarar

    # (Páll) klappaði og strauk kettinum
    # (Kjósandi) velur [í lok maí] þá frambjóðendur sem hann kýs
    # (Við) einsetjum okkur að vinna með öðrum þjóðum
    # (Sveitarfélögin) ráða sjálf [í meginatriðum] málefnum sínum
    # (Hún) sagði lafmóð/sjálf söguna
    so_1_nf/tala/pers OgSögn_1_nf/tala/pers*
        FsRunaEftirSögn? LoTengtSögn_nf/tala/kyn? NlSagnfylling_nf NhFylling?
    | so_1/aukafall/tala/pers OgSögn_1/aukafall/tala/pers*
        FsRunaEftirSögn? LoTengtSögn_nf/tala/kyn? NlBeintAndlag/aukafall NhFylling?

    # (Ég tel/segi/álít) Pál vera dagfarsprúðan mann
    | So_1_þf_nh/tala/pers OgSögn_1_þf_nh/tala/pers* SagnarBotn

    # (Bankar) hafa reynst og gagnast mér (vel)
    | HjSögn/tala/pers FsAtv? so_mm_sagnb OgSögn_mm_sagnb* NlBeintAndlag_þgf

    # Konan hefur verið/skal/mun gefin Páli
    | HjSögnLhÞtSM/tala/pers FsAtv? so_2_þgf_þf_lhþt/tala/kyn Nl_þgf
    # Konan hefur verið/skal/mun spurð álits
    | HjSögnLhÞtSM/tala/pers FsAtv? so_2_þf_ef_lhþt/tala/kyn NlBeintAndlag_ef
    # Konan hefur verið/skal/mun kölluð öllum illum nöfnum
    | HjSögnLhÞtSM/tala/pers FsAtv? so_2_þf_þgf_lhþt/tala/kyn Nl_þgf
    # Páll fékk [í snatri] [galvaskur] bílinn [að fullu / strax] endurgreiddan
    | HjSögnFá/tala/pers FsRunaEftirSögn? LoTengtSögn_nf/tala/kyn? FáAndlag_þf

FáAndlag_þf →
    # Páll fékk bílinn strax endurgreiddan
    NlBeintAndlag_þf/tala/pers/kyn AtvFs? so_lhþt_þf/tala/kyn

# Býli í landinu eru/geta verið/verða/munu vera 3.000/tuttugu [talsins]
Sögn_1_ft/pers/kyn →
    HjSögnLhÞt_ft/pers FsAtv? Töluorð_ft_nf/kyn

# Sagnir sem geta staðið með þf + nh
# '(Ég) álít (Pál vera dagfarsprúðan mann)'
# '(Guðrún) lét (soninn hafa bílinn)'
# '(Við) sáum (strákinn vaxa úr grasi)'
# '(Jón) kvað (stíginn vera öruggan)'
# '(Myndbandið) sýndi (björgunarmenn bera hann inn í sjúkrabíl)'
So_1_þf_nh/tala/pers →
    'segja:so'_1_þf_gm/tala/pers
    | 'telja:so'_1_þf_gm/tala/pers
    | 'láta:so'_1_þf_gm/tala/pers
    | 'álíta:so'_1_þf_gm/tala/pers
    | 'sjá:so'_1_þf_gm/tala/pers
    | 'kveða:so'_1_þf_gm/tala/pers
    | 'sýna:so'_1_þf_gm/tala/pers
    | SoHorfaÁ/tala/pers

SoHorfaÁ/tala/pers →
    # 'Við horfðum á strákinn vaxa úr grasi'
    'horfa:so'_0_gm/tala/pers 'á:fs'_þf

$score(+16) SoHorfaÁ/tala/pers
$tag(purge_verb) SoHorfaÁ/tala/pers

So_1_þf_nh →
    'segja:so'_1_þf_gm_nh
    | 'telja:so'_1_þf_gm_nh
    | 'láta:so'_1_þf_gm_nh
    | 'álíta:so'_1_þf_gm_nh
    | 'sjá:so'_1_þf_gm_nh
    | 'kveða:so'_1_þf_gm_nh
    | 'sýna:so'_1_þf_gm_nh
    | SoHorfaÁ

SoHorfaÁ →
    # 'Við munum horfa á strákinn vaxa úr grasi'
    'horfa:so'_0_gm_nh 'á:fs'_þf

$score(+16) SoHorfaÁ
$tag(purge_verb) SoHorfaÁ

# segist munu/vilja borða kökuna
# telst munu/vilja kjósa flokkinn
# læst munu/vilja virða meirihlutann
# kveðst munu/vilja virða ákvörðun ráðherrans
# sýnist munu/vilja slá metið
So_1_þf_mm →
    'segja:so'_mm
    | 'telja:so'_mm
    | 'láta:so'_mm
    | 'kveða:so'_mm
    | 'sýna:so'_mm

# látið/talið/sagt Pál borða matinn
So_1_þf_nh_sagnb →
    'segja:so'_1_þf_sagnb
    | 'telja:so'_1_þf_sagnb
    | 'láta:so'_1_þf_sagnb
    | 'álíta:so'_1_þf_sagnb
    | 'sjá:so'_1_þf_sagnb
    | 'kveða:so'_1_þf_sagnb
    | 'sýna:so'_1_þf_sagnb

$score(+2) So_1_þf_nh_sagnb

SagnarBotn →
    # (Páll segir) mistökin strax valda vanda
    Nl_þf AtvFs? NhRuna
    # (Þingmaðurinn telur) niðurstöðuna vera ranga / (Verkfræðingurinn segir) útkomuna verða tvísýna
    | Nl_þf/tala/kyn AtvFs? HjSögnVeraVerða LoLiður_nf/tala/kyn

    # (Páll segir) mistökin [ekki] hafa verið gerð (á vakt Jóns)
    # (Páll segir) mistökin [ekki] hafa verið (í uppleggi Friðgeirs)
    # Hér er notað HjSögnEkkiVera í stað HjSögnVera til að forðast margræðni
    # gagnvart NhRunu
    | Nl_þf/tala/kyn AtvFs? HjSögnEkkiVera so_lhþt/tala/kyn?

    # (Hann segir) hvern farþega [strax] hafa fengið tíu evrur / hafa farið að grilla
    | Nl_þf/tala/kyn AtvFs? 'hafa:so'_0_nh HreinSögn_sagnb/tala/kyn # !!! Hér vantar að senda þolfall: 'galvaskan að grilla'

    # (Páll segir) Ásu [ekki] vera/hafa verið góðan stjórnanda
    | Nl_þf/tala AtvFs? HjSögnEkkiVera Nl_þf/tala

    # (Páll segir) skorta [á] (að farið sé að lögum)
    | so_subj_nh_none NhFylling?
    # (Páll segir) tefjast (að hefja framkvæmdir)
    | so_mm_nh NhFylling?
    # (Már segir) misskilnings gæta (hjá Hannesi)
    | Nl_ef AtvFs? so_subj_nh_ef
    # (Anna telur) fólki fækka í landinu
    | Nl_þgf AtvFs? so_subj_nh_þgf
    # (Már segir) [í grundvallaratriðum] gæta misskilnings (hjá Hannesi)
    | FsAtv? so_subj_nh_ef Nl_ef
    # !!! Ekki virðist þörf fyrir öfuga orðaröð í þgf eða þf

$score(+6) SagnarBotn

SoFrumlag_1_þf_nh →
    NlFrumlag_nf/tala/pers So_1_þf_nh/tala/pers  # (Orðin sem) Páll lét (falla) / (Orðin sem) þú lést (falla)

SögnMeðF_1/tala/pers/kyn →

    # (nf:)  (Í fyrra) voru konurnar [ekki] leikarar [sjálfar/galvaskar]
    # (þf:)  (Í dag) borðuðu konurnar [ekki] matinn [sjálfar/galvaskar]
    # (þgf:) (Í dag) afneituðu og höfnuðu konurnar [ekki] Páli [báðar/galvaskar]
    # (ef:)  (Í gærkvöldi) óskuðu konurnar [ekki] leyfis [allar/galvaskar] [að höfða mál]

    so_1_nf/tala/pers OgSögn_1_nf/tala/pers*
        NlFrumlag_nf/tala/pers/kyn NlSagnfylling_nf
        LoTengtSögn_nf/tala/kyn? NhFylling?
    | so_1/aukafall/tala/pers OgSögn_1/aukafall/tala/pers*
        NlFrumlag_nf/tala/pers/kyn NlBeintAndlag/aukafall
        LoTengtSögn_nf/tala/kyn? NhFylling?

    # (Eftir hálftíma) skal gjöfin gefin Páli
    | HjSögnLhÞtSM/tala/pers NlFrumlag_nf/tala/pers/kyn AtvFs? SagnarBotnLhÞt/tala/kyn

    # (Þegar gögnin hafa verið lesin) eru eimaðar upp úr þeim upplýsingar [á borð við nöfn]
    | HjSögnLhÞt/tala/pers FsAtv? so_lhþt_nf/tala/kyn FsAtv? NlFrumlag_nf/tala/pers/kyn

    # '(og) var Ástþór mjög ósáttur (við útvarpsstjóra)'
    | HjSögnLoMeðF/tala/pers/kyn LoTengtSögn_nf/tala/kyn

    # (Í dag) segir hann (köttinn vera svangan)
    | So_1_þf_nh/tala/pers OgSögn_1_þf_nh/tala/pers* NlFrumlag_nf/tala/pers/kyn SagnarBotn

    # (Í dag) barst lögreglunni ábending
    | so_mm_op/tala NlÓbeintAndlag_þgf NlFrumlag_nf/tala/pers/kyn

    # (Í dag) finnst þeim vera slagsíða (í umfjölluninni) / þykir okkur ósanngjarnt (að haldið sé áfram)
    | HjSögnFinnst NlFrumlag_þgf/tala/pers/kyn SögnFinnstBotn

    # (Áður) hafði KR látið Jón og Gunnar fara / sagt niðurstöðuna vera ranga
    | HjSögn/tala/pers NlFrumlag_nf/tala/pers/kyn So_1_þf_nh_sagnb SagnarBotn

    # (Í dag) vantar mig vakran hest
    | So_subj_op/fall/aukafall NlFrumlag/aukafall/tala/pers/kyn NlBeintAndlag/fall

SögnMeðF_1/tala/pers/kyn →
    # (Í dag) eru og verða þetta tvítugir strákar [galvaskir] [að hella sig fulla]
    VeraVerða_gm_p3/tala OgVeraVerða_gm_p3/tala* 'þessi:fn'_nf_hk_et
        NlSagnfylling_nf/tala/pers/kyn LoTengtSögn_nf/tala/kyn? NhFylling?

SagnarBotnLhÞt/tala/kyn →
    # (Í dag) verður konan gefin Páli [sjálf]
    so_2_þgf_þf_lhþt/tala/kyn Nl_þgf LoTengtSögn_nf/tala/kyn?
    # (Í dag) er/skal/mun þingmaðurinn kallaður illum nöfnum [saklaus]
    | so_2_þf_þgf_lhþt/tala/kyn Nl_þgf LoTengtSögn_nf/tala/kyn?
    # (Í dag) mun konan spurð álits [galvösk]
    | so_2_þf_ef_lhþt/tala/kyn Nl_ef LoTengtSögn_nf/tala/kyn?

# Sögn með tveimur viðföngum ('gaf Páli gjöfina')
Sögn_2/tala/pers/kyn →

    # Þágufall + þolfall ('að gefa einhverjum kött')
    so_2_þgf_þf/tala/pers Andlög_þgf_þf/tala/kyn     # Konurnar gáfu Páli [sjálfar] gjöfina / dýrari gjöf en mér

    # Þolfall + eignarfall ('að spyrja einhvern einhvers')
    | so_2_þf_ef/tala/pers Andlög_þf_ef/tala/kyn     # Konurnar kröfðu Pál [herskáar] svars / ítarlegri svara en mig

    # Þágufall + eignarfall ('að óska einhverjum góðs bata')
    | so_2_þgf_ef/tala/pers Andlög_þgf_ef/tala/kyn   # Konurnar óskuðu Páli [allar] góðs bata / skjótari bata en mér

    # Þágufall + þágufall ('að skila einhverjum einhverju')
    | so_2_þgf_þgf/tala/pers Andlög_þgf_þgf/tala/kyn # Konurnar skiluðu Páli [sjálfar] bílnum / betri bíl en mér

    # Þágufall + nefnifall ('að vera einhverjum eitthvað')
    | so_2_þgf_nf/tala/pers Andlög_þgf_nf/tala/kyn   # Konurnar voru Páli [allar] stoð og stytta / traustari vinir en mér

    # Þolfall + þolfall ('að kosta einhvern eitthvað')
    | so_2_þf_þf/tala/pers Andlög_þf_þf/tala/kyn     # Konurnar kostuðu Pál [allar] skildinginn / minna en mig

    # Þolfall + þágufall ('að kalla einhvern illum nöfnum')
    | so_2_þf_þgf/tala/pers Andlög_þf_þgf/tala/kyn   # Konurnar kölluðu Pál [æstar] illum nöfnum / verri nöfnum en mig

    # Nefnifall + nefnifall ('að þykja eitthvað algjör viðbjóður')
    | so_2_nf_nf/tala/pers Andlög_nf_nf/tala/kyn    # Konunum þótti Páll algjör viðbjóður

    # gera + lo ('að gera einhverjum kleift að fara út')
    | 'gera:so'_1_þgf/tala/pers NlÓbeintAndlag_þgf LoTengtSögn_nf_et_hk NhFyllingAtv # Konurnar gerðu Páli kleift að matast með reisn

SögnMeðF_2/tala/pers/kyn →

    # Þolfall + þolfall ('að kosta einhvern eitthvað')
    so_2_þf_þf/tala/pers NlFrumlag_nf/tala/pers/kyn Andlög_þf_þf        # (Í gær) kostuðu konurnar Pál [ekki] skildinginn

    # Þolfall + þágufall ('að kalla einhvern illum nöfnum')
    | so_2_þf_þgf/tala/pers NlFrumlag_nf/tala/pers/kyn Andlög_þf_þgf    # (Í gær) kölluðu konurnar Pál [ekki] illum nöfnum

    # Þolfall + eignarfall ('að spyrja einhvern einhvers')
    | so_2_þf_ef/tala/pers NlFrumlag_nf/tala/pers/kyn Andlög_þf_ef      # (Í gær) kröfðu konurnar Pál [ekki] svars

    # Þágufall + nefnifall ('að vera einhverjum eitthvað')
    | so_2_þgf_nf/tala/pers NlFrumlag_nf/tala/pers/kyn Andlög_þgf_nf    # (Í gær) voru konurnar Páli [ekki] stoð og stytta

    # Þágufall + þolfall ('að gefa einhverjum kött')
    | so_2_þgf_þf/tala/pers NlFrumlag_nf/tala/pers/kyn Andlög_þgf_þf      # (Í dag) gáfu konurnar [bersýnilega] Páli [ekki] gjöfina

    # Þágufall + þágufall ('að skila einhverjum einhverju')
    | so_2_þgf_þgf/tala/pers NlFrumlag_nf/tala/pers/kyn Andlög_þgf_þgf  # (Í gær) skiluðu konurnar Páli [ekki] bílnum

    # Þágufall + eignarfall ('að óska einhverjum einhvers')
    | so_2_þgf_ef/tala/pers NlFrumlag_nf/tala/pers/kyn Andlög_þgf_ef    # (Í gær) óskuðu konurnar Páli [ekki] góðs bata

    # 'gera' + lo ('að gera einhverjum eitthvað kleift')
     # (Í dag) gerðu konurnar Páli kleift að matast með reisn
    | 'gera:so'_1_þgf/tala/pers NlFrumlag_nf/tala/pers/kyn NlÓbeintAndlag_þgf LoTengtSögn_nf_et_hk NhFyllingAtv

    # 'eiga' + abfn
    # (Í dag) átti sér stað atburður
    | 'eiga:so'_2_þgf_þf/tala/pers Andlög_þgf_þf NlFrumlag_nf/tala/pers/kyn

    # Sagnbót og nafnháttur    
    | HjSögn/tala/pers NlFrumlag_nf/tala/pers/kyn AtvFs? Sögn_2_sagnb  # (Í morgun) höfðu konurnar [ekki] krafið Pál svara
    | HjSögnNh/tala/pers NlFrumlag_nf/tala/pers/kyn AtvFs? Sögn_2_nh   # (Á morgun) munu konurnar [ekki] krefja Pál svara

# Hjálparsagnasambönd: vera, hafa, geta, skulu, munu...

# Aðalsögnin er sagnbót (farið/gert/viljað/borðað...)
HjSögn/tala/pers →
    'hafa:so'/tala/pers Geta_sagnb?                 # hafðir [í fátinu ekki getað] farið
    | 'geta:so'/tala/pers                           # gast farið
    | 'fá:so'/tala/pers                             # (allir) fái notið (þeirra réttinda)
    | 'geta:so'/tala/pers FsAtv? 'hafa:so'_nh       # gast [ekki] hafa farið
    | 'vilja:so'/tala/pers FsAtv? HafaGetaFá_nh     # vildir [ekki] geta/hafa farið
    | 'mega:so'/tala/pers FsAtv? HafaGetaFá_nh      # máttir [ekki] geta/hafa farið
    | 'þykja:so'/tala/pers FsAtv? HafaGetaFá_nh     # þóttir [ekki] geta/hafa farið
    | 'skulu:so'/tala/pers FsAtv? HafaGetaFá_nh     # skulir [ekki] geta/hafa farið
    | 'munu:so'/tala/pers FsAtv? HafaGetaFá_nh      # munir [ekki] geta/hafa farið
    | 'eiga:so'/tala/pers FsAtv? Nhm HafaGetaFá_nh  # átt [ekki] að geta/hafa farið
    | so_mm/tala/pers FsAtv? HjSögn_mm_nh           # segist [ekki] hafa farið

# Hjálparsagnir koma ekki til álita varðandi tengingu forsetningar við sögn
$tag(purge_verb) HjSögn/tala/pers

HafaGetaFá_nh →
    'hafa:so'_nh | 'geta:so'_nh | 'fá:so'_nh

Geta_sagnb →
    FsAtv? 'geta:so'_sagnb

GetaAtv_sagnb →
    AtvFs? 'geta:so'_sagnb

$score(+3) HafaGetaFá_nh        # Gefa plús fyrir flókin sagnform
$tag(purge_verb) HafaGetaFá_nh  # Ekki horfa á hjálparsögnina þegar forsetningar eru tengdar

HjSögn_sagnb →
    'vera:so'_sagnb                     # hefur stjórnvöldum verið ljóst
    | 'verða:so'_sagnb                  # hefur stjórnvöldum orðið ljóst
    | 'mega:so'_sagnb 'vera:so'_nh      # hefur stjórnvöldum mátt vera ljóst
    | 'mega:so'_sagnb 'verða:so'_nh     # hefur stjórnvöldum mátt verða ljóst
    | 'þykja:so'_sagnb 'vera:so'_nh     # hefur stjórnvöldum þótt vera ljóst
    | 'þykja:so'_sagnb 'verða:so'_nh    # hefur stjórnvöldum þótt verða ljóst
    | 'eiga:so'_sagnb Nhm 'vera:so'_nh  # hefur stjórnvöldum átt að vera ljóst
    | 'eiga:so'_sagnb Nhm 'verða:so'_nh # hefur stjórnvöldum átt að verða ljóst
    | 'eiga:so'_sagnb Nhm 'hafa:so'_nh 'vera:so'_sagnb  # hefur stjórnvöldum átt að hafa verið ljóst
    | 'eiga:so'_sagnb Nhm 'hafa:so'_nh 'verða:so'_sagnb # hefur stjórnvöldum átt að hafa orðið ljóst
    | 'geta:so'_sagnb 'vera:so'_sagnb   # hefur stjórnvöldum getað verið ljóst
    | 'geta:so'_sagnb 'verða:so'_sagnb  # hefur stjórnvöldum getað orðið ljóst
    | 'geta:so'_sagnb 'hafa:so'_nh 'vera:so'_sagnb  # hefur stjórnvöldum getað hafa verið ljóst
    | 'geta:so'_sagnb 'hafa:so'_nh 'verða:so'_sagnb # hefur stjórnvöldum getað hafa orðið ljóst

$tag(purge_verb) HjSögn_sagnb

HjSögn_mm_nh →
    'hafa:so'_nh                    # telst hafa farið
    | 'geta:so'_nh 'hafa:so'_nh?    # sýnist geta hafa farið
    | 'vilja:so'_nh 'hafa:so'_nh    # segist vilja hafa farið
    | 'mega:so'_nh 'hafa:so'_nh     # virðist mega hafa farið
    | 'munu:so'_nh 'hafa:so'_nh     # segist munu hafa farið
    | 'skulu:so'_nh 'hafa:so'_nh    # segjast skulu hafa farið(?)
    | 'fá:so'_nh                    # þykist fá notið

$tag(purge_verb) HjSögn_mm_nh

HjSögnVh →
    'hafa:so'_et_p3_vh                          # hafi þurft
    | 'geta:so'_et_p3_vh                        # gæti þurft
    | 'geta:so'_et_p3_vh FsAtv? 'hafa:so'_nh    # gæti [ekki] hafa þurft
    | 'mega:so'_et_p3_vh FsAtv? 'hafa:so'_nh    # mætti [ekki] hafa þurft
    | 'þykja:so'_et_p3_vh FsAtv? 'hafa:so'_nh   # þætti [ekki] hafa þurft
    | 'skulu:so'_et_p3_vh FsAtv? 'hafa:so'_nh   # skyldi [ekki] hafa þurft
    | 'munu:so'_et_p3_vh FsAtv? 'hafa:so'_nh    # myndi [ekki] hafa þurft

$tag(purge_verb) HjSögnVh

HjSögnHafaNh/tala/pers →
    'hafa:so'_gm/tala/pers 'hafa:so'_sagnb?     # hef/hefur/hafði [haft] sögu að segja
    | 'geta:so'_gm/tala/pers FsAtv? 'hafa:so'_nh? 'hafa:so'_sagnb # getur/gæti [hafa] haft sögu að segja
    | 'vilja:so'_gm/tala/pers FsAtv? 'hafa:so'_nh? 'hafa:so'_sagnb # vildu [hafa] haft sögu að segja
    | 'mega:so'_gm/tala/pers FsAtv? 'hafa:so'_nh 'hafa:so'_sagnb? # mætti [ekki] hafa sögu að segja
    | 'þykja:so'_gm/tala/pers FsAtv? 'hafa:so'_nh 'hafa:so'_sagnb? # þætti [ekki] hafa sögu að segja
    | 'skulu:so'_gm/tala/pers FsAtv? 'hafa:so'_nh 'hafa:so'_sagnb? # skyldi [ekki] hafa sögu að segja
    | 'munu:so'_gm/tala/pers FsAtv? 'hafa:so'_nh 'hafa:so'_sagnb? # myndi [ekki] hafa sögu að segja

HjSögnVeraNh →
    'vera:so'_0_gm_et_p3 'vera:so'_sagnb?       # er/var/væri [verið] að ræða
    | 'hafa:so'_0_gm_vh_et_p3 'vera:so'_sagnb   # hafi/hefði verið að ræða
    | 'geta:so'_0_gm_et_p3 FsAtv? 'hafa:so'_nh? 'vera:so'_sagnb # getur/gat/gæti [hafa] verið að ræða
    | 'skulu:so'_0_gm_et_p3 FsAtv? 'vera:so'_nh # skuli/skyldi [ekki] vera að ræða
    | 'skulu:so'_0_gm_et_p3 FsAtv? 'hafa:so'_nh 'vera:so'_sagnb # skuli/skyldi [ekki] hafa verið að ræða
    | 'munu:so'_0_gm_et_p3 FsAtv? 'vera:so'_nh  # mun/myndi [ekki] vera að ræða
    | 'munu:so'_0_gm_et_p3 FsAtv? 'hafa:so'_nh 'vera:so'_sagnb # mun/myndi [ekki] hafa verið að ræða

# Sögnin stendur í nafnhætti með annarri nafnháttarsögn,
# sbr. 'Gæta verða/skulu/eiga/vilja þeir þingskapa'
HjSögnNhMeðSo/tala/pers →
    'verða:so'/tala/pers       # gæta verða þeir / verður þú
    | 'skulu:so'/tala/pers     # gæta skulu þeir / skal ég
    | 'munu:so'/tala/pers      # gæta munu þeir / mun hann
    | 'mega:so'/tala/pers      # gæta mega þeir
    | 'eiga:so'/tala/pers      # gæta eiga þeir
    | 'þurfa:so'/tala/pers     # gæta þurfa þeir / þurfið þið
    | 'vilja:so'/tala/pers     # gæta vilja þeir / viljið þið

$tag(purge_verb) HjSögnNhMeðSo/tala/pers

HjSögnNhMeðSagnb/tala/pers →
    'verða:so'/tala/pers       # verða mennirnir að hafa/geta... lent
    | 'eiga:so'/tala/pers      # eiga mennirnir að hafa/geta... lent
    | 'þurfa:so'/tala/pers     # þurfa mennirnir að hafa/geta... lent

$tag(purge_verb) HjSögnNhMeðSagnb/tala/pers

# Sögnin stendur í nafnhætti
HjSögnNh/tala/pers →
    'vilja:so'/tala/pers       # vilja fara
    | 'láta:so'/tala/pers      # læt/læst fara
    | 'vilja:so'/tala/pers FsAtv? 'láta:so'_nh # vill láta fara
    | 'vilja:so'/tala/pers FsAtv? 'hafa:so'_nh 'láta:so'_sagnb # vildir [gjarnan] hafa látið fara
    | 'skulu:so'/tala/pers     # skulu fara
    | 'munu:so'/tala/pers      # munu fara
    | 'mega:so'/tala/pers      # mega fara
    | 'þykja:so'/tala/pers     # þykja/þykjast setja (leiðinlegan svip á bæinn)
    | 'sjá:so'/tala/pers       # sjá glitta [í eitthvað]
    | 'hyggja:so'_mm/tala/pers # hyggjast fara
    | 'segja:so'_mm/tala/pers  # segjast fara
    | 'mega:so'/tala/pers FsAtv? 'láta:so'_nh     # mætti láta fara
    | 'skulu:so'/tala/pers FsAtv? 'þykja:so'_nh   # (flegni kjóllinn) skuli [ekki] þykja vera (í lagi)
    | 'skulu:so'/tala/pers FsAtv? 'mega:so'_nh    # skyldu [ekki] mega fara
    | 'skulu:so'/tala/pers FsAtv? 'vilja:so'_nh   # skyldu [ekki] vilja fara
    | 'skulu:so'/tala/pers FsAtv? 'láta:so'_nh    # skuli [ekki] láta fara
    | 'munu:so'/tala/pers FsAtv? 'þykja:so'_nh    # (flegni kjóllinn) myndi [ekki] þykja vera (siðsamlegur)
    | 'munu:so'/tala/pers FsAtv? 'mega:so'_nh     # myndu [ekki] mega fara
    #| 'munu'/tala/pers FsAtv? 'hafa'_nh? 'vilja'_nh 'láta'_nh? # myndu [ekki] vilja [láta] fara   - GÖLLUÐ REGLA, leyfir líka "munu hafa vilja láta fara" TODO laga
    | 'munu:so'/tala/pers FsAtv? 'láta:so'_nh     # myndu [ekki] láta fara
    | 'hafa:so'/tala/pers FsAtv? 'þykja:so'_sagnb # hafa [ekki] þótt fara
    | 'hafa:so'/tala/pers FsAtv? 'mega:so'_sagnb  # hafa [ekki] mátt fara
    | 'hafa:so'/tala/pers FsAtv? 'vilja:so'_sagnb 'láta:so'_nh? # hafa [ekki] viljað [láta] fara
    | 'hafa:so'/tala/pers FsAtv? 'láta:so'_sagnb  # hafa [ekki] látið fara
    | 'geta:so'/tala/pers FsAtv? 'láta:so'_sagnb  # getur [ekki] látið fara
    | 'geta:so'/tala/pers FsAtv? 'þykja:so'_sagnb # getur [ekki] þótt fara
    | 'geta:so'/tala/pers FsAtv? 'mega:so'_sagnb  # getur [ekki] mátt fara
    | 'geta:so'/tala/pers FsAtv? 'hafa:so'_nh? 'vilja:so'_sagnb 'láta:so'_nh? # getur [ekki] viljað fara / gæti viljað láta fara

HjSögnNh_p3_et → HjSögnNh_et_p3
HjSögnNh_p3_ft → HjSögnNh_ft_p3

$tag(purge_verb) HjSögnNh/tala/pers

HjSögnÞurfaEiga_et_p3 →
    'þurfa:so'_et_p3                    # Greinarmun þarf/þyrfti að gera
    | 'eiga:so'_et_p3                   # Greinarmun á/ætti að gera
    | 'verða:so'_et_p3                  # Greinarmun verður/varð/yrði að gera
    | 'hafa:so'_et_p3 'þurfa:so'_sagnb  # Engan hefur þurft að undra
    | 'hafa:so'_et_p3 'eiga:so'_sagnb   # Engan hefði átt að undra
    | 'hafa:so'_et_p3 'verða:so'_sagnb  # Greinarmun hefði orðið að gera
    | 'munu:so'_et_p3 'þurfa:so'_nh     # Engan mun þurfa að undra
    | 'skulu:so'_et_p3 'þurfa:so'_nh    # Engan skal þurfa að undra
    | 'munu:so'_et_p3 'eiga:so'_nh      # Greinarmun mun eiga að gera
    | 'skulu:so'_et_p3 'eiga:so'_nh     # Greinarmun skal eiga að gera
    | 'munu:so'_et_p3 'verða:so'_nh     # Greinarmun mun verða að gera
    | 'skulu:so'_et_p3 'verða:so'_nh    # Greinarmun skal verða að gera

HjSögnMegiSkuli_et_p3 →
    'mega:so'_et_p3                     # (Þann lærdóm) megi/mætti/má (draga)
    | 'skulu:so'_et_p3                  # (Þann lærdóm) skuli/skyldi/skal (draga)
    | 'hafa:so'_et_p3 'mega:so'_sagnb   # (Þann lærdóm) hefur/hefði mátt (draga)

$tag(purge_verb) HjSögnMegiSkuli_et_p3

HjSögnMegiSkuliÞurfi_et_p3 →
    'mega:so'_et_p3                     # (Endurnýja) megi/mætti/má (leigusamninginn)
    | 'eiga:so'_et_p3                   # (Endurnýja) á/eigi/átti (leigusamninginn)
    | 'þurfa:so'_et_p3                  # (Endurnýja) þyrfti/þarf/þurfti (leigusamninginn)
    | 'skulu:so'_et_p3                  # (Endurnýja) skuli/skyldi/skal (leigusamninginn)
    | 'verða:so'_et_p3                  # (Endurnýja) verður/yrði/varð (leigusamninginn)
    | 'hafa:so'_et_p3 'mega:so'_sagnb   # (Endurnýja) hefur/hafði/hefði mátt (leigusamninginn)
    | 'hafa:so'_et_p3 'þurfa:so'_sagnb  # (Endurnýja) hefur/hafði/hefði þurft (leigusamninginn)
    | 'hafa:so'_et_p3 'eiga:so'_sagnb   # (Endurnýja) hefur/hafði/hefði átt (leigusamninginn)

$tag(purge_verb) HjSögnMegiSkuliÞurfi_et_p3

# Vera/verða sambönd, öll nema sjálfar sagnirnar 'vera' og 'verða'
HjSögnEkkiVera →
    'vilja:so'_nh 'vera:so'_nh                      # (Mér sýndist Anna) vilja vera (ein)
    | 'mega:so'_nh 'vera:so'_nh                     # (Mér sýndist Anna) mega vera (ein)
    | 'hafa:so'_nh 'vera:so'_sagnb                  # (Mér sýndist Anna) hafa verið (valin)
    | 'geta:so'_nh 'hafa:so'_nh? 'vera:so'_sagnb    # (Mér sýndist Anna) geta [hafa] verið (farin)
    | 'vilja:so'_nh 'hafa:so'_nh 'vera:so'_sagnb    # (Mér sýndist Anna) vilja hafa verið (ein)
    | 'mega:so'_nh 'hafa:so'_nh 'vera:so'_sagnb     # (Mér sýndist Anna) mega hafa verið (ánægð)
    | 'þykja:so'_nh 'hafa:so'_nh 'vera:so'_sagnb    # (Félögin) þykja hafa verið (illa rekin)
    | 'hafa:so'_nh 'vilja:so'_sagnb 'vera:so'_nh    # (Mér virtist Anna) hafa viljað vera (á staðnum)
    | 'vilja:so'_nh 'verða:so'_nh
    | 'mega:so'_nh 'verða:so'_nh
    | 'hafa:so'_nh 'verða:so'_sagnb
    | 'geta:so'_nh 'hafa:so'_nh? 'verða:so'_sagnb
    | 'vilja:so'_nh 'hafa:so'_nh 'verða:so'_sagnb
    | 'hafa:so'_nh 'vilja:so'_sagnb 'verða:so'_nh
    | 'mega:so'_nh 'hafa:so'_nh 'verða:so'_sagnb
    | 'þykja:so'_nh 'hafa:so'_nh 'verða:so'_sagnb # (Félögin) þykja hafa orðið (illa rekin)

HjSögnVeraVerða →
    'vera:so'_nh | 'verða:so'_nh

# Vera/verða í nafnhætti ('(Mér sýndist kötturinn) vera (gulbrúnn)')
HjSögnVera →
    HjSögnEkkiVera | HjSögnVeraVerða

# Lýsingarháttur þátíðar: vera farinn, hafa verið farinn, skal vera farinn, mun vera farinn
HjSögnLhÞt/tala/pers →
    'vera:so'/tala/pers                                 # eru farnar
    | 'þykja:so'/tala/pers FsAtv? 'vera:so'_nh          # þóttu [ekki] vera farnar
    | 'þykja:so'/tala/pers FsAtv? 'hafa:so'_nh 'vera:so'_sagnb # þóttu [ekki] hafa verið farnar
    | 'mega:so'/tala/pers FsAtv? 'vera:so'_nh           # máttu [ekki] vera farnar
    | 'mega:so'/tala/pers FsAtv? 'hafa:so'_nh 'vera:so'_sagnb # máttu [ekki] hafa verið farnar
    | 'vilja:so'/tala/pers FsAtv? 'vera:so'_nh          # vildu [ekki] vera farnar
    | 'vilja:so'/tala/pers FsAtv? 'hafa:so'_nh 'vera:so'_sagnb # vildu [ekki] hafa verið farnar
    | 'hafa:so'/tala/pers FsAtv? 'vera:so'_sagnb        # hafa [ekki] verið farnar
    | 'hafa:so'/tala/pers FsAtv? 'eiga:so'_sagnb Nhm 'vera:so'_nh # hafa [ekki] átt að vera farnar
    | 'hafa:so'/tala/pers FsAtv? 'vilja:so'_sagnb 'vera:so'_nh # hafa [ekki] viljað vera farnar
    | 'hafa:so'/tala/pers FsAtv? 'geta:so'_sagnb 'vera:so'_sagnb # hafa [ekki] getað verið farnar
    | 'geta:so'/tala/pers FsAtv? 'hafa:so'_nh? 'vera:so'_sagnb # geta [ekki] verið farnar / geta hafa verið farnar
    | 'geta:so'/tala/pers FsAtv? 'hafa:so'_nh? 'eiga:so'_sagnb Nhm 'vera:so'_nh # geta [ekki] [hafa] átt að vera farnar
    | 'skulu:so'/tala/pers FsAtv? 'vera:so'_nh          # skulu [ekki] vera farnar
    | 'skulu:so'/tala/pers FsAtv? 'hafa:so'_nh 'vera:so'_sagnb # skulu [ekki] hafa verið farnar
    | 'munu:so'/tala/pers FsAtv? 'vera:so'_nh           # munu [ekki] vera farnar
    | 'munu:so'/tala/pers FsAtv? 'hafa:so'_nh 'vera:so'_sagnb  # munu [ekki] hafa verið farnar
    | 'munu:so'/tala/pers FsAtv? 'eiga:so'_nh Nhm 'vera:so'_nh # munu [ekki] eiga að vera farnar
    | 'eiga:so'/tala/pers FsAtv? Nhm 'vera:so'_nh       # eiga [ekki] að vera farnar
    | 'eiga:so'/tala/pers FsAtv? Nhm 'hafa:so'_nh 'vera:so'_sagnb # eiga [ekki] að hafa verið farnar

    | 'verða:so'/tala/pers                              # verða farnar
    | 'þykja:so'/tala/pers FsAtv? 'verða:so'_nh         # þóttu [ekki] verða farnar
    | 'þykja:so'/tala/pers FsAtv? 'hafa:so'_nh 'verða:so'_sagnb # þóttu [ekki] hafa orðið farnar
    | 'mega:so'/tala/pers FsAtv? 'verða:so'_nh          # máttu [ekki] verða farnar
    | 'mega:so'/tala/pers FsAtv? 'hafa:so'_nh 'verða:so'_sagnb  # máttu [ekki] hafa orðið farnar
    | 'vilja:so'/tala/pers FsAtv? 'verða:so'_nh         # vildu [ekki] verða farnar
    | 'vilja:so'/tala/pers FsAtv? 'hafa:so'_nh 'verða:so'_sagnb # vildu [ekki] hafa orðið farnar
    | 'hafa:so'/tala/pers FsAtv? 'verða:so'_sagnb       # hafa [ekki] orðið farnar
    | 'hafa:so'/tala/pers FsAtv? 'eiga:so'_sagnb Nhm 'verða:so'_nh # hafa [ekki] átt að verða farnar
    | 'hafa:so'/tala/pers FsAtv? 'vilja:so'_sagnb 'verða:so'_nh # hafa [ekki] viljað verða farnar
    | 'hafa:so'/tala/pers FsAtv? 'geta:so'_sagnb 'verða:so'_sagnb # hafa [ekki] getað orðið farnar
    | 'geta:so'/tala/pers FsAtv? 'hafa:so'_nh? 'verða:so'_sagnb # geta [ekki] orðið farnar / geta hafa orðið farnar
    | 'geta:so'/tala/pers FsAtv? 'hafa:so'_nh? 'eiga:so'_sagnb Nhm 'verða:so'_nh # geta [ekki] [hafa] átt að verða farnar
    | 'skulu:so'/tala/pers FsAtv? 'verða:so'_nh         # skulu [ekki] verða farnar
    | 'skulu:so'/tala/pers FsAtv? 'hafa:so'_nh 'verða:so'_sagnb # skulu [ekki] hafa orðið farnar
    | 'munu:so'/tala/pers FsAtv? 'verða:so'_nh          # munu [ekki] verða farnar
    | 'munu:so'/tala/pers FsAtv? 'hafa:so'_nh 'verða:so'_sagnb  # munu [ekki] hafa orðið farnar
    | 'munu:so'/tala/pers FsAtv? 'eiga:so'_nh Nhm 'verða:so'_nh # munu [ekki] eiga að verða farnar
    | 'eiga:so'/tala/pers FsAtv? Nhm 'verða:so'_nh      # áttu [ekki] að verða farnar
    | 'eiga:so'/tala/pers FsAtv? Nhm 'hafa:so'_nh 'verða:so'_sagnb  # eiga [ekki] að hafa orðið farnar

$tag(purge_verb) HjSögnLhÞt/tala/pers

HjSögnLhÞt_p3_et → HjSögnLhÞt_et_p3
HjSögnLhÞt_p3_ft → HjSögnLhÞt_ft_p3

# Hjálparsagnir með inniföldu frumlagi þegar lýsingarorð kemur á eftir
# ('Í dag þykir Ástþór [ekki lengur] vera (sáttur)')
HjSögnLoMeðF/tala/pers/kyn →
    'vera:so'/tala/pers NlFrumlag_nf/tala/pers/kyn                          # er Páll (sáttur)
    | so_mm/tala/pers NlFrumlag_nf/tala/pers/kyn                            # telst Páll (sáttur)

    | 'vilja:so'/tala/pers NlFrumlag_nf/tala/pers/kyn AtvFs? 'vera:so'_nh   # vill Páll [ekki] vera (sáttur)
    | 'þykja:so'/tala/pers NlFrumlag_nf/tala/pers/kyn AtvFs? 'vera:so'_nh   # þótti Páll [ekki] vera (sáttur)
    | 'mega:so'/tala/pers NlFrumlag_nf/tala/pers/kyn AtvFs? 'vera:so'_nh    # mátti Páll [ekki] vera (sáttur)
    | 'skulu:so'/tala/pers NlFrumlag_nf/tala/pers/kyn AtvFs? 'vera:so'_nh   # skal Páll [ekki] vera (sáttur)
    | 'munu:so'/tala/pers NlFrumlag_nf/tala/pers/kyn AtvFs? 'vera:so'_nh    # mun Páll [ekki] vera (sáttur)
    | 'eiga:so'/tala/pers NlFrumlag_nf/tala/pers/kyn AtvFs? Nhm 'vera:so'_nh # á Páll [ekki] að vera (sáttur)
    | so_mm/tala/pers NlFrumlag_nf/tala/pers/kyn AtvFs? 'vera:so'_nh        # sagðist Páll [ekki] vera (sáttur)

    | 'hafa:so'/tala/pers NlFrumlag_nf/tala/pers/kyn AtvFs? 'vera:so'_sagnb  # hefur Páll [ekki] verið (sáttur)
    | 'geta:so'/tala/pers NlFrumlag_nf/tala/pers/kyn AtvFs? 'hafa:so'_nh? 'vera:so'_sagnb # getur Páll [ekki] [hafa] verið (sáttur)
    | 'vilja:so'/tala/pers NlFrumlag_nf/tala/pers/kyn AtvFs? 'hafa:so'_nh 'vera:so'_sagnb # vildi Páll [ekki] hafa verið (sáttur)
    | 'þykja:so'/tala/pers NlFrumlag_nf/tala/pers/kyn AtvFs? 'hafa:so'_nh 'vera:so'_sagnb # þótt Páll [ekki] hafa verið (sáttur)
    | 'mega:so'/tala/pers NlFrumlag_nf/tala/pers/kyn AtvFs? 'hafa:so'_nh 'vera:so'_sagnb # mátti Páll [ekki] hafa verið (sáttur)
    | 'skulu:so'/tala/pers NlFrumlag_nf/tala/pers/kyn AtvFs? 'hafa:so'_nh 'vera:so'_sagnb # skal Páll [ekki] hafa verið (sáttur)
    | 'munu:so'/tala/pers NlFrumlag_nf/tala/pers/kyn AtvFs? 'hafa:so'_nh 'vera:so'_sagnb  # mun Páll [ekki] hafa verið (sáttur)
    | 'eiga:so'/tala/pers NlFrumlag_nf/tala/pers/kyn AtvFs? Nhm 'hafa:so'_nh 'vera:so'_sagnb  # á Páll [ekki] að hafa verið (sáttur)
    | so_mm/tala/pers NlFrumlag_nf/tala/pers/kyn AtvFs? 'hafa:so'_nh 'vera:so'_sagnb  # sagðist Páll [ekki] hafa verið (sáttur)

    | 'verða:so'/tala/pers NlFrumlag_nf/tala/pers/kyn                          # verður Páll (sáttur)
    | 'hafa:so'/tala/pers NlFrumlag_nf/tala/pers/kyn AtvFs? 'verða:so'_sagnb   # hefur Páll [ekki] orðið (sáttur)
    | 'vilja:so'/tala/pers NlFrumlag_nf/tala/pers/kyn AtvFs? 'verða:so'_nh     # vildi Páll [ekki] verða (sáttur)
    | 'þykja:so'/tala/pers NlFrumlag_nf/tala/pers/kyn AtvFs? 'verða:so'_nh     # þótti Páll [ekki] verða (sáttur)
    | 'mega:so'/tala/pers NlFrumlag_nf/tala/pers/kyn AtvFs? 'verða:so'_nh      # mátti Páll [ekki] verða (sáttur)
    | 'skulu:so'/tala/pers NlFrumlag_nf/tala/pers/kyn AtvFs? 'verða:so'_nh     # skal Páll [ekki] verða (sáttur)
    | 'munu:so'/tala/pers NlFrumlag_nf/tala/pers/kyn AtvFs? 'verða:so'_nh      # mun Páll [ekki] verða (sáttur)
    | 'eiga:so'/tala/pers NlFrumlag_nf/tala/pers/kyn AtvFs? Nhm 'verða:so'_nh  # á Páll [ekki] að verða (sáttur)
    | so_mm/tala/pers NlFrumlag_nf/tala/pers/kyn AtvFs? 'verða:so'_nh          # sagðist Páll [ekki] verða (sáttur)

    | 'geta:so'/tala/pers NlFrumlag_nf/tala/pers/kyn AtvFs? 'hafa:so'_nh? 'verða:so'_sagnb # getur Páll [ekki] orðið (sáttur) / geta hafa orðið (sáttur)
    | 'vilja:so'/tala/pers NlFrumlag_nf/tala/pers/kyn AtvFs? 'hafa:so'_nh 'verða:so'_sagnb # vildi Páll [ekki] hafa orðið (sáttur)
    | 'þykja:so'/tala/pers NlFrumlag_nf/tala/pers/kyn AtvFs? 'hafa:so'_nh 'verða:so'_sagnb # þótti Páll [ekki] hafa orðið (sáttur)
    | 'mega:so'/tala/pers NlFrumlag_nf/tala/pers/kyn AtvFs? 'hafa:so'_nh 'verða:so'_sagnb  # mátti Páll [ekki] hafa orðið (sáttur)
    | 'skulu:so'/tala/pers NlFrumlag_nf/tala/pers/kyn AtvFs? 'hafa:so'_nh 'verða:so'_sagnb # skal Páll [ekki] hafa orðið (sáttur)
    | 'munu:so'/tala/pers NlFrumlag_nf/tala/pers/kyn AtvFs? 'hafa:so'_nh 'verða:so'_sagnb  # mun Páll [ekki] hafa orðið (sáttur)
    | 'eiga:so'/tala/pers NlFrumlag_nf/tala/pers/kyn AtvFs? Nhm 'hafa:so'_nh 'verða:so'_sagnb  # á Páll [ekki] að hafa orðið (sáttur)
    | so_mm/tala/pers NlFrumlag_nf/tala/pers/kyn AtvFs? 'hafa:so'_nh 'verða:so'_sagnb   # sagðist Páll [ekki] hafa orðið (sáttur)

# 'Konan skal/mun gefin Páli'
HjSögnSkuluMunu/tala/pers →
    'skulu:so'/tala/pers | 'munu:so'/tala/pers

$tag(purge_verb) HjSögnSkuluMunu/tala/pers

# 'Því ber/bæri að fagna að...' / 'Því verður/varð/yrði að fagna að...''
# 'Þess þyrfti að geta að...' / 'Því ætti að leyna að...'
HjSögnBer →
    'bera:so'_0_gm_et_p3 | 'verða:so'_0_gm_et_p3
    | 'þurfa:so'_0_gm_et_p3 | 'eiga:so'_0_gm_et_p3

$tag(purge_verb) HjSögnBer
$score(+10) HjSögnBer

# Sameiginleg regla fyrir LhÞt og SkuluMunu

HjSögnLhÞtSM/tala/pers →
    HjSögnLhÞt/tala/pers | HjSögnSkuluMunu/tala/pers

HjSögnLhÞtSM_p3_et →
    HjSögnLhÞt_et_p3 | HjSögnSkuluMunu_et_p3

HjSögnLhÞtSM_p3_ft →
    HjSögnLhÞt_ft_p3 | HjSögnSkuluMunu_ft_p3

HjSögnSagnb →
    'vera:so'_gm_et_p3      # var/væri sagt
    | 'verða:so'_gm_et_p3   # verður/yrði sagt
    | 'skulu:so'_gm_et_p3   # skal/skyldi sagt
    | 'munu:so'_gm_et_p3    # mun/myndi sagt

# 'Hælisleitendum fer/fór/færi/hefur farið/mun fara fækkandi'

HjSögnLhNt →
    'fara:so'_gm_p3_et
    | 'hafa:so'_gm_p3_et FsAtv? 'geta:so'_sagnb? 'fara:so'_sagnb    # hefur/hafði/hefði [ekki] [getað] farið (fækkandi)
    | 'geta:so'_gm_p3_et FsAtv? 'hafa:so'_nh? 'fara:so'_sagnb       # getur/gat/gæti [hafa] farið (fækkandi)
    | 'munu:so'_gm_p3_et FsAtv? 'fara:so'_nh                        # mun/myndi [ekki] fara (fækkandi)
    | 'þykja:so'_gm_p3_et FsAtv? 'fara:so'_nh                       # þykir/þótti/þætti [ekki] fara (fækkandi)
    | 'munu:so'_gm_p3_et FsAtv? 'hafa:so'_nh 'fara:so'_sagnb        # mun/myndi [ekki] hafa farið (fækkandi)
    | 'þykja:so'_gm_p3_et FsAtv? 'hafa:so'_nh 'fara:so'_sagnb       # þykir/þótti/þætti [ekki] hafa farið (fækkandi)

$score(+16) HjSögnLhNt

# 'Íslandsbanka hefur tekist að halda í horfinu'

HjSögnMM →
    'hafa:so'_gm_p3_et FsAtv?                   # hefur [ekki] tekist
    | 'geta:so'_gm_p3_et FsAtv? 'hafa:so'_nh?   # getur [ekki] [hafa] tekist
    | 'munu:so'_gm_p3_et FsAtv? 'hafa:so'_nh    # mun [ekki] hafa tekist
    | 'skulu:so'_gm_p3_et FsAtv? 'hafa:so'_nh   # skal [ekki] hafa tekist
    | 'þykja:so'_gm_p3_et FsAtv? 'hafa:so'_nh   # þykir [ekki] hafa tekist
    | 'mega:so'_gm_p3_et FsAtv? 'hafa:so'_nh    # má [ekki] hafa tekist

HjSögnFinnst →
    'finna:so'_mm_p3_et      # Okkur finnst/fannst skemmtilegt
    | 'finna:so'_mm_vh_p3_et # Okkur finnist/fyndist skemmtilegt
    | 'þykja:so'_gm_p3_et    # Okkur þykir/þótti skemmtilegt
    | 'þykja:so'_gm_vh_p3_et # Okkur þyki/þætti skemmtilegt

HjSögnFinnst_sagnb →
    'finna:so'_mm_sagnb      # Okkur hefur fundist skemmtilegt
    | 'þykja:so'_gm_sagnb    # Okkur hefur þótt skemmtilegt

HjSögnFinnst/tala/pers →
    'finna:so'_op_mm/tala/pers      # Konunum finnst Páll skemmtilegur
    | 'finna:so'_op_mm_vh/tala/pers # Konunum fyndist Páll skemmtilegur
    | 'þykja:so'_op_gm/tala/pers    # Konunum þótti Páll skemmtilegur
    | 'þykja:so'_op_gm_vh/tala/pers # Konunum þætti Páll skemmtilegur

HjSögnVantaSkorta →
    'vanta:so'_nh           # Okkur þykir/þótti vanta mýkt í rödd Pálínu
    | 'skorta:so'_nh        # Páli þykir/þótti skorta samkvæmni í málflutningi Jóns

HjSögnFá/tala/pers →
    'fá:so'_1_þf_gm/tala/pers                   # Páll fékk bílinn endurgreiddan
    | 'hafa:so'_gm/tala/pers 'fá:so'_gm_sagnb   # Páll hefur fengið bílinn endurgreiddan
    | 'hafa:so'_gm/tala/pers 'geta:so'_gm_sagnb 'fá:so'_gm_sagnb # Páll hefur getað fengið bílinn endurgreiddan
    | 'hafa:so'_gm/tala/pers 'vilja:so'_gm_sagnb 'fá:so'_nh # Páll hefur viljað fá bílinn endurgreiddan
    | 'geta:so'_gm/tala/pers 'hafa:so'_nh? 'fá:so'_gm_sagnb # Páll getur [hafa] fengið bílinn endurgreiddan
    | 'vilja:so'_gm/tala/pers 'fá:so'_nh        # Páll vill fá bílinn endurgreiddan
    | 'vilja:so'_gm/tala/pers 'hafa:so'_nh 'fá:so'_gm_sagnb # Páll vill hafa fengið bílinn endurgreiddan
    | 'vilja:so'_gm/tala/pers 'geta:so'_nh 'fá:so'_gm_sagnb # Páll vill geta fengið bílinn endurgreiddan
    | 'mega:so'_gm/tala/pers 'fá:so'_nh         # Páll má fá bílinn endurgreiddan
    | 'mega:so'_gm/tala/pers 'hafa:so'_nf 'fá:so'_gm_sagnb # Páll má hafa fengið bílinn endurgreiddan
    | 'skulu:so'_gm/tala/pers 'fá:so'_nh        # Páll skal fá bílinn endurgreiddan
    | 'skulu:so'_gm/tala/pers 'hafa:so'_nh 'fá:so'_gm_sagnb # Páll skal hafa fengið bílinn endurgreiddan
    | 'skulu:so'_gm/tala/pers 'geta:so'_nh 'fá:so'_gm_sagnb # Páll skal geta fengið bílinn endurgreiddan
    | 'munu:so'_gm/tala/pers 'vilja:so'_nh? 'fá:so'_nh      # Páll mun [vilja] fá bílinn endurgreiddan
    | 'munu:so'_gm/tala/pers 'hafa:so'_nh 'fá:so'_gm_sagnb # Páll mun hafa fengið bílinn endurgreiddan
    | 'munu:so'_gm/tala/pers 'geta:so'_nh 'fá:so'_gm_sagnb # Páll mun geta fengið bílinn endurgreiddan

# HjSögnÞaðLo_et_p3 →
#     'vera:so'_gm_p3_et 'verða:so'_sagnb? # Það er [orðið] brýnt að...
#     | 'verða:so'_gm_p3_et  # Það verður brýnt að bæta umgengni
#     | 'hafa:so'_gm_p3_et 'vera:so'_sagnb  # Það hefur verið brýnt að...
#     | 'hafa:so'_gm_p3_et 'verða:so'_sagnb # Það hefur orðið brýnt að...
#     | 'hafa:so'_gm_p3_et 'þykja:so'_sagnb # Það hefur þótt brýnt að...
#     | 'þykja:so'_gm_p3_et 'vera:so'_nh? # Það þykir [vera] brýnt að...
#     | 'þykja:so'_gm_p3_et 'vera:so'_nh? 'verða'_sagnb # Það þykir [vera] orðið brýnt að...
#     | 'munu:so'_gm_p3_et 'vera:so'_nh 'verða:so'_sagnb? # Það mun vera [orðið] brýnt að...
#     | 'munu:so'_gm_p3_et 'hafa:so'_nh 'þykja:so'_sagnb # Það mun hafa þótt brýnt að...
#     | 'munu:so'_gm_p3_et 'þykja:so'_nh # Það mun þykja brýnt að...
#     | 'geta:so'_gm_p3_et 'hafa:so'_nh? 'vera:so'_sagnb # Það getur [hafa] verið brýnt að...
#     | 'geta:so'_gm_p3_et 'hafa:so'_nh? 'verða:so'_sagnb # Það getur [hafa] orðið brýnt að...
#     | 'geta:so'_gm_p3_et 'hafa:so'_nh? 'þykja:so'_sagnb # Það getur [hafa] þótt brýnt að...

# Nafnháttarfylling
# - tengd frumlagi
# '(Þeir ætluðu) [sannarlega galvaskir/sjálfir] að þvo bílinn í gær'
NhFylling/tala/kyn →
    NhFylling_nf/tala/kyn    # Nefnifall sjálfgefið

$score(+6) NhFylling/tala/kyn # Gefa 'að borða köku' forgang sem NhFylling fremur en fs+no, til dæmis fs+no_þgf+no_ef

# Aðeins nefnifall og þágufall eru notuð
NhFylling_nf/tala/kyn → EinNhFylling_nf/tala/kyn OgNhFylling_nf/tala/kyn*
NhFylling_þgf/tala/kyn → EinNhFylling_þgf/tala/kyn OgNhFylling_þgf/tala/kyn*

EinNhFylling/fall/tala/kyn →
    # '(Hann ætlar) strax að fara að millilenda'
    SagnInnskot? NhLiðir
    # '(Hann er) í reynd strax sjálfur að hafna þessu'
    | FsRuna? LoTengtSögn/tala/fall/kyn NhLiðir
    # 'að þykja og vera fallegt'
    | SagnInnskot? Nhm so_1_nf_nh OgNhSögn_nf* LoTengtSögn/tala/fall/kyn
    # 'að sýnast og þykjast fallegt'
    | SagnInnskot? NhEinfaldur_mm OgSögn_mm_nh* LoTengtSögn/tala/fall/kyn

OgNhFylling/fall/tala/kyn →
    OgEða EinNhFylling/fall/tala/kyn

# - ótengd frumlagi: lýsingarliður ekki mögulegur
# (nema þá hugsanlega í 3p. et.?)
NhFylling →
    FsAtv? NhLiðir

NhFyllingAtv →
    NhFylling SagnInnskot?

$score(+2) NhFyllingAtv

NhFyllingÁnAtv →
    NhLiðir SagnInnskot? # Getur ekki byrjað á FsAtv

$tag(begin_prep_scope) NhFyllingAtv # Nýtt undirtré fyrir tengingu sagnar og forsetningar
$tag(begin_prep_scope) NhFyllingÁnAtv # Nýtt undirtré fyrir tengingu sagnar og forsetningar

NhFyllingEðaAtv →
    NhFyllingAtv | SagnInnskot

# Mér fannst skemmtilegt [ að Jón læsi bókina / að fara út í þessu veðri / í dag ]
AðLiður →
    NhFylling SagnInnskotAtv?
    | SagnInnskotAtv
    # Málinu væri stillt upp þannig að Sjálfstæðisflokkurinn bæri ábyrgð
    | Atviksliður? AðvörunKomma? Skýringarsetning

# Mér var sagt að fara út / að Jón væri farinn
AðAndlag →
    NhFyllingAtv
    # Málinu væri stillt upp þannig að Sjálfstæðisflokkurinn bæri ábyrgð
    | Atviksliður? AðvörunKomma? Skýringarsetning

# Nafnháttarliður
# 'að lenda snarlega á flugvellinum'
# 'að [láta strax] sigla skipinu í land'
# 'að [láta í snarheitum] bera þangað mat og drykk'
NhLiður →
    Nhm NhLáta? NhRuna

$score(+2) NhLiður

NhEinfaldur →
    Nhm so_0_nh

NhEinfaldur_mm →
    Nhm so_0_nh_mm

NhLáta →
    'láta:so'_0_nh FsAtv?

NhLiðurFrh →
    SagnInnskot? NhLiður

# 'að fara strax að athuga málið'
NhLiðir →
    NhLiður NhLiðurFrh*

NhRuna → NhSögn OgNhLiður*

OgNhLiður →
    # (að selja skyldi hestinn) snarlega, en ekki hætta baráttunni
    # (er ekki unnt að fara) nema nota allt fé ríkisins
    SagnInnskot? KommaOgEðaEnNema SagnInnskot? NhSögn

NhSögn →
    so_0_nh so_0_gm_vh_p3_et? OgNhSögn*                                     # að kjósa [þurfi] og telja (upp á nýtt)
    | so_1_nf_nh so_0_gm_vh_p3_et? OgNhSögn_nf* FsRunaEftirSögn? NlBeintAndlag_nf    # að hrjósa [muni] hugur
    | so_1_þf_nh so_0_gm_vh_p3_et? OgNhSögn_þf* FsRunaEftirSögn? NlBeintAndlag_þf    # að selja [skyldi] í snatri hestinn
    | so_1_þgf_nh so_0_gm_vh_p3_et? OgNhSögn_þgf* FsRunaEftirSögn? NlBeintAndlag_þgf # að hætta [ætti] baráttunni
    | so_1_ef_nh so_0_gm_vh_p3_et? OgNhSögn_ef* FsRunaEftirSögn? NlBeintAndlag_ef    # að freista [eigi] gæfunnar
    | so_2_þgf_þf_nh so_0_gm_vh_p3_et? OgNhSögn_þgf_þf* FsRunaEftirSögn? NlÓbeintAndlag_þgf NlBeintAndlag_þf # að skrifa [skuli] og senda Karólínu bréfið
    | so_2_þf_ef_nh so_0_gm_vh_p3_et? OgNhSögn_þf_ef* FsRunaEftirSögn? NlÓbeintAndlag_þf NlBeintAndlag_ef    # að spyrja og krefja Pál svars
    | so_2_þf_þgf_nh so_0_gm_vh_p3_et? OgNhSögn_þf_þgf* FsRunaEftirSögn? Nl_þf Nl_þgf # að kalla [ætti] Pál illum nöfnum
    | so_2_þgf_ef_nh so_0_gm_vh_p3_et? OgNhSögn_þgf_ef* FsRunaEftirSögn? NlÓbeintAndlag_þgf NlBeintAndlag_ef # að óska [eigi] og óska Páli góðs bata
    | so_2_þgf_nf_nh so_0_gm_vh_p3_et? OgNhSögn_þgf_nf* FsRunaEftirSögn? Nl_þgf Nl_nf # að vera [eigi] og vera Páli stoð og stytta
    | 'gera:so'_1_þgf_nh so_0_gm_vh_p3_et? NlÓbeintAndlag_þgf LoTengtSögn_nf_et_hk NhFyllingAtv # að gera [eigi] Páli kleift að matast með reisn
    # að reistar yrðu og byggðar íbúðir / að barinn skyldi fiskurinn og flakaður
    # !!! Ath: sennilega geta aðeins hjálparsagnir staðið í viðtengingarhætti hér,
    # !!! ekki allar sagnir
    | so_0_lhþt/tala/kyn so_0_gm_vh/tala OgNhSögn_lhþt/tala/kyn* NlFrumlag_nf/tala/kyn OgNhSögn_lhþt/tala/kyn*

    # (Páll fór inn eftir að) hafa fært bílinn / (fyrir að) geta lyft grettistaki
    | NhSögnHafaGetaFá

    # láta sjúklinginn ná eðlilegum líkamshita
    # segja Pál fara með rangt mál
    # telja verkefnið verða óviðráðanlegt
    # sjá drenginn fara í hundana
    | So_1_þf_nh Nl_þf NhSögn

    # betur megi fara / strax skuli tilkynna
    | HjSögnMegiSkuli_et_p3 NhSögn

    # (Hann tók á öllu sínu til að) verða ekki fúll
    | NhSögnLo

NhSögnLo →
    HjSögnVeraVerða SögnErLoBotn/tala/kyn

$score(+24) NhSögnLo

NhSögnHafaGetaFá →
    HafaGetaFá_nh HreinSögn_sagnb/tala/kyn # !!! Strangt til tekið ætti að sækja tölu og kyn ofan að úr trénu

# Gefa þessu gott forskot til að keppa við SamstættFall með greini,
# til dæmis "hafa ráðið Jóni bana", þar sem "ráðið" er sögnin "ráða"
# en ekki nafnorðið "ráð" með greini
$score(+8) NhSögnHafaGetaFá

# Samtengingar sagna

OgNhSögn →
    OgEðaHeldur so_0_nh so_0_gm_vh_p3_et?
OgNhSögn_þf →
    OgEðaHeldur so_1_þf_nh so_0_gm_vh_p3_et?
OgNhSögn_þgf →
    OgEðaHeldur so_1_þgf_nh so_0_gm_vh_p3_et?
OgNhSögn_ef →
    OgEðaHeldur so_1_ef_nh so_0_gm_vh_p3_et?
OgNhSögn_nf →
    OgEðaHeldur so_1_nf_nh so_0_gm_vh_p3_et?
OgNhSögn_þgf_þf →
    OgEðaHeldur so_2_þgf_þf_nh so_0_gm_vh_p3_et?
OgNhSögn_þf_ef →
    OgEðaHeldur so_2_þf_ef_nh so_0_gm_vh_p3_et?
OgNhSögn_þf_þgf →
    OgEðaHeldur so_2_þf_þgf_nh so_0_gm_vh_p3_et?
OgNhSögn_þgf_ef →
    OgEðaHeldur so_2_þgf_ef_nh so_0_gm_vh_p3_et?
OgNhSögn_þgf_nf →
    OgEðaHeldur so_2_þgf_nf_nh so_0_gm_vh_p3_et?
OgNhSögn_lhþt/tala/kyn →
    OgEðaHeldur so_0_lhþt/tala/kyn

# Gefa til kynna hvar lesa á upp sagnir til að nota í
# tengingu sagna og forsetninga

$tag(pick_up_verb) Sagnliður/tala/pers/kyn
$tag(pick_up_verb) Sagnliður_sagnb
$tag(pick_up_verb) SögnMeðF/tala/pers/kyn
$tag(pick_up_verb) ÖfugSögn_0/tala/pers/kyn
$tag(pick_up_verb) ÖfugSögn_1/tala/pers/kyn
$tag(pick_up_verb) ÖfugSögn_2/tala/pers/kyn
$tag(pick_up_verb) SagnliðurÁnF
$tag(pick_up_verb) NhLiður

# Nafnliðir

# (Kötturinn ákvað) [strax] [,] að Jón væri farinn heim
NlSkýringarsetning → AtviksliðurEinkunn* AðvörunKomma? Skýringarsetning
$score(-2) NlSkýringarsetning

# (Lögreglan rannsakar) [nú] hvort Jón hafi ekið bílnum
NlSpurnaraukasetning → AtviksliðurEinkunn* Spurnaraukasetning

NlFrumlagÞað → 'það:pfn'_hk_nf_et

# Hafa sérstakan hnút fyrir frumlagslepp (NP-ES)
NlFrumlagLeppur → 'það:pfn'_hk_nf_et
# Merkja til að koma í veg fyrir að hnúturinn sé fjarlægður,
# þar sem hann bendir beint á annan hnút
$tag(keep) NlFrumlagLeppur

NlFrumlag/fall → Nl/fall | NlSkýringarsetning
NlFrumlag/fall/tala/pers/kyn → Nl/fall/tala/pers/kyn
NlFrumlag_nf_et_p3_hk → NlSkýringarsetning
NlFrumlag_þf_et_p3_hk → NlSkýringarsetning
NlFrumlag_þgf_et_p3_hk → NlSkýringarsetning
NlFrumlag_ef_et_p3_hk → NlSkýringarsetning
NlFrumlag/fall/tala/kyn → Nl/fall/tala/kyn
NlFrumlag_nf_et_hk → NlSkýringarsetning
NlFrumlag_þf_et_hk → NlSkýringarsetning
NlFrumlag_þgf_et_hk → NlSkýringarsetning
NlFrumlag_ef_et_hk → NlSkýringarsetning
NlFrumlag/fall/tala → Nl/fall/tala
NlFrumlag_nf_et → NlSkýringarsetning
NlFrumlag_þf_et → NlSkýringarsetning
NlFrumlag_þgf_et → NlSkýringarsetning
NlFrumlag_ef_et → NlSkýringarsetning
NlFrumlag/fall/tala/pers → Nl/fall/tala/pers
NlFrumlag_nf_et_p3 → NlSkýringarsetning
NlFrumlag_þf_et_p3 → NlSkýringarsetning
NlFrumlag_þgf_et_p3 → NlSkýringarsetning
NlFrumlag_ef_et_p3 → NlSkýringarsetning
NlBeintAndlag/fall → Nl/fall | NlSpurnaraukasetning | NlSkýringarsetning
NlBeintAndlag/fall/tala/pers/kyn → Nl/fall/tala/pers/kyn
NlBeintAndlag_nf_et_p3_hk → NlSpurnaraukasetning | NlSkýringarsetning
NlBeintAndlag_þf_et_p3_hk → NlSpurnaraukasetning | NlSkýringarsetning
NlBeintAndlag_þgf_et_p3_hk → NlSpurnaraukasetning | NlSkýringarsetning
NlBeintAndlag_ef_et_p3_hk → NlSpurnaraukasetning | NlSkýringarsetning
NlBeintAndlag/fall/tala/pers → Nl/fall/tala/pers
NlBeintAndlag_nf_et_p3 → NlSpurnaraukasetning | NlSkýringarsetning
NlBeintAndlag_þf_et_p3 → NlSpurnaraukasetning | NlSkýringarsetning
NlBeintAndlag_þgf_et_p3 → NlSpurnaraukasetning | NlSkýringarsetning
NlBeintAndlag_ef_et_p3 → NlSpurnaraukasetning | NlSkýringarsetning
NlÓbeintAndlag/fall → Nl/fall | NlSkýringarsetning
NlSagnfylling/fall → Nl/fall | NlSkýringarsetning
NlSagnfylling/fall/tala/pers/kyn → Nl/fall/tala/pers/kyn
NlSagnfylling_nf_et_p3_hk → NlSkýringarsetning
NlSagnfylling_þf_et_p3_hk → NlSkýringarsetning
NlSagnfylling_þgf_et_p3_hk → NlSkýringarsetning
NlSagnfylling_ef_et_p3_hk → NlSkýringarsetning

$score(+1) NlBeintAndlag/fall
$score(+1) NlBeintAndlag/fall/tala/pers
$score(+1) NlBeintAndlag/fall/tala/pers/kyn
$score(+1) NlFrumlag/fall
$score(+1) NlFrumlag/fall/tala
$score(+1) NlFrumlag/fall/tala/kyn
$score(+1) NlFrumlag/fall/tala/pers
$score(+1) NlFrumlag/fall/tala/pers/kyn
$score(+1) NlSagnfylling/fall
$score(+1) NlSagnfylling/fall/tala/pers/kyn
$score(+1) NlÓbeintAndlag/fall

# Nafnliðarkjarni

Einkunn/tala/fall/kyn →

    LoLiður/tala/fall/kyn           # glæsileg (kona), bókfært (virði) (so_lhþt), leiðandi (fyrirtæki) (so_lh_nt)
    # AtviksliðurEinkunn undanskilur atviksorð sem geta líka verið forsetningar;
    # það dregur verulega úr margræðni
    | AtviksliðurEinkunn            # rúmlega (helmingur)
    | FornafnEinkunn/tala/fall/kyn  # Sá (stóri maður), allt þetta (víkjandi lán)
    | TöluorðEinkunn/tala/fall/kyn  # [samtals] þrjá (gangandi menn) / [hátt í] 26
    | EignarFornafn                 # 'okkar/ykkar/hans/hennar/þess/þeirra' (pfn)
    | GreinirMeðLo/tala/fall/kyn 

# 'Skjölin voru mörg hundruð síður', 'Í húsinu voru fleiri þúsund hænur', 'hún saknaði mörg hundruð hálsmena'
Einkunn_ft/fall/kyn →
    HundruðForskeyti Hundruð

$score(+4) Einkunn/tala/fall/kyn

HundruðForskeyti → 'margur:lo'_sb_hk_nf_ft | 'margur:lo'_mst_hk_nf_ft # mörg, fleiri

Hundruð → 'hundrað:hk'_nf_ft | 'þúsund:hk'_nf_ft # hundruð, þúsund

TöluorðEinkunn/tala/fall/kyn →
    TöluorðForskeyti/tala/fall/kyn? Töluorð/tala/fall/kyn

# Gefa góðan plús fyrir töluorð í einkunn, þannig að þau límist
# frekar við nafnorð heldur en að vera sjálfstæðir nafnliðir
$score(+8) TöluorðEinkunn/tala/fall/kyn

# Hvetja til notkunar fornafna í einkunn með nafnlið,
# fremur en að þau standi sjálfstæð
FornafnEinkunn/tala/fall/kyn →
    Fornafn/tala/fall/kyn

$score(+18) FornafnEinkunn/tala/fall/kyn

# Lýsingarorð eða eignarfallsliður sem kemur á eftir nafnlið

NlViðbót/tala/fall/kyn →
    LoEftirNl/tala/fall/kyn     # (Jón) sterki / (Jón), sultuslakur eftir gufubaðið,
    | EfLiður LoEftirNlMeðAndlagi/fall/tala/kyn? # (forstöðumenn) fræðaseturs [sunnlendinga] [tengdir flokknum]

# Eintala

NlKjarni_et/fall/kyn →

    # 'fullorðni sprúðlandi sonur Íslands'

    EfLiðurForskeyti?           # 1. deildar (lið Vals)
    Einkunn_et/fall/kyn*        # leiðandi (fyrirtæki) / bókfært (virði)
    Fyrirbæri/fall/kyn          # Már Guðmundsson seðlabankastjóri
    NlViðbót_et/fall/kyn?       # (Jón) sterki / (Jón), sultuslakur eftir gufubaðið, / (sonur) skósmiðsins
    Númer?                      # (ákvæði laga) nr. 33

NlKjarni_et_kk/fall →
    tala_kk/fall 'hluti:kk'_et/fall EfLiður? # 1/3 hluti Alþingismanna (getur krafist atkvæðagreiðslu)

NlKjarni_et_nf_kk → NlKjarni_et_kk_nf
NlKjarni_et_þf_kk → NlKjarni_et_kk_þf
NlKjarni_et_þgf_kk → NlKjarni_et_kk_þgf
NlKjarni_et_ef_kk → NlKjarni_et_kk_ef

# Fleirtala

NlKjarni_ft/fall/kyn →

    EfLiðurForskeyti?
    Einkunn_ft/fall/kyn*
    Fyrirbæri_ft/fall/kyn
    NlViðbót_ft/fall/kyn?
    Númer?

Fyrirbæri_ft/fall/kyn →
    Skst? no_ft/fall/kyn                            # [Commodore PET] tölvur
    | Skst? no_gr_ft/fall/kyn Fjöldi_ft/fall/kyn    # Mennina tíu / félögin tvö
    | FyrirbæriMeðGreini_ft/fall/kyn

Númer →
   'númer:hk' Tala_hk_nf

$score(+8) Númer

Fjöldi_ft/fall/kyn → to_ft/fall/kyn | töl_ft/fall/kyn

# Persónufornöfn þriðju persónu eintölu
# Þessi orð eru ekki merkt með kyni í BÍN;
# þess vegna þurfa þau sérstaka meðhöndlun hér

NlKjarni_et_nf_kk → 'hann:pfn'_kk_et_nf
NlKjarni_et_þf_kk → 'hann:pfn'_kk_et_þf
NlKjarni_et_þgf_kk → 'hann:pfn'_kk_et_þgf
NlKjarni_et_ef_kk → 'hann:pfn'_kk_et_ef

NlKjarni_et_nf_kvk → 'hún:pfn'_kvk_et_nf
NlKjarni_et_þf_kvk → 'hún:pfn'_kvk_et_þf
NlKjarni_et_þgf_kvk → 'hún:pfn'_kvk_et_þgf
NlKjarni_et_ef_kvk → 'hún:pfn'_kvk_et_ef

NlKjarni_et_nf_hk → 'það:pfn'_hk_et_nf
NlKjarni_et_þf_hk → 'það:pfn'_hk_et_þf
NlKjarni_et_þgf_hk → 'það:pfn'_hk_et_þgf
NlKjarni_et_ef_hk → 'það:pfn'_hk_et_ef

NlKjarni_et_nf_hk → 'hán:pfn'_hk_et_nf
NlKjarni_et_þf_hk → 'hán:pfn'_hk_et_þf
NlKjarni_et_þgf_hk → 'hán:pfn'_hk_et_þgf
NlKjarni_et_ef_hk → 'hán:pfn'_hk_et_ef

# Fleirtölumyndir 3. persónu eru í BÍN undir 'sá'

# Afturbeygt fornafn, eins í tölum og kynjum, aðeins til í aukaföllum 3p.

# sjálfan sig / sjálfa sig
# sjálfum sér / sjálfum sér
# sjálfs sín  / sjálfra sín

NlKjarni/tala/aukafall/kyn →
    'sjálfur:fn'/tala/aukafall/kyn abfn/aukafall/tala/kyn # Hér vitum við tölu og kyn
    | abfn/aukafall # Hér vitum við hvorki tölu né kyn

# Fornöfn sem tákna fleirtölu og standa með eignarfallslið:
# Mörg/flest þessara mála, sum þeirra sem lentu í slysinu
NlKjarni_ft/fall/kyn →
    'margur:lo'_sb_ft/fall/kyn EfLiður_ft/kyn
    | 'allmargur:lo'_sb_ft/fall/kyn EfLiður_ft/kyn
    | 'fár:lo'_sb_ft/fall/kyn EfLiður_ft/kyn
    | 'örfár:lo'_sb_ft/fall/kyn EfLiður_ft/kyn
    | 'sumur:fn'_ft/fall/kyn EfLiður_ft/kyn
    | 'allur:fn'_ft/fall/kyn EfLiður_ft/kyn
    | 'báðir:fn'_ft/fall/kyn EfLiður_ft/kyn
    | 'fáeinir:fn'_ft/fall/kyn EfLiður_ft/kyn
    | 'flestallir:fn'_ft/fall/kyn EfLiður_ft/kyn
    | 'flestir:fn'_ft/fall/kyn EfLiður_ft/kyn
    | 'ýmis:fn'_ft/fall/kyn EfLiður_ft/kyn
    | 'þónokkur:fn'_ft/fall/kyn EfLiður_ft/kyn

# Fornöfn sem tákna eintölu og standa með eignarfallslið:
NlKjarni_et/fall/kyn →
    'annar:fn'_et/fall/kyn EfLiður_ft/kyn
    | 'hvor:fn'_et/fall/kyn EfLiður_ft/kyn
    | 'hver:fn'/tala/fall/kyn EfLiður_ft/kyn
    | 'hvorugur:fn'_et/fall/kyn EfLiður_ft/kyn
    | 'einn:fn'_et/fall/kyn EfLiður_ft/kyn
    | 'enginn:fn'_et/fall/kyn EfLiður_ft/kyn
    | 'neinn:fn'_et/fall/kyn EfLiður_ft/kyn
    | 'sá:fn'_et/fall/kyn EfLiður_ft/kyn
    | 'sérhver:fn'_et/fall/kyn EfLiður_ft/kyn
    | 'sinnhver:fn'_et/fall/kyn EfLiður_ft/kyn
    | 'sinnhvor:fn'_et/fall/kyn EfLiður_ft/kyn
    | 'sínhver:fn'_et/fall/kyn EfLiður_ft/kyn
    | 'sínhvor:fn'_et/fall/kyn EfLiður_ft/kyn
    | 'sitthvað:fn'_et/fall/kyn EfLiður_ft/kyn
    | 'sitthver:fn'_et/fall/kyn EfLiður_ft/kyn
    | 'sitthvor:fn'_et/fall/kyn EfLiður_ft/kyn

# Fornöfn sem geta staðið bæði í eintölu og fleirtölu:
NlKjarni/tala/fall/kyn →
    'nokkur:fn'/tala/fall/kyn EfLiður_ft/kyn
    | 'einhver:fn'/tala/fall/kyn EfLiður_ft/kyn

# Prósentubil með eignarfalli

NlKjarni_ft/fall/kyn →
    Prósentubil EfLiður_ft/kyn # 10-25% kvenna eru galvaskar

NlKjarniPrósent_hk →
    Prósentubil EfLiður_ft/kyn # 10-25% kvenna eru galvösk

NlKjarni_ft_nf_hk → NlKjarniPrósent_hk
NlKjarni_ft_þf_hk → NlKjarniPrósent_hk
NlKjarni_ft_þgf_hk → NlKjarniPrósent_hk
NlKjarni_ft_ef_hk → NlKjarniPrósent_hk

# Umraðanir

NlStak_p3/fall/kyn → NlStak_et_p3/fall/kyn | NlStak_ft_p3/fall/kyn

NlStak_et_ef/kyn → NlStak_et_p1_ef/kyn | NlStak_et_p2_ef/kyn | NlStak_et_p3_ef/kyn
NlStak_ft_ef/kyn → NlStak_ft_p1_ef/kyn | NlStak_ft_p2_ef/kyn | NlStak_ft_p3_ef/kyn

# Nafnliðir

NlStak_et_p1/fall/kyn →
    'ég:pfn'_et/fall PfnTagl/fall/kyn? FsRunaEftirNl?           # Ég í öllu mínu veldi / (Hvað) mig sjálfan (varðar)
    | 'sjálfur:fn'_et/fall/kyn 'ég:pfn'_et/fall FsRunaEftirNl?  # Sjálfan mig hef ég aldrei þolað

PfnTagl/fall/kyn →
    'sjálfur:fn'_et/fall/kyn
    | 'undirritaður:lo'_et/fall/kyn
    | 'persónulega:ao'

# Gefa lægra skor fyrir 'ég' í hvorugkyni
# ('ég er reitt', 'ég fór sjálft út í búð')
$score(-4) NlStak_et_p1_nf_hk
$score(-4) NlStak_et_p1_þf_hk
$score(-4) NlStak_et_p1_þgf_hk
$score(-4) NlStak_et_p1_ef_hk

NlStak_et_p2/fall/kyn →
    'þú:pfn'_et/fall PfnTagl/fall/kyn? FsRunaEftirNl?           # Þú [sjálfur/persónulega] ert ekki sem verstur
    | 'sjálfur:fn'_et/fall/kyn 'þú:pfn'_et/fall FsRunaEftirNl?  # Þú horfist í augu við sjálfan þig

NlStak_p1_ft/fall/kyn →
    'ég:pfn'_ft/fall NlStakViðbót/fall/kyn?     # Við gömlu hjónin, við Sigmundur, við flest, við sjálfar
    | 'vér:pfn'_ft/fall NlStak_ft_p3/fall/kyn?  # Vér fóstbræður
    | NlEind/fall KommaOgEða 'ég:pfn'_et/fall   # Þú og ég fórum, Jón og ég fórum, stelpurnar og ég fórum
    | 'ég:pfn'_et/fall KommaOgEða NlEind/fall   # Ég og þú fórum, ég og Jón fórum, ég og stelpurnar fórum

NlStakViðbót/fall/kyn →
    NlStakViðbótNl/fall/kyn
    | 'allur:fn'_ft/fall/kyn            # við öll, okkur alla, okkar allra
    | 'sumur:fn'_ft/fall/kyn            # við sum, okkur sumra, okkar sumra
    | 'margur:lo'_ft/fall/kyn           # við mörg/flest, okkur margra/flestra, okkar margra/flestra
    | 'sjálfur:fn'_ft/fall/kyn          # við sjálf, okkur sjálfra, okkar sjálfra
    | 'undirritaður:lo'_ft/fall/kyn     # við undirritaðar, okkur undirritaða, okkar undirritaðra
    | 'persónulega:ao'                  # við persónulega

$score(+8) NlStakViðbót/fall/kyn

NlStakViðbótNl/fall/kyn →
    NlStak_ft_p3/fall/kyn               # (ft.) við fóstbræður, við alþingismennirnir
    | no_gr_et/fall/kyn                 # (et.) við fjölskyldan, við hópurinn

NlStakViðbótNl/fall/kyn →
    Manneskja/fall/kyn                  # (við) Sóley Tómasdóttir (fórum), (hann ræddi við okkur) Pál dómkirkjuprest

$score(-20) NlStakViðbótNl/fall/kyn     # Draga af stig fyrir "við fóstbræður" vegna hættu á ruglingi við forsetninguna 'við'

NlStak_ft_p1/fall/kyn → NlStak_p1_ft/fall/kyn FsRunaEftirNl?

NlStak_p2_ft/fall/kyn →
    'þú:pfn'_ft/fall NlStakViðbót/fall/kyn?     # þið öll, þið flest, þið stelpurnar, þið eplin
    | 'þér:pfn'_ft/fall NlStak_ft_p3/fall/kyn?  # Þér eplin
    | NlEind/fall KommaOgEða 'þú:pfn'_et/fall   # Jón og þú fóruð, stelpurnar og þú fóruð
    | 'þú:pfn'_et/fall KommaOgEða NlEind/fall   # Þú og Jón fóruð, þú og stelpurnar fóruð

NlStak_ft_p2/fall/kyn → NlStak_p2_ft/fall/kyn FsRunaEftirNl?

# !!! ATH: reglurnar hér fyrir ofan leyfa 'ég og þú fóruð til Frakklands'

# Sameiginlegar reglur fyrir 3p. eintölu og fleirtölu

NlStak_p3/tala/fall/kyn →

    NlKjarni/tala/fall/kyn NlStakTagl/tala/fall/kyn?
    | NlFornafn/tala/fall/kyn
    | Pfn/kyn/tala/fall to/tala/fall/kyn? 'persónulega:ao'? # 'þið tvær', 'við fjögur', 'hann einn'
    | LoTöluorð_ef 'konar:kk'_ef_et NlKjarni/tala/fall/kyn  # 'tvenns/þrenns/ferns konar innstungur'
    | Raðnr/fall/kyn NlKjarni/tala/fall/kyn Fornafn/tala/fall/kyn*   # 1. verðlaunin öll, 1.-3. sætið, 10. og 12. árið
    | NlStakNema/tala/fall/kyn

# '(unnu) alla leiki sína nema þann síðasta'
# '(söknuðu) alls/einskis nema hestsins'
# '(söknuðu) alls síns farangurs nema bakpokans'
# '(afskrifuðu) engar eignir nema hlutaféð'
NlStakNema/tala/fall/kyn →
    NlStakNemaFn/tala/fall/kyn Nl/fall/tala/kyn? NlNema/fall

NlNema/fall → 'nema:st' Nl/fall

NlStakNemaFn/tala/fall/kyn →
    'allur:fn'/tala/fall/kyn | 'enginn:fn'/tala/fall/kyn

NlStakTagl/tala/fall/kyn →
    FornafnEinkunn/tala/fall/kyn+ to/tala/fall/kyn?    # (stjórnarframkvæmdum) þessum öllum, (bílana) mína tvo
    | EignarFornafn/tala/fall/kyn KommaOgEðaEn EfLiður # skattskýrslur mínar og eiginkonu minnar
    | EinnOgSér/tala/fall/kyn

NlStak_p3_ft/fall/hk →
    'það:pfn'_hk_ft/fall Persóna/fall # Þau Páll / Þau Pál / þeim Páli / þeirra Páls

EinnOgSér/tala/fall/kyn →
    'einn:fn'/tala/fall/kyn "og:st"? "sér:ao"           # skipulagsbreytingar einar [og] sér

$score(+50) EinnOgSér/tala/fall/kyn # Þarf hærra skor en FornafnEinkunn

LoTöluorð_ef →
    'tvennur:lo'_ef_et_hk
    | 'þrennur:lo'_ef_et_hk
    | 'fern:lo'_ef_et_hk

# Feðgarnir séra Jón Steinsson [í Víðihlíð] og Arnaldur Jónsson bóndi
Skyldleiki_kk/fall → 'feðgar:kk'_gr_ft/fall | 'bróðir:kk'_gr_ft/fall | 'frændi:kk'_gr_ft/fall
# Mæðgurnar frú Jóhanna Sigurjónsdóttir og Anna Kristjánsdóttir
Skyldleiki_kvk/fall → 'mæðgur:kvk'_gr_ft/fall | 'systir:kvk'_gr_ft/fall | 'frænka:kvk'_gr_ft/fall
# Systkinin Jón og Anna, feðginin Friðmar og Jóna
Skyldleiki_hk/fall → 'systkin:hk'_gr_ft/fall | 'frændsystkin:hk'_gr_ft/fall | 'mæðgin:hk'_gr_ft/fall | 'feðgin:hk'_gr_ft/fall

Fjölskylda_ft/kyn/fall → Skyldleiki/kyn/fall Manneskja/fall/kyn FsRuna? Og Manneskja/fall/kyn
Fjölskylda_ft_hk/fall → Skyldleiki_hk/fall Manneskja/fall FsRuna? Og Manneskja/fall

NlStak_ft_p3/fall/kyn → Fjölskylda_ft/kyn/fall FsRunaEftirNl?

$score(+8) Fjölskylda_ft/kyn/fall

# Sértilvik fyrir "vinstri-græn" (ath: stutt bandstrik, ekki en- eða em-dash)
NlStak_ft_p3_nf_hk → 'vinstri:lo'_ft_nf_hk "-"? 'grænn:lo'_ft_nf_hk
NlStak_ft_p3_þf_hk → 'vinstri:lo'_ft_þf_hk "-"? 'grænn:lo'_ft_þf_hk
NlStak_ft_p3_þgf_hk → 'vinstri:lo'_ft_þgf_hk "-"? 'grænn:lo'_ft_þgf_hk
NlStak_ft_p3_ef_hk → 'vinstri:lo'_ft_ef_hk "-"? 'grænn:lo'_ft_ef_hk

# Sértilvik fyrir "COVID-19" (ath: stutt bandstrik, ekki en- eða em-dash)
NlStak_et_p3/fall/kvk → NlCovid/fall
NlCovid/fall → "covid:kvk" "-" "19"

$score(+4) NlCovid/fall

# 'hvorugt okkar systkinanna', 'enginn okkar', 'einhver ykkar bræðranna', 'einn liðsmanna', 'annar mannanna'
NlStak_et_p3/fall/kyn → OkkarFornafn/fall/kyn OkkarFramhald/kyn

OkkarFramhald/kyn →
    'ég:pfn'_ft_ef Nl_ef_ft/kyn?
    | Nl_ef_ft/kyn

OkkarFornafn/fall/kyn →
    'hvorugur:fn'_et/fall/kyn | 'enginn:fn'_et/fall/kyn | 'einhver:fn'_et/fall/kyn
    | 'einn:fn'_et/fall/kyn | 'sérhver:fn'_et/fall/kyn
    | 'annar:fn'_et/fall/kyn 'hvor:fn'_et/fall/kyn?
    | VillaAnnaðhvort  # 'annaðhvort systkinanna'

# Eignarfornöfn standa með nafnlið í sama falli og hann,
# þannig að gera þarf undantekningu fyrir þau þegar þau
# tengjast eignarfallslið ('Ég seldi ekki íbúðina þína, heldur frænda þíns')
EignarFornafn/tala/fall/kyn →
    'minn:fn'/tala/fall/kyn | 'þinn:fn'/tala/fall/kyn

NlFornafn/tala/fall/kyn →
    # Stakt fornafn, e.t.v. með einkunn og töluorði
    # 'augljóslega sá vondi' / 'einhverjir tveir góðir' / 'ekkert uppbyggilegt' / 'fjórir aðrir'
    # 'þeir báðir' / 'hún öll' / 'þær sjálfar'
    # 'öllu því' / 'neinum þeirra'
    AtviksliðurEinkunn* Töluorð/tala/fall/kyn?
        Fornafn/tala/fall/kyn FornafnViðskeyti/tala/fall/kyn? LoLiður/tala/fall/kyn*
    # 'okkar besta' / 'óumdeilanlega hans versta'
    | AtviksliðurEinkunn* EignarFornafn LoLiður/tala/fall/kyn

# 'Hann hafði aldrei heyrt neitt slíkt'
NlFornafn_et_nf_hk → NeittSlíkt_nf
NlFornafn_et_þf_hk → NeittSlíkt_þf
NlFornafn_et_þgf_hk → NeittSlíkt_þgf
NlFornafn_et_ef_hk → NeittSlíkt_ef

NeittSlíkt/fall → 'neinn:fn'_et_hk/fall 'slíkur:fn'_et_hk/fall

$score(+4) NeittSlíkt/fall

NlFornafn_et_nf_hk → AllurFornafn_nf
NlFornafn_et_þf_hk → AllurFornafn_þf
NlFornafn_et_þgf_hk → AllurFornafn_þgf
NlFornafn_et_ef_hk → AllurFornafn_ef

AllurFornafn/fall → AllurÞessi/fall | AllurMinn/fall | AllurÞinn/fall | AllurSinn/fall

$score(+4) AllurFornafn/fall

# 'Allt þetta (vekur mér ugg)'
# '(Við ræddum um) allt þetta'
# '(Ég vildi komast frá) öllu þessu'
# '(Vegna) alls þessa (er mér ekki rótt)'
AllurÞessi/fall → 'allur:fn'_et_hk/fall 'þessi:fn'_et_hk/fall

# 'Allt mitt (er þitt)'
# '(Ég fór með) allt mitt (til sýslumanns)'
AllurMinn/fall → 'allur:fn'_et_hk/fall 'minn:fn'_et_hk/fall

# 'Allt þitt (er mitt)'
AllurÞinn/fall → 'allur:fn'_et_hk/fall 'þinn:fn'_et_hk/fall

# '(Hann fór með) allt sitt (og henti því)'
AllurSinn/fall → 'allur:fn'_et_hk/fall 'sinn:fn'_et_hk/fall

Pfn_kk/tala/fall → 'hann:pfn'_kk/tala/fall
Pfn_kvk/tala/fall → 'hún:pfn'_kvk/tala/fall
Pfn_hk/tala/fall → 'það:pfn'_hk/tala/fall
# 'hán' er aðeins til í eintölu; fleirtölumyndin er 'þau'
Pfn_hk_et/fall → 'hán:pfn'_hk/tala/fall

Pfn/tala/fall/kyn → Pfn/kyn/tala/fall

FornafnViðskeyti/tala/fall/kyn →
    AtviksliðurAuka? LoFornafn/fall/tala/kyn # (hafa þeir) [þó] báðir (lýst yfir sigri)
    | Pfn/kyn/tala/fall         # öllu því, ýmsum þeim
    | Töluorð/tala/fall/kyn     # öllum fjórum, þeirra tveggja
    | 'hinn:gr'/tala/fall/kyn   # allt hið versta

FornafnViðskeyti_et/fall/kyn →
    # neinum þeirra þriggja, öllum þeirra, hvern þeirra fjögurra
    Pfn_ft_ef/kyn Töluorð_ft_ef/kyn? FornafnViðskeytiLo_ft_ef/kyn?

FornafnViðskeytiLo_ft_ef/kyn →
    AtviksliðurEinkunn* lo_vb_ft_ef/kyn # hvern þeirra fjögurra langsamlega sigursælustu

FornafnViðskeyti_ft/fall/kyn →
    HverHvor/fall/kyn       # þá hvern fyrir sig / þær hverja í sínu lagi / öll hvert á fætur öðru

SáÞessi/fall/tala/kyn →
    'sá:fn'/fall/tala/kyn | 'þessi:fn'/fall/tala/kyn

# Sértilvik þar sem "allt" kemur framan við fornafn óháð tölu, falli og kyni
# allt annar | allt aðrir
# allt önnur | allt aðrar
# allt annað | allt önnur
NlFornafn/tala/fall/kyn →
    Allt 'annar:fn'/tala/fall/kyn

# Sértilvik þar sem lýsingarorðin "margur"/"fjölmargur" standa
# fyrir framan fornafn og úr verður sjálfstæður nafnliður.
# "flestur" er fornafn í BÍN og þarf ekki sérreglu.
NlFornafn/tala/fall/kyn →
    # margt annað / mörgu öðru / margs annars
    # margir aðrir / marga aðra / mörgum öðrum / margra annarra
    # fáir aðrir / örfáir aðrir
    AtviksliðurEinkunn* 'margur:lo'_sb/tala/fall/kyn 'annar:fn'/tala/fall/kyn
    | 'allmargur:lo'_sb/tala/fall/kyn 'annar:fn'/tala/fall/kyn
    | 'fjölmargur:lo'_sb/tala/fall/kyn 'annar:fn'/tala/fall/kyn
    | AtviksliðurEinkunn* 'fár:lo'_sb/tala/fall/kyn 'annar:fn'/tala/fall/kyn
    | AtviksliðurEinkunn* 'örfár:lo'_sb/tala/fall/kyn 'annar:fn'/tala/fall/kyn

# Sértilvik þar sem "ýmislegt annað" er sjálfstæður nafnliður:
# 'Ýmislegt annað (bendir til þess að Jón sé þjófurinn)'
# Þetta virðist aðeins vera hægt í eintölu, ekki fleirtölu.
NlFornafn_et_nf_hk → ÝmislegtAnnað_nf
NlFornafn_et_þf_hk → ÝmislegtAnnað_þf
NlFornafn_et_þgf_hk → ÝmislegtAnnað_þgf
NlFornafn_et_ef_hk → ÝmislegtAnnað_ef

ÝmislegtAnnað/fall →
    'ýmislegur:lo'_sb_et_hk/fall 'annar:fn'_et_hk/fall

$score(+15) ÝmislegtAnnað/fall

NlFornafn/tala/fall/kyn →
    SáSem/tala/fall/kyn

# Reyna sterklega að greina 'sá sem...' sem einn samhangandi lið
SáSem/tala/fall/kyn →
    'sá:fn'/tala/fall/kyn Tilvísunarsetning_p3/tala/kyn

$score(+96) SáSem/tala/fall/kyn

# Gefa stökum fornöfnum mínusstig - við viljum frekar sjá þau sem hluta af stærri nafnlið
# Mínusinn þarf að vera nógu stór til að yfirvinna plúsinn fyrir andlag sagnar
$score(-12) NlFornafn/tala/fall/kyn

GreinirMeðLo/tala/fall/kyn →
    Greinir/tala/fall/kyn LoLiður/tala/fall/kyn

Greinir/tala/fall/kyn →
    'hinn:gr'/tala/fall/kyn

# Stigin þurfa að yfirvinna FornafnEinkunn, þannig að
# 'hinn/hinu' sé þáttað sem greinir fremur en fornafn
$score(+24) GreinirMeðLo/tala/fall/kyn

# Lýsingarorð sem stendur sem nafnliður:
# 'Margir/fjölmargir/allmargir/heilmargir komu og aðstoðuðu Pál'
# 'Páll aðstoðaði töluvert marga'
# 'Óleyfilegt er að mismuna samkynhneigðum'

NlStak_p3/tala/fall/kyn →
    AtviksliðurEinkunn* LoSemNafnliður/tala/fall/kyn

# 'Fleiri/færri komu í veisluna en ég bjóst við'
# 'Fleirum fannst kvikmyndin leiðinleg'
NlStak_p3_ft/fall/kyn →
    'margur:lo'_mst_ft/fall/kyn
    | 'fár:lo'_mst_ft/fall/kyn

NlStak_p3/tala/fall/kyn →
    Magn/fall/tala/kyn MagnAfLiður?

MagnAfLiður → 'af:fs'_þgf Nl_þgf/kyn

$score(+30) MagnAfLiður

# Í uppskriftum má segja "fjórir bollar hveiti", "einn desilítri sítrónusafi",
# "hálft kíló ólífur", en bara í nefnifalli

NlStak_p3_et_nf/kyn →
    KynlaustMagn Nl_nf_et/kyn

NlStak_p3_ft_nf/kyn →
    KynlaustMagn Nl_nf_ft/kyn

KynlaustMagn →
    Magn_nf/tala/kyn

Magn/fall/tala/kyn →
    TöluorðForskeyti/tala/fall/kyn* mælieining # rúmlega 690 MW
    | tala "-:en" mælieining # 100-400 g
    | TöluorðForskeyti/tala/fall/kyn* Töluorð/tala/fall/kyn Mælieining/kyn/tala/fall # tæplega fjögurra tonna

Mælieining_hk/tala/fall →
    MælieiningÞyngd_hk/tala/fall
    | MælieiningLengd_hk/tala/fall

Mælieining_kvk/tala/fall →
    MælieiningÞyngd_kvk/tala/fall
    | MælieiningHiti_kvk/tala/fall
    | MælieiningLengd_kvk/tala/fall
    | MælieiningRúmmál_kvk/tala/fall

Mælieining_kk/tala/fall →
    MælieiningLengd_kk/tala/fall
    | MælieiningRúmmál_kk/tala/fall
    | MælieiningFlatarmál_kk/tala/fall

MælieiningÞyngd_hk/tala/fall →
    'kíló:hk'/tala/fall
    | 'tonn:hk'/tala/fall
    | 'gramm:hk'/tala/fall
    | 'kílógramm:hk'/tala/fall
    | 'míkrógramm:hk'/tala/fall
    | 'milligramm:hk'/tala/fall
    | 'millígramm:hk'/tala/fall

MælieiningÞyngd_kvk/tala/fall →
    'lest:kvk'/tala/fall

MælieiningHiti_kvk/tala/fall →
    'gráða:kvk'/tala/fall

MælieiningLengd_kk/tala/fall →
    'metri:kk'/tala/fall
    | 'kílómetri:kk'/tala/fall
    | 'millimetri:kk'/tala/fall
    | 'millímetri:kk'/tala/fall
    | 'sentimetri:kk'/tala/fall
    | 'sentímetri:kk'/tala/fall
    | 'faðmur:kk'/tala/fall

MælieiningLengd_kvk/tala/fall →
    'alin:kvk'/tala/fall

MælieiningLengd_hk/tala/fall →
    'fet:hk'/tala/fall

MælieiningRúmmál_kk/tala/fall →
    'rúmmetri:kk'/tala/fall
    | 'rúmsentimetri:kk'/tala/fall
    | 'rúmsentímetri:kk'/tala/fall
    | 'lítri:kk'/tala/fall
    | 'hektólítri:kk'/tala/fall
    | 'desilítri:kk'/tala/fall
    | 'desílítri:kk'/tala/fall
    | 'sentilítri:kk'/tala/fall
    | 'sentílítri:kk'/tala/fall
    | 'millilítri:kk'/tala/fall
    | 'millílítri:kk'/tala/fall
    | 'bolli:kk'/tala/fall

MælieiningRúmmál_kvk/tala/fall →
    'rúmlest:kvk'/tala/fall

MælieiningFlatarmál_kk/tala/fall →
    'fermetri:kk'/tala/fall
    | 'fersentímetri:kk'/tala/fall
    | 'fersentimetri:kk'/tala/fall
    | 'ferkílómetri:kk'/tala/fall
    | 'hektari:kk'/tala/fall

$score(+8) Magn_nf/tala/kyn
$score(+8) Magn_þf/tala/kyn
$score(+8) Magn_þgf/tala/kyn
$score(+16) Magn_ef/tala/kyn # Gefa eignarfalli aukastig til að hvetja til forskeytis við nafnlið

# Unnt er að hafa nafnlið í þágufalli á undan lýsingarorðinu:
# '(sem varðar hagsmuni hans eða) honum [verulega] nákominna'
# Að svo komnu máli er slíkt aðeins leyft með lýsingarorðum í eignarfalli
# enda óljóst að þörf sé fyrir önnur föll.

NlStak_p3_et_ef/kyn →
    Nl_þgf AtviksliðurEinkunn* LoSemNafnliður_et_ef/kyn

NlStak_p3_ft_ef/kyn →
    Nl_þgf AtviksliðurEinkunn* LoSemNafnliður_ft_ef/kyn

LoSemNafnliður/tala/fall/kyn →
    lo_sb/tala/fall/kyn | lo_mst/tala/fall/kyn # Efsta stig (ESB) er innifalið í _sb
    | AlgengLoSemNafnliður_kk/tala/fall
    | AlgengLoSemNafnliður_hk_et/fall

# Margir telja að myndin sé falleg / Margur telur mig sig
# Fáir álíta að flokkurinn eigi möguleika
# Þeir vita mikið / Mikið af því sem Jón sagði er della / Ég vænti mikils af honum
AlgengLoSemNafnliður_kk_ft/fall →
    'margur:lo'_sb_ft_kk/fall           # margir telja
    | 'fjölmargur:lo'_sb_ft_kk/fall     # fjölmargir telja
    | 'allmargur:lo'_sb_ft_kk/fall      # allmargir telja
    | 'allflestur:lo'_sb_ft_kk/fall     # allflestir telja
    | 'langflestur:lo'_sb_ft_kk/fall    # langflestir telja
    | 'fár:lo'_sb_ft_kk/fall            # fáir / fæstir telja
    | 'örfár:lo'_sb_ft_kk/fall          # örfáir telja

AlgengLoSemNafnliður_kk_et/fall →
    'margur:lo'_sb_et_kk/fall           # margur telur

AlgengLoSemNafnliður_hk_et/fall →
    'mikill:lo'_sb_et_hk/fall           # þeir vita mikið / ég vænti mikils af honum

# Gefa lýsingarorðum sem nafnliðum mínusstig
$score(-16) LoSemNafnliður/tala/fall/kyn

# ...en hlífa þó lýsingarorðum sem standa oft sjálfstætt sem nafnliðir
$score(+12) AlgengLoSemNafnliður_kk/tala/fall
$score(+12) AlgengLoSemNafnliður_hk_et/fall

NlStak_p3/tala/fall/kyn →
    'hver:fn'/tala/fall/kyn 'sá:fn'/tala/fall/kyn # Hver sá, hver sú, hvert það, hverjir þeir, hverjar þær, hver þau
    | 'hvor:fn'/tala/fall/kyn 'tveir:to'_ft_ef/kyn  # (eða) hvor/hvort tveggja
    # '(Sýslumenn sinna) sama hlutverki og prestar'
    # '(Ríkissaksóknari nýtur) sömu verndar í starfi og dómarar'
    # '(Hér gilda) sömu reglur [eins] og í Danmörku'
    # !!! ATH: Ekki virðist virka að nota 'samur:fn' hér
    | eo* 'sami:fn'/tala/fall/kyn Nl/fall/tala/kyn SamurTagl

SamurTagl →
    Og Nl_nf | "eins:ao"? Og FsRunaEftirNl

$score(+8) SamurTagl

NlStak_p3/tala/fall/kyn →
    # 'Sú útskýring [núna], að Samfylkingin sé óalandi og óferjandi, (dugir ekki lengur)'
    NlSáAð/tala/fall/kyn

NlSáAð/tala/fall/kyn →
    # (Jón gaf út yfirlýsingu) þess efnis að hann væri hættur í pólitík
    'sá:fn'/tala/fall/kyn Nl/fall/tala/kyn AtvFs? Útskýring

$score(+16) NlSáAð/tala/fall/kyn

NlStak_p3_et_nf_hk → NlHittOgÞetta_nf
NlStak_p3_et_þf_hk → NlHittOgÞetta_þf
NlStak_p3_et_þgf_hk → NlHittOgÞetta_þgf
NlStak_p3_et_ef_hk → NlHittOgÞetta_ef

NlHittOgÞetta/fall →
    'hinn:fn'_et_hk/fall Og 'þessi:fn'_et_hk/fall  # hitt og þetta

$score(+16) NlHittOgÞetta/fall

# Töluorð sem sjálfstæðir nafnliðir

NlStak_p3/tala/fall/kyn →
    NlTöluorð/tala/fall/kyn

NlTöluorð/tala/fall/kyn →
    # '(Ég talaði við) tvær þeirra', 'fjórum þeirra (var kalt)'
    # 'Að minnsta kosti fjórir létust'
    # 'Um þrír af hverjum fjórum landsmönnum telja...'
    TöluorðForskeyti/tala/fall/kyn* TöluorðLo/tala? Töluorð/tala/fall/kyn 'sá:fn'_ft_ef/kyn?
    | TöluorðForskeyti/tala/fall/kyn* Töluorð/tala/fall/kyn TöluorðLo/tala  # (önnur af) þremur hröðustu (sem eftir standa)
    | 'sá:fn'_ft/fall/kyn? Töluorð/tala/fall/kyn EfLiður_ft/kyn?            # (ein af) þeim fjórum stúlknanna (sem stóðu sig best)

NlTöluorð_ft_nf_hk → Prósentubil
NlTöluorð_ft_þf_hk → Prósentubil
NlTöluorð_ft_þgf_hk → Prósentubil
NlTöluorð_ft_ef_hk → Prósentubil

Prósentubil → TöluorðForskeyti* tala "-:en" prósenta

$score(+4) Prósentubil

TöluorðForskeyti →
    "um:ao" | "minnst:ao" | "einungis:ao" | "aðeins:ao" | "ekki:ao"
    | 'að_minnsta_kosti:ao' | 'um_það_bil:ao'
    | "tæplega:ao" | "rúmlega:ao" | "ríflega:ao" | "einnig:ao" | "samtals:ao"
    | "næstum:ao" | "nálægt:ao" | "yfir:ao"
    # Fleiryrt forskeyti
    | "hátt:ao" "í:ao"
    | Allt "að:ao"
    | "innan:ao" "við:ao"
    | "svo:st" "sem:st"

TöluorðForskeyti/tala/fall/kyn →
    TöluorðForskeyti
    | lo_sb/tala/fall/kyn # tæpar/rúmar 115 milljónir
    | 'sá:fn'/tala/fall/kyn # þeim tæpu 120 milljónum

TöluorðForskeyti_ef_et_kk → TöluorðForskeyti_et_ef_kk
TöluorðForskeyti_ef_et_kvk → TöluorðForskeyti_et_ef_kvk
TöluorðForskeyti_ef_et_hk → TöluorðForskeyti_et_ef_hk
TöluorðForskeyti_ef_ft_kk → TöluorðForskeyti_ft_ef_kk
TöluorðForskeyti_ef_ft_kvk → TöluorðForskeyti_ft_ef_kvk
TöluorðForskeyti_ef_ft_hk → TöluorðForskeyti_ft_ef_hk

TöluorðLo/tala →
    lo_evb_nf_hk/tala   # efstu tvo / efstu tvö / efstu tvær
    | lo_mst_nf_hk/tala # lægri tvo / lægri tvö / lægri tvær

# Gefa mínusstig fyrir stök töluorð - viljum helst sjá þau sem einkunn í stærri nafnlið
$score(-10) NlTöluorð/tala/fall/kyn
$score(+12) TöluorðForskeyti # Rétta mínusinn af ef fleira fylgir

# '1100 manns (voru á skemmtuninni)', '(Ég pantaði mat fyrir) sautján manns'
# Virðist ekki ganga upp í eignarfalli
# ('Ég lagði bílum 18 manns'?)
NlStak_p3_ft_nf_kk → Manns_nf
NlStak_p3_ft_þf_kk → Manns_þf
NlStak_p3_ft_þgf_kk → Manns_þgf

# Leyfa '30.000 manns, íklædd gulum vestum, mótmæltu í gær'
NlStak_p3_ft_nf_hk → Manns_hk_nf
NlStak_p3_ft_þf_hk → Manns_hk_þf
NlStak_p3_ft_þgf_hk → Manns_hk_þgf

Manns_hk/fall → Manns/fall

# Hafa veika tilhneigingu til að velja karlkyn á 'N manns' umfram hvorugkyn
$score(-2) Manns_hk/fall

# Hér ganga beygjanleg töluorð ekki (?'fjórir manns')
Manns/fall →
    eo* TöluorðForskeyti? Tala_ft_kk/fall "manns:kk"
    | eo* Talnabil "manns:kk" # 50-100 manns

# Tölur í tölustöfum og óbeygjanleg töluorð (fimm, sex, ...)
Tala/tala/kyn/fall →
    töl/tala/kyn/fall | tala/tala/kyn/fall

Tala/kyn/fall →
    töl/kyn/fall | tala/kyn/fall

# Í númerum og öðrum álíka tilvikum leyfum við einnig
# beygjanleg töluorð: 'herbergi númer þrjú'
Tala_hk_nf → to_et_hk_nf | to_ft_hk_nf

# 3p. eintala

NlStak_et_p3/fall/kyn →
    NlStak_p3_et/fall/kyn VeikSögnLhÞt_et/fall/kyn? FsRunaEftirNl?  # fulltrúi bankans í nefndinni
    | NlStak_et_p3/fall/kyn Eða NlStak_p3_et/fall              # Þingnefnd eða hluti Alþingismanna getur lagt fram frumvarp
    | 'hver:fn'_et/fall/kyn Og 'einn:fn'_et/fall/kyn FsRunaEftirNl?   # 'hver og einn [í sinni sveit] (ber ábyrgð)'
    | EinnAf_et_p3/fall/kyn                                         # einn af drengjunum [sem tóku þátt] (vann keppnina)

# '(svör við bréfi sínu) dagsettu (13. mars sl.)'
VeikSögnLhÞt/tala/fall/kyn →
    Atviksliður? EinVeikSögnLhÞt/tala/fall/kyn                      # (Stúlkan sá pabba sinn) [vel og snyrtilega] rakaðan
    | "," so_lhþt/tala/fall/kyn FsAtv? LokatáknEðaKomma

EinVeikSögnLhÞt/tala/fall/kyn → so_lhþt/tala/fall/kyn

$score(-4) EinVeikSögnLhÞt/tala/fall/kyn

EinnAf_et_p3/fall/kyn → 
    'einn:fn'_et/fall/kyn AfLiður/kyn

$score(+12) EinnAf_et_p3/fall/kyn

# Meðhöndla "árið 2012" og "árin 2013 og 2014" með háum forgangi

NlStak_et_p3_nf_hk → Árið_nf
NlStak_et_p3_þf_hk → Árið_þf
NlStak_et_p3_þgf_hk → Árið_þgf
NlStak_et_p3_ef_hk → Árið_ef

Árið/fall →
    ártal
    | 'rekstrarár:hk'_gr_et/fall tala
    | 'skólaár:hk'_gr_et/fall tala

$score(+6) Árið/fall

NlStak_ft_p3_nf_hk → Árin_nf
NlStak_ft_p3_þf_hk → Árin_þf
NlStak_ft_p3_þgf_hk → Árin_þgf
NlStak_ft_p3_ef_hk → Árin_ef

Árin/fall → 
    ártal ÁraRuna* Til tala                          # (Ég á hér við) 1987, 1988 og 1989
    | 'ár:hk'_ft_gr/fall tala ÁraRuna* Til tala      # (Hann ræddi) árin 670-80, (Bókin tekur til) áranna 2016 t.o.m. 2017

ÁraRuna → "," tala

$score(+20) Árin/fall

# Leyfa frístandandi sérnöfn (skrifuð sem runa orða með stórum staf)
# sem nafnliði 3p. eintölu hvorugkyns, í öllum föllum
# Orka Energy, OG Capital

NlStak_p3_et_nf_hk → NlSérnafn
NlStak_p3_et_þf_hk → NlSérnafn
NlStak_p3_et_þgf_hk → NlSérnafn
NlStak_p3_et_ef_hk → NlSérnafn

# Leyfa frístandandi sérnöfn í karlkyni og kvenkyni,
# t.d. 'Cole var giftur Sigríði',
# en þá með lægra skori en hvorugkynið

NlStak_p3_et/fall/nkyn → NlSérnafn_nkyn

NlSérnafn_nkyn → NlSérnafn

$score(-8) NlSérnafn_nkyn

# Sérnöfn þurfa einnig að geta staðið í fleirtölu,
# sbr. 'Valsmenn skoruðu flest mörk', 'Dodgers áttu sigurinn vísan',
# 'Essenar voru trúflokkur'

NlStak_p3_ft_nf_hk → NlSérnafn_ft
NlStak_p3_ft_þf_hk → NlSérnafn_ft
NlStak_p3_ft_þgf_hk → NlSérnafn_ft
NlStak_p3_ft_ef_hk → NlSérnafn_ft

NlSérnafn_ft → NlSérnafn

# Gefa mínus fyrir fleirtölu m.v. eintölu
$score(-2) NlSérnafn_ft

# !!! no_ef er notað í stað EfLiður til að minnka margræðni þegar
# !!! mörg sérnöfn standa í röð
NlSérnafn → eo* Sérnafn NlSérnafnEf? # ekki Nakamoto, örugglega Nelson Mandela, Hong Kong norðursins

NlSérnafnEf → no_ef

# Forðast að nota sérnöfn sem nafnliði, ef aðrir kostir eru í boði
$score(-16) NlSérnafn

Sérnafn →
    sérnafn tala? | entity tala? | LangtSérnafn

$tag(apply_length_bonus) Sérnafn # Gefa bónus eftir því hversu margar eindir (tokens) falla undir sérnafnið

# ATH: eitt 'entity' getur verið fleiri en eitt samliggjandi orð.
# Ekki ætti að hafa samliggjandi 'entity' eða 'entity+' í málfræðireglu.
# Hér eru höfð stutt bandstrik milli nafna, ekki en- eða em-dash.

LangtSérnafn →
    sérnafn "-"? sérnafn+ tala?             # Glass-Steagall lögin
    | sérnafn+ "-"? entity sérnafn* tala?   # Eining með nafni sem þekkt er úr gagnagrunni eða runu slíkra
    | entity "-"? sérnafn+ tala?            # Eining með nafni sem þekkt er úr gagnagrunni eða runu slíkra
    | entity "-" entity tala?               # Villeneuve-Loubet
    | entity "-" tala                       # FTSE-100
    | SérnafnFyrirtæki

# Reglu breytt þar sem fyrirtækjaheiti eru nú sameinuð í einn tóka.
SérnafnFyrirtæki →
    sérnafn+ BandstrikSérnöfn? tala? fyrirtæki # Standard Oil Corp. / Ísgerð Dísu ehf.
    | fyrirtæki

BandstrikSérnöfn →
    "-" sérnafn*

Sérnafn/fall →
    sérnafn/fall tala? | LangtSérnafn/fall tala?

$tag(apply_length_bonus) Sérnafn/fall # Gefa bónus eftir því hversu margar eindir (tokens) falla undir sérnafnið

LangtSérnafn/fall →
    sérnafn/fall "-"? sérnafn/fall+ tala?              # Stóra-Felli
    | sérnafn/fall+ "-"? entity sérnafn/fall* tala?    # Eining með nafni sem þekkt er úr gagnagrunni eða runu slíkra
    | entity "-"? sérnafn/fall+ tala?                  # Eining með nafni sem þekkt er úr gagnagrunni eða runu slíkra

# Löng sérnöfn, þ.e. runur orða með upphafsstöfum, eru mjög líklega rétt greind

$score(+6) LangtSérnafn
$score(+10) LangtSérnafn/fall

# Heimilisfang (götuheiti + húsnúmer)

NlStak_et_p3/fall/kyn → Heimilisfang/fall/kyn

# Ath: 'gata' getur tengst við fl='göt' og fl='örn'
Heimilisfang/fall/kyn → gata/fall/kyn Húsnúmer

Húsnúmer → tala | talameðbókstaf

$score(+4) Heimilisfang/fall/kyn

# Hér er '/fall/hk' hakk til að þurfa ekki að taka fram hvert einasta fall með hvorugkyni
NlStak_p3_et/fall/hk → 
    Lén/fall
    | Myllumerki/fall
    | Notandanafn/fall
    | Kennitala/fall
    | Símanúmer/fall
    | Sameind/fall
    | Tölvupóstfang/fall
    | Vörunúmer/fall
    | Vefslóð/fall

Lén/fall → lén/fall
Myllumerki/fall → myllumerki/fall
Notandanafn/fall → notandanafn/fall
Kennitala/fall → kennitala/fall
Símanúmer/fall → símanúmer/fall
Sameind/fall → sameind/fall
Tölvupóstfang/fall → tölvupóstfang/fall
Vörunúmer/fall → vörunúmer/fall
Vefslóð/fall → vefslóð/fall

NlStak_p3_et/fall → NlStak_p3_et/fall/kyn # Samtenging tveggja eintöluliða með 'eða' krefst ekki sama kyns báðum megin

# 3p. fleirtala

NlStak_ft_p3/fall/kyn →
    NlStak_p3_ft/fall/kyn VeikSögnLhÞt_ft/fall/kyn? FsRunaEftirNl?  # fulltrúar bankans í nefndinni
    | 'allur:fn'_ft/fall/kyn 'sá:fn'_ft/fall/kyn                    # Allir þeir, allar þær, öll þau
    | VillaEinnAf/fall/kyn           # Einn af mönnunum *gengu á vegg; ein af hverjum fjórum stúlkum *stóðu sig vel
    | VillaFjöldiHluti/fall/kyn      # Mikill fjöldi/meirihluti/minnihluti unglinga [reyndust hæfir → reyndist hæfur]

# Hvorki X né Y
# Bæði X og Y
# Annaðhvort X eða Y
# Annað hvort X eða Y
# Ýmist X eða Y

# Sama kyn á X og Y

NlStak_ft_p3/fall/kyn → NlBæðiHvorki/fall/kyn

NlBæðiHvorki/fall/kyn →
    Bæði Nl/fall/kyn Og Nl/fall/kyn       # (Við þurftum að lóga) bæði hestinum og kettinum
    | Hvorki Nl/fall/kyn Né Nl/fall/kyn   # Hvorki hestinum né kettinum (var lógað)
    | AnnaðhvortSt Nl/fall/kyn Eða Nl/fall/kyn   # Annaðhvort hestinum eða kettinum (var lógað)
    | Ýmist Nl/fall/kyn Eða Nl/fall/kyn   # Ýmist hestinum eða kettinum (var lógað)

$score(+4) NlBæðiHvorki/fall/kyn

NlStak_ft_p3_nf_hk → NlStak_ft_p3_hk_nf
NlStak_ft_p3_þf_hk → NlStak_ft_p3_hk_þf
NlStak_ft_p3_þgf_hk → NlStak_ft_p3_hk_þgf
NlStak_ft_p3_ef_hk → NlStak_ft_p3_hk_ef

Nl/kyn/fall → Nl/fall/kyn

# Mismunandi kyn á X og Y, útkoman er hvorugkyns

NlStak_ft_p3_hk/fall → NlBæðiHvorki_p3_hk/fall

NlBæðiHvorki_p3_hk/fall →

    Bæði Nl_kk/fall Og Nl_kvk/fall
    | Bæði Nl_kvk/fall Og Nl_kk/fall
    | Bæði Nl_kk/fall Og Nl_hk/fall
    | Bæði Nl_hk/fall Og Nl_kk/fall
    | Bæði Nl_kvk/fall Og Nl_hk/fall
    | Bæði Nl_hk/fall Og Nl_kvk/fall

    | Hvorki Nl_kk/fall Né Nl_kvk/fall
    | Hvorki Nl_kvk/fall Né Nl_kk/fall
    | Hvorki Nl_kk/fall Né Nl_hk/fall
    | Hvorki Nl_hk/fall Né Nl_kk/fall
    | Hvorki Nl_kvk/fall Né Nl_hk/fall
    | Hvorki Nl_hk/fall Né Nl_kvk/fall

    | AnnaðhvortSt Nl_kk/fall Eða Nl_kvk/fall
    | AnnaðhvortSt Nl_kvk/fall Eða Nl_kk/fall
    | AnnaðhvortSt Nl_kk/fall Eða Nl_hk/fall
    | AnnaðhvortSt Nl_hk/fall Eða Nl_kk/fall
    | AnnaðhvortSt Nl_kvk/fall Eða Nl_hk/fall
    | AnnaðhvortSt Nl_hk/fall Eða Nl_kvk/fall

    | Ýmist Nl_kk/fall Eða Nl_kvk/fall
    | Ýmist Nl_kvk/fall Eða Nl_kk/fall
    | Ýmist Nl_kk/fall Eða Nl_hk/fall
    | Ýmist Nl_hk/fall Eða Nl_kk/fall
    | Ýmist Nl_kvk/fall Eða Nl_hk/fall
    | Ýmist Nl_hk/fall Eða Nl_kvk/fall

$score(+4) NlBæðiHvorki_p3_hk/fall

NlStak_ft_p1/fall/kyn → NlBæðiHvorki_p1_hk/fall

NlBæðiHvorki_p1_hk/fall →
    Bæði Nl_p1/fall/kyn Og Nl_p2/fall/kyn             # bæði mér og þér þykir þetta óásættanlegt
    | Hvorki Nl_p1/fall/kyn Né Nl_p2/fall/kyn         # hvorki ég né þú höfum vit á þessu
    | AnnaðhvortSt Nl_p1/fall/kyn Eða Nl_p2/fall/kyn  # annaðhvort ég eða þú þurfum að fara
    | Bæði Nl_p2/fall/kyn Og Nl_p1/fall/kyn           # bæði þér og mér þykir þetta óásættanlegt
    | Hvorki Nl_p2/fall/kyn Né Nl_p1/fall/kyn         # hvorki þú né ég erum fallegar/falleg/fallegir
    | AnnaðhvortSt Nl_p2/fall/kyn Eða Nl_p1/fall/kyn  # annaðhvort þú eða ég erum fallegar/falleg/fallegir
    | Ýmist Nl_p2/fall/kyn Eða Nl_p1/fall/kyn         # ýmist þú eða ég erum fallegar/falleg/fallegir

$score(+4) NlBæðiHvorki_p1_hk/fall

# Skammstafanir með bandstriki nota stutt bandstrik, ekki en- eða em-dash

# Athugið að þessar reglur eiga einungis við um laus bandstrik, þ.e. með
# eyðum utan um; 'Microsoft-hugbúnaðarrisinn' er tókað sem eitt orð,
# en 'Microsoft - hugbúnaðarrisinn' eru þrír tókar.

Skst →
    Skst_abbrev
    | Sérnafn "-"?              # Barclays - bankann, Al Thani - málið
    | Myllumerki_abbrev         # #metoo-umræðan, #eurovision tístin

$score(+8) Skst

Skst_abbrev →
    no_abbrev "-"?          # FIH bankann
    | no_abbrev "-" tala    # REMAX-100

$score(+8) Skst_abbrev

Myllumerki_abbrev →
    myllumerki "-"?                     # #metoo - umræðan, #eurovision tístin

$score(+24) Myllumerki_abbrev

Fyrirbæri/fall/kyn →
    Skst? no_et/fall/kyn                # Microsoft - hugbúnaðarrisinn, Nikon myndavél
    | FyrirbæriMeðGreini/fall/kyn
    | FyrirbæriMeðEignarfornafni/fall/kyn
    | Úrslit/fall/kyn

# Raðnúmer

Raðnr/fall/kyn →
    raðnr/fall/kyn OgRaðnr/fall/kyn*
    | raðnr/fall/kyn Til raðnr/fall/kyn # 2.-3. sæti listans / 2. til 3. sæti listans

OgRaðnr/fall/kyn →
    Og raðnr/fall/kyn

# Talnabil nota en-dash
Til → "-:en" | "til:fs" | "og:st" | 'til:fs'_ef "og:st" 'með:fs'_þgf | 'til_og_með:ao'

# Úrslit í leikjum (32:27 sigur, 0:2-tap, 8:0 burst)

Úrslit →
    tala ":" tala

Úrslit/fall/kyn →
    Úrslit "-:en"? no_et/fall/kyn

$score(+2) Úrslit/fall/kyn

# Manneskja getur verið kk, kvk eða hk ('hán')
Fyrirbæri/fall/kyn →
    Manneskja/fall/kyn

# Fyrirtækisnöfn - hvorugkyn
# !!! Ath: Eiga fyrirtækisnöfn e.t.v. að vera NlKjarni, ekki Fyrirbæri?

Fyrirbæri_nf_hk →
    Fyrirtæki_nf
Fyrirbæri_þf_hk →
    Fyrirtæki_þf
Fyrirbæri_þgf_hk →
    Fyrirtæki_þgf
Fyrirbæri_ef_hk →
    Fyrirtæki_ef

# Reglu breytt þar sem fyrirtækjanöfn eru nú sameinuð í einn tóka
Fyrirtæki/fall →
    Sérnafn/fall no/fall* fyrirtæki # hf., ehf., Inc., Corporation, SA o.s.frv.

# Fyrirtæki eru næstum örugglega rétt greind
$score(+12) Fyrirtæki/fall

# '(Danska) byggingavörukeðjan Bygma'
# '(Jón vann hjá) verkfræðistofunni Afli'
# 'Tryggingafélagið Vörður (hagnaðist um 300 milljónir)'

FyrirbæriMeðGreini/fall/kyn →
    SamstættFall/fall/kyn

FyrirbæriMeðGreini/aukafall/kyn →
    ÓsamstættFall/aukafall/kyn

# Hún skaust fram á sjónarsviðið með vefsíðu sinni Gulir miðar
# (ef kommur eru notaðar þá er liðurinn innan þeirra þáttaður sem NlSkýring)
FyrirbæriMeðEignarfornafni/fall/kyn →
    Skst? no_et/fall/kyn 'sinn:fn'_et/fall/kyn EignarfornafnsBotn/fall

EignarfornafnsBotn/fall →
    SérnafnEðaManneskja
    | SérnafnEðaManneskja/fall

$score(+2) FyrirbæriMeðEignarfornafni/fall/kyn

# !!! ATH: Ef þessu er breytt gæti þurft að breyta processors/entities.py
SamstættFall/fall/kyn →
    # (Ég versla við) norsku herrafataverslunina Smekkmann
    # Lagahöfundurinn góðkunni Jónas Friðrik (hefur glatt marga)
    VeiktLo_et/fall/kyn* no_gr_et/fall/kyn VeiktLo_et/fall/kyn? SérnafnEðaManneskja/fall

SamstættFall_nf/kyn →
    # Enska dagblaðið The Mirror
    VeiktLo_et_nf/kyn* no_gr_et_nf/kyn VeiktLo_et_nf/kyn? SérnafnEðaManneskja

# !!! ATH: Ef þessu er breytt gæti þurft að breyta processors/entities.py
ÓsamstættFall/aukafall/kyn →
    VeiktLo_et/aukafall/kyn* no_gr_et/aukafall/kyn
        VeiktLo_et/aukafall/kyn? SérnafnEðaManneskja # (Ég versla við) herrafataverslunina Smekkmaður

SérnafnEðaManneskja/fall →
    # (danska húsgagnahönnuðinn) Arne Jacobsen
    # (suður-kóreska símaframleiðandann) Samsung
    # (kvikmyndina umtöluðu) 50 gráa skugga
    Sérnafn/fall | Persóna/fall | Fyrirtæki/fall | Nafn/fall

Persóna/fall →
    person/fall

$score(+8) Persóna/fall # Velja Persónu frekar en Sérnafn

SérnafnEðaManneskja →
    # (kvikmyndina umtöluðu) 50 gráir skuggar
    Sérnafn | Persóna | Nafn_nf

$score(-2) SérnafnEðaManneskja # Velja form með falli frekar en án falls

Nafn/fall →
    NlKjarni/tala/fall/kyn # 50 gráir skuggar

# Ekki sækja í að nota nafnlið sem nafn í kjölfar annars nafnliðar með greini
$score(-4) Nafn/fall

Persóna →
    person

$score(+8) Persóna

VeiktLo/tala/fall/kyn →
    SkstAtv? lo_vb/tala/fall/kyn

# suður-kóreski farsímaframleiðandinn Samsung

SkstAtv → SkstAtvOrð "-" # Stutt bandstrik

SkstAtvOrð →
    "norður:ao" | "suður:ao" | "vestur:ao" | "austur:ao"

# Hvetja til runa af lýsingarorðum í veikri beygingu með skilgreiningu sérnafna
$score(+12) VeiktLo/tala/fall/kyn
$score(+2) SkstAtv

$score(+5) SamstættFall/fall/kyn
$score(-1) ÓsamstættFall/aukafall/kyn

# 'Frumkvöðlarnir Jón og Guðrún'
# 'bandarísku sjóðirnir Eaton Vance Corp. og Autonomy Capital LP'

FyrirbæriMeðGreini_ft/fall/kyn →
    Skilgreining/fall/kyn EindalistiFyrirbæri/fall/kyn

Skilgreining/fall/kyn →
    VeiktLo_ft/fall/kyn* no_gr_ft/fall/kyn

EindalistiFyrirbæri/fall/kyn →
    Eindalisti/fall/kyn
    | Eindalisti

Eindalisti/fall/kyn →
    SérnafnEðaManneskja/fall KommaSérnafnEðaManneskja/fall* Og SérnafnEðaManneskja/fall
    | Töluorð_ft/fall/kyn # skipverjanna tveggja, börnin tvö, stúlkunum tveimur

Eindalisti →
    SérnafnEðaManneskja KommaSérnafnEðaManneskja* Og SérnafnEðaManneskja

$score(-2) Eindalisti   # Velja form með falli frekar en án falls

KommaSérnafnEðaManneskja/fall →
    "," SérnafnEðaManneskja/fall

KommaSérnafnEðaManneskja →
    "," SérnafnEðaManneskja

$score(+8) FyrirbæriMeðGreini_ft/fall/kyn

# [séra] Jóns Dalbú Hróbjartssonar [, dómkirkjuprests]
# [frú] Vigdís Finnbogadóttir [fyrrverandi forseti]
# [stórvesír] Ásbjörn Friðriksson [, alþingismann]
# [hún] Kristín (okkar)
# [hán] Alda Villiljós [er sannfært um að málstaðurinn sé góður]

# Hrein mannanöfn eru látin halda málfræðilegu kyni sínu,
# þ.e. karl- eða kvenkyni
Mannsnafn/fall/nkyn → person/fall/nkyn

Manneskja/fall/nkyn → Ávarp/nkyn/fall? Mannsnafn/fall/nkyn Titill/fall?

# Ef notað er ávarp ('hán') leyfa reglurnar nöfn í hvort sem er
# [málfræðilegu] karl- eða kvenkyni, en hnúturinn 'Manneskja' fær
# málfræðilegt hvorugkyn.
Manneskja_nf_hk → ÞekktÁvarp_hk_nf Mannsnafn_nf/nkyn Titill_nf?
Manneskja_þf_hk → ÞekktÁvarp_hk_þf Mannsnafn_þf/nkyn Titill_þf?
Manneskja_þgf_hk → ÞekktÁvarp_hk_þgf Mannsnafn_þgf/nkyn Titill_þgf?
Manneskja_ef_hk → ÞekktÁvarp_hk_ef Mannsnafn_ef/nkyn Titill_ef?

Manneskja/fall → Manneskja/fall/kyn

Ávarp/kyn/fall →
    ÞekktÁvarp/kyn/fall

Ávarp/nkyn/fall →
    no_et/fall

$score(-6) Ávarp/kyn/fall

ÞekktÁvarp_kk/fall →
    'herra:kk'_et/fall | 'séra:kk'_et/fall | 'hann:pfn'_kk_et/fall
    | 'doktor:kk'_et/fall | 'prófessor:kk'_et/fall
    | "dj:kk"

ÞekktÁvarp_kvk/fall →
    'frú:kvk'_et/fall | 'séra:kk'_et/fall | 'hún:pfn'_kvk_et/fall
    | 'doktor:kk'_et/fall | 'prófessor:kk'_et/fall | 'maddama:kvk'_et/fall
    | "dj:kvk"

ÞekktÁvarp_hk/fall →
    'hán:pfn'_hk_et/fall

$score(+10) ÞekktÁvarp/kyn/fall

# 'Már Guðmundsson, bankastjóri [Seðlabanka Íslands]'
# Reynt er að láta titil ekki enda á FsRuna/FsAtv eða á EfLiður
# því það skapar margræðni við nafnliðinn sjálfan
Titill/fall →
    "," NlTitill/fall       # (Jón), [þá] formaður [félagsins]
    | KommulausTitill/fall  # (Jón) [þá] formaður [félagsins]
    | KommuTitill/fall      # (Jón), [þá] formaður námsmannafélagsins í Árósum og talsmaður þess,
    # Gunnlaugur H Jónsson eðlisfræðingur og fyrrverandi starfsmaður Orkustofnunar
    | EinnTitill/fall OgTitill/fall* ","? Og NlTitill/fall

NlTitill/fall →
    "þá:ao"? TitilEinkunn/fall/kyn? no_et/fall/kyn EignarFornafn_et/fall/kyn? # (Jón) [þá] glæsilegur fulltrúi [minn] (félagsins)

TitilEinkunn/fall/kyn →
    Einkunn_et/fall/kyn

# Titileinkunn er frekar fátíð
$score(-2) TitilEinkunn/fall/kyn

EinnTitill/fall →
    NlTitill/fall EfLiður? FsRunaEftirNl?  # sendiherra Bandaríkjanna hérlendis, handhafi bókmenntaverðlaunanna í ár

FæðingarOgDánardægur →
    "(" Fæddur FdDags FdSamtenging Dáinn FdDags ")"
    | "(" FdDags "-:en" FdDags ")"
    | "(" Fæddur FdDags ")"
    | "(" Dáinn FdDags ")"

Fæddur → 'fæddur:lo' # Skst. "f."

Dáinn → 'dáinn:lo' # Skst. "d."

FdDags → dagsföst | ártal

FdSamtenging →
    "," | "-:en" | ";"

OgTitill/fall →
    KommaEðaOg EinnTitill/fall

KommaEðaOg →
    "," | Og | "," Og

KommuTitill/fall →
    "," EinnTitill/fall LokatáknEðaKomma
    | "," EinnTitill/fall OgTitill/fall* ","? Og EinnTitill/fall LokatáknEðaKomma

KommulausTitill/fall →
    NlTitill/fall
    | FæðingarOgDánardægur KommuTitill/fall?

# Gefa plús fyrir titla
$score(+5) Titill/fall

# ...en ekki eins mikinn fyrir kommulausan titil
$score(-5) KommulausTitill/fall

# Gefa enn meiri plús fyrir heila titla innan kommu
$score(+14) KommuTitill/fall

# Eignarfallsliður á eftir nafnlið
# ('formaður Gimlis, félags eldri borgara')
# ('amma Jóns, föður Bjarna')
# karlsins langa, konunnar löngu, barnsins langa
# stóra karlsins, stóru konunnar, stóra barnsins
# karlanna löngu, kvennanna löngu, barnanna löngu

EfLiður → EfLiðurStakur EfLiðurFramhald? # Jóns, föður Bjarna sterka
EfLiður/tala/kyn → Nl_ef/tala/kyn # margar þessara kvenna, suma þeirra sem lentu í slysinu
EfLiður_ft → EfLiður_ft/kyn

# Eignarfallsliður getur verið í eintölu eða fleirtölu, og í
# öllum kynjum, óháð nafnliðnum sem hann stendur með
EfLiðurStakur → Nl_ef

EfLiðurFramhald → KommaEfLiður+ ","?

KommaEfLiður →
    "," EfLiðurStakur

$score(+5) EfLiður
$score(+5) EfLiður_ft/kyn

# Eignarfallsliður sem stendur á undan nafnlið
# ('Íslands fullorðnu synir', 'allra uppáhald')
# Þessi liður er einfaldari en EfLiður til að
# minnka margræðni; leyfir til dæmis ekki einkunn
# (?'Litla Íslands fullorðnu synir'), né að
# lýsingarorð sé notað sem nafnorð

EfLiðurForskeytiTala/tala/kyn →
    Töluorð_ef/tala/kyn TilTölu/tala/kyn?  # (vel rúmlega) þriggja (klukkutíma bið) / 30 þúsund (króna tjón)

EfLiðurForskeytiTala_ft/kyn →
    'margur:lo'_sb_ef_ft/kyn # margra (tíma bið)
    | 'allmargur:lo'_sb_ef_ft/kyn
    | 'fár:lo'_sb_ef_ft/kyn
    | 'örfár:lo'_sb_ef_ft/kyn

TilTölu/tala/kyn →
    'til:fs'_ef Töluorð_ef/tala/kyn+   # (á) tveggja til þriggja metra dýpi

EfLiðurForskeyti →
    Fornafn_ef/tala/kyn* eo* EfLiðurForskeytiTala/tala/kyn? EfLiðurForskeytiKjarni/tala/kyn
    | Fornafn_ef/tala/kyn+ eo* EfLiðurForskeytiTala/tala/kyn?
    | eo* EfLiðurForskeytiTala/tala/kyn ÓbeygðEining? # rúmlega tveggja prósenta (hækkun frá fyrra ári)
    | 'meiri:lo'_mst_kk_ef_et 'háttur:kk'_ef_et     # meiri háttar vandræði
    | 'minni:lo'_mst_kk_ef_et 'háttur:kk'_ef_et     # minni háttar vandamál
    | Prósentubil               # (Spáð er) a.m.k. 10-20% (aukningu)
    | eo* Prósenta              # (eigi) nú 50% (hlut í fjarskiptafyrirtækinu)
    | eo* 'hálfur:lo'_sb_ef/tala/kyn EfLiðurForskeytiKjarni/tala/kyn # [rúmlega] hálfs mánaðar bið
    | eo* lo_ef_et/kyn EfLiðurForskeytiStig/kyn # fjórða árs (nemendur), annars stigs (brunasár)
    | Magn_ef/tala/kyn          # 100 fermetra (einbýlishúsum), 10 tonna (trukk), rúmlega 690 MW (virkjun)

Prósenta →
    prósenta

$score(+8) Prósenta

EfLiðurForskeytiStig_hk →
    'ár:hk'_ef_et | 'stig:hk'_ef_et # fjórða árs nemendur, annars stigs brunasár

EfLiðurForskeytiStig_kvk →
    'gráða:kvk'_ef_et | 'öld:kvk'_ef_et # þriðju gráðu yfirheyrsla, sautjándu aldar heimspeki

EfLiðurForskeytiStig_kk →
    'flokkur:kk'_ef_et      # fyrsta flokks hótel

# Sennilega rétt greint
$score(+4) EfLiðurForskeytiStig/kyn

ÓbeygðEining →
    'prósent:hk'_ef_ft
    | 'tomma:kvk'_ef_et     # '45 tommu flatskjá' (í stað '45 tomma flatskjá', sem er málfræðilega rétt)
    | 'stjarna:kvk'_ef_et   # Fjögurra stjörnu veitingastaður (ekki fjögurra stjarna veitingastaður)
    | 'hola:kvk'_ef_et      # Átján holu golfvöllur (ekki átján hol(n)a golfvöllur)

EfLiðurForskeytiKjarni_et/kyn → Fyrirbæri_ef/kyn
EfLiðurForskeytiKjarni_ft/kyn → no_ft_ef/kyn

# Ef hægt er að nota t.d. fornafn í stað eignarfalls-forskeytis er það betra
$score(-1) EfLiðurForskeyti
# Eignarfalls-forskeyti sem eru ekki tölur eða fornöfn eru sjaldgæf
$score(-5) EfLiðurForskeytiKjarni/tala/kyn

Fornafn/fall/tala/kyn → Fornafn/tala/fall/kyn

Fornafn/tala/fall/kyn → fn/tala/fall/kyn

# Einn og sami veiðimaðurinn, eitt og sama fyrirbæri
Fornafn_et/fall/kyn → 'einn:fn'_et/fall/kyn Og 'sami:lo'_et/fall/kyn

# Persónufornöfn sem notuð eru eins og eignarfornöfn
# en beygjast ekki með nafnlið og eru ekki merkt fn í BÍN, þ.e.
# okkar / ykkar / hans / hennar / háns / þess / þeirra
# (Bíllinn okkar, hans skoðanir...)
# Persónufornöfnin (pfn) "hans", "hennar" og "það" eru án kyns í BÍN
EignarFornafn →
    'ég:pfn'_ef_ft          # okkar
    | 'þú:pfn'_ef_ft        # ykkar
    | 'hann:pfn'_kk_ef_et   # hans
    | 'hún:pfn'_kvk_ef_et   # hennar
    | 'hán:pfn'_hk_ef_et    # háns
    | 'það:pfn'_hk_ef/tala  # þess / þeirra

Töluorð/tala/fall/kyn → to/tala/fall/kyn | tala/tala/fall/kyn

# Öll óbeygjanleg töluorð (merkt 'töl' í BÍN) tákna fleirtölu
# Talnabil eru líka alltaf í fleirtölu
Töluorð_ft/fall/kyn → töl_ft/fall/kyn | Talnabil

Talnabil → tala Bil tala
$score(+4) Talnabil

Bil → "-:en"

Töluorð/fall/tala/kyn → Töluorð/tala/fall/kyn

# Undirsetningar

# 1p: 'ég sem alltaf elda matinn samviskusamlega (nenni þessu ekki)'
# 2p: 'þú sem ég elska (vilt ekki giftast mér)'
# 3p: 'sjóarinn sem hafið hafnaði í fyrra (kom hér í gær og fékk sér kaffi)'

# Gefa undirsetningum afmörkuðum með kommum forgang,
# en forðast verri kosti

TilvísunarsetningMeðKommu/tala/pers/kyn →
    Ts2Kommur/tala/pers/kyn
    | Ts0Kommur/tala/pers/kyn
    | Ts1Komma/tala/pers/kyn

Ts2Kommur/tala/pers/kyn →
    "," Tilvísunarsetning/tala/pers/kyn ","

Ts0Kommur/tala/pers/kyn →
    Tilvísunarsetning/tala/pers/kyn

Ts1Komma/tala/pers/kyn →
    Tilvísunarsetning/tala/pers/kyn ","
    | "," Tilvísunarsetning/tala/pers/kyn

$score(+32) Ts2Kommur/tala/pers/kyn
$score(-8) Ts1Komma/tala/pers/kyn

# KommaTilvísunarsetning er einungis notað í lok venjulegrar
# setningar með frumlagi í nefnifalli, sbr.
# 'Allir mega koma með okkur sem eru fullklæddir'
KommaTilvísunarsetning/tala/pers/kyn →
    ","? Sem SetningÁnF/tala/pers/kyn

$score(-8) KommaTilvísunarsetning/tala/pers/kyn

Tilvísunarsetning/tala/pers/kyn →
    Tilvísunartenging Tengisetning/tala/pers/kyn OgTengisetning/tala/pers/kyn*

$tag(begin_prep_scope) Tilvísunarsetning/tala/pers/kyn

Tilvísunarsetning_p3_et/kyn →
    Tilvísunarsetning_et_p3/kyn

Tilvísunarsetning_p3_ft/kyn →
    Tilvísunarsetning_ft_p3/kyn

Tilvísunartenging →
    TengiorðSem | TengiorðEr | TengiorðÞarSem

TengiorðSem → "eða:st"? "sem:stt" AðvörunAð?

TengiorðÞarSem → "þar:ao" "sem:stt" AðvörunAð? # í landinu þar sem Jón átti heima

$score(-4) TengiorðÞarSem # Þetta er oftar samtenging setninga ofar í trénu

TengiorðEr → "er:stt"

$score(-16) TengiorðEr # Sjaldgæft - ekki velja nema það þurfi

Tengisetning/tala/pers/kyn →
    # (Maðurinn sem) konan kyssti í gær / konurnar kysstu í gær
    TengiYfirsetning
    # (Maðurinn sem) kyssti konuna í gær [fyrstur allra]/ (Maðurinn sem) var fallegur
    | BeygingarliðurÁnF/tala/pers/kyn LoTengtSögn_nf/tala/kyn?
    | TengisetningSoÞað # (Lausnirnar sem) kallað var eftir
    | BeygingarliðurStýftAndlag

TengisetningSoÞað →
    SetningSoÞað # (Lausnirnar sem) kallað var eftir

$score(-4) TengisetningSoÞað

# Beygingarliður þar sem eitt andlag er stýft af sögn
# vegna þess að það er komið fram í nafnliðnum sem vísað er til:
# "bréfið sem maðurinn sendi ráðuneytinu [galvaskur] [í dag]"
# (þarna er sem sagt verið að nota 'senda e-m e-ð', þ.e. so_2_þgf_þf,
# en aðeins með einu andlagi í þágufalli)
BeygingarliðurStýftAndlag →
    Frumlag/pers/tala/kyn FrumlagsInnskot/tala/pers/kyn? AtviksliðurEinkunn*
        SagnRunaStýfð/tala/pers LoTengtSögn_nf/tala/kyn? SagnInnskotAtv?

SagnRunaStýfð/tala/pers →
    # Í bréfinu sem maðurinn sendi ráðuneytinu
    # (maðurinn sendi ráðuneytinu bréf)
    so_2_þgf_þf/tala/pers NlBeintAndlag_þgf
    # Í spurningunni sem maðurinn spurði ráðuneytið
    # (maðurinn spurði ráðuneytið spurningar)
    | so_2_þf_ef/tala/pers NlBeintAndlag_þf
    # Í batanum sem maðurinn óskaði kettinum
    # (maðurinn óskaði kettinum bata)
    | so_2_þgf_ef/tala/pers NlBeintAndlag_þgf
    # Í bókinni sem maðurinn skilaði safninu
    # (maðurinn skilaði safninu bókinni)
    | so_2_þgf_þgf/tala/pers NlBeintAndlag_þgf
    # Í útgjöldunum sem maðurinn kostaði skattborgara
    # (maðurinn kostaði skattborgara útgjöld)
    | so_2_þf_þf/tala/pers NlBeintAndlag_þf
    # Í nöfnunum sem konurnar kölluðu Pál
    # (konurnar kölluðu Pál nöfnum)
    | so_2_þf_þgf/tala/pers NlBeintAndlag_þf

# (Nokkurra ára niðursveiflur) sem þessar (eru óþekktar)
Tengisetning_et_p3/kyn →
    'þessi:fn'_et_nf/kyn
 
Tengisetning_ft_p3/kyn →
    'þessi:fn'_ft_nf/kyn
 
TengiYfirsetning →
    HreinAukasetning

# Sama innihald að mestu og HreinYfirsetning
# En því er varpað í S, sem á ekki heima undir CP.
HreinAukasetning →
    Setning
    | SetningAukafall
    | SetningAukafallForgangur
    | SetningLo
    | SetningSo
    | AtvSetning        # Atvikssetning fremst í aðalsetningu
    | Spurnarsetning
    | SamsettSetning    # Annaðhvort X eða Y; bæði X og Y; ýmist X eða Y


# Hér er gert ráð fyrir að frumlagslausar atvikssetningar
# séu alltaf í 3p et hk, sbr. 'þegar kallað var eftir lækni',
# 'meðan farið var yfir málið'
BeygingarliðurÁnF →
    BeygingarliðurÁnF_et_p3_hk

$score(-8) BeygingarliðurÁnF

BeygingarliðurÁnF/tala/pers/kyn →
    SetningÁnF/tala/pers/kyn

$score(+8) BeygingarliðurÁnF/tala/pers/kyn

BeygingarliðurTími →
    # '(þegar) líða/síga/saxast fer/tekur á daginn'
    HjSögnTímiNh HjSögnTími FsAtv

HjSögnTímiNh →
    'líða:so'_0_nh          # þegar líða tekur á daginn
    | 'síga:so'_0_nh        # þegar síga fer á seinni partinn
    | 'saxa:so'_0_mm_nh     # þegar saxast fer á þolinmæðina
    | 'lengja:so'_0_mm_nh   # þegar lengjast fer í sólarhringnum
    | 'stytta:so'_0_mm_nh   # þegar styttast tekur í jólin

HjSögnTími →
    'fara:so'_0_gm_et_p3 | 'taka:so'_0_gm_et_p3

# Nota frekar SetningÁnF heldur en HreinYfirsetning, ef völ er á hvoru tveggja
$score(-96) TengiYfirsetning

# ...sem hætta gæti stafað af og eru kílómetri í þvermál
OgTengisetning/tala/pers/kyn →
    ","? OgSem Tengisetning/tala/pers/kyn

$score(+2) OgTengisetning/tala/pers/kyn

NlEind/fall →
    NlEind/tala/pers/fall/kyn

NlEind/fall/kyn →
    NlEind/tala/pers/fall/kyn

NlEind_p3/fall/kyn →
    NlEind_et_p3/fall/kyn | NlEind_ft_p3/fall/kyn

NlEind/tala/pers/fall/kyn →
    NlStak/tala/pers/fall/kyn StakViðhengi/tala/fall/kyn? TilvísunarsetningMeðKommu/tala/pers/kyn?
        Þó/tala/pers/kyn? NlSkýring/tala/pers/fall/kyn?

NlEind_et_p3/fall/kyn → NlEkkiHeldur_et/fall/kyn
NlEind_ft_p3/fall/kyn → NlEkkiHeldur_ft/fall/kyn

# (Hún á) þrjú/fjölmörg gæludýr: svarta kisu, grænt kamelljón og feitan naggrís
NlEind_ft_p3/fall/kyn →
    Nl_ft_p3/fall/kyn ":" Nl/fall KommaNl/fall* OgNl/fall

KommaNl/fall →
    "," Nl/fall

OgNl/fall →
    ","? Og Nl/fall

# (Hann málaði) ekki girðinguna, heldur staurana (, sem voru bláir)
NlEkkiHeldur/tala/fall/kyn →
    "ekki:ao" Nl/fall ","? "heldur:st" Nl/fall/tala/kyn

$score(+12) NlEkkiHeldur/tala/fall/kyn

# (vegna nauðasamninganna) sem slíkra
StakViðhengi/tala/fall/kyn →
    "," Sem 'slíkur:lo'/tala/fall/kyn LokatáknEðaKomma
    | Sem 'slíkur:lo'/tala/fall/kyn

# (að endurskapa sig) sem þjóðhetju
# 'sem' með nefnifalli er talið vera forsetning
StakViðhengi/tala/aukafall/kyn →
    SemAukafall/aukafall/tala

SemAukafall/aukafall/tala →
    Sem Nl/aukafall/tala

$score(-4) SemAukafall/aukafall/tala

NlEind_et_p3/fall/kyn →
    # það [eitt] [,] hvernig kerfið virkar [,] (er óskiljanlegt)
    'sá:fn'_et/fall/kyn 'einn:fn'_et/fall/kyn? Útskýring

NlEind_ft_p3/fall/kyn →
    # (Niðurstöðurnar voru) þær [einar] [,] að Katrín myndaði stjórn [,] (og kom það sumum á óvart)
    'sá:fn'_ft/fall/kyn 'einn:fn'_ft/fall/kyn? Útskýring

NlEind_et_p3_þgf_hk →
    # (Hann gekk út frá) því sem vísu/öruggu/afgreiddu að hann gæti greitt sektina
    'sá:fn'_et_þgf_hk "sem:stt" lo_sb_hk_et_þgf Útskýring

ÞaðTenging →
    # (Þá eru dæmi þess)[,] í veraldarsögunni [,] að eldfjöll hafi gosið
    AtvFsAukaInnskot? Skýringarsetning
    # (Það) hvernig kerfið virkar (er óskiljanlegt)
    | Spurnaraukasetning
    # Það að fara til tannlæknis (er ánægjuleg reynsla)
    | NhFyllingÁnAtv

$score(+32) ÞaðTenging

$tag(begin_prep_scope) ÞaðTenging

# ÞaðTenging sem getur verið innan kommu

Útskýring →
    ÞaðTenging
    | "," ÞaðTenging LokatáknEðaKomma

# Aðdróttanir, þó [í meginatriðum] sannar séu, varða fangelsisvist

Þó/tala/pers/kyn →
    Viðurkenningartenging ÞóBotn/tala/pers/kyn
    | "," Viðurkenningartenging ÞóBotn/tala/pers/kyn LokatáknEðaKomma

ÞóBotn/tala/pers/kyn →
    FsRuna? LoLiður_nf/tala/kyn VeraVerða_vh/tala/pers

NlSkýring/tala/pers/fall/kyn →
    "(" SvigaForskeyti? SvigaInnihald/tala/pers/fall/kyn ")"
    | "[" SvigaForskeyti? SvigaInnihald/tala/pers/fall/kyn "]"
    | "-:em" SvigaForskeyti? SvigaInnihald/tala/pers/fall/kyn "-:em"
    | "," Skýring Skýringarliður LokatáknEðaKomma
    # (Verslunin þjónar fjölbreyttum markhópi), allt frá börnum til aldraðra, (og er vinsæl í hverfinu)
    | Samanburðarmerki Allt 'frá:fs'_þgf Nl_þgf
        'til:fs'_ef Nl_ef LokatáknEðaKomma
    | NlSkýringKomma/tala/pers/fall/kyn

NlSkýringKomma/tala/pers/fall/kyn →
    "," SvigaInnihald/tala/pers/fall/kyn LokatáknEðaKomma

# Draga úr skýringum sem eru ekki auðkenndar betur en með einfaldri kommu
$score(-10) NlSkýringKomma/tala/pers/fall/kyn

# Gefa aukastig fyrir skýringar á nafnliðum
$score(+6) NlSkýring/tala/pers/fall/kyn

SvigaInnihald/tala/pers/fall/kyn →
    Árabil
    > Tilvísunarsetning/tala/pers/kyn # (og sem ég er almennt fylgjandi)
    > Nl/fall/tala          # 'Ég spurði forsætisráðherra, Sigmund Davíð, um málið'
    > Nl/fall               # Sama fall en ekki sama tala
    > SvigaInnihaldFsRuna
    > SvigaInnihaldNl/fall  # Ef ekki finnst sama fall og tala, prófa almennan nafnlið
    > HreinYfirsetning

# Árabil nota en-dash

Árabil →
    ártal "-:en" ártal   # 1951-1983
    > ártal "-:en" tala  # 2001-8

SvigaInnihaldNl/fall →
    Nl

# Nafnliður í öðru falli en það sem verið er að skýra er ekki spennandi kostur
# og gæti mjög vel verið villa
$score(-8) SvigaInnihaldNl/fall
$tag(error) SvigaInnihaldNl/fall

SvigaInnihaldFsRuna →
    FsRuna

# FsRuna innan kommu er oftast margræð við FsRunu eftir nafnlið
$score(-4) SvigaInnihaldFsRuna

Skýring →
    "svo:st" "sem:st"
    | "sem:st" 'segja:so'_lhþt_sb_hk_nf_et      # sem sagt
    | 'til:fs'_ef 'dæmi:hk'_ef_et | 'til_dæmis:ao'
    | "þar:ao" 'á_meðal:ao'
    | 'meðal_annars:ao'
    | 'það:pfn'_hk_et_nf 'vera:so'_0_gm_fh_et_p3
    | 'með:fs'_þgf 'annar:lo'_hk_þgf_ft 'orð:hk'_þgf_ft | 'með_öðrum_orðum:ao'
    | "en:st" "ekki:ao"
    | "þó:st" "ekki:ao"
    | "samt:ao" "ekki:ao"

SvigaForskeyti →
    Skýring
    | 'dæmi:hk'_et_nf ":"            # Ráðamenn (dæmi: forsetinn) hafa tjáð sig fjálglega
    | 'lesa:so'_0_gm_fh_et_p3 ":"    # Hann sagði að óþjóðalýður (les: kommúnistar) hefði ráðist á húsið
    # TODO: "lesist"?

Skýringarliður →
    Nl | FsAtv      # , meðal annars Jón í Holti, / , oft snarlega,

# Runur nafnliða

# !!! TODO: Skilja á milli samtenginga sem mynda fleirtölu úr eintöluliðum
# !!! (strákurinn og stelpan eru...), og þeirra sem halda eintölu
# !!! (hópurinn, þar á meðal ég, var... / Össur, ekki síður en þingflokkurinn, var...)

OgEða →
    "og:st" OgAo?
    | "eða:st" | "ellegar:st" | "eður:st"
    | "svo:st" "og:st" | AðvörunSem "og:st" | "né:st"
    | ÞarÁMeðal | 'meðal_annars:ao'
    | 'það:pfn'_hk_et_nf 'vera:so'_0_gm_fh_et_p3
    | "eins:ao" "og:st" | "líkt:ao" "og:st"
    | "og:st"? "þar:ao" "með:ao" 'telja:so'_lhþt_sb_hk_nf_et?
    | "svo:st" "sem:st" | "jafnt:ao" "sem:st"
    | 'og_fleiri:ao'  # (Vísbendingar eru um að samkomubann) o.fl. (aðgerðir séu á döfinni)
    | EnÍOgEða

# 'sem og'
AðvörunSem → "sem:st"
$tag(error) AðvörunSem

# Við þurfum að hætta að meta fólk út frá aldri heldur meta það út frá hæfileikum
OgEðaHeldur → OgEða | "heldur:ao"

EnÍOgEða →
    "en:st"

$score(-26) EnÍOgEða # Frekar að líta á "en" sem samtengingu ef það er hægt

OgAo →
    "jafnframt:ao" | "síðar:ao" | "síðan:ao" | "þá:ao"? "einnig:ao" | "líka:ao"

OgSem →
    "og:st" "sem:stt"? | "eða:st" "sem:stt"? | "ellegar:st" | "en:st" "sem:stt"?

ÞarÁMeðal → "þar:ao" 'á_meðal:ao'

$score(+16) ÞarÁMeðal # Líta frekar á "þar á meðal" sem samtengingu en skýringu

KommaOgEða →
    ","? OgEða
    | "-:em" "eða:st"           # 'Ég tók tvö tonn af eldsneyti - eða tvö þúsund lítra'
    | KommaOgEðaStrik
    | "," "ekki:ao"             # Við vildum gin, ekki viskí
    | "," "núna:ao" "síðast:ao" # Tóku á móti mörgum flóttamönnum, núna síðast þremur þúsundum

KommaOgEðaStrik → "-:em"        # 'Ég tók tvö tonn af eldsneyti - tvö þúsund lítra'

$score(-8) KommaOgEðaStrik

# (Í fyrra barði Hannes Guðmund) og (særði Pál í dag)
# (Í fyrra barði Hannes Guðmund), (særði Pál), og (neitaði sök)
# (Í gær börðu strákarnir ekki Guðmund), heldur (stríddu Páli)

Aðaltenging →
    "og:st" | "svo:st" "og:st" | "eða:st" | "ellegar:st" | "heldur:st"
    | "eins:ao" "og:st" | "en:st" | AðvörunSem "og:st"

En → "en:st"

OgEðaEnNema → Aðaltenging | Nema

OgEðaEnAtv → Aðaltenging FsAtv?

KommaOgEðaEn → "," | ","? Aðaltenging | "-:em" Aðaltenging
KommaOgEðaEnNema → "," | ";" | ","? OgEðaEnNema
KommaSemíkomma → "," | ";"

# Allir liðir af sama kyni

NlRuna/fall/kyn →
    KommuSamtenging/fall/kyn* KommuEndir/fall/kyn
    | "," KommuStForskeyti/kyn/fall? NlEind/fall/kyn AtvKomma # Konurnar, sérstaklega Anna, voru óánægðar

KommuStForskeyti/fall →
    FsRunaEftirNl
    | "þar:ao" "af:ao"    # fjórir þingmenn, þar af einn nýútskrifaður, 
    | 'það:pfn'_ft/fall   # þau Jóna og Friðgeir
    | StForskeytiHannHún/fall

StForskeytiHannHún/fall →
    'hann:pfn'_kk_ft/fall      # þeir Jón og Páll
    | 'hún:pfn'_kvk_ft/fall     # Þær Jóna og Gunna
    # Ekki þarf 'hán' hér þar sem notað er 'þau' í fleirtölu í því tilviki

# Draga úr 'þeir' eða 'þær' sem forskeyti í lista sem ekki er af sama kyni
# (en það er samt nauðsynlegt að leyfa þetta því stundum innihalda listar
# erlend nöfn sem talin eru vera í hvorugkyni)
$score(-8) StForskeytiHannHún/fall

KommuStForskeyti/kyn/fall →
    FsRunaEftirNl
    | "þar:ao" "af:ao"    # fjórir þingmenn, þar af einn nýútskrifaður, 

KommuStForskeyti_kk/fall →
    'hann:pfn'_kk_ft/fall  # þeir Jón og Páll

KommuStForskeyti_kvk/fall →
    'hún:pfn'_kvk_ft/fall  # Þær Jóna og Gunna

KommuStForskeyti_hk/fall →
    'það:pfn'_hk_ft/fall   # Þau barnið og dýrið

# Draga úr 'þau' sem forskeyti í lista sem inniheldur aðeins
# hvorugkyn, því það er frekar óvenjuleg notkun
$score(-4) KommuStForskeyti_hk/fall

KommuSamtenging/fall/kyn → KommaSemíkomma KommuStForskeyti/kyn/fall? NlEind/fall/kyn

KommuEndir/fall/kyn →
    Atviksliður? KommaOgEða KommuStForskeyti/kyn/fall? NlEind/fall/kyn AtvKomma?
    | 'og_fleira:ao' KommaSemíkomma?
    | 'og_margt_fleira:ao' KommaSemíkomma?
    | 'og_svo_framvegis:ao' KommaSemíkomma?
    | 'og_þess_háttar:ao' KommaSemíkomma?

# Hvetja til eins langra lista og mögulegt er
$score(+6) KommuSamtenging/fall/kyn

# Blandað kyn

NlRuna/fall →
    KommuSamtenging/fall* KommuEndir/fall
    | "," KommuStForskeyti/fall? NlEind/fall AtvKomma # Þingmenn, sérstaklega Jóhanna, voru óánægðir

KommuSamtenging/fall → KommaSemíkomma KommuStForskeyti/fall? NlEind/fall

KommuEndir/fall →
    Atviksliður? KommaOgEða KommuStForskeyti/fall? NlEind/fall AtvKomma?
    | 'og_fleira:ao' KommaSemíkomma?
    | 'og_margt_fleira:ao' KommaSemíkomma?
    | 'og_svo_framvegis:ao' KommaSemíkomma?
    | 'og_þess_háttar:ao' KommaSemíkomma?

# Runa af nafnliðum í tilteknu falli sem endar á "eða X"

NlRunaEða/fall →
    NlEind/fall KommuSamtenging/fall* EðaEndir/fall

EðaEndir/fall →
    Atviksliður? Eða KommuStForskeyti/fall? NlEind/fall

# Hvetja til eins langra lista og mögulegt er
$score(+6) KommuSamtenging/fall

# Nafnliðir sem geta verið stakar eindir eða runur einda
# !!! ATH: Forskeytið "Nl" á nafnliði er harðkóðað á nokkrum
# !!! stöðum í Python kóðanum, þannig að því má ekki breyta

Nl →
    Nl/tala/pers/fall/kyn

Nl_et →
    Nl_et/pers/fall/kyn

Nl_ft →
    Nl_ft/pers/fall/kyn

# Mark these two nonterminals as roots since they are used as
# such in nounphrase.py
$root(Nl_et)
$root(Nl_ft)

Nl/fall →
    Nl/tala/pers/fall/kyn

Nl/fall/kyn →
    Nl/tala/pers/fall/kyn

Nl/fall/tala →
    Nl/tala/pers/fall/kyn

Nl/fall/pers →
    Nl/tala/pers/fall/kyn

Nl/fall/tala/kyn →
    Nl/tala/pers/fall/kyn

Nl/fall/tala/pers →
    Nl/tala/pers/fall/kyn

Nl/pers/fall/kyn →
    Nl/tala/pers/fall/kyn

Nl/fall/tala/pers/kyn →
    Nl/tala/pers/fall/kyn

Nl/tala/pers/fall/kyn →
    NlEind/tala/pers/fall/kyn

# Gefa nefnifalli forskot ef mismunandi föll eru í boði
$score(+4) Nl_nf

# Fleirtölu-nafnliður í 3p sem leyfir runu í sama falli og kyni
# Leyfir einnig 'þeir Jón og Gunnar [sendu bréfið]' eða
# 'þær Elísabet og mamma hennar [byggðu húsið]'
Nl_ft_p3/fall/kyn →
    NlSamaKyn/fall/kyn

NlSamaKyn/fall/kyn →
    NlSá_ft/fall/kyn? NlEind_p3/fall/kyn NlRuna/fall/kyn+ TilvísunarsetningMeðKommu_ft_p3/kyn?

# Fleirtölunafnliðir af sama kyni eru betri en blandaðir
$score(+4) NlSamaKyn/fall/kyn

NlSá_ft/fall/kyn →
    eo* 'sá:fn'_ft/fall/kyn # einnig þau Jón og Gunna

# Blandaðir fleirtölu-nafnliðir eru í hvorugkyni

# Leyfa 'maðurinn og konan' og 'kötturinn, hundurinn og bráðlega músin',
# og einnig 'sumir, þar á meðal Jón,'

Nl_ft_p3_nf_hk →
    NlSá_ft_nf_hk? NlEind_nf NlRuna_nf+ TilvísunarsetningMeðKommu_ft_p3_hk? # Blandað kyn verður að hvorugkyni fleirtölu

Nl_ft_p3_þf_hk →
    NlSá_ft_þf_hk? NlEind_þf NlRuna_þf+ TilvísunarsetningMeðKommu_ft_p3_hk? # Blandað kyn verður að hvorugkyni fleirtölu

Nl_ft_p3_þgf_hk →
    NlSá_ft_þgf_hk? NlEind_þgf NlRuna_þgf+ TilvísunarsetningMeðKommu_ft_p3_hk? # Blandað kyn verður að hvorugkyni fleirtölu

Nl_ft_p3_ef_hk →
    NlSá_ft_ef_hk? NlEind_ef NlRuna_ef+ TilvísunarsetningMeðKommu_ft_p3_hk? # Blandað kyn verður að hvorugkyni fleirtölu

# Lýsingarorðaliður

# Lýsingarorð með nafnlið

# !!! TODO: Hér er ekki gerður greinarmunur á sterkri og veikri
# beygingu lýsingarorða sem standa með nafnorðum, eftir því hvort
# nafnorðið er með greini eða án. (Sbr.: 'Fallega konan talaði við mig'
# vs. 'Falleg kona talaði við mig'.)
Lo/fall/tala/kyn →
    LoSemLo/fall/tala/kyn
    | LoMun/fall/tala/kyn
    | LoSem/fall/tala/kyn
    | LoSemSo/fall/tala/kyn

LoSemLo/fall/tala/kyn → 
    LoAtviksliður* lo/fall/tala/kyn # (Þingmenn kosnir) algerlega leynilegri (kosningu)
    | LoRöðun lo_evb/fall/tala/kyn # (Ég átti) allra / 4. stærsta (bílinn)
    | RaðTala/fall/tala/kyn lo_evb/fall/tala/kyn # (Ég átti) þriðja stærsta (bílinn)

LoRöðun →
    raðnr | Allra

LoMun/fall/tala/kyn →
    'munur:kk'_þgf_et lo_mst/fall/tala/kyn # (Schengen rekur) mun öflugri gagnagrunn / (þau voru) mun frjálsari skoðana sinna

$score(+10) LoMun/fall/tala/kyn

LoSem/fall/tala/kyn →
    "sem:ao" lo_esb/fall/tala/kyn # (hvernig skuli stuðla að) sem jöfnustu hlutfalli

$score(+4) LoSem/fall/tala/kyn

# Leyfa sagnorðum að standa í stað lýsingarorða ef þau
# eru í lýsingarhætti þátíðar, sterkri eða veikri beygingu
# (_vb leyfir veika beygingu ásamt sterkri)
# ('Við litum til bókfærðs hagnaðar')
LoSemSo/fall/tala/kyn →
    LoAtviksliður* LoSemSoBotn/fall/tala/kyn
    # '(hvort það gæfi vísbendingu um) hina [nýlega] eyddu (tölvupósta)'
    | 'hinn:gr'/fall/tala/kyn eo* so_lhþt_vb/fall/tala/kyn

LoSemSoBotn/fall/tala/kyn →
    so_lhþt/fall/tala/kyn | so_lh_nt    # bráðlega verðandi brúður

$score(-16) LoSemSo/fall/tala/kyn

$score(+6) LoSemLo/fall/tala/kyn    # Tilbrigði 3.1, eina sem þarf að toggla.

# 'Ég vaknaði ekkert alltof fersk'
# 'Atburðarásin var [þó] nokkuð skýr'
# 'Hagsmunirnir eru það miklir að aðgerða er þörf'
# Ath.: 'nokkuð' er ekki leyft sem eo þar sem það er einnig fornafn.
# Þess vegna er það sértilvik hér. Ekki er hins vegar ráðlegt að
# leyfa atviksorðið almennt sem einkunn með nafnliðum; það myndi
# rekast á fornafnið.
LoAtviksliður →
    eo | 'enginn:fn'_hk_et_nf | "þó:ao"? "nokkuð:ao" | LoAtvÞað

LoAtvÞað →
    'sá:fn'_hk_nf_et

# Koma í veg fyrir að 'það' sé túlkað fremur sem atviksliður með
# lýsingarorði heldur en sem andlag sagnar
$score(-64) LoAtvÞað

# Við viljum yfirleitt frekar fá atviksorð framan við lýsingarorð
# heldur en að fá mörg lýsingarorð í röð, þ.e. við viljum fremur
# 'gríðarlega:ao' 'stóra:lo' en 'gríðarlega:lo' 'stóra:lo'.
$score(+12) LoAtviksliður

LoAtviksliðir →
    LoAtviksliður+ | "og:ao" # 'honum er og heimilt að merkja við fleiri en einn frambjóðanda'

# Lýsingarorð í sterkri beygingu
# ('Hún fór frjáls skoðana sinna í útlegð')
# 'Hún taldi mig tengdan Framsóknarflokknum'

# Öfug orðaröð leyfist í einhverjum tilvikm:

# 'Hann var málinu efnislega hlynntur en hafði efasemdir um formið.'
# 'Rithöfundurinn er heiðursins svo sannarlega verður.'
# 'Páll er alls góðs maklegur en þetta var of mikið.'

# Öfug orðaröð virðist ekki leyfileg í þolfalli:
# '?Hann var sýninguna viðstaddur'

Lo_sb/tala/fall/kyn →
    LoAtviksliðir? lo_sb/tala/fall/kyn
    # Hún taldi mig hlynntan Framsóknarflokknum
    | LoAtviksliðir? lo_sb/sfall/tala/fall/kyn LoViðhengi/sfall

Lo_sb_nf/tala/kyn →
    # (Hann vann keppnina) fyrstur [karla/allra]
    # (Jóna var) glöðust [allra gesta] (í partíinu)
    LoAtviksliðir? lo_sb_nf/tala/kyn EfLiður_ft?
    # (Hann var forsvarsmaður starfssviða sem eiga að vera) [algjörlega] aðskilin
    # 'aðskilja' er einungis til sem sagnorð í BÍN, ekki lýsingarorð
    | eo* so_lhþt_sb_nf/tala/kyn
    # (Bréfið var) dagsett 10. júlí
    | LoAtviksliðir? 'dagsetja:so'_lhþt_sb_nf/tala/kyn Dagsetningarliður
    # Jón hefur verið hlynntur málinu
    | LoAtviksliðir? lo_sb_nf/sfall/tala/kyn LoViðhengi/sfall
    # Hann er málinu [efnislega] hlynntur
    | LoViðhengi_sþgf LoAtviksliðir? lo_sb_nf_sþgf/tala/kyn
    # Hann er heiðursins [svo sannarlega] verður
    | LoViðhengi_sef LoAtviksliðir? lo_sb_nf_sef/tala/kyn
    # Hann er þess [algerlega] fullviss að Jón sé sekur
    | 'það:pfn'_hk_ef_et LoAtviksliðir? lo_sb_nf_sef/tala/kyn Skýringarsetning

Lo_sb_nf_ft/kyn →
    'margur:lo'_sb_nf_ft/kyn 'hver:fn'_nf_ft/kyn # margir hverjir, mörg hver, margar hverjar

EnOgEða → "og:st" | "eða:st" | "en:st"

# (Ég var reiður) og sár
OgLo_sb/tala/fall/kyn →
    EnOgEða Lo_sb/tala/fall/kyn

# (Flokknum er rétt) [,] og skylt [,] (að fara að beiðni forsetans)
OgLo_sb_nf/tala/kyn →
    EnOgEða Lo_sb_nf/tala/kyn
    | "," EnOgEða Lo_sb_nf/tala/kyn ","

$score(+2) OgLo_sb_nf/tala/kyn

# Miðstig
Lo_mst/tala/fall/kyn →
    LoAtviksliðir? lo_mst/tala/fall/kyn # Miðstig
    | LoAtviksliðir? lo_mst/sfall/tala/fall/kyn LoViðhengi/sfall # Miðstig

Lo_mst_nf/tala/kyn →
    LoAtviksliðir? lo_mst_nf/tala/kyn # Miðstig
    | LoAtviksliðir? lo_mst_nf/sfall/tala/kyn LoViðhengi/sfall # Miðstig
    | LoViðhengi_sþgf LoAtviksliðir? lo_mst_nf_sþgf/tala/kyn
    | LoViðhengi_sef LoAtviksliðir? lo_mst_nf_sef/tala/kyn

# Lýsingarorð sem stendur með sögn getur viðhengi í aukafalli, sbr.:
# þf.: 'Hann var viðstaddur fundinn'
# þgf.: 'Sýningin var opin almenningi'
# ef.: 'Þau voru frjáls ferða sinna'
LoViðhengi_sþf → Nl_þf
LoViðhengi_sþgf → Nl_þgf
LoViðhengi_sef → Nl_ef

$score(+8) LoViðhengi/sfall

# Lýsingarorð sem einkunn með nafnlið

# '(Jón), sultuslakur eftir gufubaðið, (lék á als oddi)'
# '(Maður) alvarlega háður eiturlyfjum (bað mig um peninga)'
# '(Stjórnmálamaður) tengdur Framsóknarflokknum (hringdi í mig)'
# '(Ég ræddi við) konu tengda verkefninu'
# 'Skýrsla varðandi málið (kom út í gær)'
LoEftirNl/tala/fall/kyn →
    StaktLoEftirNl/tala/fall/kyn
    | "," LoLiður/tala/fall/kyn FsAtv? LokatáknEðaKomma
    | LoEftirNlMeðAndlagi/fall/tala/kyn
    | "," LoEftirNlMeðAndlagi/fall/tala/kyn LokatáknEðaKomma

# '(Stjórnmálaflokkarnir), hver um sig, (lýstu andstöðu við málið)'
LoEftirNl_ft/fall/kyn →
    HverHvor/fall/kyn
    # | "," HverHvor/fall/kyn LokatáknEðaKomma # Er höndlað sem NlSkýring

HverHvor/fall/kyn →
    'hver:fn'_et/fall/kyn HverHvorBotn/kyn      # hvern fyrir sig / af öðrum
    | 'hvor:fn'_et/fall/kyn HverHvorBotn/kyn    # hvorn í sínu lagi / á fætur öðrum

$score(+4) HverHvor/fall/kyn

HverHvorBotn/kyn →
    'fyrir:fs'_þf 'sig:abfn'_þf                         # fyrir sig
    | 'um:fs'_þf 'sig:abfn'_þf                          # um sig
    | 'í:fs'_þgf 'sinn:fn'_et_hk_þgf 'lag:hk'_et_þgf    # í sínu lagi
    | 'á_fætur:ao' 'annar:fn'_et_þgf/kyn                # á fætur annarri
    | 'af:fs'_þgf 'annar:fn'_et_þgf/kyn                 # af öðru

StaktLoEftirNl/tala/fall/kyn →
    ","? LoLiður/tala/fall/kyn

# Stakt lýsingarorð eftir nafnorði gæti líklega tilheyrt næsta nafnorði
$score(-8) StaktLoEftirNl/tala/fall/kyn

# Lýsingarorð sem getur tekið með sér þágufall
# !!! ATH: Til eru listar yfir slík orð

# '[sterklega] tengdur Framsóknarflokknum'
LoEftirNlMeðAndlagi/fall/tala/kyn →
    LoAtviksliðir? lo_sb/sfall/tala/fall/kyn LoViðhengi/sfall

$score(+4) LoEftirNlMeðAndlagi/fall/tala/kyn

# '(Ég drakk) hvítu, volgu og feitu mjólkina' / '(Ég sá) hvíta, volga og feita mjólk'
# '(Íslendingar fluttu fé til annarra) og [í sumum tilvikum] öruggari landa'

LoLiður/tala/fall/kyn →
    Lo/fall/tala/kyn
    | LoLiður/tala/fall/kyn LoKomma/tala/fall/kyn* KommaOgEðaEn FsRuna? Lo/fall/tala/kyn ","?

LoLiður_nf/tala/kyn → LoLiður_et_nf/kyn | LoLiður_ft_nf/kyn

LoKomma/tala/fall/kyn →
    "," Lo/fall/tala/kyn

# Fornöfn sem geta hegðað sér eins og lýsingarorð:
# 'Hann komst sjálfur út úr flakinu', 'Hún fór öll inn um gluggann',
# 'Þau eru bæði viðskiptafræðingar',  'Það reyndist vera sú sama'
# 'Þeir fóru sumir hverjir með ferjunni'
# ('báðir' er aðeins til í fleirtölu en það veldur ekki vandræðum hér)

LoFornafn/fall/tala/kyn →
    'allur:fn'/fall/tala/kyn | 'sjálfur:fn'/fall/tala/kyn | 'báðir:fn'/fall/tala/kyn
    | 'sami:fn'/fall/tala/kyn | 'sumur:fn'/fall/tala/kyn 'hver:fn'/fall/tala/kyn?

$score(+4) LoFornafn/fall/tala/kyn

# Lýsingarorð sem andlag sagnar
# Þar kemur til greina miðstig + 'en' + nafnliður

# '(Ég var) reiður' / '(Ég var) reiðari en þú' / '(Mér var verr við þetta) en þér'
# '(Ég var) reiðastur allra' / '(Ég talaði við þann) reiðasta þeirra allra'

# Ath.: miðstig lýsingarorða er merkt MST í BÍN og passar því ekki við lo_sb
# (en frumstig sterkrar beygingar er FSB og efsta stig er ESB)
LoSamanb/tala/fall/kyn →
    Lo_sb/tala/fall/kyn OgLo_sb/tala/fall/kyn?
    # Miðstig: (Ég varðveitti laxinn) [mun] betur [en bleikjuna / nú en áður]
    | MiðstigForskeyti? Lo_mst/tala/fall/kyn EnSamanb/fall?
    # (Hann tók við hrósinu) [,] ýmist glaður eða með fýlusvip
    | LoSamanbFleiri/fall/tala/kyn

LoSamanbFleiri/fall/tala/kyn →
    # ýmist blár eða með gullnum blæ
    Samanburðarmerki Ýmist LoTengtSögn/tala/fall/kyn Eða LoTengtSögn/tala/fall/kyn LokatáknEðaKomma
    | Samanburðarmerki Ýmist LoTengtSögn/tala/fall/kyn Eða FsAtv LokatáknEðaKomma
    | Samanburðarmerki Ýmist FsAtv Eða LoTengtSögn/tala/fall/kyn LokatáknEðaKomma
    | Ýmist LoTengtSögn/tala/fall/kyn Eða LoTengtSögn/tala/fall/kyn
    | Ýmist FsAtv Eða LoTengtSögn/tala/fall/kyn
    | Ýmist LoTengtSögn/tala/fall/kyn Eða FsAtv
    
    # bæði skjálfandi og í svitabaði
    | Samanburðarmerki Bæði LoTengtSögn/tala/fall/kyn Og LoTengtSögn/tala/fall/kyn LokatáknEðaKomma
    | Samanburðarmerki Bæði LoTengtSögn/tala/fall/kyn Og FsAtv LokatáknEðaKomma
    | Samanburðarmerki Bæði FsAtv Og LoTengtSögn/tala/fall/kyn LokatáknEðaKomma
    | Bæði LoTengtSögn/tala/fall/kyn Og LoTengtSögn/tala/fall/kyn
    | Bæði FsAtv Og LoTengtSögn/tala/fall/kyn
    | Bæði LoTengtSögn/tala/fall/kyn Og FsAtv

    # annaðhvort með fullan maga eða hungurmorða
    | Samanburðarmerki AnnaðhvortSt LoTengtSögn/tala/fall/kyn Eða LoTengtSögn/tala/fall/kyn LokatáknEðaKomma
    | Samanburðarmerki AnnaðhvortSt LoTengtSögn/tala/fall/kyn Eða FsAtv LokatáknEðaKomma
    | Samanburðarmerki AnnaðhvortSt FsAtv Eða LoTengtSögn/tala/fall/kyn LokatáknEðaKomma
    | AnnaðhvortSt LoTengtSögn/tala/fall/kyn Eða LoTengtSögn/tala/fall/kyn
    | AnnaðhvortSt FsAtv Eða LoTengtSögn/tala/fall/kyn
    | AnnaðhvortSt LoTengtSögn/tala/fall/kyn Eða FsAtv

    # hvorki í djúpum þönkum né hugsandi
    | Samanburðarmerki Hvorki LoTengtSögn/tala/fall/kyn Né LoTengtSögn/tala/fall/kyn LokatáknEðaKomma
    | Samanburðarmerki Hvorki LoTengtSögn/tala/fall/kyn Né FsAtv LokatáknEðaKomma
    | Samanburðarmerki Hvorki FsAtv Né LoTengtSögn/tala/fall/kyn LokatáknEðaKomma
    | Hvorki LoTengtSögn/tala/fall/kyn Né LoTengtSögn/tala/fall/kyn
    | Hvorki FsAtv Né LoTengtSögn/tala/fall/kyn
    | Hvorki LoTengtSögn/tala/fall/kyn Né FsAtv
    # Við vorum flest [,] ef ekki öll [,] hrifin af söng Maríu
    | LoEðaFs/fall/tala/kyn "ef:st" "ekki:ao" LoEðaFs/fall/tala/kyn
    | LoEðaFs/fall/tala/kyn "," "ef:st" "ekki:ao" LoEðaFs/fall/tala/kyn LokatáknEðaKomma
    | LoEðaFs/fall/tala/kyn "-:em" "ef:st" "ekki:ao" LoEðaFs/fall/tala/kyn "-:em"

$score(+4) LoSamanbFleiri/fall/tala/kyn

LoEðaFs/fall/tala/kyn →
     LoTengtSögn/tala/fall/kyn | FsAtv

MiðstigForskeyti →
    'munur:kk'_þgf_et           # mun meira
    | 'mikill:lo'_sb_hk_þgf_et  # miklu meira  !!! Ath: skrýtið að þurfa að marka sem þgf
    | Sinnum                    # x sinnum hraðar
    | 'tvisvar_sinnum:ao'
    | 'þrisvar_sinnum:ao'
    | 'fjórum_sinnum:ao'
    | 'mörgum_sinnum:ao'

$score(+2) MiðstigForskeyti

EnSamanb/fall →
    HeldurEn Nl/fall                             # (Ég var [mun] reiðari) en Páll
    | HeldurEn HreinYfirsetning                  # (Ég er ósáttari) en Páll gæti verið
    | "nú:ao"? HeldurEn "áður:ao"   # (Ég er ánægðari) nú en áður

$score(+4) EnSamanb/fall

# Hann er reiður / [!!! ATH:] Hann er Önnu mun reiðari en Páli
LoSamanb_nf/tala/kyn →
    Lo_sb_nf/tala/kyn OgLo_sb_nf/tala/kyn?
    | MiðstigForskeyti? Lo_mst_nf/tala/kyn EnSamanb_nf? # Miðstig: (Ég var) [mun] reiðari [en Páll]
    # (Hann tók við hrósinu) [,] ýmist glaður eða með fýlusvip
    | LoSamanbFleiri_nf/tala/kyn

LoTengtSögn/tala/fall/kyn →
    LoSamanb/tala/fall/kyn
    | AtviksliðurEinkunn* LoFornafn/fall/tala/kyn # (Hann komst) [hins vegar] sjálfur (út úr flakinu)
    | LoTengtSögn/tala/fall/kyn LoKommaTengdSögn/tala/fall/kyn* KommaOgEðaEn LoSamanb/tala/fall/kyn

LoKommaTengdSögn/tala/fall/kyn →
    "," LoSamanb/tala/fall/kyn

LoTengtSögn_nf/tala/kyn →
    LoSamanb_nf/tala/kyn
    | AtviksliðurEinkunn* LoFornafn_nf/tala/kyn # (Hann komst) sjálfur (út úr flakinu)
    | LoTengtSögn_nf/tala/kyn LoKommaTengdSögn_nf/tala/kyn* KommaOgEðaEn LoSamanb_nf/tala/kyn

LoTengtSögn_nf_ft/kyn →
    HverHvor_nf/kyn  # Norðurlöndin eru hvert/hvort um/fyrir sig ófær um að taka á vandanum

LoKommaTengdSögn_nf/tala/kyn →
    "," LoSamanb_nf/tala/kyn

# Viðhengi með miðmyndarsögnum: (okkur finnst) sjálfum/mörgum/flestum [hverjum]...
LoÞgf_et/kyn →
    lo_sb_þgf_et/kyn
    | LoFornafn_þgf_et/kyn

LoÞgf_ft/kyn →
    lo_sb_þgf_ft/kyn 'hver:fn'_þgf_ft/kyn?
    | LoFornafn_þgf_ft/kyn 'hver:fn'_þgf_ft/kyn?

# Forsetningarliðir

FsMeðFallstjórn →

    fs/fall Nl/fall

    # Forsetningar sem geta tekið með sér sagnlið í nafnhætti
    # eru sérmerktar 'nh' í Main.conf og tengjast við fs_nh í binparser.py.
    # '(Beiðnin) um að [fara]...', '(Fíknin) í að [gera]...',
    # '(Biðin) eftir að [sækja]...', '(Þörfin) fyrir að [kaupa]...',
    # '(Útrásin) gegnum að [tala]...', '(Bannhelgin) kringum að [tala]...',
    # 'þrátt fyrir að (fara aldrei í Costco)'
    # 'með að [gera]...', '(Löngunin) til að [berja]...', '(Áhættan) við að [kaupa]...',
    # '(Hættan) á að [aka]...'
    | fs_nh FsAð

    # Fleiryrtar forsetningar sem eru ekki dregnar saman í Phrases.conf
    | 'nær:fs'_þgf 'draga:so'_op_gm_et_p3 Nl_þgf    # 'þegar nær dró/dregur jólum'
    | 'beggja:fn'_hk_ef_ft 'vegna:fs'_ef Nl_ef      # 'beggja vegna vegarins'
    | 'bera:so'_lhþt_sb_hk_nf_et "saman:ao" 'við:fs'_þf Nl_þf  # borið saman við (fyrra ár)
    | "fyrir:ao" "nú:ao"? 'utan:fs'_þf Nl_þf
    | "innan:ao" "sem:st" 'utan:fs'_ef Nl_ef
    | "hvað:ao" FsHvaðVarðar
    # allt frá hruni, allt frá börnum til fullorðinna
    | Allt 'frá:fs'_þgf Nl_þgf
    # 'um að lög um endurupptökunefnd standist ekki stjórnarskrá' - Ath: 'um það að' er höndlað í ÞaðTenging
    | 'um:fs'_þf "að:st" HreinYfirsetning ","?
    # 'um hvort/hvenær/af hverju/til hvers/hví Jón kaus Framsóknarflokkinn'
    | 'um:fs'_þf Spurnaraukasetning ","?
    # 'er bjartsýnn á að Alfreð komi aftur í liðið' - Ath: 'á það/því að' er höndlað í ÞaðTenging
    | 'á:fs'_þf "að:st" HreinYfirsetning ","?
    | 'að:fs'_þgf 'nafn:hk'_þgf_et person_nf            # Ég ræddi við konu að nafni Aðalbjörg Jónsdóttir
    | 'að:fs'_þgf Nl_þgf/tala/kyn lo_sb/fall/tala/kyn   # 'að virðisaukaskatti meðtöldum'
    | 'af:fs'_þgf Nl_þgf NhEinfaldur                    # Af fyrstu fregnum að dæma (virðist veiðitímabilið byrja vel)
    | 'með:fs'_þgf Eða 'án:fs'_ef Nl_ef                 # 'með eða án greindarskerðingar'
    | FsÚtFrá
    | FsÍGegnUm
    | FsUmOgEftir
    | FsAðBaki
    | FsTilÞgf
    | AtvFsSaman    # inn eftir firðinum, fram undan rúminu
    | FsUmLeið
    | FsMilli
    | FsAðFjárhæð                                       # 'að fjárhæð/upphæð 10 milljónir króna'
    # 'hvorki við stjórn spítalans né ráðherrann'
    # 'hvorki til mín né stjórnarinnar'
    | Hvorki fs/fall Nl/fall Né Nl/fall
    | FsÞgfTilEf

    | "(" FsMeðFallstjórn ")"

$score(+1) FsMeðFallstjórn

FsAð →
    NhLiðir SagnInnskot?    # Þörfin fyrir að læra spænsku [í skólum]
    | Skýringarsetning      # Þörfin fyrir að Jón seldi bílinn

$score(+4) FsAð

$tag(begin_prep_scope) FsAð # Nýtt samhengi fyrir samtengingu sagna og forsetninga

# Sagnorð í lýsingarhætti nútíðar sem stendur sjálfstætt með viðfangi, eins og forsetning
# 'Hún stóð í sömu sporum reykjandi sígarettu'
# 'Verandi lögmaður sakborningsins get ég ekki tjáð mig um málið'
# 'Bölvandi þjónustukerfinu beit hún á jaxlinn'
SögnLhNt → EinSögn_lhnt

$score(+7) SögnLhNt

# Áttirnar eru aðeins forsetningar í BÍN, ekki atviksorð - þess vegna þurfa þær sérmeðhöndlun

Átt → 'vestur:fs'_þf | 'austur:fs'_þf | 'suður:fs'_þf | 'norður:fs'_þf

ÁttFs → Átt Nl_þf    # suður götuna

$score(+12) ÁttFs

FsÞgfTilEf →
    Nl_þgf 'til:fs'_ef Nl_ef # máli sínu til stuðnings, barninu til hróss, henni til bjargar

$tag(purge_prep) FsÞgfTilEf

# Ekki velja þetta nema nauðsyn beri til
$score(-6) FsÞgfTilEf

FsUmOgEftir →
    # 'kvikmyndir um og eftir konur'
    'um:fs'_þf Og 'eftir:fs'_þf Nl_þf

$score(+8) FsUmOgEftir

FsÍGegnUm →
    "í:ao" "gegn:ao" 'um:fs'_þf Nl_þf
    | "í:ao" 'gegnum:fs'_þf Nl_þf

$tag(purge_prep) FsÍGegnUm

$score(+6) FsÍGegnUm

FsUmLeið →
    'um:fs'_þf 'leið:kvk'_þf_et

$score(+6) FsUmLeið

# Gefa aukastig fyrir hið algenga form 'milli X og Y'
# þar sem annars er tilhneiging til að tengja 'og Y' við
# eignarfallslið ofar í trénu
FsMilli →
    'milli:fs'_ef Nl_ef "og:st" Nl_ef

$score(+6) FsMilli

# Sérregla fyrir "út frá" vegna þess að "út frá því að S" er
# algengt mynstur sem annars þáttast sem Tíðarsetning
FsÚtFrá →
    "út:ao" 'frá:fs'_þgf Nl_þgf

$score(+24) FsÚtFrá

FsAðBaki →
    'að:fs'_þgf 'bak:hk'_þgf_et Nl_þgf # 'að baki hverju bundnu sæti'

$score(+4) FsAðBaki

FsTilÞgf →
    'til:fs'_ef FsTilÞgfOrð Nl_þgf # 'til stuðnings/heiðurs Páli'

FsTilÞgfOrð →
    'stuðningur:kk'_ef_et | 'heiður:kk'_ef_et | 'háðung:kvk'_ef_et

# að fjárhæð [rúmlega] 10 milljónir króna

FsAðFjárhæð →
     'að:fs'_þgf NlFjárhæð_þgf

NlFjárhæð_þgf → 'fjárhæð:kvk'_þgf_et ao* amount | 'upphæð:kvk'_þgf_et ao* amount

So_1_gm_fh_p3_et_nf → so_1_nf_gm_fh_p3_et
So_1_gm_fh_p3_et_þf → so_1_þf_gm_fh_p3_et
So_1_gm_fh_p3_et_þgf → so_1_þgf_gm_fh_p3_et
So_1_gm_fh_p3_et_ef → so_1_ef_gm_fh_p3_et

FsHvaðVarðar →
    Nl/fall So_1_gm_fh_p3_et/fall   # '(hvað) Pál varðar/Páli viðvíkur'
    | So_1_gm_fh_p3_et/fall Nl/fall # '(hvað) varðar Pál/viðvíkur Páli'

$score(+4) FsHvaðVarðar

FsLiður →
    AtviksliðurOg? FsMeðFallstjórn  # [síðastliðið sumar [og]] í flýti

AtviksliðurOg →
    AtviksliðurAuka+ Og?

# FsAtv getur verið runa af forsetningarliðum með atviksliðum inn á milli,
# á undan og í lok - eða einungis atviksliðir.

FsAtv →
    # (Ég samþykkti þetta), en með semingi þó [,]
    Samanburðarmerki? En? FsAtvRuna FsAtvFramhald* ","?

FsAtvFramhald →
    KommaOgEðaEn EinFsAtv FsAtvRuna?

FsAtvRuna →
    EinFsAtv+
    | EinFsAtv* "-:em" EinFsAtv+ "-:em"

EinFsAtv →
    Nema? FsMeðFallstjórn
    | EinnAl
    # '(hann tók á móti mér) hlæjandi'
    | SögnLhNt
    | SamsettFsAtv
    | AlTilv            # Hún horfir þangað sem hann stóð

# Atviksliðir sem innihalda tilvísunarsetningu
# Geta verið staðar-, stefnu- og spurnaratviksorð
AlTilv →
    SemAtv Tilvísunarsetning/tala/pers/kyn

SemAtv → "þar:ao" | "þangað:ao" | "hingað:ao" | "þarna:ao" | "hér:ao" | "þaðan:ao"
        | "hvar:ao" | "hvaðan:ao" | "hvenær:ao" | "hvernig:ao" | "hvert:ao" | "hversu:ao"

SamsettFsAtv →
    # 'hvorki í Frakklandi né annars staðar í Evrópusambandinu'
    Hvorki EinEinföldFsAtv+ Né EinEinföldFsAtv
    # 'annaðhvort í Breiðholti eða við World Class'
    | AnnaðhvortSt EinEinföldFsAtv+ Eða EinEinföldFsAtv
    # 'bæði við handavinnu og í viðgerðum'
    | Bæði EinEinföldFsAtv+ ","? Og EinEinföldFsAtv
    # 'ýmist að fullu eða til hálfs'
    | Ýmist EinEinföldFsAtv+ Eða EinEinföldFsAtv
    # Hann sagði ósatt, annars vegar í fyrstu yfirheyrslu og hins vegar með framburði sínum í lok málsins
    | 'annars_vegar:ao' EinEinföldFsAtv+ ","? "og:st" 'hins_vegar:ao' EinEinföldFsAtv

# Gefa samsetningum auka stig, þær smella vel saman
$score(+4) SamsettFsAtv

EinEinföldFsAtv → 
    FsMeðFallstjórn 
    | EinnAl 
    | SögnLhNt

Nema →
    "nema:st" "þá:ao"? "helst:ao"?

# AtvFs hefst alltaf á atvikslið en á eftir geta fylgt forsetningarliðir
# og atviksliðir.

AtvFs →
    ","? EinnAl EinFsAtv* FsAtvFramhald* ","?

# AL gefa vísbendingu um fall á FL
AtvFsSaman →
    BreytiAtv FsEftir_þgf
    | BreytiAtv FsVið_þf
    | "handan:ao" FsVið_þf
    | StaðarAtv FsUndirÁÍ_þgf
    | StefnuAtv FsUndirÁÍ_þf
    | ÁttarAtv FsUndirÁÍ_þf
    | ÁttarAtv FsUndirÁÍ_þgf
    | StefnuAtv FsYfirFyrir_þf
    | StefnuAtv FsMeð_þgf
    | ÁttarAtv FsMeð_þgf

$score(+18) AtvFsSaman

# Taka sérstaklega á runu forsetningarliða sem er afmörkuð milli komma;
# það minnkar tvíræðni

FsLiðirMeðKommu →
    FsLiður
    | FsLiðirMeðKommu KommaOgEðaEn? FsLiður

# FsRuna er runa forsetninga og atviksliða, sem getur þó ekki
# endað á atvikslið. Hún er því rétti kosturinn á undan
# nafnlið, enda geta nafnliðir byrjað á atviksliðum (nánar
# til tekið á AtviksliðurEinkunn).

# FsRunaInnskot er FsRuna sem fær mínusstig og skal notast
# þar sem innskot forsetningar er klasturslegt en ekki með
# öllu óleyfilegt.

# FsRunaEftirSögn er FsRuna sem getur innifalið tímalið
# (sem er atviksliður en er ekki leyfður í AtviksliðurEinkunn).
# Nota á FsRunaEftirSögn á milli sagnar og nafnliðar (andlags),
# þar sem tímaliður getur passað við sögnina. Dæmi:
# 'Kjarninn birti í lok janúar úttekt á málinu.'

# FsRunaEftirNl er FsRuna sem stendur á eftir nafnlið og
# getur innifalið viss atviksorð sem hengja má á nafnliðinn,
# svo sem "erlendis" og "hérlendis". Dæmi:
# 'Lagaramminn hérlendis leyfir þetta ekki.'

# FsRuna getur ekki endað á atvikslið, nema innan kommu
# 'var, líkt og í búsáhaldabyltingunni, byggð á óánægju'
FsRuna →
    FsInnanKommu | FsLiðirMeðKommu

FsInnanKommu →
    "," FsForskeyti? FsLiðirMeðKommu LokatáknEðaKomma

$score(+4) FsInnanKommu

FsRunaEftirNl →
    # í Danmörku og hérlendis
    # !!! ATH: Þetta leyfir ekki 'hérlendis og í Danmörku'
    FsRuna OgAtvEftirNl*
    # hérlendis og erlendis
    | AtvEftirNl OgAtvEftirNl*

# Atviksorð sem geta fylgt á eftir nafnliðum
# !!! Setja í conf?
AtvEftirNl →
    "hérlendis:ao"
    | "erlendis:ao"
    | 'í_ár:ao'

OgAtvEftirNl →
    OgEða AtvEftirNl

# Hvetja létt til þess að forsetningar séu tengdar nafnlið
$score(+1) FsRunaEftirNl
$tag(purge_prep) FsRunaEftirNl # Ekki senda innifaldar forsetningar upp tréð

# Atviksliðir eru ekki leyfðir milli sagnar og andlags, enda
# er ætlast til að AtviksliðurEinkunn fylgi andlaginu sem slíku.
# Hins vegar eru til dæmis tímaliðir ekki leyfðir í AtviksliðurEinkunn
# og verða því að vera leyfðir hér - enda rökréttara að þeir fylgi
# sögninni fremur en andlaginu.
# Ath: Í tilviki tímaliðar þarf FsRunaEftirSögn ekki að enda á
# forsetningarlið.
FsRunaEftirSögn →
    FsRuna AtvTímaliður?    # (Jón ítrekaði) á Alþingi síðastliðinn vetur (beiðni sína)
    | AtvTímaliður          # (Kjarninn birti) í lok janúar (úttekt á málinu)

AtvTímaliður →
    Atviksliður? Tímaliður  # strax síðastliðið sumar
    | FyrrEn Tímaliður      # fyrr en síðastliðið sumar / á föstudagskvöld
    | FyrrEn FsMeðFallstjórn # fyrr en eftir dúk og disk
    # Undantekningar sem falla ekki undir eo, gegna helst hlutverki talmálsmarkara
    | "nú:ao" | "þá:ao"     # Lögreglan rannsakar þá/nú málið

$score(+10) AtvTímaliður

# $tag(apply_length_bonus) FsRunaEftirSögn

# Forskeyti sem leyfileg eru á undan forsetningarlið innan kommu
FsForskeyti →
    Samanburðartenging | 'það:pfn'_hk_et_nf 'vera:so'_0_gm_fh_p3_et

# Forsetningar þar sem AL á undan gefur vísbendingar um fall í FL
FsEftir/fall →
    'eftir:fs'/fall Nl/fall
FsMeð/fall →
    'með:fs'/fall Nl/fall
FsVið/fall →
    'við:fs'/fall Nl/fall
FsYfirFyrir/fall →
    'yfir:fs'/fall Nl/fall
    | 'fyrir:fs'/fall Nl/fall
FsUndirÁÍ/fall →
    'undir:fs'/fall Nl/fall
    | 'á:fs'/fall Nl/fall
    | 'í:fs'/fall Nl/fall

# Atviksliður

Al → EinnAl+

EinnAl →
    ao                      # Strax tvöfalt til baka, loksins
    | Tímaliður             # (Ég tilkynnti þetta) strax 3. september
    | EkkiNeitt
    | UndirEins
    | SemStendur
    | FyrirLöngu
    | Sinnum
    | Úrslit                # KR sigraði Val 6:0
    | AlMiðstig             # [mun] hraðar en augað sér
    | AlHvortSemUmErAðRæða  # Hvort sem um er að ræða X eða Y

    # Fjölyrtir atviksliðir sem ekki er unnt að búa til fasta frasa úr

    | 'einn:fn'_kvk_et_þf 'ferð:kvk'_þf_et_gr "enn:ao"  # eina ferðina enn
    | "heima:ao" "fyrir:ao"
    | "hingað:ao" "til:ao"
    | "sem:ao" 'segja:so'_lhþt_sb_hk_nf_et  # sem sagt
    | "svo:ao" "sem:ao"
    | "sem:ao" "fyrst:ao"
    | 'vinsamlegur:lo'_esb_nf_et_hk     # (Þátttakendur eru) vinsamlegast (beðnir að fylla út eyðublað)
    | 'náðarsamlegur:lo'_esb_nf_et_hk   # (Þátttakendur eru) náðarsamlegast (beðnir að fylla út eyðublað)
    | "mest:ao" Allra     # (Ferðamönnum hefur fjölgað) mest allra
    | Allra "mest:ao"     # (Ferðamönnum hefur fjölgað) allra mest
    | "minnst:ao" Allra   # (Ferðamönnum hefur fjölgað) minnst allra
    | Allra "minnst:ao"   # (Ferðamönnum hefur fjölgað) allra minnst
    | 'nokkur:fn'_et_þf_kk 'tími:kk'_et_þf # Þetta er meira en við höfum nokkurn tíma[nn] gert
    | 'allur:fn'_et_þf_kk 'tími:kk'_gr_et_þf # Allan tímann var mér ljóst að þetta var vonlaust
    | 'allur:fn'_ft_þf_kk "jafna:ao"    # Alla jafna eru menn rólegir yfir þessu
    | 'heill:lo'_sb_nf_et_hk "yfir:ao"  # Heilt yfir eru menn rólegir yfir þessu
    | HliðViðHlið

    | ao_zlega so_lhþt_nf_hk_et         # Hreinskilnislega sagt, gróflega séð
    | lo_vb_nf_hk_et_zlega so_lhþt_nf_hk_et # Stórkarlalega ályktað

$tag(no_prep) EinnAl        # Engar forsetningar hér inni - óþarfi að skoða dótturgreinar

HliðViðHlið →
    'hlið:kvk'_et_þf 'við:fs'_þf 'hlið:kvk'_et_þf # (...starfa) hlið við hlið (með félögum sínum)

$score(+10) HliðViðHlið

AlMiðstig →
    MiðstigForskeyti? ao_mst EnSamanb_nf? # (Gunnar ók) [mun/miklu] hraðar [en Jón gat ímyndað sér]

$score(-3) AlMiðstig

EkkiNeitt →
    "ekki:ao" "neitt:ao"    # (Ég þekki þetta) ekki neitt

$score(-6) EkkiNeitt

FyrirLöngu →
    'fyrir:fs'_þgf 'langur:lo'_þgf_et_hk # 'fyrir löngu'

$score(+8) FyrirLöngu

# Sem stendur er hann ekki viðlátinn
SemStendur →
    "sem:ao" 'standa:so'_0_gm_fh_et_p3

$score(-6) SemStendur

UndirEins →
    "undir:ao" "eins:ao"    # 'Ég fór undir eins og kannaði málið'

$score(+4) UndirEins

Sinnum →
    # tvisvar, þrisvar og fjórum sinnum er skilgreint sem atviksliður í Phrases.conf
    TalaOg? töl_ft_hk_þgf TalaTil? 'sinn:hk'_þgf_ft     # tuttugu og fimm sinnum
    | tala_ft_hk_þgf TalaTil? 'sinn:hk'_þgf_ft          # 5 sinnum
    | tala_et_hk_þgf 'sinn:hk'_þgf_et                   # 101 sinni
    | TalaOg 'tvisvar_sinnum:ao'
    | TalaOg 'þrisvar_sinnum:ao'
    | TalaOg 'fjórum_sinnum:ao'

TalaOg →
    töl_ft_hk_þgf Og

TalaTil →
    "til:st" töl_ft_hk_þgf
    | "til:st" tala_ft_hk_þgf

$score(+24) Sinnum

# Hvort [heldur] sem um er að ræða nýja byggingu, timburhús eða eldra mannvirki
AlHvortSemUmErAðRæða →
    HvortSem FsUmAðRæða

FsUmAðRæða →
    'um:fs'_þf SögnUmAðRæða NlRunaEða_þf

SögnUmAðRæða →
    HjSögnVeraNh Nhm 'ræða:so'_0_gm_nh FsRunaInnskot?

SögnVarUmAðRæða →
    HjSögnVeraNh FsVarUmAðRæða

FsVarUmAðRæða →
    'um:fs'_þf SögnAðRæða Nl_þf

SögnAðRæða →
    Nhm 'ræða:so'_0_gm_nh

# Þessu virðist þurfa að gefa mjög hátt skor svo það sigri stigagjöf
# annarra mögulegra trjáa
$score(+340) AlHvortSemUmErAðRæða

Atviksliður →
    Al OgAtviksliður*

OgAtviksliður →
    KommaOgEðaEn Al         # Hratt, en örugglega og fumlaust

# Fanga hugsanlegan atvikslið innan kommu:
# Fyrirtækin annars vegar, og heimilin hins vegar, hafa dregið úr fjárfestingu
AtvKomma →
    AtviksliðurAuka? ","

AtviksliðurKomma →
    AtviksliðurAuka ","?

AtviksliðurEinkunn →
    eo                              # Einkunnarorð = atviksorð sem geta ekki jafnframt verið forsetningar
    | Aldur                         # Hin 29 ára gamla fyrirsæta
    | Allt "að:ao"                  # 'fjárfesta fyrir allt að 100 milljónir króna'
    | "svo:ao" "sem:ao"             # 'enda kemur kynlíf þeirra svo sem engum við'
    | "innan_við:fs"
    | UmEðaYfir

UmEðaYfir →
    "um:ao" Eða "yfir:ao"      # Um eða yfir 100 tyrkneskum eldflaugum rigndi yfir Aleppo

$score(+8) UmEðaYfir

# Atviksorð sem geta breytt falli innan forsetningarliðar sem fylgir þeim
BreytiAtv →
    StaðarAtv
    | StefnuAtv
    | UpprunaAtv
#    | ÁttarAtv

StaðarAtv →
    "frammi:ao"
    | "inni:ao"
    | "niðri:ao"
    | "uppi:ao"
    | "úti:ao"

StefnuAtv →
    "aftur:ao"
    | "fram:ao"
    | "inn:ao"
    | "niður:ao"
    | "upp:ao"
    | "út:ao"

UpprunaAtv →
    "aftan:ao"
    | "austan:ao"
    | "framan:ao"
    | "innan:ao"
    | "neðan:ao"
    | "norðan:ao"
    | "ofan:ao"
    | "sunnan:ao"
    | "utan:ao"
    | "vestan:ao"

ÁttarAtv →
    "austur:ao"
    | "norður:ao"
    | "suður:ao"
    | "vestur:ao"

# AL eða Atvikssetning koma bæði til greina
AtvFsAuka → 
    AtvFs | Atvikssetning
AtviksliðurAuka → 
    Atviksliður | Atvikssetning

AtvFsAukaInnskot → AtvFsAuka

$score(+4) AtvFsAukaInnskot

# [tæpra] 29 ára, [rúmlega] eins árs, tveggja vikna, margra ára
Aldur →
    TöluorðForskeyti_ef/tala/kyn? Töluorð_ef/tala/kyn AldurBotn/tala/kyn
    | 'margur:lo'_sb_ef_ft/kyn TímaNafnorðStærri_ef_ft/kyn
    | 'allmargur:lo'_sb_ef_ft/kyn TímaNafnorðStærri_ef_ft/kyn
    | 'fjölmargur:lo'_sb_ef_ft/kyn TímaNafnorðStærri_ef_ft/kyn
    | 'fár:lo'_sb_ef_ft/kyn TímaNafnorðStærri_ef_ft/kyn
    | 'örfár:lo'_sb_ef_ft/kyn TímaNafnorðStærri_ef_ft/kyn

# Gefa plús fyrir aldur
$score(+6) Aldur

AldurBotn/tala/kyn →
    TímaNafnorðStærri_ef/tala/kyn
    # eins og hálfs árs, tveggja og hálfrar aldar
    | Og 'hálfur:lo'_sb_et_ef/kyn TímaNafnorðStærri_ef_et/kyn

# Dagsetning og tími

Tímaliður → 
    FöstDagsetning
    | AfstæðDagsetning
    | FasturTímapunktur
    | AfstæðurTímapunktur
    | Tíðni
    | Tímabil

$score(+8) Tímaliður
$tag(apply_prep_bonus) Tímaliður    # Gefa bónus þegar dagsetning fylgir sögn frekar en nafnlið
$tag(apply_length_bonus) Tímaliður  # Gefa bónus eftir því hversu margar eindir (tokens) falla undir dagsetninguna

Dagsetningarliður →
    FöstDagsetning | AfstæðDagsetning

$score(+12) Dagsetningarliður
$tag(apply_prep_bonus) Dagsetningarliður    # Gefa bónus þegar dagsetning fylgir sögn frekar en nafnlið
$tag(apply_length_bonus) Dagsetningarliður  # Gefa bónus eftir því hversu margar eindir (tokens) falla undir dagsetninguna

# Aðalliður: Föst dagsetning
FöstDagsetning → 
    Vikudagur_gr_þf_et? dagsföst
    | HinnSá_þf dagsföst
    | DaganaFast

DaganaFast →
    'dagur:kk'_ft_gr_þf? dagsafs Til dagsföst     # (dagana) 13. október og/til/- 14. nóvember 2017
    | 'dagur:kk'_ft_gr_þf? raðnr Til dagsföst     # (dagana) 13. og/til/- 14. nóvember 2017

# Aðalliður: Afstæð dagsetning 
AfstæðDagsetning →
    ÓfullkominDagsetning
    | ÍFs_þf Árstíð_þf_et/kyn        # í haust, í sumar
    | ÍFs_þgf 'liðinn:lo'_sb_þgf_et_kvk 'vika:kvk'_þgf_et # í liðinni viku
    | ÁFs_þgf 'liðinn:lo'_sb_þgf_ft_hk 'ár:hk'_þgf_ft OgÁratugum? # á liðnum árum [og áratugum]
    | ÍFs_þgf dagsafs NæstSíðast_þgf_et_kk? # í október [1933] [síðastliðnum]

    | ÍByrjunLok Mánuður_et_ef NæstSíðast_ef_et_kk? # í byrjun septembermánaðar [síðastliðins]
    | ÍByrjunLok dagsafs NæstSíðast_ef_et_kk? # í byrjun október (2014) [síðastliðins]
    | ÍByrjunLok 'ár:hk'_et_ef ártal    # í byrjun árs 1945
    | ÍByrjunLok TímaÁkvæðisliður_ef_et/kyn? TímaNafnorðStærri_ef_et/kyn # í byrjun/lok dags/viku

    | TímaNafnliðurStærri_þgf/tala/kyn ÁðurSíðarSeinna             # (hér um bil) 10 vikum síðar
    | 'einhver:fn'_hk_þgf_et ÁðurSíðarSeinna                       # Einhverju síðar (var bíllinn færður)
    | 'einn:fn'_kk_et_þf 'dagur:kk'_et_þf_gr                       # Einn daginn (var bíllinn færður)
    | 'fyrir:fs'_þgf TímaNafnliðurStærri_þgf/tala/kyn AðvörunSíðan?  # fyrir (rúmlega) 10 árum (síðan)
    | 'eftir:fs'_þf TímaNafnliðurStærri_þf/tala/kyn                # eftir tæplega átta daga

    | FyrrSíðarSeinna? AfstæðDagsetningFs                          # fyrr á árinu, síðar á árinu
    | FyrrSíðarSeinna TímaVísiNafnliðurStærri_þf_et/kyn            # fyrr sama mánuð, fyrr sama dag   - of vítt með spurningarmerki á FyrrSíðarSeinna
    | 'í_hitteðfyrra:ao'
    | 'í_hittiðfyrra:ao'
    | VísiDagsetning                                               # á föstudag, um helgina, á morgun, í gær

OgÁratugum →
    Og 'áratugur:kk'_þgf_ft

ÓfullkominDagsetning →
    Vikudagur_þf_et? dagsafs NæstSíðast_þf_et_kk?
    | HinnSá_þf dagsafs NæstSíðast_þf_et_kk?                # hinn/þann 14. nóvember sl.
    | ártal
    | Árstíð_þf_et/kyn ártal_þf_et                          # haustið 2016, veturinn 1998
    | HinnSá_þf raðnr SáÞessi_ef_et_kk 'mánuður:kk'_ef_et   # þann/hinn 23. þess/þessa mánaðar
    | Mánaðamót                                             # mánaðamótin mars-apríl
    | DaganaAfs NæstSíðast_þf_et_kk?                        # dagana 14. og 15. október
    | ÁrinAfs                                               # árin 1938 og 1967
    | TímaÁkvæðisliður_þf_et_kk Vikudagur_et_þf OgVikudagur_et_þf? # næsta fimmtudag [[, föstudag] og/eða laugardag]
    | TímaÁkvæðisliður_þf_et/kyn Árstíð_þf_et/kyn           # næsta haust, þann sama vetur
    | TímaNafnorðStærri_gr_þf/tala FsFyrirEftir             # daginn fyrir útskriftina, mánuðina eftir jarðskjálftann
    | HinnSá_þf? RaðTala_þf_et_kk MánaðarNafn_þf            # [hinn/þann] sjöunda febrúar

VísiDagsetning → 
    ÁFs_þf Vikudagur_et_þf OgVikudagur_et_þf? # á föstudag [eða laugardag]
    | 'um:fs'_þf 'helgi:kvk'_þf_et_gr       # um helgina
    | 'um:fs'_þf 'mánaðamót:hk'_þf_ft_gr    # um mánaðamótin
    | 'um:fs'_þf 'áramót:hk'_þf_ft_gr       # um áramótin
    | 'á_morgun:ao'                         # á morgun
    | 'í:fs'_þf 'dagur:kk'_þf_et            # í dag
    | 'í_gær:ao'                            # í gær
    | 'í_fyrradag:ao'                       # í fyrradag
    | 'í:fs'_þf 'fyrri:lo'_þf_et_mst        # í fyrra
    | 'dagur:kk'_þf_et_gr "eftir:ao"        # daginn eftir
    | 'annar:fn'_et_þf_hk 'kvöld:hk'_et_þf  # annað kvöld
    | 'annar:fn'_et_þf_kvk 'nótt:kvk'_et_þf # aðra nótt
    | ÁFs_þf Vikudagur_gr_þf_et 'koma:so'_0_gm_fh_et_p3          # á sunnudaginn kemur
    | ÁFs_þf VikuMorgunnKvöld_gr_þf_et 'koma:so'_0_gm_fh_et_p3   # á laugardagskvöldið kemur

# Hér eru aðeins orð yfir mánuði sem ekki tengjast við 'dagsafs' lauf
Mánuður_et/fall →
    'janúarmánuður:kk'_et/fall
    | 'febrúarmánuður:kk'_et/fall
    | 'marsmánuður:kk'_et/fall
    | 'aprílmánuður:kk'_et/fall
    | 'maímánuður:kk'_et/fall
    | 'júnímánuður:kk'_et/fall
    | 'júlímánuður:kk'_et/fall
    | 'ágústmánuður:kk'_et/fall
    | 'septembermánuður:kk'_et/fall
    | 'októbermánuður:kk'_et/fall
    | 'nóvembermánuður:kk'_et/fall
    | 'desembermánuður:kk'_et/fall

# Ath: Hér þýðir ekki að nota 'janúar:kk'_et/fall eða álíka,
# þar sem tókinn er DATEREL en ekki WORD
MánaðarNafn_þf →
    "janúar"
    | "febrúar"
    | "mars"
    | "apríl"
    | "maí"
    | "júní"
    | "júlí"
    | "ágúst"
    | "september"
    | "október"
    | "nóvember"
    | "desember"

Dagana →
    'dagur:kk'_gr_ft_þf | 'helgi:kvk'_gr_et_þf  # dagana/helgina

DaganaAfs → 
    Dagana? dagsafs Til dagsafs  # (dagana/helgina) 13. október og/til/- 14. nóvember
    | Dagana? raðnr Til dagsafs  # (dagana/helgina) 13. og/til/- 14. nóvember 

FsFyrirEftir →
    FyrirEftir Nl_þf

FyrirEftir →
    'fyrir:fs'_þf | 'eftir:fs'_þf

ÁrinAfs →
    ártal ÁraRuna* Til tala                        # 2016-2017, 2016 til 17
    | 'ár:hk'_ft_gr_þf tala ÁraRuna* Til tala      # árin 670-80, árin 2016 til 2017

Mánaðamót →
    'mánaðamót:hk'_ft_þf dagsafs '-:en' dagsafs

Árstíð_þf_et_hk →
    'vor:hk'_þf_et | 'sumar:hk'_þf_et | 'haust:hk'_þf_et

Árstíð_þf_et_kk →
    'vetur:kk'_þf_et

Árstíð_þf_et_kvk →
    "" # Never matches

# Bara AfstæðDagsetning -- aðrir svipaðir liðir fara í aðrar reglur
# (í viku (Tímabil), á ári (Tíðni), í mánuð (Tímabil), ...)
# á árinu, í vikunni, í mánuðinum
AfstæðDagsetningFs → 
    ÁFs_þgf 'ár:hk'_þgf_et_gr                                # á árinu
    | ÁFs_þgf TímaÁkvæðisliður_þgf_et_hk 'ár:hk'_þgf_et      # á næsta ári
    | ÍFs_þgf 'mánuður:kk'_þgf_et_gr                         # í mánuðinum
    | ÍFs_þgf TímaÁkvæðisliður_þgf_et_kk 'mánuður:kk'_þgf_et # í síðasta mánuði
    | ÍFs_þgf 'vika:kvk'_þgf_et_gr                           # í vikunni
    | ÍFs_þgf TímaÁkvæðisliður_þgf_et_kvk 'vika:kvk'_þgf_et  # í sömu viku
    | ÍFs_þf Árstíð_þf_et/kyn                                # í haust, í vetur, í vor, í sumar

# Nauðsynlegar reglur til að einangra liði í úttaki
ÍFs/fall → 'í:fs'/fall
ÁFs/fall → 'á:fs'/fall

# Gefa slatta af stigum til að yfirvinna sögnina 'að eiga' með þolfalli
$score(+40) ÁFs_þf

# Aðalliður: Fastur tímapunktur 
FasturTímapunktur →
    tímapunkturfast                                 # 14. nóvember 2017 kl. 14:35
    | 'áramót:hk'_gr_ft_þf tala '-:en' tala         # áramótin 2016-2017

# Aðalliður: Afstæður tímapunktur 
AfstæðurTímapunktur →
    ÓfullkominnTímapunktur      # Timestamp, underspecified
    | FasturTími                # Time, absolute
    | AfstæðurTími              # Time, relative
    | ÓfullkominnTími           # Time, underspecified

ÓfullkominnTímapunktur →
    tímapunkturafs
    | HinnSá_þf tímapunkturafs
    | HinnSá_þf dagsafs NæstSíðast_þf_et_kk? tími
    | Vikudagur_et_þf tímapunkturafs
    | Vikudagur_gr_et_þf dagsafs NæstSíðast_þf_et_kk? tími
    | tími Vikudagur_gr_et_þf dagsafs NæstSíðast_þf_et_kk?
    | tími Vikudagur_gr_et_þf dagsföst
    | tími VísiDagsetning
    | VísiDagsetning tími
    | tími HinnSá_þf dagsafs NæstSíðast_þf_et_kk?   # kl. 14:00 hinn/þann 14. nóvember [síðastliðinn]
    | tími HinnSá_þf dagsföst                       # kl. 14:00 hinn/þann 14. nóvember 2017

FasturTími →
    tími Dagspartur?                                # 14:00, kl. 14.00 [síðdegis/árdegis]
    | Mínútur YfirÍ_þf tími Dagspartur?             # tuttugu mínútur í tvö, korter yfir átta
    | Mínútur YfirÍ_þf Klukkutala_þf/tala Dagspartur?

AfstæðurTími →
    TímaNafnliðurMinni_þgf/tala/kyn ÁðurSíðarSeinna               # um það bil tæpum 10 sekúndum síðar
    | 'fyrir:fs'_þgf TímaNafnliðurMinni_þgf/tala/kyn "síðan:ao"?  # fyrir hér um bil rúmum 10 klukkustundum síðan
    | 'eftir:fs'_þf TímaNafnliðurMinni_þf/tala/kyn

ÓfullkominnTími →
    ÁTímanum                                        # á sjötta tímanum
    | Dagspartur
    | ParturHluti_þf_et Vikudagur_ef_et OgVikudagur_et_ef? # fyrri part sunnudags / seinni helming mánudags
    | 'aðfaranótt:kvk'_et_þf AfstæðDagsetning
    | 'aðfaranótt:kvk'_et_þf TímaÁkvæðisliður_ef_et_kk? Vikudagur_ef_et OgVikudagur_et_ef?
    | TímaVísiNafnliðurMinni_þf_et/kyn              # sömu mínútu, sama klukkutíma
    | TímaVísiNafnliðurStærri_þf/tala/kyn           # undanfarið ár / undanfarna áratugi
    | FyrrSíðarSeinna? TímaÁkvæðisliður_þf_et/kyn 
        MorgunnKvöld/kyn                            # sama kvöld, fyrr sama morgun
    | FyrrSíðarSeinna? AfstæðurTímiFs
    # Það sem af er kvöldi/ári/degi
    | 'það_sem_af_er:ao' TímaNafnorð_þgf_et
    # Það sem af var/væri kvöldi/ári/degi
    | 'það:pfn'_hk_nf_et "sem:st" 'af:fs'_þgf 'vera:so'_0_gm_et_p3 TímaNafnorð_þgf_et
    # Það sem eftir lifir dags/kvölds/nætur
    | 'það:pfn'_hk_nf_et "sem:st" "eftir:ao" 'lifa:so'_0_gm_et_p3 TímaNafnorð_ef_et

ÁTímanum → 
    ÁFs_þgf RaðKlukkuTala_þgf_et_kk 'tími:kk'_þgf_et_gr  # Á sjötta tímanum

RaðKlukkuTala/fall/tala/kyn →
    RaðTala1til9/fall/tala/kyn
    | RaðTala10til12/fall/tala/kyn

RaðTala1til9/fall/tala/kyn →
    'fyrstur:lo'/fall/tala/kyn
    | 'annar:lo'/fall/tala/kyn
    | 'þriðji:lo'/fall/tala/kyn
    | 'fjórði:lo'/fall/tala/kyn
    | 'fimmti:lo'/fall/tala/kyn
    | 'sjötti:lo'/fall/tala/kyn
    | 'sjöundi:lo'/fall/tala/kyn
    | 'áttundi:lo'/fall/tala/kyn
    | 'níundi:lo'/fall/tala/kyn

RaðTala10til12/fall/tala/kyn →
    'tíundi:lo'/fall/tala/kyn
    |'ellefti:lo'/fall/tala/kyn
    | 'tólfti:lo'/fall/tala/kyn

OgRaðTala1til9/fall/tala/kyn →
    "og:st" RaðTala1til9/fall/tala/kyn

RaðTala13til100/fall/tala/kyn →
    'þrettándi:lo'/fall/tala/kyn
    | 'fjórtándi:lo'/fall/tala/kyn
    | 'fimmtándi:lo'/fall/tala/kyn
    | 'sextándi:lo'/fall/tala/kyn
    | 'sautjándi:lo'/fall/tala/kyn
    | 'átjándi:lo'/fall/tala/kyn
    | 'nítjándi:lo'/fall/tala/kyn
    | 'tuttugasti:lo'/fall/tala/kyn OgRaðTala1til9/fall/tala/kyn?
    | 'þrítugasti:lo'/fall/tala/kyn OgRaðTala1til9/fall/tala/kyn?
    | 'fertugasti:lo'/fall/tala/kyn OgRaðTala1til9/fall/tala/kyn?
    | 'fimmtugasti:lo'/fall/tala/kyn OgRaðTala1til9/fall/tala/kyn?
    | 'sextugasti:lo'/fall/tala/kyn OgRaðTala1til9/fall/tala/kyn?
    | 'sjötugasti:lo'/fall/tala/kyn OgRaðTala1til9/fall/tala/kyn?
    | 'áttugasti:lo'/fall/tala/kyn OgRaðTala1til9/fall/tala/kyn?
    | 'nítugasti:lo'/fall/tala/kyn OgRaðTala1til9/fall/tala/kyn?
    | 'hundraðasti:lo'/fall/tala/kyn OgRaðTala1til9/fall/tala/kyn?

RaðTala/fall/tala/kyn →
    RaðTala1til9/fall/tala/kyn
    | RaðTala10til12/fall/tala/kyn
    | RaðTala13til100/fall/tala/kyn

ParturHluti/fall/tala →
    'fyrripartur:kk'/fall/tala
    | 'seinnipartur:kk'/fall/tala
    | 'fyrri:lo'_kk/fall/tala 'hluti:kk'/fall/tala/kyn
    | 'seinni:lo'_kk/fall/tala 'hluti:kk'/fall/tala/kyn
    | 'fyrri:lo'_kk/fall/tala 'helmingur:kk'/fall/tala/kyn
    | 'seinni:lo'_kk/fall/tala 'helmingur:kk'/fall/tala/kyn

MorgunnKvöld_kk → 'morgunn:kk'_þf_et
MorgunnKvöld_hk → 'kvöld:hk'_þf_et
MorgunnKvöld_kvk → "" # Never matches

AfstæðurTímiFs →
    ÁFs_þgf TímaÁkvæðisliður_þgf_et_kvk 'sekúnda:kvk'_þgf_et         # á sömu sekúndu
    | ÁFs_þgf TímaÁkvæðisliður_þgf_et_kvk 'mínúta:kvk'_þgf_et        # á næstu mínútu
    | ÁFs_þgf TímaÁkvæðisliður_þgf_et_hk 'kortér:hk'_þgf_et          # á sama kortéri
    | ÁFs_þgf TímaÁkvæðisliður_þgf_et_hk 'korter:hk'_þgf_et          # á næsta korteri
    | ÁFs_þgf TímaÁkvæðisliður_þgf_et_kvk 'klukkustund:kvk'_þgf_et   # á síðustu klukkustund
    | ÁFs_þgf TímaÁkvæðisliður_þgf_et_kk 'klukkutími:kk'_þgf_et      # á sama klukkutíma

Mínútur →
    Mínútutala/tala 'mínúta:kvk'_þf/tala    # eina mínútu, tíu mínútur (yfir tvö)
    | 'korter:hk'_þf_et
    | 'korter:hk'_þgf_et                    # korteri (fyrir tólf)
    | 'kortér:hk'_þf_et
    | 'kortér:hk'_þgf_et                    # kortéri (eftir þrjú)
    | 'stundarfjórðungur:kk'_þf_et          # stundarfjórðung (fyrir tíu)
    | 'stundarfjórðungur:kk'_þgf_et         # stundarfjórðungi (eftir sjö)

Mínútutala/tala →
    Klukkutala_þf/tala

Mínútutala_ft → 
    "þrettán:töl"
    | "fjórtán:töl"
    | "fimmtán:töl"
    | "sextán:töl"
    | "sautján:töl"
    | "átján:töl"
    | "nítján:töl"
    | "tuttugu:töl"

# Útskrifaðar tölur til að tákna tímasetningu
Klukkutala_þf_et → 
    'einn:to'_et_hk/fall        # eitt

Klukkutala_þf_ft →
    'tveir:to'_ft_hk_þf         # tvö
    | 'þrír:to'_ft_hk_þf        # þrjú
    | 'fjórir:to'_ft_hk_þf      # fjögur
    | "fimm:töl"
    | "sex:töl"
    | "sjö:töl"
    | "átta:töl"
    | "níu:töl"
    | "tíu:töl"
    | "ellefu:töl"
    | "tólf:töl"

YfirÍ_þf → 'yfir:fs'_þf | 'í:fs'_þf

# Liðir sem geta staðið einir og sér en eru minni eining en dagur
Dagspartur →
    "árdegis:ao"
    | "miðdegis:ao"
    | "síðdegis:ao"
    | FyrirEftirÁ/fall HádegiMiðnætti/fall
    | 'við:fs'_þf Sól_þf
    | 'við:fs'_þf DagsByrjunLok_þf
    | 'að:fs'_þgf Lag_þgf
    | ÍByrjunLok 'dagur:kk'_et_ef
    | ÍByrjunLok KvöldNótt_ef
    | ÍFs_þf KvöldNótt_þf
    | 'í_morgun:ao'
    | 'í_gærmorgun:ao'
    | 'í_gærkvöld:ao'
    | 'í_gærkvöldi:ao'
    | ÍFs_þf DagsByrjunLok_þf
    | ÍFs_þgf 'hádegi:hk'_þgf_et_gr
    | ÁFs_þf VikuMorgunnKvöld_et_þf      # á mánudagsmorgun, á fimmtudagskvöld
    | ÁFs_þgf VikuMorgunnKvöld_et_þgf    # á mánudagsmorgni, á fimmtudagskvöldi
    | 'um:fs'_þf UmTímanafnorð_þf

FyrirEftirÁ_nf → "" # Never matches
FyrirEftirÁ_þf → 'fyrir:fs'_þf | 'eftir:fs'_þf
FyrirEftirÁ_þgf → ÁFs_þgf
FyrirEftirÁ_ef → "" # Never matches

HádegiMiðnætti/fall → 'hádegi:hk'_et/fall | 'miðnætti:hk'_et/fall

Sól/fall →
    'sólarupprás:kvk'_et/fall
    | 'sólsetur:hk'_et/fall

DagsByrjunLok/fall →
    'dagsbyrjun:kvk'_et/fall
    | 'dagslok:hk'_ft/fall

Lag/fall →
    'næturlag:hk'_et/fall
    | 'morgunlag:hk'_et/fall
    | 'kvöldlag:hk'_et/fall

KvöldNótt/fall → 'kvöld:hk'_et/fall | 'nótt:kvk'_et/fall

VikuMorgunnKvöld/tala/fall → VikuMorgunnKvöld/fall/tala
VikuMorgunnKvöld/fall/tala →
    'mánudagsmorgunn:kk'/fall/tala
    | 'mánudagskvöld:hk'/fall/tala
    | 'þriðjudagsmorgunn:kk'/fall/tala
    | 'þriðjudagskvöld:hk'/fall/tala
    | 'miðvikudagsmorgunn:kk'/fall/tala
    | 'miðvikudagskvöld:hk'/fall/tala
    | 'fimmtudagsmorgunn:kk'/fall/tala
    | 'fimmtudagskvöld:hk'/fall/tala
    | 'föstudagsmorgunn:kk'/fall/tala
    | 'föstudagskvöld:hk'/fall/tala
    | 'laugardagsmorgunn:kk'/fall/tala
    | 'laugardagskvöld:hk'/fall/tala
    | 'sunnudagsmorgunn:kk'/fall/tala
    | 'sunnudagskvöld:hk'/fall/tala

VikuMorgunnKvöld_gr/fall/tala →
    'mánudagsmorgunn:kk'_gr/fall/tala
    | 'mánudagskvöld:hk'_gr/fall/tala
    | 'þriðjudagsmorgunn:kk'_gr/fall/tala
    | 'þriðjudagskvöld:hk'_gr/fall/tala
    | 'miðvikudagsmorgunn:kk'_gr/fall/tala
    | 'miðvikudagskvöld:hk'_gr/fall/tala
    | 'fimmtudagsmorgunn:kk'_gr/fall/tala
    | 'fimmtudagskvöld:hk'_gr/fall/tala
    | 'föstudagsmorgunn:kk'_gr/fall/tala
    | 'föstudagskvöld:hk'_gr/fall/tala
    | 'laugardagsmorgunn:kk'_gr/fall/tala
    | 'laugardagskvöld:hk'_gr/fall/tala
    | 'sunnudagsmorgunn:kk'_gr/fall/tala
    | 'sunnudagskvöld:hk'_gr/fall/tala

# Safn tímanafnorða sem geta staðið í FL með 'um' og tákna einingu sem er styttri en dagur
UmTímanafnorð/fall →
    'aftann:kk'_et/fall
    | 'árdegi:hk'_et/fall
    | 'árdegisbil:hk'_et/fall
    | 'eftirmiðdagur:kk'_et/fall
    | 'eftirmiðdegi:hk'_et/fall
    | 'hádegi:hk'_et/fall
    | 'hádegisbil:hk'_et/fall
    | 'hádegisleyti:hk'_et/fall
    | 'kvöld:hk'_et/fall
    | 'kvöldbil:hk'_et/fall
    | 'kvöldleyti:hk'_et/fall
    | 'kvöldmatarleyti:hk'_et/fall
    | 'miðdegi:hk'_et/fall
    | 'miðdegisbil:hk'_et/fall
    | 'miðdegisleyti:hk'_et/fall
    | 'miðnætti:hk'_et/fall
    | 'miðnæturbil:hk'_et/fall
    | 'miðnæturleyti:hk'_et/fall
    | 'morgunbil:hk'_et/fall
    | 'morgunleyti:hk'_et/fall
    | 'morgunn:kk'_et/fall
    | 'nótt:kvk'_et/fall
    | 'síðdegi:hk'_et/fall
    | 'síðdegisbil:hk'_et/fall

# Aðalliður: Tíðni
Tíðni →
    'að_jafnaði:ao'
    | 'ótt_og_títt:ao'
    | 'sí_og_æ:ao'
    | 'í_sífellu:ao'
    | "aldrei:ao"
    | "alltaf:ao"
    | "árlega:ao"
    | "daglega:ao"
    | "dagsdaglega:ao"
    | "mánaðarlega:ao"
    | "oft:ao"
    | "reglulega:ao"
    | "sjaldan:ao"
    | "stundum:ao"
    | "títt:ao"
    | "vikulega:ao"
    | TíðniNafnliðurStærri
    | TíðniNafnliðurMinni
    | TíðniForsetningarliðurStærri
    | TíðniForsetningarliðurMinni

TíðniNafnliðurStærri →
    TíðniLo_þf_et/kyn TímaNafnorðStærri_þf_et/kyn       # sérhvern dag, hverja viku
    | 'dagur:kk'_þf_et TíðniLo_þf_et_kk                 # dag hvern
    | TíðniLo_þf_et_kk Vikudagur_þf_et OgVikudagur_et_þf?  # sérhvern fimmtudag
    | 'allur:fn'_þf_ft/kyn 'virkur:lo'_þf_ft/kyn?       # alla (virka) daga - ath. et. væri tímabil; allan daginn
        TímaNafnorðStærri_þf_ft/kyn       
    | 'allur:fn'_þf_ft_kk Vikudagur_þf_ft OgVikudagur_ft_þf? # alla fimmtudaga

TíðniForsetningarliðurStærri →
    ÁFs_þgf TíðniLo_þgf_et_kk                        # á hverjum (virkum) degi
        'virkur:lo'_þgf_et_kk? 'dagur:kk'_þgf_et    
    | ÁFs_þgf TíðniLo_þgf_et_kk Vikudagur_þgf_et OgVikudagur_et_þgf? # á hverjum fimmtudegi
    | ÁFs_þgf Vikudagur_þgf_ft OgVikudagur_ft_þgf    # á fimmtudögum [og þriðjudögum]
    | ÍFs_þgf TíðniLo_þgf_et_kvk 'vika:kvk'_þgf_et   # í hverri viku
    | ÍFs_þgf TíðniLo_þgf_et_kk 'mánuður:kk'_þgf_et  # í hverjum mánuði
    | ÁFs_þgf TíðniLo_þgf_et_hk 'ár:hk'_þgf_et       # á hverju ári

TíðniNafnliðurMinni →
    TíðniLo_þf_et/kyn MorgunnKvöld/kyn                  # sérhvern morgun, hvert kvöld
    | 'allur:fn'_þf_ft/kyn 'virkur:lo'_þf_ft/kyn?       # alla (virka) morgna, öll kvöld - ath. et. væri tímabil; allt kvöldið
        MorgunnKvöld/kyn

TíðniForsetningarliðurMinni →
    ÁFs_þgf TíðniLo_þgf_et/kyn                       # á hverjum (virkum) morgni, á sérhverju kvöldi
        'virkur:lo'_þgf_et/kyn? MorgunnKvöld/kyn
    | ÁFs_þgf VikuMorgunnKvöld_þgf_ft                # á fimmtudagsmorgnum, á sunnudagskvöldum
    | ÁFs_þgf TíðniLo_þgf_et/kyn                     # á hverju fimmtudagskvöldi, á sérhverjum sunnudagsmorgni
        VikuMorgunnKvöld_þgf_et
    | 'á_morgnana:ao'
    | 'á_kvöldin:ao'

TíðniLo/fall/tala/kyn → 'hver:fn'/fall/tala/kyn | 'sérhver:fn'/fall/tala/kyn

# Aðalliður: Tímabil
Tímabil →
    FastTímabil
    | AfstættTímabil
    | TímabilTími

FastTímabil →
    DaganaTímabilFast
    | PunkturFs/aukafall FasturPunkturTNL
    | 'milli:fs'_ef FasturPunkturTNL Og FasturPunkturTNL
    | 'frá:fs'_þgf FasturPunkturTNL 'til:fs'_ef FasturPunkturTNL
    | 'milli:fs'_ef TímiPunkturTNL Og tímapunkturfast          # milli kl. 14:00 og 17:00 14. nóvember 2017

AfstættTímabil → 
    DaganaTímabilAfstætt
    | ÁrinTímabilAfstætt
    | FráTilTími VísiDagsetning
    | VísiDagsetning FráTilTími
    | MagnFs/aukafall TímaMagnNafnliðurStærri/aukafall/tala/kyn     # á 20 dögum, innan 6 daga, í 30 ár
    | 'út:fs'_þf TímaMagnNafnliðurStærri_þf/tala/kyn                # út árið
    | 'út:fs'_þf ártal
    | 'yfir:fs'_þf TímaMagnNafnliðurStærri_þf/tala/kyn              # yfir mánuðinn
    | 'allur:fn'_þf_et/kyn TímaNafnorðStærri/kyn/þf/et              # allan daginn/vikuna/mánuðinn
    | PunkturFs/aukafall AfsPunkturTNL/aukafall
    | TímaNafnorðStærri_þf/tala/kyn "þar:ao"? ÁðurEftir
    | TímaNafnorðStærri_þgf_ft/kyn "saman:ao"                       # Nefndin fundaði ekki mánuðum saman
    | "svo:ao" TímaNafnorðStærri_þgf_ft/kyn 'skipta:so'_1_þgf_gm_fh_et_p3 # svo mánuðum skiptir/skipti
    | ÞegarLíðaTekurÁ
    | AfstættTímabilTvöfalt

AfstættTímabilTvöfalt →
    'milli:fs'_ef AfsPunkturTNL_ef Og AfsPunkturTNL_ef
    | 'frá:fs'_þgf AfsPunkturTNL_þgf 'til:fs'_ef AfsPunkturTNL_ef
    | 'milli:fs'_ef FasturPunkturTNL Og AfsPunkturTNL_ef
    | 'frá:fs'_þgf FasturPunkturTNL 'til:fs'_ef AfsPunkturTNL_ef
    | 'milli:fs'_ef AfsPunkturTNL_ef Og FasturPunkturTNL
    | 'frá:fs'_þgf AfsPunkturTNL_þgf 'til:fs'_ef FasturPunkturTNL
    | 'milli:fs'_ef TímiPunkturTNL Og tímapunkturafs
    | 'frá:fs'_þgf ártal Til tala                                   # frá 2011 til og með 2014
    | 'ár:hk'_þf_ft_gr ártal Til tala                               # árin 2017-20

$score(+16) AfstættTímabilTvöfalt

TímabilTími →
    FráTilTími 
    # | TímaNafnliðurMinni/fall/tala/kyn                              # um það bil tæpar 10 sekúndur
    | TímaMagnNafnliðurMinni_þf/tala/kyn                            # næstu 8 mínútur
    | MagnFs/aukafall TímaMagnNafnliðurMinni/aukafall/tala/kyn      # á 20 sekúndum, innan 6 sekúndna, í 30 millisekúndur
    | PunkturFs/aukafall TímiPunkturTNL                             # eftir kl. 14:00
    | 'milli:fs'_ef TímiPunkturTNL Og TímiPunkturTNL
    | 'frá:fs'_þgf TímiPunkturTNL 'til:fs'_ef TímiPunkturTNL
    | TímaNafnorðMinni_þf/tala/kyn "þar:ao"? ÁðurEftir
    | TímaNafnorðMinni_þgf_ft/kyn "saman:ao"                        # Hann kom ekki heim klukkutímunum saman
    | "svo:ao" TímaNafnorðMinni_þgf_ft/kyn 'skipta:so'_1_þgf_gm_fh_et_p3  # svo mínútum skiptir/skipti

FasturPunkturTNL → FöstDagsetning | FasturTímapunktur

TímiPunkturTNL → FasturTími | ÓfullkominnTími

# Dagsetningar/tímapunktar sem ganga innan FL til að tákna tímabil
AfsPunkturTNL/fall → 
    ÓfullkominDagsetning
    | ÓfullkominnTímapunktur
    | Vikudagur/fall/tala tími?

# Forsetningar sem standa með TNL sem sýnir magn - 20 sekúndur, 30 ár
MagnFs_þf →
    'í:fs'_þf

MagnFs_þgf →
    'á:fs'_þgf

MagnFs_ef →
    'innan:fs'_ef

PunkturAtv →
    "rétt:ao"

# Forsetningar sem standa með TNL sem sýna fastan punkt - 14. nóvember 2016, kl. 14:00
PunkturFs_þf →
    PunkturAtv? 'fyrir:fs'_þf       # Ath. með þgf táknar það dagsetningu/tíma.
    | PunkturAtv? 'eftir:fs'_þf
    | 'síðan:fs'_þf
    | 'skömmu_fyrir:fs'_þf
    | 'skömmu_eftir:fs'_þf
    | 'örskömmu_fyrir:fs'_þf
    | 'örskömmu_eftir:fs'_þf

PunkturFs_þgf →
    'frá:fs'_þgf OgMeð?

PunkturFs_ef →
    'til:fs'_ef OgMeð?
    | 'til_og_með:ao'

DaganaTímabilAfstætt →
    DaganaAfs NæstSíðast_þf_et_kk?
    | Vikudagur_et_þf tímapunkturafs Til tími   # miðvikudaginn 13. desember kl 14:00 - 15:00

ÁrinTímabilAfstætt →
    ártal ÁraRuna* Til tala                     # 2016-2017 / 2016 til 2017
    | 'ár:hk'_ft_gr_þf tala ÁraRuna* Til tala   # árin 2016-2017, árin 2016 til 2017

DaganaTímabilFast →
    Dagana? dagsafs Til dagsföst                # (dagana/helgina) 13. október til 14. nóvember 2017; 13. október-14. nóvember 2017
    | Dagana? raðnr Til dagsföst                # (dagana/helgina) 13.-14. nóvember 2017, 13. til 14. nóvember 2017
    | Vikudagur_et_þf tímapunkturfast Til tími  # miðvikudaginn 13. desember 2017 kl 14:00 - 15:00

FráTilTími → 
    'frá:fs'_þgf? tími Til tími DaganaTímabilAfstætt?
    | 'frá:fs'_þgf? tími Til tími DaganaTímabilFast?
    | 'frá:fs'_þgf? tími Til tímapunkturfast                # kl. 14:00-17:00 14. nóvember 2017
    | 'frá:fs'_þgf? tími Til tímapunkturafs                 # kl. 14:00-17:00 14. nóvember
    | 'frá:fs'_þgf? tími Til tími HinnSá_þf? dagsföst       # kl. 14:00 til 17:00 hinn 14. nóvember 2017
    | 'frá:fs'_þgf? tími Til tími HinnSá_þf? dagsafs        # kl. 14:00 til 17:00 þann 14. nóvember

ÞegarLíðaTekurÁ →
    "þegar:st" 'líða:so'_0_nh 'taka:so'_0_gm_p3_et ÁFs_þf TímaNafnorðStærri_þf/tala/kyn

OgMeð → Og 'með:fs'_þgf    # (frá) og með, (til) og með

# Tímanafnliðir
TímaNafnliðurStærri/fall/tala/kyn →
    TímaAtviksEinkunn? TímaLo/fall/tala/kyn? 
        Tímatala/fall/tala/kyn? TímaNafnorðStærri/fall/tala/kyn

TímaNafnliðurMinni/fall/tala/kyn → 
    TímaAtviksEinkunn? TímaLo/fall/tala/kyn? 
        Tímatala/fall/tala/kyn? TímaNafnorðMinni/fall/tala/kyn

# næsta dag / undanfarin tvö ár
TímaVísiNafnliðurStærri/fall/tala/kyn → 
    TímaÁkvæðisliður/fall/tala/kyn Tímatala/fall/tala/kyn? TímaNafnorðStærri/fall/tala/kyn

# sömu mínútu
TímaVísiNafnliðurMinni/fall/tala/kyn → 
    TímaÁkvæðisliður/fall/tala/kyn Tímatala/fall/tala/kyn? TímaNafnorðMinni/fall/tala/kyn

# næstu 20 daga
TímaMagnNafnliðurStærri/fall/tala/kyn →
    TímaAtviksEinkunn? TímaÁkvæðisliður/fall/tala/kyn? 
        Tímatala/fall/tala/kyn? TímaNafnorðStærri/fall/tala/kyn

# síðustu 40 mínútur
TímaMagnNafnliðurMinni/fall/tala/kyn →
    TímaAtviksEinkunn? TímaÁkvæðisliður/fall/tala/kyn? 
        Tímatala/fall/tala/kyn? TímaNafnorðMinni/fall/tala/kyn

TímaNafnorð/fall/tala →
    TímaNafnorð/fall/tala/kyn

TímaNafnorð/fall/tala/kyn →
    TímaNafnorðMinni/fall/tala/kyn
    | TímaNafnorðStærri/fall/tala/kyn

# Tímaeiningar sem eru styttri en dagur
TímaNafnorðMinni/fall/tala/kyn → TímaNafnorðMinni/kyn/fall/tala

TímaNafnorðMinni_kvk/fall/tala →
    'nanósekúnda:kvk'/fall/tala
    | 'míkrósekúnda:kvk'/fall/tala
    | 'millisekúnda:kvk'/fall/tala
    | 'sekúnda:kvk'/fall/tala
    #| 'sek.'
    | 'mínúta:kvk'/fall/tala
    #| 'mín.'
    | 'klukkustund:kvk'/fall/tala
    #| 'klst.'

TímaNafnorðMinni_hk/fall/tala →
    'sekúndubrot:hk'/fall/tala
    | 'korter:hk'/fall/tala
    | 'kortér:hk'/fall/tala
    | 'kvöld:hk'/fall/tala

TímaNafnorðMinni_kk/fall/tala →
    'stundarfjórðungur:kk'/fall/tala
    | 'hálftími:kk'/fall/tala
    | 'klukkutími:kk'/fall/tala
    | 'morgunn:kk'/fall/tala

# Tímaeiningar sem eru dagur eða lengri
TímaNafnorðStærri/fall/tala/kyn → TímaNafnorðStærri/kyn/fall/tala

TímaNafnorðStærri_kk/fall/tala →
    'dagur:kk'/fall/tala
    | 'sólarhringur:kk'/fall/tala
    | 'mánuður:kk'/fall/tala
    | 'ársfjórðungur:kk'/fall/tala
    | 'áratugur:kk'/fall/tala
    | 'aldarfjórðungur:kk'/fall/tala
    | 'vetur:kk'/fall/tala

TímaNafnorðStærri_kvk/fall/tala →
    'vika:kvk'/fall/tala
    | 'önn:kvk'/fall/tala
    | 'öld:kvk'/fall/tala
    | 'þúsöld:kvk'/fall/tala

TímaNafnorðStærri_hk/fall/tala →
    'misseri:hk'/fall/tala
    | 'ár:hk'/fall/tala
    | 'leikár:hk'/fall/tala
    | 'reikningsár:hk'/fall/tala
    | 'árhundrað:hk'/fall/tala
    | 'árþúsund:hk'/fall/tala
    | 'sumar:hk'/fall/tala
    | 'kjörtímabil:hk'/fall/tala

TímaNafnorðStærri_gr/fall/tala →
    'dagur:kk'_gr/fall/tala
    | 'sólarhringur:kk'_gr/fall/tala
    | 'mánuður:kk'_gr/fall/tala
    | 'ársfjórðungur:kk'_gr/fall/tala
    | 'áratugur:kk'_gr/fall/tala
    | 'aldarfjórðungur:kk'_gr/fall/tala
    | 'vetur:kk'_gr/fall/tala
    | 'vika:kvk'_gr/fall/tala
    | 'önn:kvk'_gr/fall/tala
    | 'öld:kvk'_gr/fall/tala
    | 'þúsöld:kvk'_gr/fall/tala
    | 'misseri:hk'_gr/fall/tala
    | 'ár:hk'_gr/fall/tala
    | 'leikár:hk'_gr/fall/tala
    | 'reikningsár:hk'_gr/fall/tala
    | 'árhundrað:hk'_gr/fall/tala
    | 'árþúsund:hk'_gr/fall/tala
    | 'sumar:hk'_gr/fall/tala
    | 'kjörtímabil:hk'_gr/fall/tala

TímaAtviksEinkunn →
    "rétt:ao" TímaAtviksEinkunn
    | "rúmlega:ao"
    | "næstum:ao"
    | "innan_við:ao"
    | "tæplega:ao"
    | "nákvæmlega:ao"
    | "hér_um_bil:ao"
    | "ríflega:ao"
    | "næstum:ao" 'það:fn'_þgf?     # næstum, næstum því
    | "um:ao"
    | 'um:fs'_þf 'sá:fn'_þf_et_hk 'bil:hk'_þf_et # um það bil

Tímatala/fall/tala/kyn →
    tala/fall/tala/kyn
    | töl/fall/tala/kyn
    | to/fall/tala/kyn
    | 'hálfur:lo'/fall/tala/kyn
    | 'nokkur:fn'/fall/tala/kyn # nokkrum dögum seinna
    | 'fár:lo'_sb/fall/tala/kyn # fáum dögum síðar
    | 'örfár:lo'_sb/fall/tala/kyn # örfáum vikum síðar
    | 'margur:lo'_sb/fall/tala/kyn # mörgum vikum síðar
    | 'allmargur:lo'_sb/fall/tala/kyn # allmörgum vikum síðar
    | Tímatala/fall/tala/kyn "til:ao" Tímatala/fall/tala/kyn
    | Tímatala/fall/tala/kyn '-:en' Tímatala/fall/tala/kyn

TímaLo/fall/tala/kyn → 'rúmur:lo'_sb/fall/tala/kyn | 'tæpur:lo'_sb/fall/tala/kyn

$score(+14) TímaLo/fall/tala/kyn

# Hjálparreglur
HinnSá/fall → 'sá:fn'_et_kk/fall | 'hinn:gr'_et_kk/fall | 'aðfaranótt:kvk'_et/fall

# Stigin hér þurfa að yfirvinna EinkunnFornafn, sem skeytir
# 'þann' aftan við nafnlið
$score(+20) HinnSá/fall

Vikudagur/tala/fall → Vikudagur/fall/tala
Vikudagur/fall/tala →
    # !!! Do the following in the tokenizer instead?
    'sunnudagur:kk'/fall/tala
    | 'mánudagur:kk'/fall/tala
    | 'þriðjudagur:kk'/fall/tala
    | 'miðvikudagur:kk'/fall/tala
    | 'fimmtudagur:kk'/fall/tala
    | 'föstudagur:kk'/fall/tala
    | 'laugardagur:kk'/fall/tala
    | 'sunnudagur:kk'/fall/tala

Vikudagur_gr/tala/fall → Vikudagur_gr/fall/tala
Vikudagur_gr/fall/tala →
    'sunnudagur:kk'_gr/fall/tala
    | 'mánudagur:kk'_gr/fall/tala
    | 'þriðjudagur:kk'_gr/fall/tala
    | 'miðvikudagur:kk'_gr/fall/tala
    | 'fimmtudagur:kk'_gr/fall/tala
    | 'föstudagur:kk'_gr/fall/tala
    | 'laugardagur:kk'_gr/fall/tala
    | 'sunnudagur:kk'_gr/fall/tala

OgVikudagur/tala/fall →
    KommaVikudagur/tala/fall* OgEða Vikudagur/tala/fall

KommaVikudagur/tala/fall →
    ',' Vikudagur/tala/fall

FyrrSíðarSeinna →
    'snemma:ao'_mst     # fyrr
    | "síðar:ao"        # síðar
    | 'seint:ao'_mst    # seinna

ÁðurSíðarSeinna →
    "áður:ao" EnTagl?
    | "síðar:ao"
    | 'seint:ao'_mst
    | 'snemma:ao'_mst # fyrr
    | 'eftir:fs'_þf "að:st" HreinYfirsetning # þremur dögum eftir að Jón var ráðinn
    | 'eftir:fs'_þf Nl_þf # (tveimur dögum) eftir þennan atburð
    | 'fyrir:fs'_þf Nl_þf # (viku) fyrir þennan atburð

# (tveimur vikum áður) en skriðan féll á húsið
EnTagl → "en:st" HreinYfirsetning

# !!! Það er skrýtið að lýsingarorðin séu á víxl í sterkri og veikri beygingu,
# !!! en það virðist vera tilfellið skv. BÍN.
TímaÁkvæðisliður/fall/tala/kyn →
    'næstur:lo'_evb/fall/tala/kyn       # næstu tvö árin
    | 'síðari:lo'_evb/fall/tala/kyn     # síðustu tvö árin
    | 'síðari:lo'_mst/fall/tala/kyn     # síðari tvö árin
    | SáÞessi/fall/tala/kyn? 'sami:fn'/fall/tala/kyn # 'fyrr í þeim sama mánuði' -- hegðar sér frekar sem lo hér.
    | 'seinn:lo'_evb/fall/tala/kyn      # seinustu tvö árin
    | 'seinn:lo'_mst/fall/tala/kyn      # seinni tvö árin
    | 'næstkomandi:lo'_sb/fall/tala/kyn
    | 'síðastliðinn:lo'_sb/fall/tala/kyn
    | 'undanfarinn:lo'_sb/fall/tala/kyn
    | 'undangenginn:lo'_sb/fall/tala/kyn
    | 'fyrstur:lo'_sb/fall/tala/kyn     # fyrstu tvö árin
    # !!! Neðangreind tölu-lýsingarorð eru hvorki merkt með veikri né sterkri beygingu í BÍN
    | 'annar:lo'/fall/tala/kyn          # annan daginn [í röð]
    | 'þriðji:lo'/fall/tala/kyn         # þriðja daginn [í röð]
    | 'fjórði:lo'/fall/tala/kyn         # fjórða daginn [í röð]
    | 'fimmti:lo'/fall/tala/kyn         # fimmta daginn [í röð]
    | 'sjötti:lo'/fall/tala/kyn         # sjötta daginn [í röð]
    | 'sjöundi:lo'/fall/tala/kyn        # sjöunda daginn [í röð]
    | 'áttundi:lo'/fall/tala/kyn        # áttunda daginn [í röð]
    | 'níundi:lo'/fall/tala/kyn         # níunda daginn [í röð]
    | 'tíundi:lo'/fall/tala/kyn         # tíunda daginn [í röð]
    | 'ellefti:lo'/fall/tala/kyn        # ellefta daginn [í röð]
    | 'tólfti:lo'/fall/tala/kyn         # tólfta daginn [í röð]
    | SáÞessi/fall/tala/kyn

# Lýsingarorð sem ganga á eftir dagsetningum
NæstSíðast/fall/tala/kyn →
    'næstkomandi:lo'/fall/tala/kyn
    | 'síðastliðinn:lo'/fall/tala/kyn
    | VillaSíðastLiðinn/fall/tala/kyn

# Grípa villuna 'síðast liðinn'
# Við þurfum sérstaka villureglu fyrir þetta þar sem orðin
# 'síðast liðin/ð/nn' geta komið fyrir í öðru samhengi án þess að það sé villa
VillaSíðastLiðinn/fall/tala/kyn →
    'síðar:ao' 'líða:so'_lhþt/fall/tala/kyn

$tag(error) VillaSíðastLiðinn/fall/tala/kyn

$score(+20) VillaSíðastLiðinn/fall/tala/kyn

ÁðurEftir →
    "áður:ao"
    | "eftir:ao"
    | 'á_undan:ao'
    | 'á_eftir:ao'

ÍByrjunLok →
    'í:fs'_þgf 'byrjun:kvk'_þgf
    | 'í:fs'_þf 'lok:hk'_ft_þf
    | 'undir:fs'_þf 'lok:hk'_ft_þf

# "annaðhvort" í samtengingum (hér er 'annað hvort' álitið villa)
AnnaðhvortSt →
    "annaðhvort:st" | VillaAnnaðHvort

# Villureglur // Error rules

AðvörunKomma →
    ","

$tag(error) AðvörunKomma
$score(-99) AðvörunKomma

AðvörunAð →
    "að:st"

$tag(error) AðvörunAð

# 'annað hvort' í tveimur orðum en á að vera eitt
VillaAnnaðHvort →
    'annar:fn'_et_nf_hk 'hvor:fn'_et_nf_hk

$tag(error) VillaAnnaðHvort

# "annaðhvort" í einu orði en á að vera í tveimur
VillaAnnaðhvort →
    "annaðhvort:st"

$tag(error) VillaAnnaðhvort
$score(-2) VillaAnnaðhvort

VillaÞóAð →
    "jafnvel:ao"? "þó:st"

$tag(error) VillaÞóAð
$score(-64) VillaÞóAð  # Nægur mínus til að núlla út plús fyrir ViðurkenningarTenging

VillaFráÞvíAð →
    Allt? 'frá:fs'_þgf 'það:pfn'_hk_þgf_et

$tag(error) VillaFráÞvíAð
$score(+2) VillaFráÞvíAð

VillaVístAð →
    "víst:ao" "að:st"  

$tag(error) VillaVístAð
$score(-32) VillaVístAð

AðvörunHeldur →
    "heldur:st"

$tag(error) AðvörunHeldur
$score(+4) AðvörunHeldur

AðvörunSíðan →
    "síðan:ao"
$tag(error) AðvörunSíðan

# Villa í tölu: Einn af drengjunum sem tóku þátt unnu keppnina → vann keppnina
VillaEinnAf/fall/kyn →
    'einn:fn'_et/fall/kyn AfLiður/kyn
$tag(error) VillaEinnAf/fall/kyn

# Villa í kyni: Hún er ein af þátttakendum → einn
# Hef þá kk → kvk, kvk → kk, kk → hk, hk → kk, hk → kvk, kvk → hk

VillaFjöldiHluti/fall/kyn →
    'mikill:lo'_et_kk/fall? FjöldiHluti_et_kk/fall EfLiður_ft/kyn
    | 'stór:lo'_et_kk/fall? 'hluti:kk'_et/fall EfLiður_ft/kyn

FjöldiHluti_et_kk/fall →
    'fjöldi:kk'_et/fall
    | 'minnihluti:kk'_et/fall
    | 'meirihluti:kk'_et/fall
    | 'helmingur:kk'_et/fall

$tag(error) VillaFjöldiHluti/fall/kyn
$score(-4) VillaFjöldiHluti/fall/kyn


# ----------------------------------------------
#
# Rules for common grammar errors
#
# The following rules are exclusive to the
# grammar checker (in the GreynirCorrect package)
#
# ----------------------------------------------

$if(include_errors)

# Öll föll nema nefnifall
/fallxnf = þf þgf ef
# Öll föll nema þolfall
/fallxþf = nf þgf ef
# Öll föll nema þágufall
/fallxþgf = nf þf ef
# Öll föll nema eignarfall
/fallxef = nf þf þgf

# Regla fyrir villu þar sem fornafn í einkunn
# er ekki í sama falli og nafnorð, t.d. 'þessum mann'

Einkunn/tala/fall/kyn →
    > VillaEinkunn/fall/tala/kyn

VillaEinkunn_nf/tala/kyn →
    Fornafn/fallxnf/tala/kyn

VillaEinkunn_þf/tala/kyn →
    Fornafn/fallxþf/tala/kyn

VillaEinkunn_þgf/tala/kyn →
    Fornafn/fallxþgf/tala/kyn

VillaEinkunn_ef/tala/kyn →
    Fornafn/fallxef/tala/kyn

$tag(error) VillaEinkunn/fall/tala/kyn
$score(-999) VillaEinkunn/fall/tala/kyn

# Regla fyrir villu þar sem ritað er 'annara'/'nokkura' í stað 'annarra'/'nokkurra'
Fornafn_ft_ef/kyn → VillaAnnara
# Regla fyrir villu þar sem ritað er 'annarar'/'nokkurar' í stað 'annarrar'/'nokkurrar'
Fornafn_et_þgf_kvk → VillaAnnari
Fornafn_et_ef_kvk → VillaAnnarar

$tag(keep) Fornafn_et_þgf_kvk
$tag(keep) Fornafn_et_ef_kvk

VillaAnnara → "annara" | "nokkura"
VillaAnnari → "annari" | "nokkuri"
VillaAnnarar → "annarar" | "nokkurar"

$tag(error) VillaAnnara
$tag(error) VillaAnnari
$tag(error) VillaAnnarar
$score(-999) VillaAnnara
$score(-999) VillaAnnari
$score(-999) VillaAnnarar

FsMeðFallstjórn →
    # The following notation means that the production is
    # added to the existing productions of FsMeðFallstjórn
    # but with lower priority, so that it is only derived
    # if no higher-priority derivations are possible
    # for the same token span
    > VillaFsMeðFallstjórn/fall

VillaFsMeðFallstjórn_nf →
    fs_nf Nl/fallxnf

VillaFsMeðFallstjórn_þf →
    fs_þf Nl/fallxþf

VillaFsMeðFallstjórn_þgf →
    fs_þgf Nl/fallxþgf

VillaFsMeðFallstjórn_ef →
    fs_ef Nl/fallxef

$tag(error) VillaFsMeðFallstjórn/fall
$score(-999) VillaFsMeðFallstjórn/fall

# Villa þar sem tvípunkti er skeytt á milli forsetningar og
# nafnliðar, t.d. "með: góðri efnahagsstjórn"

FsMeðFallstjórn →
    > VillaTvípunkturFs

VillaTvípunkturFs →
    fs/fall ":" Nl/fall

$tag(error) VillaTvípunkturFs
$score(+99) VillaTvípunkturFs

NauðsynlegKomma →
    > VillaVantarKommu

VillaVantarKommu → ∅

$tag(error) VillaVantarKommu
$score(-999) VillaVantarKommu

Aðaltenging →
    > VillaNé

VillaNé → "né:st"
$tag(error) VillaNé
$score(-999) VillaNé

# Sögn í annarri tölu en frumlag: 'Vinnuvika sjómanna eru 7 dagar'

BeygingarliðurÁnUmröðunar/tala/pers/kyn →
    > VillaÍTölu/tala/pers/kyn

VillaÍTölu_et_p1/kyn →
    Frumlag_p1_et/kyn BeygingarliðurMegin_ft_p1/kyn
VillaÍTölu_et_p2/kyn →
    Frumlag_p2_et/kyn BeygingarliðurMegin_ft_p2/kyn
VillaÍTölu_et_p3/kyn →
    Frumlag_p3_et/kyn BeygingarliðurMegin_ft_p3/kyn

VillaÍTölu_ft_p1/kyn →
    Frumlag_p1_ft/kyn BeygingarliðurMegin_et_p1/kyn
VillaÍTölu_ft_p2/kyn →
    Frumlag_p2_ft/kyn BeygingarliðurMegin_et_p2/kyn
VillaÍTölu_ft_p3/kyn →
    Frumlag_p3_ft/kyn BeygingarliðurMegin_et_p3/kyn

$tag(error) VillaÍTölu/tala/pers/kyn
$score(-999) VillaÍTölu/tala/pers/kyn

# Orð sem endar á -ir í nefnifalli notað í stað þolfalls:
# 'ég sá læknir[inn]', 'skipið sigldi á tundurspillir[inn]',
# 'kjötið var í kælir'

Fyrirbæri_þf_kk →
    > VillaEndingIR

VillaEndingIR →
    # The "xir" variant on nouns causes the terminal to only match
    # words with lemmas ending with "ir"
    no_et_nf_kk_xir

$tag(error) VillaEndingIR
$score(-999) VillaEndingIR

# Orð sem endar á -ana í þolfalli notað í stað eignarfalls:
# 'niðurstaða þingflokkana', 'hattar karlana'

FyrirbæriMeðGreini_ft_ef_kk →
    > VillaEndingANA

VillaEndingANA →
    # The "zana" variant on nouns causes the terminal to only match
    # words ending with "ana"
    no_ft_þf_kk_gr_zana

$tag(error) VillaEndingANA
$score(-999) VillaEndingANA

$endif(include_errors)
